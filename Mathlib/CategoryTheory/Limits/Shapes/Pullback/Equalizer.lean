/-
Copyright (c) 2024 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.IsPullback.Defs

/-!
# Equalizers as pullbacks of products

Also see `CategoryTheory.Limits.Constructions.Equalizers` for very similar results.

-/

public section

universe v u

namespace CategoryTheory.Limits

variable {C : Type u} [Category.{v} C] {X Y : C} (f g : X âŸ¶ Y)

set_option backward.isDefEq.respectTransparency false in
/-- The equalizer of `f g : X âŸ¶ Y` is the pullback of the diagonal map `Y âŸ¶ Y Ã— Y`
along the map `(f, g) : X âŸ¶ Y Ã— Y`. -/
lemma isPullback_equalizer_prod [HasEqualizer f g] [HasBinaryProduct Y Y] :
    IsPullback (equalizer.Î¹ f g) (equalizer.Î¹ f g â‰« f) (prod.lift f g) (prod.lift (ğŸ™ _) (ğŸ™ _)) := by
  refine âŸ¨âŸ¨by ext <;> simp [equalizer.condition f g]âŸ©, âŸ¨PullbackCone.IsLimit.mk _ ?_ ?_ ?_ ?_âŸ©âŸ©
  Â· refine fun s â†¦ equalizer.lift s.fst ?_
    have Hâ‚ : s.fst â‰« f = s.snd := by simpa using congr($s.condition â‰« prod.fst)
    have Hâ‚‚ : s.fst â‰« g = s.snd := by simpa using congr($s.condition â‰« prod.snd)
    exact Hâ‚.trans Hâ‚‚.symm
  Â· exact fun s â†¦ by simp
  Â· exact fun s â†¦ by simpa using congr($s.condition â‰« prod.fst)
  Â· exact fun s m hm _ â†¦ by ext; simp [*]

set_option backward.isDefEq.respectTransparency false in
/-- The coequalizer of `f g : X âŸ¶ Y` is the pushout of the diagonal map `X â¨¿ X âŸ¶ X`
along the map `(f, g) : X â¨¿ X âŸ¶ Y`. -/
lemma isPushout_coequalizer_coprod [HasCoequalizer f g] [HasBinaryCoproduct X X] :
    IsPushout (coprod.desc f g) (coprod.desc (ğŸ™ _) (ğŸ™ _))
      (coequalizer.Ï€ f g) (f â‰« coequalizer.Ï€ f g) := by
  refine âŸ¨âŸ¨by ext <;> simp [coequalizer.condition f g]âŸ©, âŸ¨PushoutCocone.IsColimit.mk _ ?_ ?_ ?_ ?_âŸ©âŸ©
  Â· refine fun s â†¦ coequalizer.desc s.inl ?_
    have Hâ‚ : f â‰« s.inl = s.inr := by simpa using congr(coprod.inl â‰« $s.condition)
    have Hâ‚‚ : g â‰« s.inl = s.inr := by simpa using congr(coprod.inr â‰« $s.condition)
    exact Hâ‚.trans Hâ‚‚.symm
  Â· exact fun s â†¦ by simp
  Â· exact fun s â†¦ by simpa using congr(coprod.inl â‰« $s.condition)
  Â· exact fun s m hm _ â†¦ by ext; simp [*]

section

variable [HasEqualizers C] [HasPullbacks C] {X Y S T : C}

set_option backward.isDefEq.respectTransparency false in
/-- The natural isomorphism `eq(f Ã—[S] T, g Ã—[S] T) â‰… eq(f, g) Ã—[S] T`. -/
noncomputable def equalizerPullbackMapIso {f g : X âŸ¶ Y} {s : X âŸ¶ S} {t : Y âŸ¶ S}
    (hf : f â‰« t = s) (hg : g â‰« t = s) (v : T âŸ¶ S) :
    equalizer
      (pullback.map s v t v f (ğŸ™ T) (ğŸ™ S) (by simp [hf]) (by simp))
      (pullback.map s v t v g (ğŸ™ T) (ğŸ™ S) (by simp [hg]) (by simp)) â‰…
    pullback (equalizer.Î¹ f g) (pullback.fst s v) :=
  letI lhs := pullback.map s v t v f (ğŸ™ T) (ğŸ™ S) (by simp [hf]) (by simp)
  letI rhs := pullback.map s v t v g (ğŸ™ T) (ğŸ™ S) (by simp [hg]) (by simp)
  haveI hl : pullback.fst s v â‰« f = lhs â‰« pullback.fst _ _ := by simp [lhs]
  haveI hr : pullback.fst s v â‰« g = rhs â‰« pullback.fst _ _ := by simp [rhs]
  letI e : equalizer lhs rhs â‰… pullback (equalizer.Î¹ f g) (pullback.fst s v) :=
    { hom := pullback.lift
        (equalizer.lift (equalizer.Î¹ _ _ â‰« pullback.fst _ _) (by
          simp [hl, hr, equalizer.condition_assoc lhs rhs]))
        (pullback.lift (equalizer.Î¹ _ _ â‰« pullback.fst _ _)
          (equalizer.Î¹ _ _ â‰« pullback.snd _ _) (by simp [pullback.condition]))
        (by simp)
      inv := equalizer.lift
        (pullback.map _ _ _ _ (equalizer.Î¹ _ _) (pullback.snd _ _) s rfl
          (by simp [pullback.condition]))
        (by ext <;> simp [lhs, rhs, equalizer.condition f g])
      hom_inv_id := by ext <;> simp
      inv_hom_id := by ext <;> simp [pullback.condition] }
  e

variable {f g : X âŸ¶ Y} {s : X âŸ¶ S} {t : Y âŸ¶ S} (hf : f â‰« t = s) (hg : g â‰« t = s) (v : T âŸ¶ S)

set_option backward.isDefEq.respectTransparency false in
@[reassoc (attr := simp)]
lemma equalizerPullbackMapIso_hom_fst :
    (equalizerPullbackMapIso hf hg v).hom â‰« pullback.fst _ _ â‰« equalizer.Î¹ _ _ =
      equalizer.Î¹ _ _ â‰« pullback.fst _ _ := by
  simp [equalizerPullbackMapIso]

set_option backward.isDefEq.respectTransparency false in
@[reassoc (attr := simp)]
lemma equalizerPullbackMapIso_hom_snd :
    (equalizerPullbackMapIso hf hg v).hom â‰« pullback.snd _ _ =
      equalizer.Î¹ _ _ := by
  ext <;> simp [equalizerPullbackMapIso]

set_option backward.isDefEq.respectTransparency false in
@[reassoc (attr := simp)]
lemma equalizerPullbackMapIso_inv_Î¹_fst :
    (equalizerPullbackMapIso hf hg v).inv â‰« equalizer.Î¹ _ _ â‰« pullback.fst _ _ =
      pullback.fst _ _ â‰« equalizer.Î¹ _ _ := by
  simp [equalizerPullbackMapIso]

set_option backward.isDefEq.respectTransparency false in
@[reassoc (attr := simp)]
lemma equalizerPullbackMapIso_inv_Î¹_snd :
    (equalizerPullbackMapIso hf hg v).inv â‰« equalizer.Î¹ _ _ â‰« pullback.snd _ _ =
      pullback.snd _ _ â‰« pullback.snd _ _ := by
  simp [equalizerPullbackMapIso]

end

end CategoryTheory.Limits
