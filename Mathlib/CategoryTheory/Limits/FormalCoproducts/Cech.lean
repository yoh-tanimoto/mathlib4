/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.AlgebraicTopology.SimplicialObject.Basic
public import Mathlib.CategoryTheory.Limits.FormalCoproducts.Basic

/-!
# The Cech object for formal coproducts

Let `C` be a category that has finite products. In this file, we define a
functor `cechFunctor : FormalCoproduct C ‚•§ SimplicialObject (FormalCoproduct C)`
which sends a formal coproduct of objects `U j` (for `j : Œπ`) to the simplicial object
which sends `‚¶ãn‚¶å` to the formal coproduct, indexed by `i : Fin (n + 1) ‚Üí Œπ`,
of the products of the objects `U (i a)` for all `a : Fin (n + 1)`.

-/

@[expose] public section

universe w t v u

namespace CategoryTheory.Limits.FormalCoproduct

variable {C : Type u} [Category.{v} C]

/-- Given `U : FormalCoproduct C` and a type `Œ±`, this is the formal coproduct
indexed by all `i : Œ± ‚Üí U.I` of the products of the objects `U.obj (i a)`
for all `a : Œ±`. -/
@[simps]
noncomputable def power (U : FormalCoproduct.{w} C) (Œ± : Type t)
    [HasProductsOfShape Œ± C] : FormalCoproduct.{max w t} C where
  I := Œ± ‚Üí U.I
  obj i := ‚àè·∂ú (U.obj ‚àò i)

/-- For any morphism `f : U ‚ü∂ V` in `FormalCoproduct C` and a type `Œ±`,
this is the induced map `U.power Œ± ‚ü∂ V.power Œ±`. -/
@[simps -fullyApplied]
noncomputable def powerMap {U V : FormalCoproduct.{w} C} (f : U ‚ü∂ V) (Œ± : Type t)
    [HasProductsOfShape Œ± C] :
    U.power Œ± ‚ü∂ V.power Œ± where
  f i := f.f ‚àò i
  œÜ i := Pi.map (fun a ‚Ü¶ f.œÜ (i a))

set_option backward.isDefEq.respectTransparency false in
@[simp]
lemma powerMap_id (U : FormalCoproduct.{w} C) (Œ± : Type t) [HasProductsOfShape Œ± C] :
    powerMap (ùüô U) Œ± = ùüô _ := by
  cat_disch

set_option backward.isDefEq.respectTransparency false in
@[reassoc]
lemma powerMap_comp {U V W : FormalCoproduct.{w} C} (f : U ‚ü∂ V) (g : V ‚ü∂ W) (Œ± : Type t)
    [HasProductsOfShape Œ± C] :
    powerMap (f ‚â´ g) Œ± = powerMap f Œ± ‚â´ powerMap g Œ± := by
  ext
  ¬∑ cat_disch
  ¬∑ dsimp
    ext
    simp only [Category.comp_id, Category.assoc, Pi.map_œÄ, Function.comp_apply,
      Pi.map_œÄ_assoc]
    apply Pi.map_œÄ

attribute [local simp] powerMap_comp

/-- Given a type `Œ±`, this is the functor `FormalCoproduct C ‚•§ FormalCoproduct C`
which sends `U` to `U.power Œ±`. -/
@[simps]
noncomputable def powerFunctor (Œ± : Type t) [HasProductsOfShape Œ± C] :
    FormalCoproduct.{w} C ‚•§ FormalCoproduct.{max w t} C where
  obj U := U.power Œ±
  map f := powerMap f Œ±

/-- The functoriality of `FormalCoproduct.power` with respect to the index type. -/
@[simps -fullyApplied]
noncomputable def mapPower (U : FormalCoproduct.{w} C) {Œ± Œ≤ : Type t}
    [HasProductsOfShape Œ± C] [HasProductsOfShape Œ≤ C] (f : Œ± ‚Üí Œ≤) :
    U.power Œ≤ ‚ü∂ U.power Œ± where
  f i := i ‚àò f
  œÜ _ := Pi.lift (fun _ ‚Ü¶ Pi.œÄ _ _)

set_option backward.isDefEq.respectTransparency false in
@[simp]
lemma mapPower_id (U : FormalCoproduct.{w} C) (Œ± : Type t)
    [HasProductsOfShape Œ± C] :
    U.mapPower (id : Œ± ‚Üí Œ±) = ùüô _ := by
  cat_disch

set_option backward.isDefEq.respectTransparency false in
@[reassoc]
lemma mapPower_comp (U : FormalCoproduct.{w} C) {Œ± Œ≤ Œ≥ : Type t}
    [HasProductsOfShape Œ± C] [HasProductsOfShape Œ≤ C] [HasProductsOfShape Œ≥ C]
    (f : Œ± ‚Üí Œ≤) (g : Œ≤ ‚Üí Œ≥) :
    U.mapPower (g ‚àò f) = U.mapPower g ‚â´ U.mapPower f := by
  ext
  ¬∑ cat_disch
  ¬∑ dsimp
    ext
    dsimp
    simp only [Category.comp_id, Category.assoc, Pi.lift_œÄ]
    apply Pi.lift_œÄ

set_option backward.isDefEq.respectTransparency false in
@[reassoc]
lemma mapPower_powerMap {U V : FormalCoproduct.{w} C} (f : U ‚ü∂ V)
    {Œ± Œ≤ : Type t} [HasProductsOfShape Œ± C] [HasProductsOfShape Œ≤ C] (g : Œ± ‚Üí Œ≤) :
    U.mapPower g ‚â´ powerMap f Œ± = powerMap f Œ≤ ‚â´ V.mapPower g := by
  ext
  ¬∑ cat_disch
  ¬∑ dsimp
    ext
    simp only [Function.comp_apply, limit.lift_map, Cones.postcompose, Fan.mk_pt, Category.comp_id,
      Category.assoc, limit.lift_œÄ, Fan.mk_œÄ_app, Pi.map_œÄ]
    apply limit.lift_œÄ

attribute [local simp] mapPower_comp mapPower_powerMap

set_option backward.isDefEq.respectTransparency false in
/-- The functor `(Type t)·µí·µñ ‚•§ FormalCoproduct.{w} C ‚•§ FormalCoproduct.{max w t} C`
which sends a type `Œ±` and `U : FormalCoproduct C` to `U.power Œ±`. -/
@[simps]
noncomputable def powerBifunctor [HasProducts.{t} C] :
    (Type t)·µí·µñ ‚•§ FormalCoproduct.{w} C ‚•§ FormalCoproduct.{max w t} C where
  obj Œ± := powerFunctor Œ±.unop
  map f := { app _ := mapPower _ f.unop }
  map_comp _ _ := by ext : 2; dsimp; apply mapPower_comp

variable [HasFiniteProducts C]

/-- Given `U : FormalCoproduct C`, this is the simplicial object
in `FormalCoproduct C` which sends `‚¶ãn‚¶å` to `U.power (Fin (n + 1))`. -/
@[simps]
noncomputable def cech (U : FormalCoproduct.{w} C) :
    SimplicialObject (FormalCoproduct.{w} C) where
  obj n := U.power (ToType n.unop)
  map f := U.mapPower f.unop.toOrderHom.toFun

/-- The functor `FormalCoproduct C ‚•§ SimplicialObject (FormalCoproduct C)`
which sends a formal coproduct to its Cech object. -/
@[simps]
noncomputable def cechFunctor :
    FormalCoproduct.{w} C ‚•§ SimplicialObject (FormalCoproduct.{w} C) where
  obj U := U.cech
  map f := { app _ := powerMap f _ }
  map_comp _ _ := by ext : 1; simp

end CategoryTheory.Limits.FormalCoproduct
