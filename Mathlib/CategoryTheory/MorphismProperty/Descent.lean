/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.CategoryTheory.MorphismProperty.Limits
public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.Equalizer

/-!
# Descent of morphism properties

Given morphism properties `P` and `Q` we say that `P` descends along `Q` (`P.DescendsAlong Q`),
if whenever `Q` holds for `X ‚ü∂ Z`, `P` holds for `X √ó[Z] Y ‚ü∂ X` implies `P` holds for `Y ‚ü∂ Z`.
Dually, we define `P.CodescendsAlong Q`.
-/

@[expose] public section

namespace CategoryTheory.MorphismProperty

open Limits

variable {C : Type*} [Category* C]

variable {P Q W : MorphismProperty C}

/-- `P` descends along `Q` if whenever `Q` holds for `X ‚ü∂ Z`,
`P` holds for `X √ó[Z] Y ‚ü∂ X` implies `P` holds for `Y ‚ü∂ Z`. -/
class DescendsAlong (P Q : MorphismProperty C) : Prop where
  of_isPullback {A X Y Z : C} {fst : A ‚ü∂ X} {snd : A ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} :
    IsPullback fst snd f g ‚Üí Q f ‚Üí P fst ‚Üí P g

section DescendsAlong

variable {A X Y Z : C} {fst : A ‚ü∂ X} {snd : A ‚ü∂ Y} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z}

lemma of_isPullback_of_descendsAlong [P.DescendsAlong Q] (h : IsPullback fst snd f g)
    (hf : Q f) (hfst : P fst) : P g :=
  DescendsAlong.of_isPullback h hf hfst

lemma iff_of_isPullback [P.IsStableUnderBaseChange] [P.DescendsAlong Q] (h : IsPullback fst snd f g)
    (hf : Q f) : P fst ‚Üî P g :=
  ‚ü®fun hfst ‚Ü¶ of_isPullback_of_descendsAlong h hf hfst, fun hf ‚Ü¶ P.of_isPullback h.flip hf‚ü©

lemma of_pullback_fst_of_descendsAlong [P.DescendsAlong Q] [HasPullback f g] (hf : Q f)
    (hfst : P (pullback.fst f g)) : P g :=
  of_isPullback_of_descendsAlong (.of_hasPullback f g) hf hfst

lemma pullback_fst_iff [P.IsStableUnderBaseChange] [P.DescendsAlong Q] [HasPullback f g]
    (hf : Q f) : P (pullback.fst f g) ‚Üî P g :=
  iff_of_isPullback (.of_hasPullback f g) hf

lemma of_pullback_snd_of_descendsAlong [P.DescendsAlong Q] [HasPullback f g] (hg : Q g)
    (hsnd : P (pullback.snd f g)) : P f :=
  of_isPullback_of_descendsAlong (IsPullback.of_hasPullback f g).flip hg hsnd

lemma pullback_snd_iff [P.IsStableUnderBaseChange] [P.DescendsAlong Q] [HasPullback f g]
    (hg : Q g) : P (pullback.snd f g) ‚Üî P f :=
  iff_of_isPullback (IsPullback.of_hasPullback f g).flip hg

instance DescendsAlong.top : (‚ä§ : MorphismProperty C).DescendsAlong Q where
  of_isPullback _ _ _ := trivial

instance DescendsAlong.inf [P.DescendsAlong Q] [W.DescendsAlong Q] : (P ‚äì W).DescendsAlong Q where
  of_isPullback h hg hfst :=
    ‚ü®DescendsAlong.of_isPullback h hg hfst.1, DescendsAlong.of_isPullback h hg hfst.2‚ü©

lemma DescendsAlong.of_le [P.DescendsAlong Q] (hle : W ‚â§ Q) : P.DescendsAlong W where
  of_isPullback h hg hfst := DescendsAlong.of_isPullback h (hle _ hg) hfst

/-- Alternative constructor for `CodescendsAlong` using `HasPullback`. -/
lemma DescendsAlong.mk' [P.RespectsIso]
    (H : ‚àÄ {X Y Z : C} {f : X ‚ü∂ Z} {g : Y ‚ü∂ Z} [HasPullback f g],
      Q f ‚Üí P (pullback.fst f g) ‚Üí P g) :
    P.DescendsAlong Q where
  of_isPullback {A X Y Z fst snd f g} h hf hfst := by
    have : HasPullback f g := h.hasPullback
    apply H hf
    rwa [‚Üê P.cancel_left_of_respectsIso h.isoPullback.hom, h.isoPullback_hom_fst]

instance [Q.IsStableUnderBaseChange] [P.HasOfPrecompProperty Q] [P.RespectsRight Q] :
    P.DescendsAlong Q where
  of_isPullback {A X Y Z fst snd f g} h hf hfst := by
    apply P.of_precomp (W' := Q) _ _ (Q.of_isPullback h hf)
    rw [‚Üê h.1.1]
    exact RespectsRight.postcomp _ hf _ hfst

set_option backward.isDefEq.respectTransparency false in
/-- If `P` descends along `Q`, then `P.diagonal` descends along `Q`. -/
instance [HasPullbacks C] (P Q : MorphismProperty C) [P.DescendsAlong Q] [P.RespectsIso]
    [Q.IsStableUnderBaseChange] :
    DescendsAlong (diagonal P) Q := by
  apply DescendsAlong.mk'
  introv hf hfst
  have heq : pullback.fst (pullback.fst (pullback.snd g g ‚â´ g) f) (pullback.diagonal g) =
      (pullbackSymmetry _ _).hom ‚â´
      (pullbackRightPullbackFstIso _ _ _).hom ‚â´
      (pullback.congrHom (by simp) rfl).hom ‚â´
      (pullbackSymmetry _ _).hom ‚â´
      pullback.diagonal (pullback.fst f g) ‚â´
      (diagonalObjPullbackFstIso f g).hom := by
    apply pullback.hom_ext
    apply pullback.hom_ext <;> simp [pullback.condition]
    simp [pullback.condition]
  rw [diagonal_iff]
  apply MorphismProperty.of_pullback_fst_of_descendsAlong (P := P) (Q := Q)
      (f := pullback.fst (pullback.snd g g ‚â´ g) f)
  ¬∑ exact MorphismProperty.pullback_fst _ _ hf
  ¬∑ rw [heq]
    iterate 4 rw [cancel_left_of_respectsIso (P := P)]
    rwa [cancel_right_of_respectsIso (P := P)]

lemma eq_of_isomorphisms_descendsAlong [(MorphismProperty.isomorphisms C).DescendsAlong P]
    [P.IsStableUnderBaseChange] [HasEqualizers C]
    [HasPullbacks C] {X Y S T : C} {f g : X ‚ü∂ Y} {s : X ‚ü∂ S} {t : Y ‚ü∂ S} (hf : f ‚â´ t = s)
    (hg : g ‚â´ t = s) (v : T ‚ü∂ S) (hv : P v)
    (H :
      pullback.map s v t v f (ùüô T) (ùüô S) (by simp [hf]) (by simp) =
        pullback.map s v t v g (ùüô T) (ùüô S) (by simp [hg]) (by simp)) :
    f = g := by
  suffices IsIso (equalizer.Œπ f g) from Limits.eq_of_epi_equalizer
  change MorphismProperty.isomorphisms C _
  apply (MorphismProperty.isomorphisms C).of_isPullback_of_descendsAlong
    (IsPullback.of_hasPullback _ _).flip (P.pullback_fst s v hv)
  have : pullback.snd (equalizer.Œπ f g) (pullback.fst s v) =
      (equalizerPullbackMapIso hf hg _).inv ‚â´ equalizer.Œπ _ _ := by
    ext <;> simp [pullback.condition]
  simpa [this] using equalizer.Œπ_of_eq H

set_option backward.isDefEq.respectTransparency false in
lemma faithful_overPullback_of_isomorphisms_descendAlong
    [(MorphismProperty.isomorphisms C).DescendsAlong P] [P.IsStableUnderBaseChange]
    [HasPullbacks C] [HasEqualizers C] {S T : C} {f : T ‚ü∂ S} (hf : P f) :
    (Over.pullback f).Faithful := by
  refine ‚ü®fun {X} Y a b hab ‚Ü¶ ?_‚ü©
  ext
  apply P.eq_of_isomorphisms_descendsAlong (Over.w a) (Over.w b) f hf
  convert congr($(hab).left) <;> ext <;> simp

end DescendsAlong

/-- `P` codescends along `Q` if whenever `Q` holds for `Z ‚ü∂ X`,
`P` holds for `X ‚ü∂ X ‚àê[Z] Y` implies `P` holds for `Z ‚ü∂ Y`. -/
class CodescendsAlong (P Q : MorphismProperty C) : Prop where
  of_isPushout {Z X Y A : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ A} {inr : Y ‚ü∂ A} :
    IsPushout f g inl inr ‚Üí Q f ‚Üí P inl ‚Üí P g

section CodescendsAlong

variable {Z X Y A : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} {inl : X ‚ü∂ A} {inr : Y ‚ü∂ A}

lemma of_isPushout_of_codescendsAlong [P.CodescendsAlong Q] (h : IsPushout f g inl inr)
    (hf : Q f) (hinl : P inl) : P g :=
  CodescendsAlong.of_isPushout h hf hinl

lemma iff_of_isPushout [P.IsStableUnderCobaseChange] [P.CodescendsAlong Q]
    (h : IsPushout f g inl inr) (hg : Q f) : P inl ‚Üî P g :=
  ‚ü®fun hinl ‚Ü¶ of_isPushout_of_codescendsAlong h hg hinl, fun hf ‚Ü¶ P.of_isPushout h hf‚ü©

lemma of_pushout_inl_of_codescendsAlong [P.CodescendsAlong Q] [HasPushout f g] (hf : Q f)
    (hinl : P (pushout.inl f g)) : P g :=
  of_isPushout_of_codescendsAlong (.of_hasPushout f g) hf hinl

lemma pushout_inl_iff [P.IsStableUnderCobaseChange] [P.CodescendsAlong Q] [HasPushout f g]
    (hf : Q f) : P (pushout.inl f g) ‚Üî P g :=
  iff_of_isPushout (.of_hasPushout f g) hf

lemma of_pushout_inr_of_descendsAlong [P.CodescendsAlong Q] [HasPushout f g] (hg : Q g)
    (hinr : P (pushout.inr f g)) : P f :=
  of_isPushout_of_codescendsAlong (IsPushout.of_hasPushout f g).flip hg hinr

lemma pushout_inr_iff [P.IsStableUnderCobaseChange] [P.CodescendsAlong Q] [HasPushout f g]
    (hg : Q g) : P (pushout.inr f g) ‚Üî P f :=
  iff_of_isPushout (IsPushout.of_hasPushout f g).flip hg

lemma CodescendsAlong.of_le [P.CodescendsAlong Q] (hle : W ‚â§ Q) : P.CodescendsAlong W where
  of_isPushout h hg hinl := CodescendsAlong.of_isPushout h (hle _ hg) hinl

instance CodescendsAlong.top : (‚ä§ : MorphismProperty C).CodescendsAlong Q where
  of_isPushout _ _ _ := trivial

instance CodescendsAlong.inf [P.CodescendsAlong Q] [W.CodescendsAlong Q] :
    (P ‚äì W).CodescendsAlong Q where
  of_isPushout h hg hfst :=
    ‚ü®CodescendsAlong.of_isPushout h hg hfst.1, CodescendsAlong.of_isPushout h hg hfst.2‚ü©

/-- Alternative constructor for `CodescendsAlong` using `HasPushout`. -/
lemma CodescendsAlong.mk' [P.RespectsIso]
    (H : ‚àÄ {X Y Z : C} {f : Z ‚ü∂ X} {g : Z ‚ü∂ Y} [HasPushout f g], Q f ‚Üí P (pushout.inl f g) ‚Üí P g) :
    P.CodescendsAlong Q where
  of_isPushout {A X Y Z f g inl inr} h hf hfst := by
    have : HasPushout f g := h.hasPushout
    apply H hf
    rwa [‚Üê P.cancel_right_of_respectsIso _ h.isoPushout.inv, h.inl_isoPushout_inv]

instance [Q.IsStableUnderCobaseChange] [P.HasOfPostcompProperty Q] [P.RespectsLeft Q] :
    P.CodescendsAlong Q where
  of_isPushout {X Y Z A f g inl inr} h hf hinl := by
    apply P.of_postcomp (W' := Q) g inr (Q.of_isPushout h.flip hf)
    rw [‚Üê h.1.1]
    exact RespectsLeft.precomp _ hf _ hinl

end CodescendsAlong

end CategoryTheory.MorphismProperty
