/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou
-/
module

public import Mathlib.CategoryTheory.Sites.DenseSubsite.Basic

/-!
# Equivalence of categories of sheaves with a dense subsite that is 1-hypercover dense

Let `F : C‚ÇÄ ‚•§ C` be a functor equipped with Grothendieck topologies `J‚ÇÄ` and `J`.
Assume that `F` is a dense subsite. We introduce a typeclass
`IsOneHypercoverDense.{w} F J‚ÇÄ J` which roughly says that objects in `C`
admit a `1`-hypercover consisting of objects in `C‚ÇÄ`.

-/

@[expose] public section

universe w v‚ÇÄ v v' u‚ÇÄ u u'

namespace CategoryTheory

open Category Limits Opposite

variable {C‚ÇÄ : Type u‚ÇÄ} {C : Type u} [Category.{v‚ÇÄ} C‚ÇÄ] [Category.{v} C]

namespace Functor

variable (F : C‚ÇÄ ‚•§ C) (J‚ÇÄ : GrothendieckTopology C‚ÇÄ)
  (J : GrothendieckTopology C) {A : Type u'} [Category.{v'} A]

/-- Given a functor `F : C‚ÇÄ ‚•§ C` and an object `S : C`, this structure roughly
contains the data of a pre-`1`-hypercover of `S` consisting of objects in `C‚ÇÄ`. -/
structure PreOneHypercoverDenseData (S : C) where
  /-- the index type of the covering of `S` -/
  I‚ÇÄ : Type w
  /-- the objects in the covering of `S` -/
  X (i : I‚ÇÄ) : C‚ÇÄ
  /-- the morphisms in the covering of `S` -/
  f (i : I‚ÇÄ) : F.obj (X i) ‚ü∂ S
  /-- the index type of the coverings of the fibre products -/
  I‚ÇÅ (i‚ÇÅ i‚ÇÇ : I‚ÇÄ) : Type w
  /-- the objects in the coverings of the fibre products -/
  Y ‚¶Éi‚ÇÅ i‚ÇÇ : I‚ÇÄ‚¶Ñ (j : I‚ÇÅ i‚ÇÅ i‚ÇÇ) : C‚ÇÄ
  /-- the first projection `Y j ‚ü∂ X i‚ÇÅ` -/
  p‚ÇÅ ‚¶Éi‚ÇÅ i‚ÇÇ : I‚ÇÄ‚¶Ñ (j : I‚ÇÅ i‚ÇÅ i‚ÇÇ) : Y j ‚ü∂ X i‚ÇÅ
  /-- the second projection `Y j ‚ü∂ X i‚ÇÇ` -/
  p‚ÇÇ ‚¶Éi‚ÇÅ i‚ÇÇ : I‚ÇÄ‚¶Ñ (j : I‚ÇÅ i‚ÇÅ i‚ÇÇ) : Y j ‚ü∂ X i‚ÇÇ
  w ‚¶Éi‚ÇÅ i‚ÇÇ : I‚ÇÄ‚¶Ñ (j : I‚ÇÅ i‚ÇÅ i‚ÇÇ) : F.map (p‚ÇÅ j) ‚â´ f i‚ÇÅ = F.map (p‚ÇÇ j) ‚â´ f i‚ÇÇ

namespace PreOneHypercoverDenseData

attribute [reassoc] w

variable {F} {X : C} (data : PreOneHypercoverDenseData.{w} F X)

/-- The pre-`1`-hypercover induced by a `PreOneHypercoverDenseData` structure. -/
@[simps]
def toPreOneHypercover : PreOneHypercover X where
  I‚ÇÄ := data.I‚ÇÄ
  X i := F.obj (data.X i)
  f i := data.f i
  I‚ÇÅ := data.I‚ÇÅ
  Y _ _ j := F.obj (data.Y j)
  p‚ÇÅ _ _ j := F.map (data.p‚ÇÅ j)
  p‚ÇÇ _ _ j := F.map (data.p‚ÇÇ j)
  w := data.w

/-- The sigma type of all `data.I‚ÇÅ i‚ÇÅ i‚ÇÇ` for `‚ü®i‚ÇÅ, i‚ÇÇ‚ü© : data.I‚ÇÄ √ó data.I‚ÇÄ`. -/
abbrev I‚ÇÅ' : Type w := Sigma (fun (i : data.I‚ÇÄ √ó data.I‚ÇÄ) ‚Ü¶ data.I‚ÇÅ i.1 i.2)

/-- The shape of the multiforks attached to `data : F.PreOneHypercoverDenseData X`. -/
@[simps]
def multicospanShape : MulticospanShape where
  L := data.I‚ÇÄ
  R := data.I‚ÇÅ'
  fst j := j.1.1
  snd j := j.1.2

/-- The diagram of the multiforks attached to `data : F.PreOneHypercoverDenseData X`. -/
@[simps]
def multicospanIndex (P : C‚ÇÄ·µí·µñ ‚•§ A) : MulticospanIndex data.multicospanShape A where
  left i := P.obj (Opposite.op (data.X i))
  right j := P.obj (Opposite.op (data.Y j.2))
  fst j := P.map ((data.p‚ÇÅ j.2).op)
  snd j := P.map ((data.p‚ÇÇ j.2).op)

/-- The functoriality of the diagrams attached to `data : F.PreOneHypercoverDenseData X`
with respect to morphisms in `C‚ÇÄ·µí·µñ ‚•§ A`. -/
@[simps]
def multicospanMap {P Q : C‚ÇÄ·µí·µñ ‚•§ A} (f : P ‚ü∂ Q) :
    (data.multicospanIndex P).multicospan ‚ü∂ (data.multicospanIndex Q).multicospan where
  app x := match x with
    | WalkingMulticospan.left i => f.app _
    | WalkingMulticospan.right j => f.app _
  naturality := by
    rintro (i‚ÇÅ|j‚ÇÅ) (i‚ÇÇ|j‚ÇÇ) (_|_)
    all_goals simp

/-- The natural isomorphism between the diagrams attached to `data : F.PreOneHypercoverDenseData X`
that are induced by isomorphisms in `C‚ÇÄ·µí·µñ ‚•§ A`. -/
@[simps]
def multicospanMapIso {P Q : C‚ÇÄ·µí·µñ ‚•§ A} (e : P ‚âÖ Q) :
    (data.multicospanIndex P).multicospan ‚âÖ (data.multicospanIndex Q).multicospan where
  hom := data.multicospanMap e.hom
  inv := data.multicospanMap e.inv

/-- Given `data : F.PreOneHypercoverDenseData X`, an object `W‚ÇÄ : C‚ÇÄ` and two
morphisms `p‚ÇÅ : W‚ÇÄ ‚ü∂ data.X i‚ÇÅ` and `p‚ÇÇ : W‚ÇÄ ‚ü∂ data.X i‚ÇÇ`, this is the sieve of `W‚ÇÄ`
consisting of morphisms `g : Z‚ÇÄ ‚ü∂ W‚ÇÄ` such that there exists a morphism `Z‚ÇÄ ‚ü∂ data.Y j`
such that `g ‚â´ p‚ÇÅ = h ‚â´ data.p‚ÇÅ j` and `g ‚â´ p‚ÇÇ = h ‚â´ data.p‚ÇÇ j`. -/
@[simps]
def sieve‚ÇÅ‚ÇÄ {i‚ÇÅ i‚ÇÇ : data.I‚ÇÄ} {W‚ÇÄ : C‚ÇÄ} (p‚ÇÅ : W‚ÇÄ ‚ü∂ data.X i‚ÇÅ) (p‚ÇÇ : W‚ÇÄ ‚ü∂ data.X i‚ÇÇ) :
    Sieve W‚ÇÄ where
  arrows Z‚ÇÄ g := ‚àÉ (j : data.I‚ÇÅ i‚ÇÅ i‚ÇÇ) (h : Z‚ÇÄ ‚ü∂ data.Y j),
    g ‚â´ p‚ÇÅ = h ‚â´ data.p‚ÇÅ j ‚àß g ‚â´ p‚ÇÇ = h ‚â´ data.p‚ÇÇ j
  downward_closed := by
    rintro Z Z' g ‚ü®j, h, fac‚ÇÅ, fac‚ÇÇ‚ü© œÜ
    exact ‚ü®j, œÜ ‚â´ h, by simpa using œÜ ‚â´= fac‚ÇÅ, by simpa using œÜ ‚â´= fac‚ÇÇ‚ü©

end PreOneHypercoverDenseData

/-- Given a functor `F : C‚ÇÄ ‚•§ C`, Grothendieck topologies `J‚ÇÄ` on `C‚ÇÄ` and `J`
on `C`, an object `S. : C`, this structure roughly contains the data of a `1`-hypercover
of `S` consisting of objects in `C‚ÇÄ`. -/
structure OneHypercoverDenseData (S : C) extends PreOneHypercoverDenseData.{w} F S where
  mem‚ÇÄ : toPreOneHypercoverDenseData.toPreOneHypercover.sieve‚ÇÄ ‚àà J S
  mem‚ÇÅ‚ÇÄ (i‚ÇÅ i‚ÇÇ : I‚ÇÄ) ‚¶ÉW‚ÇÄ : C‚ÇÄ‚¶Ñ (p‚ÇÅ : W‚ÇÄ ‚ü∂ X i‚ÇÅ) (p‚ÇÇ : W‚ÇÄ ‚ü∂ X i‚ÇÇ)
    (w : F.map p‚ÇÅ ‚â´ f i‚ÇÅ = F.map p‚ÇÇ ‚â´ f i‚ÇÇ) :
    toPreOneHypercoverDenseData.sieve‚ÇÅ‚ÇÄ p‚ÇÅ p‚ÇÇ ‚àà J‚ÇÄ W‚ÇÄ

/-- Given a functor `F : C‚ÇÄ ‚•§ C`, Grothendieck topologies `J‚ÇÄ` on `C‚ÇÄ`, this is
the property that any object in `C` has a `1`-hypercover consisting of objects in `C‚ÇÄ`. -/
class IsOneHypercoverDense : Prop where
  nonempty_oneHypercoverDenseData (X : C) :
    Nonempty (OneHypercoverDenseData.{w} F J‚ÇÄ J X)

section

variable [IsOneHypercoverDense.{w} F J‚ÇÄ J]

/-- A choice of a `OneHypercoverDenseData` structure when `F` is `1`-hypercover dense. -/
noncomputable def oneHypercoverDenseData (X : C) : F.OneHypercoverDenseData J‚ÇÄ J X :=
  (IsOneHypercoverDense.nonempty_oneHypercoverDenseData X).some

lemma isDenseSubsite_of_isOneHypercoverDense [F.IsLocallyFull J] [F.IsLocallyFaithful J]
    (h : ‚àÄ {X‚ÇÄ : C‚ÇÄ} {S‚ÇÄ : Sieve X‚ÇÄ},
      Sieve.functorPushforward F S‚ÇÄ ‚àà J.sieves (F.obj X‚ÇÄ) ‚Üî S‚ÇÄ ‚àà J‚ÇÄ.sieves X‚ÇÄ) :
    IsDenseSubsite J‚ÇÄ J F where
  isCoverDense' := ‚ü®fun X ‚Ü¶ by
    refine J.superset_covering ?_ (F.oneHypercoverDenseData J‚ÇÄ J X).mem‚ÇÄ
    rintro Y _ ‚ü®_, a, _, h, rfl‚ü©
    cases h
    exact ‚ü®{ fac := rfl, ..}‚ü©‚ü©
  functorPushforward_mem_iff := h

end

variable [IsDenseSubsite J‚ÇÄ J F]

variable {F J‚ÇÄ J} in
/-- Constructor for `IsOneHypercoverDense.{w} F J‚ÇÄ J` for a dense subsite
when the functor `F : C‚ÇÄ ‚•§ C` is fully faithful, `C` has pullbacks, and
any object in `C` admits a `w`-small covering family consisting of objects in `C‚ÇÄ`. -/
lemma IsOneHypercoverDense.of_hasPullbacks [HasPullbacks C] [F.Full] [F.Faithful]
    (hF : ‚àÄ (S : C), ‚àÉ (Œπ : Type w) (U : Œπ ‚Üí C‚ÇÄ) (f : ‚àÄ i, F.obj (U i) ‚ü∂ S),
      Sieve.ofArrows _ f ‚àà J S) :
    IsOneHypercoverDense.{w} F J‚ÇÄ J where
  nonempty_oneHypercoverDenseData S := by
    choose Œπ U f hf using hF
    exact ‚ü®{
      I‚ÇÄ := Œπ S
      X := U S
      f := f S
      I‚ÇÅ i j := Œπ (pullback (f _ i) (f _ j))
      Y i j := U (pullback (f _ i) (f _ j))
      p‚ÇÅ i j k := F.preimage (f _ k ‚â´ pullback.fst _ _)
      p‚ÇÇ i j k := F.preimage (f _ k ‚â´ pullback.snd _ _)
      w i j k := by simp [pullback.condition]
      mem‚ÇÄ := hf S
      mem‚ÇÅ‚ÇÄ i j W‚ÇÄ p‚ÇÅ p‚ÇÇ hp := by
        have := IsDenseSubsite.isCoverDense J‚ÇÄ J F
        rw [‚Üê functorPushforward_mem_iff J‚ÇÄ J F]
        refine J.superset_covering ?_
          (IsCoverDense.functorPullback_pushforward_covering
            ‚ü®_, J.pullback_stable (pullback.lift _ _ hp) (hf (pullback (f _ i) (f _ j)))‚ü©)
        rintro T _ ‚ü®Z, q, r, ‚ü®_, s, _, ‚ü®k‚ü©, fac‚ü©, rfl‚ü©
        have fac‚ÇÅ := fac =‚â´ pullback.fst _ _
        have fac‚ÇÇ := fac =‚â´ pullback.snd _ _
        simp only [Category.assoc, pullback.lift_fst, pullback.lift_snd] at fac‚ÇÅ fac‚ÇÇ
        exact ‚ü®Z, q, r, ‚ü®k, F.preimage s, F.map_injective (by simp [fac‚ÇÅ]),
          F.map_injective (by simp [fac‚ÇÇ])‚ü©, rfl‚ü© }‚ü©

namespace OneHypercoverDenseData

variable {F J‚ÇÄ J}

section

variable {X : C} (data : OneHypercoverDenseData.{w} F J‚ÇÄ J X)

set_option backward.isDefEq.respectTransparency false in
lemma mem‚ÇÅ (i‚ÇÅ i‚ÇÇ : data.I‚ÇÄ) {W : C} (p‚ÇÅ : W ‚ü∂ F.obj (data.X i‚ÇÅ)) (p‚ÇÇ : W ‚ü∂ F.obj (data.X i‚ÇÇ))
    (w : p‚ÇÅ ‚â´ data.f i‚ÇÅ = p‚ÇÇ ‚â´ data.f i‚ÇÇ) : data.toPreOneHypercover.sieve‚ÇÅ p‚ÇÅ p‚ÇÇ ‚àà J W := by
  have := IsDenseSubsite.isCoverDense J‚ÇÄ J F
  let S := Sieve.bind (Sieve.coverByImage F W).arrows
    (fun Y f hf ‚Ü¶ ((F.imageSieve (hf.some.map ‚â´ p‚ÇÅ) ‚äì
        F.imageSieve (hf.some.map ‚â´ p‚ÇÇ)).functorPushforward F).pullback hf.some.lift)
  let T := Sieve.bind S.arrows (fun Z g hg ‚Ü¶ by
    letI str := Presieve.getFunctorPushforwardStructure hg.bindStruct.hg
    exact Sieve.pullback str.lift
      (Sieve.functorPushforward F (data.sieve‚ÇÅ‚ÇÄ str.cover.1.choose str.cover.2.choose)))
  have hS : S ‚àà J W := by
    apply J.bind_covering
    ¬∑ apply is_cover_of_isCoverDense
    ¬∑ intro Y f hf
      apply J.pullback_stable
      rw [Functor.functorPushforward_mem_iff J‚ÇÄ]
      apply J‚ÇÄ.intersection_covering
      all_goals apply IsDenseSubsite.imageSieve_mem J‚ÇÄ J
  have hT : T ‚àà J W := J.bind_covering hS (fun Z g hg ‚Ü¶ by
    apply J.pullback_stable
    rw [Functor.functorPushforward_mem_iff J‚ÇÄ]
    let str := Presieve.getFunctorPushforwardStructure hg.bindStruct.hg
    apply data.mem‚ÇÅ‚ÇÄ
    simp only [str.cover.1.choose_spec, str.cover.2.choose_spec, assoc, w])
  refine J.superset_covering ?_ hT
  rintro U f ‚ü®V, a, b, hb, h, _, rfl‚ü©
  let str := Presieve.getFunctorPushforwardStructure hb.bindStruct.hg
  obtain ‚ü®W‚ÇÄ, c : _ ‚ü∂ _, d, ‚ü®j, e, h‚ÇÅ, h‚ÇÇ‚ü©, fac‚ü© := h
  dsimp
  refine ‚ü®j, d ‚â´ F.map e, ?_, ?_‚ü©
  ¬∑ rw [assoc, assoc, ‚Üê F.map_comp, ‚Üê h‚ÇÅ, F.map_comp, ‚Üê reassoc_of% fac,
      str.cover.1.choose_spec, ‚Üê reassoc_of% str.fac,
      Presieve.CoverByImageStructure.fac_assoc,
      Presieve.BindStruct.fac_assoc]
  ¬∑ rw [assoc, assoc, ‚Üê F.map_comp, ‚Üê h‚ÇÇ, F.map_comp, ‚Üê reassoc_of% fac,
      str.cover.2.choose_spec, ‚Üê reassoc_of% str.fac,
      Presieve.CoverByImageStructure.fac_assoc,
      Presieve.BindStruct.fac_assoc]

/-- The `1`-hypercover associated to a `OneHypercoverDenseData` structure. -/
@[simps toPreOneHypercover]
def toOneHypercover {X : C} (data : F.OneHypercoverDenseData J‚ÇÄ J X) :
    J.OneHypercover X where
  toPreOneHypercover := data.toPreOneHypercover
  mem‚ÇÄ := data.mem‚ÇÄ
  mem‚ÇÅ := data.mem‚ÇÅ

variable {X : C} (data : OneHypercoverDenseData.{w} F J‚ÇÄ J X) {X‚ÇÄ : C‚ÇÄ} (f : F.obj X‚ÇÄ ‚ü∂ X)

/-- Auxiliary structure for the definition `OneHypercoverDenseData.sieve`. -/
structure SieveStruct {Y‚ÇÄ : C‚ÇÄ} (g : Y‚ÇÄ ‚ü∂ X‚ÇÄ) where
  /-- the index of the intermediate object -/
  i‚ÇÄ : data.I‚ÇÄ
  /-- the morphism that is part of the factorization `fac`. -/
  q : F.obj Y‚ÇÄ ‚ü∂ F.obj (data.X i‚ÇÄ)
  fac : q ‚â´ data.f i‚ÇÄ = F.map g ‚â´ f := by simp

attribute [reassoc (attr := simp)] SieveStruct.fac

/-- Given `data : OneHypercoverDenseData F J‚ÇÄ J X` and a morphism `f : F.obj X‚ÇÄ ‚ü∂ X`,
this is the sieve of `X‚ÇÄ` consisting of morphisms `g : Y‚ÇÄ ‚ü∂ X‚ÇÄ` such that there
exists `i‚ÇÄ : data.I‚ÇÄ`, `q : F.obj Y‚ÇÄ ‚ü∂ F.obj (data.X i‚ÇÄ)` such that
we have a factorization `q ‚â´ data.f i‚ÇÄ = F.map g ‚â´ f`. -/
@[simps]
def sieve : Sieve X‚ÇÄ where
  arrows Y‚ÇÄ g := Nonempty (SieveStruct data f g)
  downward_closed := by
    rintro Y‚ÇÄ Z‚ÇÄ g ‚ü®h‚ü© p
    exact ‚ü®{ i‚ÇÄ := h.i‚ÇÄ, q := F.map p ‚â´ h.q, fac := by rw [assoc, h.fac, map_comp_assoc]}‚ü©

set_option backward.isDefEq.respectTransparency false in
lemma sieve_mem : sieve data f ‚àà J‚ÇÄ X‚ÇÄ := by
  have := IsDenseSubsite.isCoverDense J‚ÇÄ J F
  have := IsDenseSubsite.isLocallyFull J‚ÇÄ J F
  rw [‚Üê functorPushforward_mem_iff J‚ÇÄ J F]
  let R : ‚¶ÉW : C‚¶Ñ ‚Üí ‚¶Ép : W ‚ü∂ F.obj X‚ÇÄ‚¶Ñ ‚Üí
    (Sieve.pullback f data.toOneHypercover.sieve‚ÇÄ).arrows p ‚Üí Sieve W := fun W p hp ‚Ü¶
      Sieve.bind (Sieve.coverByImage F W).arrows (fun U œÄ hœÄ ‚Ü¶
        Sieve.pullback hœÄ.some.lift
          (Sieve.functorPushforward F (F.imageSieve (hœÄ.some.map ‚â´ p))))
  refine J.superset_covering ?_
    (J.bind_covering (J.pullback_stable f (data.toOneHypercover.mem‚ÇÄ)) (R := R)
    (fun W p hp ‚Ü¶ J.bind_covering (F.is_cover_of_isCoverDense J W) ?_))
  ¬∑ rintro W' _ ‚ü®W, _, p, hp, ‚ü®Y‚ÇÄ, a, b, hb, ‚ü®U, c, d, ‚ü®x‚ÇÅ, w‚ÇÅ‚ü©, fac‚ü©, rfl‚ü©, rfl‚ü©
    have hp' := Sieve.ofArrows.fac hp
    dsimp at hp'
    refine ‚ü®U, x‚ÇÅ, d, ‚ü®Sieve.ofArrows.i hp,
      F.map c ‚â´ (Nonempty.some hb).map ‚â´ Sieve.ofArrows.h hp, ?_‚ü©, ?_‚ü©
    ¬∑ rw [w‚ÇÅ, assoc, assoc, assoc, assoc, hp']
    ¬∑ rw [w‚ÇÅ, assoc, ‚Üê reassoc_of% fac, hb.some.fac_assoc]
  ¬∑ intro U œÄ hœÄ
    apply J.pullback_stable
    apply functorPushforward_imageSieve_mem

end

section

namespace isSheaf_iff

variable {data : ‚àÄ X, F.OneHypercoverDenseData J‚ÇÄ J X} {G : C·µí·µñ ‚•§ A}
  (hG‚ÇÄ : Presheaf.IsSheaf J‚ÇÄ (F.op ‚ãô G))
  (hG : ‚àÄ (X : C), IsLimit ((data X).toOneHypercover.multifork G))
  {X : C} (S : J.Cover X)

section

variable {S} (s : Multifork (S.index G))

/-- Auxiliary definition for `lift`. -/
noncomputable def liftAux (i : (data X).I‚ÇÄ) : s.pt ‚ü∂ G.obj (op (F.obj ((data X).X i))) :=
  hG‚ÇÄ.amalgamate ‚ü®_, cover_lift F J‚ÇÄ _ (J.pullback_stable ((data X).f i) S.2)‚ü©
    (fun ‚ü®W‚ÇÄ, a, ha‚ü© ‚Ü¶ s.Œπ ‚ü®_, F.map a ‚â´ (data X).f i, ha‚ü©) (by
      rintro ‚ü®W‚ÇÄ, a, ha‚ü© ‚ü®Z‚ÇÄ, b, hb‚ü© ‚ü®U‚ÇÄ, p‚ÇÅ, p‚ÇÇ, fac‚ü©
      exact s.condition
        { fst := ‚ü®_, _, ha‚ü©
          snd := ‚ü®_, _, hb‚ü©
          r := ‚ü®_, F.map p‚ÇÅ, F.map p‚ÇÇ, by
              simp only [‚Üê Functor.map_comp_assoc, fac]‚ü© })

lemma liftAux_fac {i : (data X).I‚ÇÄ} {W‚ÇÄ : C‚ÇÄ} (a : W‚ÇÄ ‚ü∂ (data X).X i)
    (ha : S (F.map a ‚â´ (data X).f i)) :
    liftAux hG‚ÇÄ s i ‚â´ G.map (F.map a).op = s.Œπ ‚ü®_, F.map a ‚â´ (data X).f i, ha‚ü© :=
  hG‚ÇÄ.amalgamate_map _ _ _ ‚ü®W‚ÇÄ, a, ha‚ü©

/-- Auxiliary definition for the lemma `OneHypercoverDenseData.isSheaf_iff`. -/
noncomputable def lift : s.pt ‚ü∂ G.obj (op X) :=
  Multifork.IsLimit.lift (hG X) (fun i ‚Ü¶ liftAux hG‚ÇÄ s i) (by
    rintro ‚ü®‚ü®i‚ÇÅ, i‚ÇÇ‚ü©, j‚ü©
    dsimp at i‚ÇÅ i‚ÇÇ j ‚ä¢
    refine Presheaf.IsSheaf.hom_ext
      hG‚ÇÄ ‚ü®_, cover_lift F J‚ÇÄ _
        (J.pullback_stable (F.map ((data X).p‚ÇÅ j) ‚â´ (data X).f i‚ÇÅ) S.2)‚ü© _ _ ?_
    rintro ‚ü®W‚ÇÄ, a, ha‚ü©
    dsimp
    simp only [assoc, ‚Üê Functor.map_comp, ‚Üê op_comp]
    have ha‚ÇÅ : S (F.map (a ‚â´ (data X).p‚ÇÅ j) ‚â´ (data X).f i‚ÇÅ) := by simpa using ha
    have ha‚ÇÇ : S (F.map (a ‚â´ (data X).p‚ÇÇ j) ‚â´ (data X).f i‚ÇÇ) := by
      rwa [Functor.map_comp_assoc, ‚Üê (data X).w j]
    rw [liftAux_fac _ _ _ ha‚ÇÅ, liftAux_fac _ _ _ ha‚ÇÇ]
    congr 2
    rw [map_comp_assoc, map_comp_assoc, (data X).w j])

@[reassoc]
lemma lift_map (i : (data X).I‚ÇÄ) :
    lift hG‚ÇÄ hG s ‚â´ G.map ((data X).f i).op = liftAux hG‚ÇÄ s i :=
  Multifork.IsLimit.fac _ _ _ _

set_option backward.isDefEq.respectTransparency false in
@[reassoc]
lemma fac (a : S.Arrow) :
    lift hG‚ÇÄ hG s ‚â´ G.map a.f.op = s.Œπ a :=
  Multifork.IsLimit.hom_ext (hG _) (fun i ‚Ü¶
    Presheaf.IsSheaf.hom_ext hG‚ÇÄ
      ‚ü®_, cover_lift F J‚ÇÄ _
        (J.pullback_stable ((data a.Y).f i ‚â´ a.f) (data X).mem‚ÇÄ)‚ü© _ _ (by
        rintro ‚ü®X‚ÇÄ, b, ‚ü®_, c, _, h, fac‚ÇÅ‚ü©‚ü©
        obtain ‚ü®j‚ü© := h
        refine Presheaf.IsSheaf.hom_ext hG‚ÇÄ
          ‚ü®_, IsDenseSubsite.imageSieve_mem J‚ÇÄ J F c‚ü© _ _ ?_
        rintro ‚ü®Y‚ÇÄ, d, e, fac‚ÇÇ‚ü©
        dsimp at i j c fac‚ÇÅ ‚ä¢
        have he : S (F.map e ‚â´ (data X).f j) := by
          rw [fac‚ÇÇ, assoc, fac‚ÇÅ]
          simpa only [assoc] using S.1.downward_closed a.hf (F.map d ‚â´ F.map b ‚â´ (data a.Y).f i)
        simp only [assoc, ‚Üê Functor.map_comp, ‚Üê op_comp, ‚Üê fac‚ÇÅ]
        conv_lhs => simp only [op_comp, Functor.map_comp, assoc, lift_map_assoc]
        rw [‚Üê Functor.map_comp, ‚Üê op_comp, ‚Üê fac‚ÇÇ, liftAux_fac _ _ _ he]
        simpa using s.condition
          { fst := { hf := he, .. }
            snd := a
            r := ‚ü®_, ùüô _, F.map d ‚â´ F.map b ‚â´ (data a.Y).f i, by
              simp only [fac‚ÇÅ, fac‚ÇÇ, assoc, id_comp]‚ü© }))

set_option backward.isDefEq.respectTransparency false in
variable {s} in
include hG hG‚ÇÄ in
lemma hom_ext {f‚ÇÅ f‚ÇÇ : s.pt ‚ü∂ G.obj (op X)}
    (h : ‚àÄ (a : S.Arrow), f‚ÇÅ ‚â´ G.map a.f.op = f‚ÇÇ ‚â´ G.map a.f.op) : f‚ÇÅ = f‚ÇÇ :=
  Multifork.IsLimit.hom_ext (hG X) (fun i ‚Ü¶ by
    refine Presheaf.IsSheaf.hom_ext hG‚ÇÄ
      ‚ü®_, cover_lift F J‚ÇÄ _ (J.pullback_stable ((data X).f i) S.2)‚ü© _ _ ?_
    rintro ‚ü®X‚ÇÄ, a, ha‚ü©
    dsimp
    simp only [assoc, ‚Üê Functor.map_comp, ‚Üê op_comp]
    exact h ‚ü®_, _, ha‚ü©)

end

/-- Auxiliary definition for the lemma `OneHypercoverDenseData.isSheaf_iff`. -/
noncomputable def isLimit : IsLimit (S.multifork G) :=
  Multifork.IsLimit.mk _
    (lift hG‚ÇÄ hG ) (fac hG‚ÇÄ hG) (fun s _ hm ‚Ü¶
      hom_ext hG‚ÇÄ hG (fun a ‚Ü¶ (hm a).trans (fac hG‚ÇÄ hG s a).symm))

end isSheaf_iff

/-- Let `F : C‚ÇÄ ‚•§ C` be a dense subsite, and assume we have a family
`data : ‚àÄ X, F.OneHypercoverDenseData J‚ÇÄ J X`.
This lemma shows that `G : C·µí·µñ ‚•§ A` is a sheaf iff `F.op F.op ‚ãô G : C‚ÇÄ·µí·µñ ‚•§ A`
is a sheaf and for any `X : C`, the multifork for `G` and the `1`-hypercover
given by `data X` is a limit. -/
lemma isSheaf_iff (data : ‚àÄ X, F.OneHypercoverDenseData J‚ÇÄ J X) (G : C·µí·µñ ‚•§ A) :
    Presheaf.IsSheaf J G ‚Üî
      Presheaf.IsSheaf J‚ÇÄ (F.op ‚ãô G) ‚àß
        ‚àÄ (X : C), Nonempty (IsLimit ((data X).toOneHypercover.multifork G)) := by
  refine ‚ü®fun hG ‚Ü¶ ‚ü®op_comp_isSheaf F J‚ÇÄ J ‚ü®_, hG‚ü©,
    fun X ‚Ü¶ ‚ü®(data X).toOneHypercover.isLimitMultifork ‚ü®G, hG‚ü©‚ü©‚ü©, fun ‚ü®hG‚ÇÄ, hG‚ü© ‚Ü¶ ?_‚ü©
  rw [Presheaf.isSheaf_iff_multifork]
  replace hG := fun X ‚Ü¶ (hG X).some
  exact fun X S ‚Ü¶ ‚ü®isSheaf_iff.isLimit hG‚ÇÄ hG S‚ü©

end

section

variable (data : ‚àÄ X, OneHypercoverDenseData.{w} F J‚ÇÄ J X)
  [HasLimitsOfSize.{w, w} A]

namespace essSurj

variable (G‚ÇÄ : Sheaf J‚ÇÄ A)

/-- Given a dense subsite `F : C‚ÇÄ ‚•§ C` and a family
`data : ‚àÄ X, OneHypercoverDenseData F J‚ÇÄ J X` and a sheaf `G‚ÇÄ` on `J‚ÇÄ`,
this is the value on an object `X : C` of the "extension" of `G‚ÇÄ`
as a sheaf on `J` (see `OneHypercoverDenseData.essSurj.presheaf` for the
construction of this extension as a presheaf): it is defined as
a multiequalizer using `data X`. -/
noncomputable def presheafObj (X : C) : A :=
  multiequalizer ((data X).multicospanIndex G‚ÇÄ.val)

/-- The projection `presheafObj data G‚ÇÄ X ‚ü∂ G‚ÇÄ.val.obj (op ((data X).X i))`. -/
noncomputable def presheafObjœÄ (X : C) (i : (data X).I‚ÇÄ) :
    presheafObj data G‚ÇÄ X ‚ü∂ G‚ÇÄ.val.obj (op ((data X).X i)) :=
  Multiequalizer.Œπ ((data X).multicospanIndex G‚ÇÄ.val) i

omit [IsDenseSubsite J‚ÇÄ J F] in
variable {data G‚ÇÄ} in
@[ext]
lemma presheafObj_hom_ext {X : C} {Z : A} {f g : Z ‚ü∂ presheafObj data G‚ÇÄ X}
    (h : ‚àÄ (i : (data X).I‚ÇÄ), f ‚â´ presheafObjœÄ data G‚ÇÄ X i = g ‚â´ presheafObjœÄ data G‚ÇÄ X i) :
    f = g :=
  Multiequalizer.hom_ext _ _ _ h

omit [IsDenseSubsite J‚ÇÄ J F] in
@[reassoc]
lemma presheafObj_condition (X : C) (i i' : (data X).I‚ÇÄ) (j : (data X).I‚ÇÅ i i') :
    presheafObjœÄ data G‚ÇÄ X i ‚â´ G‚ÇÄ.val.map ((data X).p‚ÇÅ j).op =
    presheafObjœÄ data G‚ÇÄ X i' ‚â´ G‚ÇÄ.val.map ((data X).p‚ÇÇ j).op :=
  Multiequalizer.condition ((data X).multicospanIndex G‚ÇÄ.val) ‚ü®‚ü®i, i'‚ü©, j‚ü©

lemma presheafObj_mapPreimage_condition
    (X : C) (i‚ÇÅ i‚ÇÇ : (data X).I‚ÇÄ) {Y‚ÇÄ : C‚ÇÄ}
    (p‚ÇÅ : F.obj Y‚ÇÄ ‚ü∂ F.obj ((data X).X i‚ÇÅ)) (p‚ÇÇ : F.obj Y‚ÇÄ ‚ü∂ F.obj ((data X).X i‚ÇÇ))
    (fac : p‚ÇÅ ‚â´ (data X).f i‚ÇÅ = p‚ÇÇ ‚â´ (data X).f i‚ÇÇ) :
    presheafObjœÄ data G‚ÇÄ X i‚ÇÅ ‚â´ IsDenseSubsite.mapPreimage J F G‚ÇÄ p‚ÇÅ =
      presheafObjœÄ data G‚ÇÄ X i‚ÇÇ ‚â´ IsDenseSubsite.mapPreimage J F G‚ÇÄ p‚ÇÇ := by
  refine Presheaf.IsSheaf.hom_ext G‚ÇÄ.cond ‚ü®_,
    J‚ÇÄ.intersection_covering (IsDenseSubsite.imageSieve_mem J‚ÇÄ J F p‚ÇÅ)
      (IsDenseSubsite.imageSieve_mem J‚ÇÄ J F p‚ÇÇ)‚ü© _ _ ?_
  intro ‚ü®W‚ÇÄ, a, ‚ü®b‚ÇÅ, h‚ÇÅ‚ü©, ‚ü®b‚ÇÇ, h‚ÇÇ‚ü©‚ü©
  refine Presheaf.IsSheaf.hom_ext G‚ÇÄ.cond
    ‚ü®_, (data X).mem‚ÇÅ‚ÇÄ i‚ÇÅ i‚ÇÇ b‚ÇÅ b‚ÇÇ (by simp only [h‚ÇÅ, h‚ÇÇ, assoc, fac])‚ü© _ _ ?_
  intro ‚ü®U‚ÇÄ, c, ‚ü®j, t, fac‚ÇÅ, fac‚ÇÇ‚ü©‚ü©
  simp only [assoc, ‚Üê Functor.map_comp, ‚Üê op_comp,
    IsDenseSubsite.mapPreimage_map_of_fac J F G‚ÇÄ p‚ÇÅ (c ‚â´ a) (c ‚â´ b‚ÇÅ) (by simp [‚Üê h‚ÇÅ]),
    IsDenseSubsite.mapPreimage_map_of_fac J F G‚ÇÄ p‚ÇÇ (c ‚â´ a) (c ‚â´ b‚ÇÇ) (by simp [‚Üê h‚ÇÇ])]
  simpa [fac‚ÇÅ, fac‚ÇÇ] using presheafObj_condition_assoc _ _ _ _ _ _ _

/-- The (limit) multifork with point `presheafObjœÄ data G‚ÇÄ X` for
the diagram given by `G‚ÇÄ` and `data X`. -/
noncomputable abbrev presheafObjMultifork (X : C) :
    Multifork ((data X).multicospanIndex G‚ÇÄ.val) :=
  Multifork.ofŒπ _ (presheafObj data G‚ÇÄ X) (presheafObjœÄ data G‚ÇÄ X)
    (fun _ ‚Ü¶ presheafObj_condition _ _ _ _ _ _)

set_option backward.isDefEq.respectTransparency false in
/-- The multifork `presheafObjMultifork` is a limit. -/
noncomputable def presheafObjIsLimit (X : C) :
    IsLimit (presheafObjMultifork data G‚ÇÄ X) :=
  IsLimit.ofIsoLimit (limit.isLimit _) (Multifork.ext (Iso.refl _))

namespace restriction

/-- Auxiliary definition for `OneHypercoverDenseData.essSurj.restriction`. -/
noncomputable def res {X : C} {X‚ÇÄ Y‚ÇÄ : C‚ÇÄ} {f : F.obj X‚ÇÄ ‚ü∂ X} {g : Y‚ÇÄ ‚ü∂ X‚ÇÄ}
    (h : SieveStruct (data X) f g) :
    presheafObj data G‚ÇÄ X ‚ü∂ G‚ÇÄ.val.obj (op Y‚ÇÄ) :=
  presheafObjœÄ data G‚ÇÄ X h.i‚ÇÄ ‚â´ IsDenseSubsite.mapPreimage J F G‚ÇÄ h.q

lemma res_eq_res {X : C} {X‚ÇÄ Y‚ÇÄ : C‚ÇÄ} {f : F.obj X‚ÇÄ ‚ü∂ X} {g : Y‚ÇÄ ‚ü∂ X‚ÇÄ}
    (h‚ÇÅ h‚ÇÇ : SieveStruct (data X) f g) :
    res data G‚ÇÄ h‚ÇÅ = res data G‚ÇÄ h‚ÇÇ := by
  refine Presheaf.IsSheaf.hom_ext G‚ÇÄ.cond
    ‚ü®_, J‚ÇÄ.intersection_covering (IsDenseSubsite.imageSieve_mem J‚ÇÄ J F h‚ÇÅ.q)
      (IsDenseSubsite.imageSieve_mem J‚ÇÄ J F h‚ÇÇ.q)‚ü© _ _ ?_
  rintro ‚ü®Z‚ÇÄ, a, ‚ü®b‚ÇÅ, w‚ÇÅ‚ü©, ‚ü®b‚ÇÇ, w‚ÇÇ‚ü©‚ü©
  refine Presheaf.IsSheaf.hom_ext G‚ÇÄ.cond
    ‚ü®_, (data X).mem‚ÇÅ‚ÇÄ h‚ÇÅ.i‚ÇÄ h‚ÇÇ.i‚ÇÄ b‚ÇÅ b‚ÇÇ (by rw [w‚ÇÅ, w‚ÇÇ, assoc, assoc, h‚ÇÅ.fac, h‚ÇÇ.fac])‚ü© _ _ ?_
  rintro ‚ü®W‚ÇÄ, c, hc‚ü©
  dsimp [res]
  simp only [assoc, IsDenseSubsite.mapPreimage_comp_map]
  apply presheafObj_mapPreimage_condition
  simp

end restriction

/-- Let `F : C‚ÇÄ ‚•§ C` be a dense subsite and `data : ‚àÄ X, F.OneHypercoverDenseData J‚ÇÄ J X`
be a family. Let `G‚ÇÄ` be a sheaf on `C‚ÇÄ`. Let `f : F.obj X‚ÇÄ ‚ü∂ X`.
This is the canonical morphism
`presheafObj data G‚ÇÄ X ‚ü∂ G‚ÇÄ.val.obj (op X‚ÇÄ)` (where `presheafObj data G‚ÇÄ X`
is the value on `X` of the extension to `C` of the sheaf `G‚ÇÄ`,
see `OneHypercoverDenseData.essSurj.presheaf` and
`OneHypercoverDenseData.essSurj.isSheaf`). -/
noncomputable def restriction {X : C} {X‚ÇÄ : C‚ÇÄ} (f : F.obj X‚ÇÄ ‚ü∂ X) :
    presheafObj data G‚ÇÄ X ‚ü∂ G‚ÇÄ.val.obj (op X‚ÇÄ) :=
  G‚ÇÄ.2.amalgamate ‚ü®_, (data X).sieve_mem f‚ü©
    (fun ‚ü®Y‚ÇÄ, g, hg‚ü© ‚Ü¶ restriction.res data G‚ÇÄ hg.some) (by
      rintro ‚ü®Z‚ÇÅ, g‚ÇÅ, ‚ü®h‚ÇÅ‚ü©‚ü© ‚ü®Z‚ÇÇ, g‚ÇÇ, ‚ü®h‚ÇÇ‚ü©‚ü© ‚ü®T‚ÇÄ, p‚ÇÅ, p‚ÇÇ, w‚ü©
      dsimp at g‚ÇÅ g‚ÇÇ p‚ÇÅ p‚ÇÇ w ‚ä¢
      rw [restriction.res_eq_res data G‚ÇÄ _ h‚ÇÅ, restriction.res_eq_res data G‚ÇÄ _ h‚ÇÇ]
      refine Presheaf.IsSheaf.hom_ext G‚ÇÄ.cond
        ‚ü®_, J‚ÇÄ.intersection_covering
          (IsDenseSubsite.imageSieve_mem J‚ÇÄ J F (F.map p‚ÇÅ ‚â´ h‚ÇÅ.q))
          (IsDenseSubsite.imageSieve_mem J‚ÇÄ J F (F.map p‚ÇÇ ‚â´ h‚ÇÇ.q))‚ü© _ _ ?_
      rintro ‚ü®W‚ÇÄ, a, ‚ü®q‚ÇÅ, w‚ÇÅ‚ü©, ‚ü®q‚ÇÇ, w‚ÇÇ‚ü©‚ü©
      refine Presheaf.IsSheaf.hom_ext G‚ÇÄ.cond
        ‚ü®_, (data X).mem‚ÇÅ‚ÇÄ h‚ÇÅ.i‚ÇÄ h‚ÇÇ.i‚ÇÄ q‚ÇÅ q‚ÇÇ (by
        simp only [w‚ÇÅ, w‚ÇÇ, assoc, h‚ÇÅ.fac, h‚ÇÇ.fac, ‚Üê Functor.map_comp_assoc, w])‚ü© _ _ ?_
      rintro ‚ü®U‚ÇÄ, b, hb‚ü©
      dsimp
      simp only [assoc, restriction.res, IsDenseSubsite.mapPreimage_comp_map]
      apply presheafObj_mapPreimage_condition
      simp only [assoc, h‚ÇÅ.fac, h‚ÇÇ.fac, ‚Üê Functor.map_comp_assoc, w])

lemma restriction_map {X : C} {X‚ÇÄ : C‚ÇÄ} (f : F.obj X‚ÇÄ ‚ü∂ X) {Y‚ÇÄ : C‚ÇÄ}
    (g : Y‚ÇÄ ‚ü∂ X‚ÇÄ) {i : (data X).I‚ÇÄ} (p : F.obj Y‚ÇÄ ‚ü∂ F.obj ((data X).X i))
    (fac : p ‚â´ (data X).f i = F.map g ‚â´ f) :
    restriction data G‚ÇÄ f ‚â´ G‚ÇÄ.val.map g.op =
      presheafObjœÄ data G‚ÇÄ X i ‚â´ IsDenseSubsite.mapPreimage J F G‚ÇÄ p := by
  have hg : (data X).sieve f g := ‚ü®i, p, fac‚ü©
  dsimp only [restriction]
  rw [G‚ÇÄ.2.amalgamate_map _ _ _ ‚ü®_, g, hg‚ü©]
  apply presheafObj_mapPreimage_condition
  rw [hg.some.fac, fac]

lemma restriction_eq_of_fac {X : C} {X‚ÇÄ : C‚ÇÄ} (f : F.obj X‚ÇÄ ‚ü∂ X)
    {i : (data X).I‚ÇÄ} (p : F.obj X‚ÇÄ ‚ü∂ F.obj ((data X).X i))
    (fac : p ‚â´ (data X).f i = f) :
    restriction data G‚ÇÄ f =
      presheafObjœÄ data G‚ÇÄ X i ‚â´ IsDenseSubsite.mapPreimage J F G‚ÇÄ p := by
  simpa using restriction_map data G‚ÇÄ f (ùüô _) p (by simpa using fac)

/-- Auxiliary definition for `OneHypercoverDenseData.essSurj.presheaf`. -/
noncomputable def presheafMap {X Y : C} (f : X ‚ü∂ Y) :
    presheafObj data G‚ÇÄ Y ‚ü∂ presheafObj data G‚ÇÄ X :=
  Multiequalizer.lift _ _ (fun i‚ÇÄ ‚Ü¶ restriction data G‚ÇÄ ((data X).f i‚ÇÄ ‚â´ f)) (by
    rintro ‚ü®‚ü®i‚ÇÅ, i‚ÇÇ‚ü©, j‚ü©
    obtain ‚ü®a, h‚ÇÅ, h‚ÇÇ‚ü© : ‚àÉ a, a = F.map ((data X).p‚ÇÅ j) ‚â´ (data X).f i‚ÇÅ ‚â´ f ‚àß
        a = F.map ((data X).p‚ÇÇ j) ‚â´ (data X).f i‚ÇÇ ‚â´ f := ‚ü®_, rfl, (data X).w_assoc j _‚ü©
    refine Presheaf.IsSheaf.hom_ext G‚ÇÄ.cond
      ‚ü®_, cover_lift F J‚ÇÄ _ (J.pullback_stable a (data Y).mem‚ÇÄ)‚ü© _ _ ?_
    rintro ‚ü®W‚ÇÄ, b, ‚ü®_, p, _, ‚ü®i‚ü©, fac‚ü©‚ü©
    dsimp at fac ‚ä¢
    simp only [assoc, ‚Üê map_comp, ‚Üê op_comp]
    rw [restriction_map (p := p), restriction_map (p := p)]
    all_goals simp_all)

@[reassoc (attr := simp)]
lemma presheafMap_œÄ {X Y : C} (f : X ‚ü∂ Y) (i : (data X).I‚ÇÄ) :
    presheafMap data G‚ÇÄ f ‚â´ presheafObjœÄ data G‚ÇÄ X i =
      restriction data G‚ÇÄ ((data X).f i ‚â´ f) :=
  Multiequalizer.lift_Œπ _ _ _ _ _

set_option backward.isDefEq.respectTransparency false in
@[reassoc (attr := simp)]
lemma presheafMap_restriction {X Y : C} {X‚ÇÄ : C‚ÇÄ} (f : F.obj X‚ÇÄ ‚ü∂ X) (g : X ‚ü∂ Y) :
    presheafMap data G‚ÇÄ g ‚â´ restriction data G‚ÇÄ f = restriction data G‚ÇÄ (f ‚â´ g) := by
  refine Presheaf.IsSheaf.hom_ext G‚ÇÄ.cond ‚ü®_, GrothendieckTopology.bind_covering
    (hS := cover_lift F J‚ÇÄ J (J.pullback_stable f (data X).mem‚ÇÄ)) (hR := fun Y‚ÇÄ a ha ‚Ü¶
      cover_lift F J‚ÇÄ J (J.pullback_stable
        (Sieve.ofArrows.h ha ‚â´ (data X).f (Sieve.ofArrows.i ha) ‚â´ g) (data Y).mem‚ÇÄ))‚ü© _ _ ?_
  rintro ‚ü®U‚ÇÄ, _, Y‚ÇÄ, c, d, hd, hc, rfl‚ü©
  have hc' := Sieve.ofArrows.fac hc
  have hd' := Sieve.ofArrows.fac hd
  dsimp at hc hd hc' hd' ‚ä¢
  rw [assoc, ‚Üê op_comp, restriction_map (i := Sieve.ofArrows.i hd)
    (p := F.map c ‚â´ Sieve.ofArrows.h hd) (fac := by grind),
    restriction_map (i := Sieve.ofArrows.i hc) (p := Sieve.ofArrows.h hc) (fac := by grind),
    presheafMap_œÄ_assoc]
  dsimp
  have := J‚ÇÄ.intersection_covering (IsDenseSubsite.imageSieve_mem J‚ÇÄ J F (Sieve.ofArrows.h hc))
    (J‚ÇÄ.pullback_stable c (IsDenseSubsite.imageSieve_mem J‚ÇÄ J F (Sieve.ofArrows.h hd)))
  refine Presheaf.IsSheaf.hom_ext G‚ÇÄ.cond ‚ü®_, this‚ü© _ _ ?_
  rintro ‚ü®V‚ÇÄ, a, ‚ü®x‚ÇÅ, fac‚ÇÅ‚ü©, ‚ü®x‚ÇÇ, fac‚ÇÇ‚ü©‚ü©
  dsimp
  rw [assoc, assoc,
    IsDenseSubsite.mapPreimage_map_of_fac J F G‚ÇÄ _ _ x‚ÇÇ (by simpa using fac‚ÇÇ.symm),
    IsDenseSubsite.mapPreimage_map_of_fac J F G‚ÇÄ _ _ x‚ÇÅ fac‚ÇÅ.symm,
    restriction_map data G‚ÇÄ _ _ (F.map x‚ÇÅ) (by grind), IsDenseSubsite.mapPreimage_map]

lemma presheafMap_id (X : C) :
    presheafMap data G‚ÇÄ (ùüô X) = ùüô _ := by
  ext i
  rw [presheafMap_œÄ, comp_id, id_comp,
    restriction_eq_of_fac data G‚ÇÄ ((data X).f i) (ùüô _) (by simp),
    IsDenseSubsite.mapPreimage_id, comp_id]

@[reassoc]
lemma presheafMap_comp {X Y Z : C} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) :
    presheafMap data G‚ÇÄ (f ‚â´ g) = presheafMap data G‚ÇÄ g ‚â´ presheafMap data G‚ÇÄ f := by
  ext i
  dsimp
  rw [assoc, presheafMap_œÄ, presheafMap_œÄ, presheafMap_restriction, assoc]

/-- Let `F : C‚ÇÄ ‚•§ C` be a dense subsite and `data : ‚àÄ X, F.OneHypercoverDenseData J‚ÇÄ J X`
be a family. Let `G‚ÇÄ` be a sheaf on `C‚ÇÄ`. This is a presheaf on `C` which
extends `G‚ÇÄ`, and we shall also show that it is a sheaf (TODO). -/
@[simps]
noncomputable def presheaf : C·µí·µñ ‚•§ A where
  obj X := presheafObj data G‚ÇÄ X.unop
  map f := presheafMap data G‚ÇÄ f.unop
  map_id X := presheafMap_id data G‚ÇÄ X.unop
  map_comp f g := presheafMap_comp data G‚ÇÄ g.unop f.unop

end essSurj

end

end OneHypercoverDenseData

end Functor

end CategoryTheory
