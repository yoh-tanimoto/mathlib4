/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.Algebra.Category.Grp.Limits
public import Mathlib.CategoryTheory.Monoidal.Cartesian.Mon_
public import Mathlib.CategoryTheory.Monoidal.Grp_

/-!
# Yoneda embedding of `Grp C`

We show that group objects are exactly those whose yoneda presheaf is a presheaf of groups,
by constructing the yoneda embedding `Grp C â¥¤ Cáµ’áµ– â¥¤ GrpCat.{v}` and
showing that it is fully faithful and its (essential) image is the representable functors.
-/

@[expose] public section

assert_not_exists Field

open CategoryTheory MonoidalCategory Limits Opposite CartesianMonoidalCategory MonObj

namespace CategoryTheory
universe w v u
variable {C : Type u} [Category.{v} C] [CartesianMonoidalCategory C]
  {M G H X Y : C} [MonObj M] [GrpObj G] [GrpObj H]

set_option backward.isDefEq.respectTransparency false in
variable (X) in
/-- If `X` represents a presheaf of monoids, then `X` is a monoid object. -/
def GrpObj.ofRepresentableBy (F : Cáµ’áµ– â¥¤ GrpCat.{w}) (Î± : (F â‹™ forget _).RepresentableBy X) :
    GrpObj X where
  __ := MonObj.ofRepresentableBy X (F â‹™ forgetâ‚‚ GrpCat MonCat) Î±
  inv := Î±.homEquiv.symm (Î±.homEquiv (ğŸ™ _))â»Â¹
  left_inv := by
    change lift (Î±.homEquiv.symm (Î±.homEquiv (ğŸ™ X))â»Â¹) (ğŸ™ X) â‰«
      Î±.homEquiv.symm (Î±.homEquiv (fst X X) * Î±.homEquiv (snd X X)) =
        toUnit X â‰« Î±.homEquiv.symm 1
    apply Î±.homEquiv.injective
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_one, map_mul]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp [-Functor.comp_obj]
  right_inv := by
    change lift (ğŸ™ X) (Î±.homEquiv.symm (Î±.homEquiv (ğŸ™ X))â»Â¹) â‰«
      Î±.homEquiv.symm (Î±.homEquiv (fst X X) * Î±.homEquiv (snd X X)) =
        toUnit X â‰« Î±.homEquiv.symm 1
    apply Î±.homEquiv.injective
    simp only [Î±.homEquiv_comp, Equiv.apply_symm_apply]
    simp only [Functor.comp_map, ConcreteCategory.forget_map_eq_coe, map_one, map_mul]
    simp only [â† ConcreteCategory.forget_map_eq_coe, â† Functor.comp_map, â† Î±.homEquiv_comp]
    simp [-Functor.comp_obj]

@[deprecated (since := "2025-09-13")] alias Grp_Class.ofRepresentableBy := GrpObj.ofRepresentableBy

/-- If `G` is a group object, then `Hom(X, G)` has a group structure. -/
abbrev Hom.group : Group (X âŸ¶ G) where
  inv f := f â‰« Î¹
  inv_mul_cancel f := calc
    lift (f â‰« Î¹) f â‰« Î¼
    _ = (f â‰« lift Î¹ (ğŸ™ G)) â‰« Î¼ := by simp
    _ = toUnit X â‰« Î· := by rw [Category.assoc]; simp

scoped[CategoryTheory.MonObj] attribute [instance] Hom.group

lemma Hom.inv_def (f : X âŸ¶ G) : fâ»Â¹ = f â‰« Î¹ := rfl

variable (G) in
/-- If `G` is a group object, then `Hom(-, G)` is a presheaf of groups. -/
@[simps]
def yonedaGrpObj : Cáµ’áµ– â¥¤ GrpCat.{v} where
  obj X := GrpCat.of (unop X âŸ¶ G)
  map Ï† := GrpCat.ofHom ((yonedaMonObj G).map Ï†).hom

variable (G) in
/-- If `G` is a monoid object, then `Hom(-, G)` as a presheaf of monoids is represented by `G`. -/
def yonedaGrpObjRepresentableBy : (yonedaGrpObj G â‹™ forget _).RepresentableBy G :=
  Functor.representableByEquiv.symm (.refl _)

variable (G) in
lemma GrpObj.ofRepresentableBy_yonedaGrpObjRepresentableBy :
    ofRepresentableBy G _ (yonedaGrpObjRepresentableBy G) = â€¹GrpObj Gâ€º := by
  ext; change lift (fst G G) (snd G G) â‰« Î¼ = Î¼; rw [lift_fst_snd, Category.id_comp]

@[deprecated (since := "2025-09-13")]
alias Grp_Class.ofRepresentableBy_yonedaGrpObjRepresentableBy :=
  GrpObj.ofRepresentableBy_yonedaGrpObjRepresentableBy

variable (X) in
/-- If `X` represents a presheaf of groups `F`, then `Hom(-, X)` is isomorphic to `F` as
a presheaf of groups. -/
@[simps! hom inv]
def yonedaGrpObjIsoOfRepresentableBy (F : Cáµ’áµ– â¥¤ GrpCat.{v}) (Î± : (F â‹™ forget _).RepresentableBy X) :
    letI := GrpObj.ofRepresentableBy X F Î±
    yonedaGrpObj X â‰… F :=
  letI := GrpObj.ofRepresentableBy X F Î±
  NatIso.ofComponents (fun Y â†¦ MulEquiv.toGrpIso
    { toEquiv := Î±.homEquiv
      map_mul' :=
  ((yonedaMonObjIsoOfRepresentableBy X (F â‹™ forgetâ‚‚ GrpCat MonCat) Î±).hom.app Y).hom.map_mul })
      fun Ï† â†¦ GrpCat.hom_ext <| MonoidHom.ext <| Î±.homEquiv_comp Ï†.unop

set_option backward.isDefEq.respectTransparency false in
/-- The yoneda embedding of `Grp_C` into presheaves of groups. -/
@[simps]
def yonedaGrp : Grp C â¥¤ Cáµ’áµ– â¥¤ GrpCat.{v} where
  obj G := yonedaGrpObj G.X
  map {G H} Ïˆ := { app Y := GrpCat.ofHom ((yonedaMon.map Ïˆ.hom).app Y).hom }

@[reassoc]
lemma yonedaGrp_naturality (Î± : yonedaGrpObj G âŸ¶ yonedaGrpObj H) (f : X âŸ¶ Y) (g : Y âŸ¶ G) :
    Î±.app _ (f â‰« g) = f â‰« Î±.app _ g := congr($(Î±.naturality f.op) g)

/-- The yoneda embedding for `Grp_C` is fully faithful. -/
def yonedaGrpFullyFaithful : yonedaGrp (C := C).FullyFaithful where
  preimage {G H} Î± :=
    Grp.homMk' (yonedaMonFullyFaithful.preimage ((Functor.whiskerRight Î± (forgetâ‚‚ GrpCat MonCat))))
  map_preimage {G H} Î± := by
    ext X : 3
    exact congr(($(yonedaMonFullyFaithful.map_preimage (X := G.toMon) (Y := H.toMon)
      (Functor.whiskerRight Î± (forgetâ‚‚ GrpCat MonCat))).app X).hom)
  preimage_map f := by
    ext
    congr
    apply yonedaMonFullyFaithful.preimage_map

instance : yonedaGrp (C := C).Full := yonedaGrpFullyFaithful.full
instance : yonedaGrp (C := C).Faithful := yonedaGrpFullyFaithful.faithful

lemma essImage_yonedaGrp :
    yonedaGrp (C := C).essImage = (Â· â‹™ forget _) â»Â¹' setOf Functor.IsRepresentable := by
  ext F
  constructor
  Â· rintro âŸ¨G, âŸ¨Î±âŸ©âŸ©
    exact âŸ¨G.X, âŸ¨Functor.representableByEquiv.symm (Functor.isoWhiskerRight Î± (forget _))âŸ©âŸ©
  Â· rintro âŸ¨X, âŸ¨eâŸ©âŸ©
    letI := GrpObj.ofRepresentableBy X F e
    exact âŸ¨âŸ¨XâŸ©, âŸ¨yonedaGrpObjIsoOfRepresentableBy X F eâŸ©âŸ©

@[reassoc]
lemma GrpObj.inv_comp (f : X âŸ¶ G) (g : G âŸ¶ H) [IsMonHom g] : fâ»Â¹ â‰« g = (f â‰« g)â»Â¹ := by
  simp [Hom.inv_def]

@[deprecated (since := "2025-09-13")] alias Grp_Class.inv_comp := GrpObj.inv_comp

@[reassoc]
lemma GrpObj.div_comp (f g : X âŸ¶ G) (h : G âŸ¶ H) [IsMonHom h] :
    (f / g) â‰« h = (f â‰« h) / (g â‰« h) :=
  ((yonedaGrp.map (Grp.homMk (A := .mk G) (B := .mk H) h)).app (op X)).hom.map_div f g

@[deprecated (since := "2025-09-13")] alias Grp_Class.div_comp := GrpObj.div_comp

@[reassoc]
lemma GrpObj.zpow_comp (f : X âŸ¶ G) (n : â„¤) (g : G âŸ¶ H) [IsMonHom g] :
    (f ^ n) â‰« g = (f â‰« g) ^ n :=
  ((yonedaGrp.map (Grp.homMk (A := .mk G) (B := .mk H) g)).app (op X)).hom.map_zpow f n

@[deprecated (since := "2025-09-13")] alias Grp_Class.zpow_comp := GrpObj.zpow_comp

@[reassoc]
lemma GrpObj.comp_inv (f : X âŸ¶ Y) (g : Y âŸ¶ G) : f â‰« gâ»Â¹ = (f â‰« g)â»Â¹ :=
  ((yonedaGrp.obj âŸ¨GâŸ©).map f.op).hom.map_inv g

@[deprecated (since := "2025-09-13")] alias Grp_Class.comp_inv := GrpObj.comp_inv

@[reassoc]
lemma GrpObj.comp_div (f : X âŸ¶ Y) (g h : Y âŸ¶ G) : f â‰« (g / h) = f â‰« g / f â‰« h :=
  ((yonedaGrp.obj âŸ¨GâŸ©).map f.op).hom.map_div g h

@[deprecated (since := "2025-09-13")] alias Grp_Class.comp_div := GrpObj.comp_div

@[reassoc]
lemma GrpObj.comp_zpow (f : X âŸ¶ Y) (g : Y âŸ¶ G) : âˆ€ n : â„¤, f â‰« g ^ n = (f â‰« g) ^ n
  | (n : â„•) => by simp [comp_pow]
  | .negSucc n => by simp [comp_pow, comp_inv]

@[deprecated (since := "2025-09-13")] alias Grp_Class.comp_zpow := GrpObj.comp_zpow

lemma GrpObj.inv_eq_inv : Î¹ = (ğŸ™ G)â»Â¹ := by simp [Hom.inv_def]

@[reassoc (attr := simp)]
lemma GrpObj.one_inv : Î·[G] â‰« Î¹ = Î· := by simp [GrpObj.inv_eq_inv, GrpObj.comp_inv, one_eq_one]

open scoped _root_.CategoryTheory.Obj in
/-- If `G` is a group object and `F` is monoidal,
then `Hom(X, G) â†’ Hom(F X, F G)` preserves inverses. -/
@[simp] lemma Functor.map_inv' {D : Type*} [Category* D] [CartesianMonoidalCategory D] (F : C â¥¤ D)
    [F.Monoidal] {X G : C} (f : X âŸ¶ G) [GrpObj G] :
    F.map (fâ»Â¹) = (F.map f)â»Â¹ := by
  rw [eq_inv_iff_mul_eq_one, â† Functor.map_mul, inv_mul_cancel, Functor.map_one]

@[deprecated (since := "2025-09-13")] alias Grp_Class.inv_eq_inv := GrpObj.inv_eq_inv

/-- The commutator of `G` as a morphism. This is the map `(x, y) â†¦ x * y * xâ»Â¹ * yâ»Â¹`,
see `CategoryTheory.GrpObj.lift_commutator_eq_mul_mul_inv_inv`.
This morphism is constant with value `1` if and only if `G` is commutative
(see `CategoryTheory.isCommMonObj_iff_commutator_eq_toUnit_Î·`). -/
def GrpObj.commutator (G : C) [GrpObj G] : G âŠ— G âŸ¶ G :=
  fst _ _ * snd _ _ * (fst _ _) â»Â¹ * (snd _ _) â»Â¹

@[reassoc (attr := simp)]
lemma GrpObj.lift_commutator_eq_mul_mul_inv_inv {X G : C} [GrpObj G] (fâ‚ fâ‚‚ : X âŸ¶ G) :
    lift fâ‚ fâ‚‚ â‰« commutator G = fâ‚ * fâ‚‚ * fâ‚â»Â¹ * fâ‚‚â»Â¹ := by
  simp [commutator, comp_mul, comp_inv]

@[reassoc (attr := simp)]
lemma GrpObj.Î·_whiskerRight_commutator : Î· â–· G â‰« commutator G = toUnit _ â‰« Î· := by
  simp [commutator, comp_mul, comp_inv, one_eq_one]

@[reassoc (attr := simp)]
lemma GrpObj.whiskerLeft_Î·_commutator : G â— Î· â‰« commutator G = toUnit _ â‰« Î· := by
  simp [commutator, comp_mul, comp_inv, one_eq_one]

variable [BraidedCategory C]

instance [IsCommMonObj G] : IsMonHom Î¹[G] where
  one_hom := by simp [one_eq_one, â† Hom.inv_def]
  mul_hom := by simp [GrpObj.mul_inv_rev]

attribute [local simp] Hom.inv_def in
instance [IsCommMonObj G] {f : M âŸ¶ G} [IsMonHom f] : IsMonHom fâ»Â¹ where

namespace Grp
variable {G H : Grp C} [IsCommMonObj H.X]

instance : MonObj H where
  one := Grp.homMk Î·[H.toMon].hom
  mul := Grp.homMk Î¼[H.toMon].hom

@[simp] lemma hom_one (H : Grp C) [IsCommMonObj H.X] : Î·[H].hom.hom = Î·[H.X] := rfl
@[simp] lemma hom_mul (H : Grp C) [IsCommMonObj H.X] : Î¼[H].hom.hom = Î¼[H.X] := rfl

namespace Hom

@[simp] lemma hom_one : (1 : G âŸ¶ H).hom = 1 := rfl
@[simp] lemma hom_mul (f g : G âŸ¶ H) : (f * g).hom = f.hom * g.hom := rfl
@[simp] lemma hom_pow (f : G âŸ¶ H) (n : â„•) : (f ^ n).hom = f.hom ^ n := by
  induction n with
  | zero => simp
  | succ n hn => simp [pow_succ, hn]

end Hom

/-- A commutative group object is a group object in the category of group objects. -/
instance : GrpObj H where inv := Grp.homMk' { hom := Î¹[H.X] }

namespace Hom

@[simp] lemma hom_hom_inv (f : G âŸ¶ H) : fâ»Â¹.hom.hom = f.hom.homâ»Â¹ := rfl
@[simp] lemma hom_hom_div (f g : G âŸ¶ H) : (f / g).hom.hom = f.hom.hom / g.hom.hom := rfl
@[simp] lemma hom_hom_zpow (f : G âŸ¶ H) (n : â„¤) : (f ^ n).hom.hom = f.hom.hom ^ n := by
  cases n <;> simp

@[deprecated (since := "2025-12-18")] alias hom_inv := hom_hom_inv
@[deprecated (since := "2025-12-18")] alias hom_div := hom_hom_div
@[deprecated (since := "2025-12-18")] alias hom_zpow := hom_hom_zpow

end Hom

attribute [local simp] mul_eq_mul comp_mul mul_comm mul_div_mul_comm in
/-- A commutative group object is a commutative group object in the category of group objects. -/
instance : IsCommMonObj H where

instance [IsCommMonObj G.X] (f : G âŸ¶ H) : IsMonHom f where

end Grp

/-- If `G` is a commutative group object, then `Hom(X, G)` has a commutative group structure. -/
abbrev Hom.commGroup [IsCommMonObj G] : CommGroup (X âŸ¶ G) where

scoped[CategoryTheory.MonObj] attribute [instance] Hom.commGroup

section

/-- `G` is a commutative group object if and only if the commutator map `(x, y) â†¦ x * y * xâ»Â¹ * yâ»Â¹`
is constant. -/
lemma isCommMonObj_iff_commutator_eq_toUnit_Î· :
    IsCommMonObj G â†” GrpObj.commutator G = toUnit _ â‰« Î· := by
  rw [isCommMonObj_iff_isMulCommutative]
  refine âŸ¨fun h â†¦ ?_, fun heq X â†¦ âŸ¨âŸ¨fun f g â†¦ ?_âŸ©âŸ©âŸ©
  Â· simp [GrpObj.commutator, one_eq_one]
  Â· simpa [one_eq_one, mul_inv_eq_iff_eq_mul] using congr(lift f g â‰« $heq)

end

end CategoryTheory
