/-
Copyright (c) 2026 Jo√´l Riou. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jo√´l Riou, Christian Merten
-/
module

public import Mathlib.CategoryTheory.Limits.EpiMono
public import Mathlib.CategoryTheory.Limits.Preserves.Shapes.Equalizers
public import Mathlib.CategoryTheory.Limits.Shapes.Pullback.IsPullback.Basic
public import Mathlib.CategoryTheory.MorphismProperty.Basic

/-!
# Families of functors which jointly reflect isomorphisms

Let `F·µ¢ : C ‚•§ D·µ¢` be a family of functors. The family is said to jointly reflect
isomorphisms (resp. monomorphisms, resp. epimorphisms) if every `f : X ‚ü∂ Y`
in `C` for which `F·µ¢.map f` is an isomorphism (resp. monomorphism, resp. epimorphism)
for all `i` is an isomorphism.

-/

@[expose] public section

namespace CategoryTheory

open Category Limits

variable {C : Type*} [Category C] {I : Type*} {D : I ‚Üí Type*} [‚àÄ i, Category (D i)]

/-- A family of functors jointly reflects isomorphisms if for every morphism `f : X ‚ü∂ Y`
such that the image of `f` under all `F i` is an isomorphism, then `f` is an isomorphism. -/
structure JointlyReflectIsomorphisms (F : ‚àÄ i, C ‚•§ D i) : Prop where
  isIso {X Y : C} (f : X ‚ü∂ Y) [‚àÄ i, IsIso ((F i).map f)] : IsIso f

/-- A family of functors jointly reflects monomorphisms if for every morphism `f : X ‚ü∂ Y`
such that the image of `f` under all `F i` is an monomorphism, then `f` is an monomorphism. -/
structure JointlyReflectMonomorphisms (F : ‚àÄ i, C ‚•§ D i) : Prop where
  mono {X Y : C} (f : X ‚ü∂ Y) [‚àÄ i, Mono ((F i).map f)] : Mono f

/-- A family of functors jointly reflects epimorphisms if for every morphism `f : X ‚ü∂ Y`
such that the image of `f` under all `F i` is an epimorphism, then `f` is an epimorphism. -/
structure JointlyReflectEpimorphisms (F : ‚àÄ i, C ‚•§ D i) : Prop where
  epi {X Y : C} (f : X ‚ü∂ Y) [‚àÄ i, Epi ((F i).map f)] : Epi f

/-- A family of functors is jointly faithful if whenever two morphisms `f : X ‚ü∂ Y`
and `g : X ‚ü∂ Y` become equal after applying all functors `F i`, then `f = g`. -/
structure JointlyFaithful (F : ‚àÄ i, C ‚•§ D i) : Prop where
  map_injective {X Y : C} (f g : X ‚ü∂ Y) (h : ‚àÄ i, (F i).map f = (F i).map g) : f = g

variable {F : ‚àÄ i, C ‚•§ D i}

namespace JointlyReflectIsomorphisms

variable (h : JointlyReflectIsomorphisms F)

include h

lemma isIso_iff {X Y : C} (f : X ‚ü∂ Y) :
    IsIso f ‚Üî ‚àÄ i, IsIso ((F i).map f) :=
  ‚ü®fun _ _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ h.isIso f‚ü©

lemma mono {X Y : C} (f : X ‚ü∂ Y) [hf : ‚àÄ i, Mono ((F i).map f)]
    [‚àÄ i, PreservesLimit (cospan f f) (F i)] [HasPullback f f] :
    Mono f := by
  have hc := pullbackIsPullback f f
  rw [mono_iff_isIso_fst hc, h.isIso_iff]
  intro i
  exact (mono_iff_isIso_fst ((isLimitMapConePullbackConeEquiv (F i) pullback.condition).1
    (isLimitOfPreserves (F i) hc))).1 (hf i)

lemma jointlyReflectMonomorphisms [‚àÄ i, PreservesLimitsOfShape WalkingCospan (F i)]
    [HasPullbacks C] :
    JointlyReflectMonomorphisms F where
  mono f _ := h.mono f

lemma epi {X Y : C} (f : X ‚ü∂ Y) [hf : ‚àÄ i, Epi ((F i).map f)]
    [‚àÄ i, PreservesColimit (span f f) (F i)] [HasPushout f f] : Epi f := by
  have hc := pushoutIsPushout f f
  rw [epi_iff_isIso_inl hc, h.isIso_iff]
  intro i
  exact (epi_iff_isIso_inl ((isColimitMapCoconePushoutCoconeEquiv (F i) pushout.condition).1
    (isColimitOfPreserves (F i) hc))).1 (hf i)

lemma jointlyReflectEpimorphisms [‚àÄ i, PreservesColimitsOfShape WalkingSpan (F i)]
    [HasPushouts C] :
    JointlyReflectEpimorphisms F where
  epi f _ := h.epi f

lemma jointlyFaithful [‚àÄ i, PreservesLimitsOfShape WalkingParallelPair (F i)] [HasEqualizers C] :
    JointlyFaithful F where
  map_injective {X Y} f g hfg := by
    suffices IsIso (equalizer.Œπ f g) from eq_of_epi_equalizer
    have (i : I) : IsIso ((F i).map (equalizer.Œπ f g)) := by
      let hc := isLimitForkMapOfIsLimit (F i) _ (equalizerIsEqualizer f g)
      obtain ‚ü®l, hl‚ü© := Fork.IsLimit.lift' hc (ùüô _) (by simpa using hfg i)
      exact ‚ü®l, Fork.IsLimit.hom_ext hc (by cat_disch), by cat_disch‚ü©
    exact h.isIso _

end JointlyReflectIsomorphisms

lemma JointlyReflectMonomorphisms.mono_iff (h : JointlyReflectMonomorphisms F)
    [‚àÄ i, (F i).PreservesMonomorphisms] {X Y : C} (f : X ‚ü∂ Y) :
    Mono f ‚Üî ‚àÄ i, Mono ((F i).map f) :=
  ‚ü®fun _ _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ h.mono f‚ü©

lemma JointlyReflectEpimorphisms.epi_iff (h : JointlyReflectEpimorphisms F)
    [‚àÄ i, (F i).PreservesEpimorphisms] {X Y : C} (f : X ‚ü∂ Y) :
    Epi f ‚Üî ‚àÄ i, Epi ((F i).map f) :=
  ‚ü®fun _ _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ h.epi f‚ü©

end CategoryTheory
