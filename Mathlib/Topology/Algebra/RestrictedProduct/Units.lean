/-
Copyright (c) 2026 Salvatore Mercuri. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Salvatore Mercuri
-/
module

public import Mathlib.Topology.Algebra.RestrictedProduct.Basic
public import Mathlib.Algebra.Group.Submonoid.Units
public import Mathlib.Algebra.Group.Pi.Units

/-!
# Units of restricted products

This file contains results about the units of a restricted product. The restricted
product `Î Ê³ i : Î¹, [R i, B i]_[ğ“•]` of a family of types `R` with respect to a family of
subsets `B` along a filter `ğ“•` is defined in `Mathlib.Topology.Algebra.RestrictedProduct.Basic`.
Here, we give conditions that characterize when an element of the restricted product is a unit,
and provide an isomorphism between the units of the restricted product and the restricted product
of the units.

## Main definitions

* `RestrictedProduct.unitsEquiv`: the (monoid) isomorphism between `(Î Ê³ i, [R i, B i]_[ğ“•])Ë£`
  and `Î Ê³ i, [(R i)Ë£, (B i).units]_[ğ“•]`.

## Tags

restricted product, adeles, ideles
-/

@[expose] public section

namespace RestrictedProduct

variable {Î¹ : Type*}
variable {R : Î¹ â†’ Type*} [âˆ€ i, Monoid (R i)]
variable {S : Î¹ â†’ Type*} [Î  i, SetLike (S i) (R i)] [âˆ€ (i : Î¹), SubmonoidClass (S i) (R i)]
variable {B : Î  i, S i}
variable {ğ“• : Filter Î¹}

theorem isUnit_of_eventually_isUnit {x : Î Ê³ i, [R i, B i]_[ğ“•]} (hx : âˆ€ i, IsUnit (x i))
    (hxr : âˆ€á¶  i in ğ“•, âˆƒ (h : x i âˆˆ B i), IsUnit (âŸ¨x i, hâŸ© : B i)) :
    IsUnit x := by
  rw [isUnit_iff_exists]
  use .mk (fun i â†¦ (hx i).unit.inv) (by
    filter_upwards [hxr] with i âŸ¨h, huâŸ©
    have hu : (hx i).unit.1 * hu.unit.inv = 1 := Subtype.val_inj.2 hu.mul_val_inv
    simp [â† Units.eq_inv_of_mul_eq_one_left hu])
  simp [RestrictedProduct.ext_iff]

theorem eventualy_isUnit_of_isUnit {x : Î Ê³ i, [R i, B i]_[ğ“•]} (hx : IsUnit x) :
    (âˆ€ i, IsUnit (x i)) âˆ§ âˆ€á¶  i in ğ“•, âˆƒ (h : x i âˆˆ B i), IsUnit (âŸ¨x i, hâŸ© : B i) := by
  simp only [isUnit_iff_exists, RestrictedProduct.ext_iff, â† forall_and] at hx
  simp only [isUnit_iff_exists]
  choose b hb using hx
  exact âŸ¨Classical.skolem.symm.1 âŸ¨b, hbâŸ©, by filter_upwards [x.2, b.2] using
    fun i hx hb â†¦ âŸ¨hx, âŸ¨b i, hbâŸ©, by simp_all [â† SetLike.coe_eq_coe]âŸ©âŸ©

theorem isUnit_iff {x : Î Ê³ i, [R i, B i]_[ğ“•]} :
    IsUnit x â†” (âˆ€ i, IsUnit (x i)) âˆ§ âˆ€á¶  i in ğ“•, âˆƒ (h : x i âˆˆ B i), IsUnit (âŸ¨x i, hâŸ© : B i)  :=
  âŸ¨eventualy_isUnit_of_isUnit, fun h â†¦ isUnit_of_eventually_isUnit h.1 h.2âŸ©

/-- The homomorphism from the units of a restricted product to the regular product of unit. -/
def coeUnits : Î Ê³ i, [R i, B i]_[ğ“•]Ë£ â†’* (i : Î¹) â†’ (R i)Ë£ :=
  MulEquiv.piUnits.toMonoidHom.comp <| Units.map coeMonoidHom

/-- Constructs a unit in a restricted product `Î Ê³ i, [R i, B i]_[ğ“•]` given an element `x` of
the usual product and the condition that `x` is eventually in the units of `B i` along `ğ“•`. -/
def mkUnit (x : Î  i, (R i)Ë£) (hx : âˆ€á¶  i in ğ“•, x i âˆˆ (Submonoid.ofClass (B i)).units) :
    Î Ê³ i, [R i, B i]_[ğ“•]Ë£ where
  val := âŸ¨fun i â†¦ (x i).1, by filter_upwards [hx] using fun i hi â†¦ hi.1âŸ©
  inv := âŸ¨fun i â†¦ (x i)â»Â¹.1, by filter_upwards [hx] using fun i hi â†¦ hi.2âŸ©
  val_inv := by ext; simp
  inv_val := by ext; simp

variable (R) in
/-- The ring isomorphism between the units of a restricted product `Î Ê³ i, [R i, B i]_[ğ“•]` and
the restricted product of `(R i)Ë£` with respect to `(B i)Ë£`. -/
def unitsEquiv : Î Ê³ i, [R i, B i]_[ğ“•]Ë£ â‰ƒ* Î Ê³ i, [(R i)Ë£, (Submonoid.ofClass (B i)).units]_[ğ“•] where
  toFun x := âŸ¨coeUnits x, by filter_upwards [x.val.2, x.inv.2] using fun i hi hi' â†¦ âŸ¨hi, hi'âŸ©âŸ©
  invFun y := mkUnit y.1 y.2
  left_inv _ := rfl
  right_inv _ := rfl
  map_mul' _ _ := rfl

@[simp] lemma unitsEquiv_apply (i : Î¹) (x : Î Ê³ i, [R i, B i]_[ğ“•]Ë£) :
    (unitsEquiv R x i) = x.1 i := rfl

@[simp] lemma coe_unitsEquiv_apply (x : Î Ê³ i, [R i, B i]_[ğ“•]Ë£) (i : Î¹) :
    (unitsEquiv R x).1 i = unitsEquiv R x i := rfl

end RestrictedProduct
