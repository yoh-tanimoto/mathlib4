/-
Copyright (c) 2026 Vasilii Nesterov. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Vasilii Nesterov
-/
module

public import Mathlib.Topology.Algebra.Order.Field
public import Mathlib.Topology.Maps.Basic
public import Mathlib.Analysis.Asymptotics.Lemmas

/-!
# Conversion lemmas

The main procedure of the `compute_asymptotics` tactic is able to compute limits of functions at
`atTop` filter. This file contains lemmas we use to reduce other asymptotic goals to
the case `Tendsto f atTop l`.

## Main theorems

This file contains the following lemmas:
* `tendsto_nhdsGT_of_tendsto_atTop` for `Tendsto f (ğ“[>] c) l`
* `tendsto_nhdsLT_of_tendsto_atTop` for `Tendsto f (ğ“[<] c) l`
* `tendsto_nhdsNE_of_tendsto_atTop` for `Tendsto f (ğ“[â‰ ] c) l`
* `isBigO_of_div_tendsto_atTop` and `isBigO_of_div_tendsto_atBot` for `f =O[l] g`

We also use lemmas from other files:
* `tendsto_comp_neg_atTop_iff` for `Tendsto f atBot l`
* `IsLittleO.of_tendsto_div_atBot` and `IsLittleO.of_tendsto_div_atTop` for `f =o[l] g`
* `isEquivalent_of_tendsto_one` for `f âˆ¼ g`
-/

@[expose] public section

open Filter Topology Asymptotics

namespace Tactic.ComputeAsymptotics

variable {Î± ğ•œ : Type*} [Field ğ•œ] [LinearOrder ğ•œ] [IsStrictOrderedRing ğ•œ] [TopologicalSpace ğ•œ]
  [OrderTopology ğ•œ] {l : Filter Î±} (f : ğ•œ â†’ Î±) (c : ğ•œ)

theorem tendsto_nhdsGT_of_tendsto_atTop (h : Tendsto (fun x â†¦ f (c + xâ»Â¹)) atTop l) :
    Tendsto f (ğ“[>] c) l := by
  simpa [â† Function.comp_def, Tendsto, â† Filter.map_map] using h

theorem tendsto_nhdsLT_of_tendsto_atTop (h : Tendsto (fun x â†¦ f (c - xâ»Â¹)) atTop l) :
    Tendsto f (ğ“[<] c) l := by
  convert_to Tendsto (f âˆ˜ (fun x â†¦ c + x) âˆ˜ Neg.neg âˆ˜ Inv.inv) atTop l at h
  Â· ext
    simp [AddGroupWithOne.sub_eq_add_neg]
  simpa [Tendsto, â† Filter.map_map] using h

theorem tendsto_nhdsNE_of_tendsto_atTop (h_neg : Tendsto (fun x â†¦ f (c - xâ»Â¹)) atTop l)
    (h_pos : Tendsto (fun x â†¦ f (c + xâ»Â¹)) atTop l) :
    Tendsto f (ğ“[â‰ ] c) l := by
  simpa [Tendsto, â† nhdsLT_sup_nhdsGT] using
    âŸ¨tendsto_nhdsLT_of_tendsto_atTop _ _ h_neg, tendsto_nhdsGT_of_tendsto_atTop _ _ h_posâŸ©

theorem tendsto_nhdsNE_of_tendsto_atTop_nhds_of_eq [TopologicalSpace Î±] {a b : Î±}
    (h_neg : Tendsto (fun x â†¦ f (c - xâ»Â¹)) atTop (ğ“ a))
    (h_pos : Tendsto (fun x â†¦ f (c + xâ»Â¹)) atTop (ğ“ b)) (h_eq : a = b) :
    Tendsto f (ğ“[â‰ ] c) (ğ“ a) := by
  apply tendsto_nhdsNE_of_tendsto_atTop _ _ h_neg
  convert h_pos

theorem isBigOWith_of_tendsto_top {C : â„} {f g : â„ â†’ â„} {l : Filter â„}
    (h : Tendsto (fun x â†¦ g x / f x) l atTop) (hC : 0 < C) :
    IsBigOWith C l f g :=
  Asymptotics.IsLittleO.forall_isBigOWith (.of_tendsto_div_atTop h) hC

theorem isBigOWith_of_tendsto_bot {C : â„} {f g : â„ â†’ â„} {l : Filter â„}
    (h : Tendsto (fun x â†¦ g x / f x) l atBot) (hC : 0 < C) :
    IsBigOWith C l f g :=
  Asymptotics.IsLittleO.forall_isBigOWith (.of_tendsto_div_atBot h) hC

theorem isBigO_of_div_tendsto_atTop {f g : â„ â†’ â„} {l : Filter â„}
    (h : Tendsto (fun x â†¦ g x / f x) l atTop) :
    f =O[l] g :=
  Asymptotics.IsLittleO.isBigO (.of_tendsto_div_atTop h)

theorem isBigO_of_div_tendsto_atBot {f g : â„ â†’ â„} {l : Filter â„}
    (h : Tendsto (fun x â†¦ g x / f x) l atBot) :
    f =O[l] g :=
  Asymptotics.IsLittleO.isBigO (.of_tendsto_div_atBot h)

end Tactic.ComputeAsymptotics
