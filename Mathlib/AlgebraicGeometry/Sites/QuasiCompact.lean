/-
Copyright (c) 2025 Christian Merten. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Christian Merten
-/
module

public import Mathlib.CategoryTheory.Sites.Hypercover.ZeroFamily
public import Mathlib.AlgebraicGeometry.Cover.QuasiCompact

/-!
# Quasi-compact precoverage

In this file we define the quasi-compact precoverage. A cover is covering in the quasi-compact
precoverage if it is a quasi-compact cover, i.e., if every affine open of the base can be covered
by a finite union of images of quasi-compact opens of the components.

The fpqc precoverage is the precoverage by flat covers that are quasi-compact in this sense.
-/

@[expose] public section

universe w' w v u

open CategoryTheory Limits

namespace AlgebraicGeometry.Scheme

variable {S : Scheme.{u}}

@[simp]
lemma quasiCompactCover_shrink_iff (E : PreZeroHypercover.{w} S) :
    QuasiCompactCover E.shrink â†” QuasiCompactCover E :=
  âŸ¨fun _ â†¦ .of_hom E.fromShrink, fun _ â†¦ .of_hom E.toShrinkâŸ©

/-- The pre-`0`-hypercover family on the category of schemes underlying the fpqc precoverage. -/
@[simps]
def qcCoverFamily : PreZeroHypercoverFamily Scheme.{u} where
  property X := X.quasiCompactCover
  iff_shrink {_} E := (quasiCompactCover_shrink_iff E).symm

/--
The quasi-compact precoverage on the category of schemes is the precoverage
given by quasi-compact covers. The intersection of this precoverage
with the precoverage defined by jointly surjective families of flat morphisms is
the fpqc-precoverage.
-/
def qcPrecoverage : Precoverage Scheme.{u} :=
  qcCoverFamily.precoverage

@[simp]
lemma presieveâ‚€_mem_qcPrecoverage_iff {E : PreZeroHypercover.{w} S} :
    E.presieveâ‚€ âˆˆ Scheme.qcPrecoverage S â†” QuasiCompactCover E := by
  rw [â† PreZeroHypercover.presieveâ‚€_shrink, Scheme.qcPrecoverage,
    E.shrink.presieveâ‚€_mem_precoverage_iff]
  simp

instance : qcPrecoverage.HasIsos := .of_preZeroHypercoverFamily fun X Y f hf â†¦ by
  rw [qcCoverFamily_property, Scheme.quasiCompactCover_iff]
  infer_instance

instance : qcPrecoverage.IsStableUnderBaseChange := by
  refine .of_preZeroHypercoverFamily_of_isClosedUnderIsomorphisms ?_ ?_
  Â· intro X
    exact X.isClosedUnderIsomorphisms_quasiCompactCover
  Â· intro X Y f E h hE
    simp only [qcCoverFamily_property, Scheme.quasiCompactCover_iff] at hE âŠ¢
    infer_instance

instance : qcPrecoverage.IsStableUnderComposition := by
  refine .of_preZeroHypercoverFamily fun {X} E F hE hF â†¦ ?_
  simp only [qcCoverFamily_property, Scheme.quasiCompactCover_iff] at hE hF âŠ¢
  infer_instance

instance : qcPrecoverage.IsStableUnderSup := by
  refine .of_preZeroHypercoverFamily fun {X} E F hE hF â†¦ ?_
  simp only [qcCoverFamily_property, Scheme.quasiCompactCover_iff] at hE hF âŠ¢
  infer_instance

/-- If `P` implies being an open map, the by `P` induced precoverage is coarser
than the quasi-compact precoverage. -/
lemma precoverage_le_qcPrecoverage_of_isOpenMap {P : MorphismProperty Scheme.{u}}
    (hP : P â‰¤ fun _ _ f â†¦ IsOpenMap f.base) :
    precoverage P â‰¤ qcPrecoverage := by
  refine Precoverage.le_of_zeroHypercover fun X E â†¦ ?_
  rw [presieveâ‚€_mem_qcPrecoverage_iff]
  exact .of_isOpenMap fun i â†¦ hP _ (Scheme.Cover.map_prop E i)

lemma zariskiPrecoverage_le_qcPrecoverage :
    zariskiPrecoverage â‰¤ qcPrecoverage :=
  precoverage_le_qcPrecoverage_of_isOpenMap fun _ _ f _ â†¦ f.isOpenEmbedding.isOpenMap

lemma Hom.singleton_mem_qcPrecoverage {X Y : Scheme.{u}} (f : X âŸ¶ Y) [Surjective f]
    [QuasiCompact f] : Presieve.singleton f âˆˆ qcPrecoverage Y := by
  let E : Cover.{u} _ _ := f.cover (P := âŠ¤) trivial
  rw [qcPrecoverage, PreZeroHypercoverFamily.mem_precoverage_iff]
  refine âŸ¨(f.cover (P := âŠ¤) trivial).toPreZeroHypercover, ?_, by simpâŸ©
  simp only [qcCoverFamily_property, quasiCompactCover_iff]
  infer_instance

section Property

variable {P : MorphismProperty Scheme.{u}}

/-- The `qc`-precoverage of a scheme wrt. to a morphism property `P` is the precoverage
given by quasi-compact covers satisfying `P`. -/
abbrev propQCPrecoverage (P : MorphismProperty Scheme.{u}) : Precoverage Scheme.{u} :=
  qcPrecoverage âŠ“ Scheme.precoverage P

@[grind .]
lemma propQCPrecoverage_le_precoverage : propQCPrecoverage P â‰¤ precoverage P :=
  inf_le_right

instance {S : Scheme.{u}} (ğ’° : Scheme.Cover (propQCPrecoverage P) S) :
    QuasiCompactCover ğ’°.toPreZeroHypercover := by
  rw [â† Scheme.presieveâ‚€_mem_qcPrecoverage_iff]
  exact ğ’°.memâ‚€.1

/-- Forget being quasi-compact. -/
@[simps toPreZeroHypercover]
abbrev Cover.forgetQc {S : Scheme.{u}} (ğ’° : Scheme.Cover (propQCPrecoverage P) S) :
    S.Cover (precoverage P) where
  __ := ğ’°.toPreZeroHypercover
  memâ‚€ := ğ’°.memâ‚€.2

instance {S : Scheme.{u}} (ğ’° : Scheme.Cover (propQCPrecoverage P) S) :
    QuasiCompactCover ğ’°.forgetQc.toPreZeroHypercover := by
  dsimp; infer_instance

/-- Construct a cover in the `P`-qc topology from a quasi-compact cover in the `P`-topology. -/
@[simps toPreZeroHypercover]
def Cover.ofQuasiCompactCover {S : Scheme.{u}} (ğ’° : Scheme.Cover (precoverage P) S)
    [qc : QuasiCompactCover ğ’°.1] :
    Scheme.Cover (propQCPrecoverage P) S where
  __ := ğ’°.toPreZeroHypercover
  memâ‚€ := âŸ¨Scheme.presieveâ‚€_mem_qcPrecoverage_iff.mpr â€¹_â€º, ğ’°.memâ‚€âŸ©

/-- Lift a quasi-compact `P`-cover of a `u`-scheme in an arbitrary universe to universe `u`.
This is again quasi-compact. -/
noncomputable def Cover.qculift {S : Scheme.{u}} (ğ’° : Cover.{w} (precoverage P) S)
    [QuasiCompactCover ğ’°.1] : Scheme.Cover.{u} (precoverage P) S where
  __ := ğ’°.ulift.toPreZeroHypercover.sum (QuasiCompactCover.ulift ğ’°.1)
  memâ‚€ := by
    rw [presieveâ‚€_mem_precoverage_iff]
    refine âŸ¨fun x â†¦ âŸ¨.inl x, ğ’°.covers _âŸ©, fun i â†¦ ?_âŸ©
    induction i <;> exact ğ’°.map_prop _

instance {S : Scheme.{u}} (ğ’° : S.Cover (precoverage P)) [QuasiCompactCover ğ’°.1] :
    QuasiCompactCover (Scheme.Cover.qculift ğ’°).1 :=
  .of_hom (PreZeroHypercover.sumInr _ _)

instance : Precoverage.Small.{u} (propQCPrecoverage P) where
  zeroHypercoverSmall {S} (ğ’° : S.Cover _) := by
    refine âŸ¨ğ’°.forgetQc.qculift.Iâ‚€, Sum.elim ğ’°.forgetQc.idx (QuasiCompactCover.uliftHom _).sâ‚€,
      âŸ¨?_, ?_âŸ©âŸ©
    Â· rw [Scheme.presieveâ‚€_mem_qcPrecoverage_iff]
      exact .of_hom (ğ’± := QuasiCompactCover.ulift ğ’°.1) âŸ¨Sum.inr, fun i â†¦ ğŸ™ _, by cat_dischâŸ©
    Â· rw [Scheme.presieveâ‚€_mem_precoverage_iff]
      exact âŸ¨fun x â†¦ âŸ¨Sum.inl x, ğ’°.forgetQc.covers _âŸ©, fun i â†¦ ğ’°.forgetQc.map_prop _âŸ©

lemma mem_propQCPrecoverage_iff_exists_quasiCompactCover {S : Scheme.{u}} {R : Presieve S} :
    R âˆˆ propQCPrecoverage P S â†” âˆƒ (ğ’° : Scheme.Cover.{u + 1} (precoverage P) S),
      QuasiCompactCover ğ’°.toPreZeroHypercover âˆ§ R = ğ’°.presieveâ‚€ := by
  rw [Precoverage.mem_iff_exists_zeroHypercover]
  refine âŸ¨fun âŸ¨ğ’°, hâŸ© â†¦ âŸ¨ğ’°.weaken propQCPrecoverage_le_precoverage, ?_, hâŸ©,
    fun âŸ¨ğ’°, _, hâŸ© â†¦ âŸ¨âŸ¨ğ’°.1, âŸ¨by simpa, ğ’°.memâ‚€âŸ©âŸ©, hâŸ©âŸ©
  rw [â† Scheme.presieveâ‚€_mem_qcPrecoverage_iff]
  exact ğ’°.memâ‚€.1

@[grind .]
lemma Hom.singleton_mem_propQCPrecoverage {X Y : Scheme.{u}} {f : X âŸ¶ Y} (hf : P f) [Surjective f]
    [QuasiCompact f] : Presieve.singleton f âˆˆ propQCPrecoverage P Y := by
  refine âŸ¨f.singleton_mem_qcPrecoverage, ?_âŸ©
  grind [singleton_mem_precoverage_iff]

/-- The `P`-`qc`-topology on the category of schemes wrt. to a morphism property `P` is the
topology generated by quasi-compact covers satisfying `P`. -/
abbrev propQCTopology (P : MorphismProperty Scheme.{u}) : GrothendieckTopology Scheme.{u} :=
  (propQCPrecoverage P).toGrothendieck

@[grind .]
lemma Hom.generate_singleton_mem_propQCTopology {X Y : Scheme.{u}} (f : X âŸ¶ Y) (hf : P f)
    [Surjective f] [QuasiCompact f] :
    .generate (.singleton f) âˆˆ propQCTopology P Y := by
  apply Precoverage.generate_mem_toGrothendieck
  exact f.singleton_mem_propQCPrecoverage hf

@[simp, grind .]
lemma Cover.mem_propQCTopology {S : Scheme.{u}} (ğ’° : Cover.{u} (precoverage P) S)
    [QuasiCompactCover ğ’°.1] :
    .ofArrows ğ’°.X ğ’°.f âˆˆ propQCTopology P S := by
  refine Precoverage.generate_mem_toGrothendieck âŸ¨?_, ğ’°.memâ‚€âŸ©
  rwa [presieveâ‚€_mem_qcPrecoverage_iff]

end Property

end AlgebraicGeometry.Scheme
