/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kim Morrison, Justus Springer
-/
module

public import Mathlib.Geometry.RingedSpace.LocallyRingedSpace
public import Mathlib.AlgebraicGeometry.StructureSheaf
public import Mathlib.RingTheory.Localization.LocalizationLocalization
public import Mathlib.Topology.Sheaves.SheafCondition.Sites
public import Mathlib.Topology.Sheaves.Functors
public import Mathlib.Algebra.Module.LocalizedModule.Basic

/-!
# $Spec$ as a functor to locally ringed spaces.

We define the functor $Spec$ from commutative rings to locally ringed spaces.

## Implementation notes

We define $Spec$ in three consecutive steps, each with more structure than the last:

1. `Spec.toTop`, valued in the category of topological spaces,
2. `Spec.toSheafedSpace`, valued in the category of sheafed spaces and
3. `Spec.toLocallyRingedSpace`, valued in the category of locally ringed spaces.

Additionally, we provide `Spec.toPresheafedSpace` as a composition of `Spec.toSheafedSpace` with
a forgetful functor.

## Related results

The adjunction `Œì ‚ä£ Spec` is constructed in `Mathlib/AlgebraicGeometry/GammaSpecAdjunction.lean`.

-/

@[expose] public section


-- Explicit universe annotations were used in this file to improve performance https://github.com/leanprover-community/mathlib4/issues/12737

noncomputable section

universe u v

namespace AlgebraicGeometry

open Opposite

open CategoryTheory

open StructureSheaf

open Spec (structureSheaf)

/-- The spectrum of a commutative ring, as a topological space.
-/
def Spec.topObj (R : CommRingCat.{u}) : TopCat :=
  TopCat.of (PrimeSpectrum R)

@[simp] theorem Spec.topObj_forget {R} : ToType (Spec.topObj R) = PrimeSpectrum R :=
  rfl

/-- The induced map of a ring homomorphism on the ring spectra, as a morphism of topological spaces.
-/
def Spec.topMap {R S : CommRingCat.{u}} (f : R ‚ü∂ S) : Spec.topObj S ‚ü∂ Spec.topObj R :=
  TopCat.ofHom ‚ü®_, PrimeSpectrum.continuous_comap f.hom‚ü©

@[simp]
theorem Spec.topMap_id (R : CommRingCat.{u}) : Spec.topMap (ùüô R) = ùüô (Spec.topObj R) :=
  rfl

@[simp]
theorem Spec.topMap_comp {R S T : CommRingCat.{u}} (f : R ‚ü∂ S) (g : S ‚ü∂ T) :
    Spec.topMap (f ‚â´ g) = Spec.topMap g ‚â´ Spec.topMap f :=
  rfl

-- Porting note: `simps!` generate some garbage lemmas, so choose manually,
-- if more is needed, add them here
/-- The spectrum, as a contravariant functor from commutative rings to topological spaces.
-/
@[simps!]
def Spec.toTop : CommRingCat.{u}·µí·µñ ‚•§ TopCat where
  obj R := Spec.topObj (unop R)
  map {_ _} f := Spec.topMap f.unop

/-- The spectrum of a commutative ring, as a `SheafedSpace`.
-/
@[simps]
def Spec.sheafedSpaceObj (R : CommRingCat.{u}) : SheafedSpace CommRingCat where
  carrier := Spec.topObj R
  presheaf := (structureSheaf R).1
  IsSheaf := (structureSheaf R).2

/-- The induced map of a ring homomorphism on the ring spectra, as a morphism of sheafed spaces.
-/
@[simps hom_base hom_c_app]
def Spec.sheafedSpaceMap {R S : CommRingCat.{u}} (f : R ‚ü∂ S) :
    Spec.sheafedSpaceObj S ‚ü∂ Spec.sheafedSpaceObj R where
  hom.base := Spec.topMap f
  hom.c :=
    { app := fun U => CommRingCat.ofHom <|
        comap f.hom (unop U) ((TopologicalSpace.Opens.map (Spec.topMap f)).obj (unop U)) fun _ => id
      naturality := fun {_ _} _ => by ext; rfl }

set_option backward.isDefEq.respectTransparency false in
@[simp]
theorem Spec.sheafedSpaceMap_id {R : CommRingCat.{u}} :
    Spec.sheafedSpaceMap (ùüô R) = ùüô (Spec.sheafedSpaceObj R) := by
  ext : 1
  ¬∑ exact Spec.topMap_id R
  ¬∑ ext
    dsimp
    rw [comap_id (by simp)]
    simp
    rfl

set_option backward.isDefEq.respectTransparency false in
theorem Spec.sheafedSpaceMap_comp {R S T : CommRingCat.{u}} (f : R ‚ü∂ S) (g : S ‚ü∂ T) :
    Spec.sheafedSpaceMap (f ‚â´ g) = Spec.sheafedSpaceMap g ‚â´ Spec.sheafedSpaceMap f := by
  ext : 1
  ¬∑ exact Spec.topMap_comp f g
  ¬∑ ext
    -- Porting note: was one liner
    -- `dsimp, rw category_theory.functor.map_id, rw category.comp_id, erw comap_comp f g, refl`
    rw [NatTrans.comp_app, sheafedSpaceMap_hom_c_app, Functor.whiskerRight_app, eqToHom_refl]
    erw [(sheafedSpaceObj T).presheaf.map_id]
    dsimp only [CommRingCat.hom_comp, RingHom.coe_comp, Function.comp_apply]
    rw [comap_comp]
    rfl

/-- Spec, as a contravariant functor from commutative rings to sheafed spaces.
-/
@[simps]
def Spec.toSheafedSpace : CommRingCat.{u}·µí·µñ ‚•§ SheafedSpace CommRingCat where
  obj R := Spec.sheafedSpaceObj (unop R)
  map f := Spec.sheafedSpaceMap f.unop
  map_comp f g := by simp [Spec.sheafedSpaceMap_comp]

/-- Spec, as a contravariant functor from commutative rings to presheafed spaces.
-/
def Spec.toPresheafedSpace : CommRingCat.{u}·µí·µñ ‚•§ PresheafedSpace CommRingCat :=
  Spec.toSheafedSpace ‚ãô SheafedSpace.forgetToPresheafedSpace

@[simp]
theorem Spec.toPresheafedSpace_obj (R : CommRingCat.{u}·µí·µñ) :
    Spec.toPresheafedSpace.obj R = (Spec.sheafedSpaceObj (unop R)).toPresheafedSpace :=
  rfl

theorem Spec.toPresheafedSpace_obj_op (R : CommRingCat.{u}) :
    Spec.toPresheafedSpace.obj (op R) = (Spec.sheafedSpaceObj R).toPresheafedSpace :=
  rfl

@[simp]
theorem Spec.toPresheafedSpace_map (R S : CommRingCat.{u}·µí·µñ) (f : R ‚ü∂ S) :
    Spec.toPresheafedSpace.map f = (Spec.sheafedSpaceMap f.unop).hom :=
  rfl

theorem Spec.toPresheafedSpace_map_op (R S : CommRingCat.{u}) (f : R ‚ü∂ S) :
    Spec.toPresheafedSpace.map f.op = (Spec.sheafedSpaceMap f).hom :=
  rfl

set_option backward.isDefEq.respectTransparency false in
theorem Spec.basicOpen_hom_ext {X : RingedSpace.{u}} {R : CommRingCat.{u}}
    {Œ± Œ≤ : X ‚ü∂ Spec.sheafedSpaceObj R} (w : Œ±.hom.base = Œ≤.hom.base)
    (h : ‚àÄ r : R,
      let U := PrimeSpectrum.basicOpen r
      ((CommRingCat.ofHom (algebraMap R _)) ‚â´ Œ±.hom.c.app (op U)) ‚â´
        X.presheaf.map (eqToHom (by rw [w])) =
        CommRingCat.ofHom (algebraMap _ _) ‚â´ Œ≤.hom.c.app (op U)) :
    Œ± = Œ≤ := by
  ext : 1
  ¬∑ exact w
  ¬∑ apply ((TopCat.Sheaf.pushforward _ Œ≤.hom.base).obj X.sheaf).hom_ext _
      PrimeSpectrum.isBasis_basic_opens
    intro r
    apply (StructureSheaf.to_basicOpen_epi R r).1
    simpa using h r

-- `simps!` generates some garbage lemmas, so choose manually,
-- if more is needed, add them here
/-- The spectrum of a commutative ring, as a `LocallyRingedSpace`. -/
@[simps! toSheafedSpace presheaf]
def Spec.locallyRingedSpaceObj (R : CommRingCat.{u}) : LocallyRingedSpace where
  __ := Spec.sheafedSpaceObj R
  isLocalRing x := (stalkIso R x).toRingEquiv.isLocalRing

lemma Spec.locallyRingedSpaceObj_sheaf (R : CommRingCat.{u}) :
    (Spec.locallyRingedSpaceObj R).sheaf = structureSheaf R := rfl

lemma Spec.locallyRingedSpaceObj_sheaf' (R : Type u) [CommRing R] :
    (Spec.locallyRingedSpaceObj <| CommRingCat.of R).sheaf = structureSheaf R := rfl

lemma Spec.locallyRingedSpaceObj_presheaf_map (R : CommRingCat.{u}) {U V} (i : U ‚ü∂ V) :
    (Spec.locallyRingedSpaceObj R).presheaf.map i =
    (structureSheaf R).1.map i := rfl

lemma Spec.locallyRingedSpaceObj_presheaf' (R : Type u) [CommRing R] :
    (Spec.locallyRingedSpaceObj <| CommRingCat.of R).presheaf = (structureSheaf R).1 := rfl

lemma Spec.locallyRingedSpaceObj_presheaf_map' (R : Type u) [CommRing R] {U V} (i : U ‚ü∂ V) :
    (Spec.locallyRingedSpaceObj <| CommRingCat.of R).presheaf.map i =
    (structureSheaf R).1.map i := rfl

@[elementwise]
theorem stalkMap_toStalk {R S : CommRingCat.{u}} (f : R ‚ü∂ S) (p : PrimeSpectrum S) :
    toStalk R (PrimeSpectrum.comap f.hom p) ‚â´ (Spec.sheafedSpaceMap f).hom.stalkMap p =
      f ‚â´ toStalk S p := by
  rw [‚Üê algebraMap_germ ‚ä§ p trivial, ‚Üê algebraMap_germ ‚ä§ (PrimeSpectrum.comap f.hom p) trivial,
    Category.assoc]
  erw [PresheafedSpace.stalkMap_germ (Spec.sheafedSpaceMap f).hom ‚ä§ p trivial]
  rw [Spec.sheafedSpaceMap_hom_c_app]
  erw [toOpen_comp_comap_assoc]
  rfl

set_option backward.isDefEq.respectTransparency false in
/-- Under the isomorphisms `stalkIso`, the map `stalkMap (Spec.sheafedSpaceMap f) p` corresponds
to the induced local ring homomorphism `Localization.localRingHom`.
-/
@[elementwise]
theorem localRingHom_comp_stalkIso {R S : CommRingCat.{u}} (f : R ‚ü∂ S) (p : PrimeSpectrum S) :
    CommRingCat.ofHom (stalkIso R (PrimeSpectrum.comap f.hom p)).symm.toRingHom ‚â´
      (CommRingCat.ofHom (Localization.localRingHom (PrimeSpectrum.comap f.hom p).asIdeal p.asIdeal
          f.hom rfl)) ‚â´
        CommRingCat.ofHom (stalkIso S p).toRingHom =
      (Spec.sheafedSpaceMap f).hom.stalkMap p :=
  (stalkIso R (PrimeSpectrum.comap f.hom p)).toCommRingCatIso.symm.eq_inv_comp.mp <|
    (stalkIso S p).toCommRingCatIso.symm.comp_inv_eq.mpr <| CommRingCat.hom_ext <|
      Localization.localRingHom_unique _ _ _ (PrimeSpectrum.comap_asIdeal _ _) fun x => by
  dsimp
  simp only [AlgEquiv.commutes, RingEquiv.symm_apply_eq, AlgEquiv.coe_ringEquiv]
  exact stalkMap_toStalk_apply f p x

set_option backward.isDefEq.respectTransparency false in
/--
The induced map of a ring homomorphism on the prime spectra, as a morphism of locally ringed spaces.
-/
@[simps! toHom]
def Spec.locallyRingedSpaceMap {R S : CommRingCat.{u}} (f : R ‚ü∂ S) :
    Spec.locallyRingedSpaceObj S ‚ü∂ Spec.locallyRingedSpaceObj R :=
  LocallyRingedSpace.Hom.mk (Spec.sheafedSpaceMap f).hom fun p =>
    IsLocalHom.mk fun a ha => by
    rw [‚Üê localRingHom_comp_stalkIso] at ha
    dsimp at ha
    have : IsLocalHom (stalkIso S p) := isLocalHom_equiv _
    have : IsLocalHom (stalkIso R (p.comap f.hom)).symm := isLocalHom_equiv _
    exact ((ha.of_map (stalkIso S p)).of_map _).of_map (stalkIso R (p.comap f.hom)).symm

@[simp]
theorem Spec.locallyRingedSpaceMap_id (R : CommRingCat.{u}) :
    Spec.locallyRingedSpaceMap (ùüô R) = ùüô (Spec.locallyRingedSpaceObj R) :=
  LocallyRingedSpace.Hom.ext' <| by
    rw [Spec.locallyRingedSpaceMap_toHom, Spec.sheafedSpaceMap_id]; rfl

theorem Spec.locallyRingedSpaceMap_comp {R S T : CommRingCat.{u}} (f : R ‚ü∂ S) (g : S ‚ü∂ T) :
    Spec.locallyRingedSpaceMap (f ‚â´ g) =
      Spec.locallyRingedSpaceMap g ‚â´ Spec.locallyRingedSpaceMap f :=
  LocallyRingedSpace.Hom.ext' <| by
    rw [Spec.locallyRingedSpaceMap_toHom, Spec.sheafedSpaceMap_comp]; rfl

/-- Spec, as a contravariant functor from commutative rings to locally ringed spaces.
-/
@[simps]
def Spec.toLocallyRingedSpace : CommRingCat.{u}·µí·µñ ‚•§ LocallyRingedSpace where
  obj R := Spec.locallyRingedSpaceObj (unop R)
  map f := Spec.locallyRingedSpaceMap f.unop
  map_id R := by dsimp; rw [Spec.locallyRingedSpaceMap_id]
  map_comp f g := by dsimp; rw [Spec.locallyRingedSpaceMap_comp]

section SpecŒì

open AlgebraicGeometry.LocallyRingedSpace

/-- The counit morphism `R ‚ü∂ Œì(Spec R)` given by `AlgebraicGeometry.StructureSheaf.toOpen`. -/
def toSpecŒì (R : CommRingCat.{u}) : R ‚ü∂ Œì.obj (op (Spec.toLocallyRingedSpace.obj (op R))) :=
  CommRingCat.ofHom (algebraMap _ _)

instance isIso_toSpecŒì (R : CommRingCat.{u}) : IsIso (toSpecŒì R) :=
  (ConcreteCategory.isIso_iff_bijective _).mpr algebraMap_obj_top_bijective

@[reassoc]
theorem Spec_Œì_naturality {R S : CommRingCat.{u}} (f : R ‚ü∂ S) :
    f ‚â´ toSpecŒì S = toSpecŒì R ‚â´ Œì.map (Spec.toLocallyRingedSpace.map f.op).op := by
  -- Porting note (https://github.com/leanprover-community/mathlib4/issues/11041): `ext` failed to pick up one of the three lemmas
  ext : 2
  refine Subtype.ext <| funext fun x' => ?_; symm
  erw [comap_apply]
  apply Localization.localRingHom_to_map

/-- The counit (`SpecŒìIdentity.inv.op`) of the adjunction `Œì ‚ä£ Spec` is an isomorphism. -/
@[simps! hom_app inv_app]
def LocallyRingedSpace.SpecŒìIdentity : Spec.toLocallyRingedSpace.rightOp ‚ãô Œì ‚âÖ ùü≠ _ :=
  Iso.symm <| NatIso.ofComponents.{u, u, u + 1, u + 1} (fun R ‚Ü¶ asIso (toSpecŒì R):)
    fun {X Y} f => by convert Spec_Œì_naturality (R := X) (S := Y) f

end SpecŒì

set_option backward.isDefEq.respectTransparency false in
/-- The stalk map of `Spec M‚Åª¬πR ‚ü∂ Spec R` is an iso for each `p : Spec M‚Åª¬πR`. -/
theorem isIso_SpecMap_stakMap_localization (R : CommRingCat.{u}) (M : Submonoid R)
    (x : PrimeSpectrum (Localization M)) :
    IsIso
      ((Spec.toPresheafedSpace.map
        (CommRingCat.ofHom (algebraMap R (Localization M))).op).stalkMap x) := by
  dsimp only [Spec.toPresheafedSpace_map, Quiver.Hom.unop_op]
  rw [‚Üê localRingHom_comp_stalkIso, ConcreteCategory.isIso_iff_bijective]
  dsimp
  simp only [EquivLike.bijective_comp]
  refine (stalkIso (Localization M) x).bijective.comp ?_
  suffices
    IsIso (IsLocalization.localizationLocalizationAtPrimeIsoLocalization M
        x.asIdeal).toRingEquiv.toCommRingCatIso.hom by
    rwa [ConcreteCategory.isIso_iff_bijective] at this
  infer_instance

@[deprecated (since := "2025-10-11")]
alias Spec_map_localization_isIso := isIso_SpecMap_stakMap_localization

namespace StructureSheaf

variable {R S : CommRingCat.{u}} (f : R ‚ü∂ S) (p : PrimeSpectrum R)

/-- For an algebra `f : R ‚Üí+* S`, this is the ring homomorphism `S ‚Üí+* (f‚àó ùí™‚Çõ)‚Çö` for a `p : Spec R`.
This is shown to be the localization at `p` in `isLocalizedModule_toPushforwardStalkAlgHom`.
-/
def toPushforwardStalk : S ‚ü∂ (Spec.topMap f _* (structureSheaf S).1).stalk p :=
  CommRingCat.ofHom (algebraMap _ _) ‚â´
    @TopCat.Presheaf.germ _ _ _ _ (Spec.topMap f _* (structureSheaf S).1) ‚ä§ p trivial

set_option backward.isDefEq.respectTransparency false in
@[reassoc]
theorem toPushforwardStalk_comp :
    f ‚â´ StructureSheaf.toPushforwardStalk f p =
      StructureSheaf.toStalk R p ‚â´
        (TopCat.Presheaf.stalkFunctor _ _).map (Spec.sheafedSpaceMap f).hom.c := by
  rw [StructureSheaf.toStalk, Category.assoc, TopCat.Presheaf.stalkFunctor_map_germ]
  exact Spec_Œì_naturality_assoc f _

instance : Algebra R ((Spec.topMap f _* (structureSheaf S).1).stalk p) :=
  (f ‚â´ StructureSheaf.toPushforwardStalk f p).hom.toAlgebra

theorem algebraMap_pushforward_stalk :
    algebraMap R ((Spec.topMap f _* (structureSheaf S).1).stalk p) =
      (f ‚â´ StructureSheaf.toPushforwardStalk f p).hom :=
  rfl

variable (R S)
variable [Algebra R S]

/--
This is the `AlgHom` version of `toPushforwardStalk`, which is the map `S ‚ü∂ (f‚àó ùí™‚Çõ)‚Çö` for some
algebra `R ‚ü∂ S` and some `p : Spec R`.
-/
@[simps!]
def toPushforwardStalkAlgHom :
    S ‚Üí‚Çê[R] (Spec.topMap (CommRingCat.ofHom (algebraMap R S)) _* (structureSheaf S).1).stalk p :=
  { (StructureSheaf.toPushforwardStalk (CommRingCat.ofHom (algebraMap R S)) p).hom with
    commutes' := fun _ => rfl }

set_option backward.isDefEq.respectTransparency false in
theorem isLocalizedModule_toPushforwardStalkAlgHom_aux (y) :
    ‚àÉ x : S √ó p.asIdeal.primeCompl, x.2 ‚Ä¢ y = toPushforwardStalkAlgHom R S p x.1 := by
  obtain ‚ü®U, hp, s, e‚ü© := TopCat.Presheaf.germ_exist _ _ y
  obtain ‚ü®_, ‚ü®r, rfl‚ü©, hpr : p ‚àà PrimeSpectrum.basicOpen r, hrU : PrimeSpectrum.basicOpen r ‚â§ U‚ü© :=
    PrimeSpectrum.isTopologicalBasis_basic_opens.exists_subset_of_mem_open (show p ‚àà U from hp) U.2
  change PrimeSpectrum.basicOpen r ‚â§ U at hrU
  replace e :=
    ((Spec.topMap (CommRingCat.ofHom (algebraMap R S)) _* (structureSheaf S).1).germ_res_apply
      (homOfLE hrU) p hpr _).trans e
  set s' := (Spec.topMap (CommRingCat.ofHom (algebraMap R S)) _* (structureSheaf S).1).map
      (homOfLE hrU).op s with h
  replace e : ((Spec.topMap (CommRingCat.ofHom (algebraMap R S)) _* (structureSheaf S).val).germ _
      p hpr) s' = y := by
    rw [h]; exact e
  clear_value s'; clear! U
  obtain ‚ü®‚ü®s, ‚ü®_, n, rfl‚ü©‚ü©, hsn‚ü© :=
    @IsLocalization.surj _ _ _ _ _ _
      (StructureSheaf.IsLocalization.to_basicOpen S <| algebraMap R S r) s'
  refine ‚ü®‚ü®s, ‚ü®r, hpr‚ü© ^ n‚ü©, ?_‚ü©
  rw [Submonoid.smul_def, Algebra.smul_def, algebraMap_pushforward_stalk, toPushforwardStalk,
    CommRingCat.comp_apply, CommRingCat.comp_apply]
  iterate 2
    erw [‚Üê (Spec.topMap (CommRingCat.ofHom (algebraMap R S)) _* (structureSheaf S).1).germ_res_apply
      (homOfLE le_top) p hpr]
  rw [‚Üê e]
  let f := TopCat.Presheaf.germ (Spec.topMap (CommRingCat.ofHom (algebraMap R S)) _*
      (structureSheaf S).val) _ p hpr
  rw [‚Üê map_mul, mul_comm]
  dsimp only [Subtype.coe_mk] at hsn
  rw [‚Üê map_pow (algebraMap R S)] at hsn
  congr 1

instance isLocalizedModule_toPushforwardStalkAlgHom :
    IsLocalizedModule p.asIdeal.primeCompl (toPushforwardStalkAlgHom R S p).toLinearMap := by
  apply IsLocalizedModule.mkOfAlgebra
  ¬∑ intro x hx; rw [algebraMap_pushforward_stalk, toPushforwardStalk_comp]
    change IsUnit ((TopCat.Presheaf.stalkFunctor CommRingCat p).map
      (Spec.sheafedSpaceMap (CommRingCat.ofHom (algebraMap ‚ÜëR ‚ÜëS))).hom.c _)
    exact (IsLocalization.map_units ((structureSheaf R).presheaf.stalk p) ‚ü®x, hx‚ü©).map _
  ¬∑ apply isLocalizedModule_toPushforwardStalkAlgHom_aux
  ¬∑ intro x hx
    rw [toPushforwardStalkAlgHom_apply,
      ‚Üê (toPushforwardStalk (CommRingCat.ofHom (algebraMap ‚ÜëR ‚ÜëS)) p).hom.map_zero,
      toPushforwardStalk] at hx
    rw [CommRingCat.comp_apply, map_zero] at hx
    obtain ‚ü®U, hpU, i‚ÇÅ, i‚ÇÇ, e‚ü© := TopCat.Presheaf.germ_eq (C := CommRingCat) _ _ _ _ _ _ hx
    obtain ‚ü®_, ‚ü®r, rfl‚ü©, hpr, hrU‚ü© :=
      PrimeSpectrum.isTopologicalBasis_basic_opens.exists_subset_of_mem_open (show p ‚àà U.1 from hpU)
        U.2
    apply_fun (Spec.topMap (CommRingCat.ofHom (algebraMap R S)) _* (structureSheaf S).1).map
        (homOfLE hrU).op at e
    have : algebraMap S ((structureSheaf S).presheaf.obj _) x = 0 := e
    have :=
      (@IsLocalization.mk'_one _ _ _ _ _ _
            (StructureSheaf.IsLocalization.to_basicOpen S <| algebraMap R S r) x).trans
        this
    obtain ‚ü®‚ü®_, n, rfl‚ü©, e‚ü© := (IsLocalization.mk'_eq_zero_iff _ _).mp this
    refine ‚ü®‚ü®r, hpr‚ü© ^ n, ?_‚ü©
    rw [Submonoid.smul_def, Algebra.smul_def, SubmonoidClass.coe_pow, map_pow]
    exact e

end StructureSheaf

end AlgebraicGeometry
