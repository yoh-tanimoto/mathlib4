/-
Copyright (c) 2020 Kim Morrison. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Johan Commelin, Kim Morrison, Andrew Yang
-/
module

public import Mathlib.Algebra.Category.ModuleCat.Stalk
public import Mathlib.Algebra.Category.Ring.Limits
public import Mathlib.RingTheory.Spectrum.Prime.Topology
public import Mathlib.Tactic.DepRewrite
public import Mathlib.Topology.Sheaves.LocalPredicate

/-!
# The structure sheaf on `PrimeSpectrum R`.

We define the structure sheaf on `TopCat.of (PrimeSpectrum R)`, for an `R`-module `M` and prove
basic properties about it. We define this as a subsheaf of the sheaf of dependent functions into the
localizations, cut out by the condition that the function must be locally equal to a quotient of
an element of `M` by an element of `R`.

Because the condition "is equal to a fraction" passes to smaller open subsets,
the subset of functions satisfying this condition is automatically a subpresheaf.
Because the condition "is locally equal to a fraction" is local,
it is also a subsheaf.

(It may be helpful to refer back to `Mathlib/Topology/Sheaves/SheafOfFunctions.lean`,
where we show that dependent functions into any type family form a sheaf,
and also `Mathlib/Topology/Sheaves/LocalPredicate.lean`, where we characterise the predicates
which pick out sub-presheaves and sub-sheaves of these sheaves.)

When `M = R`, the structure sheaf is furthermore a sheaf of commutative rings, which we bundle as
`structureSheaf : Sheaf CommRingCat (PrimeSpectrum.Top R)`.

We then obtain two key descriptions of the structure sheaf. We show that the stalks `M‚Çì` is the
localization of `M` at the prime corresponding to `x`, and we show that the sections `Œì(M, D(f))`
is the localization of `M` away from `f`.

Note that the results of this file are packaged into schemes and sheaf of modules in later files,
and one usually should not directly use the results in this file to respect the abstraction
boundaries.

## References

* [Robin Hartshorne, *Algebraic Geometry*][Har77]


-/


universe u

noncomputable section

variable {R M A : Type u} [CommRing R] [AddCommGroup M] [Module R M] [CommRing A] [Algebra R A]

open TopCat

open TopologicalSpace CategoryTheory Opposite

open PrimeSpectrum (basicOpen)

namespace AlgebraicGeometry

@[expose] public section Public

variable (R) in
/-- The prime spectrum as an object of `TopCat`. -/
public def PrimeSpectrum.Top : TopCat := TopCat.of (PrimeSpectrum R)

namespace StructureSheaf

variable {P : PrimeSpectrum.Top R}

variable (M P) in
/-- The type family over `PrimeSpectrum R` consisting of the localization over each point. -/
abbrev Localizations : Type u := LocalizedModule P.asIdeal.primeCompl M

/-- The predicate saying that a dependent function on an open `U` is realised as a fixed fraction
`r / s` in each of the stalks (which are localizations at various prime ideals).
-/
def IsFraction {U : Opens (PrimeSpectrum.Top R)} (f : Œ† x : U, Localizations M x.1) : Prop :=
  ‚àÉ r s, ‚àÄ x : U, ‚àÉ hs : s ‚àâ x.1.asIdeal, f x = LocalizedModule.mk r ‚ü®s, hs‚ü©

variable (R M) in
/-- The predicate `IsFraction` is "prelocal",
in the sense that if it holds on `U` it holds on any open subset `V` of `U`.
-/
def isFractionPrelocal : PrelocalPredicate (Localizations (R := R) M) where
  pred {_} f := IsFraction f
  res := by rintro V U i f ‚ü®r, s, w‚ü©; exact ‚ü®r, s, fun x => w (i x)‚ü©

variable (R M) in
/-- We will define the structure sheaf as
the subsheaf of all dependent functions in `Œ† x : U, Localizations R x`
consisting of those functions which can locally be expressed as a ratio of
(the images in the localization of) elements of `R`.

Quoting Hartshorne:

For an open set $U ‚äÜ Spec A$, we define $ùí™(U)$ to be the set of functions
$s : U ‚Üí ‚®Ü_{ùî≠ ‚àà U} A_ùî≠$, such that $s(ùî≠) ‚àà A_ùî≠$ for each $ùî≠$,
and such that $s$ is locally a quotient of elements of $A$:
to be precise, we require that for each $ùî≠ ‚àà U$, there is a neighborhood $V$ of $ùî≠$,
contained in $U$, and elements $a, f ‚àà A$, such that for each $ùîÆ ‚àà V, f ‚àâ ùîÆ$,
and $s(ùîÆ) = a/f$ in $A_ùîÆ$.

Now Hartshorne had the disadvantage of not knowing about dependent functions,
so we replace his circumlocution about functions into a disjoint union with
`Œ† x : U, Localizations x`.
-/
def isLocallyFraction : LocalPredicate (Localizations (R := R) M) :=
  (isFractionPrelocal R M).sheafify

set_option backward.isDefEq.respectTransparency false in
variable (M) in
/-- The functions satisfying `isLocallyFraction` form a submodule. -/
def sectionsSubmodule (U : (Opens (PrimeSpectrum.Top R))) :
    Submodule R (Œ† x : U, Localizations M x.1) where
  carrier := { f | (isLocallyFraction R M).pred f }
  add_mem' {a b} ha hb x := by
    obtain ‚ü®Va, ma, ia, ra, sa, wa‚ü© := ha x
    obtain ‚ü®Vb, mb, ib, rb, sb, wb‚ü© := hb x
    refine ‚ü®Va ‚äì Vb, ‚ü®ma, mb‚ü©, Opens.infLELeft _ _ ‚â´ ia, sb ‚Ä¢ ra + sa ‚Ä¢ rb, sa * sb, fun x ‚Ü¶ ?_‚ü©
    obtain ‚ü®hsax, hsa‚ü© := wa ‚ü®x.1, x.2.1‚ü©
    obtain ‚ü®hsbx, hsb‚ü© := wb ‚ü®x.1, x.2.2‚ü©
    exact ‚ü®x.1.asIdeal.primeCompl.mul_mem hsax hsbx,
      congr($hsa + $hsb).trans (LocalizedModule.mk_add_mk ..)‚ü©
  zero_mem' x := ‚ü®U, x.2, ùüô _, 0, 1, fun y ‚Ü¶ by simp [Ideal.IsPrime.one_notMem]‚ü©
  smul_mem' r {a} ha x := by
    obtain ‚ü®V, m, i, ra, sa, wa‚ü© := ha x
    exact ‚ü®V, m, i, r ‚Ä¢ ra, sa, fun x ‚Ü¶ ‚ü®(wa x).1,
      congr(r ‚Ä¢ $((wa x).2)).trans (LocalizedModule.smul'_mk ..)‚ü©‚ü©

variable (A) in
/-- The functions satisfying `isLocallyFraction` form a subalgebra. -/
def sectionsSubalgebra (U : (Opens (PrimeSpectrum.Top R))) :
    Subalgebra R (Œ† x : U, Localizations A x.1) where
  __ := sectionsSubmodule A U
  mul_mem' {a b} ha hb x := by
    obtain ‚ü®Va, ma, ia, ra, sa, wa‚ü© := ha x
    obtain ‚ü®Vb, mb, ib, rb, sb, wb‚ü© := hb x
    refine ‚ü®Va ‚äì Vb, ‚ü®ma, mb‚ü©, Opens.infLELeft _ _ ‚â´ ia, ra * rb, sa * sb, fun x ‚Ü¶ ?_‚ü©
    obtain ‚ü®hsax, hsa‚ü© := wa ‚ü®x.1, x.2.1‚ü©
    obtain ‚ü®hsbx, hsb‚ü© := wb ‚ü®x.1, x.2.2‚ü©
    exact ‚ü®x.1.asIdeal.primeCompl.mul_mem hsax hsbx,
      congr($hsa * $hsb).trans (LocalizedModule.mk_mul_mk ..)‚ü©
  algebraMap_mem' r x :=
    ‚ü®U, x.2, ùüô _, algebraMap R A r, 1, fun y ‚Ü¶ ‚ü®by simp [Ideal.IsPrime.one_notMem], rfl‚ü©‚ü©

set_option backward.isDefEq.respectTransparency false in
variable (M) in
/-- The functions satisfying `isLocallyFraction` form a submodule. -/
def sectionsSubalgebraSubmodule (U : (Opens (PrimeSpectrum.Top R))) :
    Submodule (sectionsSubalgebra R U) (Œ† x : U, Localizations M x.1) where
  __ := sectionsSubmodule M U
  smul_mem' r {a} ha x := by
    obtain ‚ü®V, hxV, hVU, rx, rs, hr‚ü© := r.2 x
    obtain ‚ü®W, hxW, hWU, ax, as, ha‚ü© := ha x
    refine ‚ü®V ‚äì W, ‚ü®hxV, hxW‚ü©, homOfLE (inf_le_left.trans hVU.le), rx ‚Ä¢ ax, as * rs, fun y ‚Ü¶ ?_‚ü©
    obtain ‚ü®hrsy, hry‚ü© := hr ‚ü®y.1, y.2.1‚ü©
    obtain ‚ü®hasy, hay‚ü© := ha ‚ü®y.1, y.2.2‚ü©
    exact ‚ü®y.1.asIdeal.primeCompl.mul_mem hasy hrsy, congr($hry ‚Ä¢ $hay)‚ü©

end StructureSheaf

open StructureSheaf

variable (R M) in
/-- The structure sheaf (valued in `Type`, not yet `CommRingCat`) is the subsheaf consisting of
functions satisfying `isLocallyFraction`. -/
def structureSheafInType : Sheaf (Type u) (PrimeSpectrum.Top R) :=
  subsheafToTypes (isLocallyFraction R M)

instance (U : (Opens (PrimeSpectrum.Top R))·µí·µñ) :
    AddCommGroup ((structureSheafInType R M).val.obj U) :=
  (sectionsSubmodule M U.unop).toAddSubgroup.toAddCommGroup

instance (U : (Opens (PrimeSpectrum.Top R))·µí·µñ) :
    Module R ((structureSheafInType R M).val.obj U) :=
  (sectionsSubmodule M U.unop).module

instance (U : (Opens (PrimeSpectrum.Top R))·µí·µñ) :
    CommRing ((structureSheafInType R A).val.obj U) :=
  (sectionsSubalgebra A U.unop).toCommRing

instance (U : (Opens (PrimeSpectrum.Top R))·µí·µñ) :
    Algebra R ((structureSheafInType R A).val.obj U) :=
  (sectionsSubalgebra A U.unop).algebra

local notation "Œì("M", "U")" => (Functor.obj (Sheaf.val (structureSheafInType _ M))) (Opposite.op U)

@[simp]
lemma structureSheafInType.add_apply {U : Opens (PrimeSpectrum.Top R)} (s t : Œì(M, U)) (x : U) :
  (s + t).1 x = s.1 x + t.1 x := rfl

@[simp]
lemma structureSheafInType.mul_apply {U : Opens (PrimeSpectrum.Top R)} (s t : Œì(A, U)) (x : U) :
  (s * t).1 x = s.1 x * t.1 x := rfl

@[simp]
lemma structureSheafInType.smul_apply {U : Opens (PrimeSpectrum.Top R)}
    (r : R) (s : Œì(M, U)) (x : U) :
  (r ‚Ä¢ s).1 x = r ‚Ä¢ s.1 x := rfl

variable (R M) in
/-- The structure presheaf, valued in `ModuleCat`, constructed by dressing up the `Type`-valued
structure presheaf. -/
@[simps obj_carrier]
def structurePresheafInModuleCat : Presheaf (ModuleCat R) (PrimeSpectrum.Top R) where
  obj U := ModuleCat.of R ((structureSheafInType R M).1.obj U)
  map i := ModuleCat.ofHom
    { toFun := (structureSheafInType R M).1.map i
      map_add' _ _ := rfl
      map_smul' _ _ := rfl }

variable (R) in
/-- The structure presheaf, valued in `CommRingCat`, constructed by dressing up the `Type`-valued
structure presheaf. -/
@[simps obj_carrier]
def structurePresheafInCommRingCat : Presheaf CommRingCat (PrimeSpectrum.Top R) where
  obj U := .of ((structureSheafInType R R).1.obj U)
  map i := CommRingCat.ofHom
    { toFun := (structureSheafInType R R).1.map i
      map_add' _ _ := rfl
      map_mul' _ _ := rfl
      map_one' := rfl
      map_zero' := rfl }

set_option backward.isDefEq.respectTransparency false in
instance (U : (Opens (PrimeSpectrum.Top R))·µí·µñ) :
    Module ((structureSheafInType R R).val.obj U) ((structureSheafInType R M).val.obj U) :=
  inferInstanceAs (Module (sectionsSubalgebra R _) (sectionsSubalgebraSubmodule M _))

instance (U : (Opens (PrimeSpectrum.Top R))·µí·µñ) :
    IsScalarTower R ((structureSheafInType R R).val.obj U) ((structureSheafInType R M).val.obj U) :=
  .of_algebraMap_smul fun r m ‚Ü¶ Subtype.ext <| funext fun x ‚Ü¶
    IsScalarTower.algebraMap_smul (Localizations R x.1) r (m.1 x)

set_option backward.isDefEq.respectTransparency false in
variable (R M) in
/-- The structure sheaf of a module as a presheaf of modules on `Spec R`.
We will later package this into a `Scheme.Modules` in `Tilde.lean`. -/
def moduleStructurePresheaf : PresheafOfModules (structurePresheafInCommRingCat R ‚ãô forget‚ÇÇ _ _) :=
  letI (X : (Opens ‚Üë(PrimeSpectrum.Top R))·µí·µñ) :
    Module ‚Üë((structurePresheafInCommRingCat R ‚ãô forget‚ÇÇ CommRingCat RingCat).obj X)
      ‚Üë((structurePresheafInModuleCat R M ‚ãô forget‚ÇÇ (ModuleCat R) Ab).obj X) := by
    dsimp; infer_instance
  .ofPresheaf (structurePresheafInModuleCat R M ‚ãô forget‚ÇÇ _ _) fun X Y f r m ‚Ü¶ rfl

variable (R) in
/-- Some glue, verifying that the structure presheaf valued in `CommRingCat` agrees
with the `Type`-valued structure presheaf. -/
def structurePresheafCompForget :
    structurePresheafInCommRingCat R ‚ãô forget CommRingCat ‚âÖ (structureSheafInType R R).1 :=
  NatIso.ofComponents fun _ => Iso.refl _

open TopCat.Presheaf

open PrimeSpectrum

open TopCat.Presheaf

namespace StructureSheaf

@[simp]
theorem res_apply (U V : Opens (PrimeSpectrum.Top R)) (i : V ‚ü∂ U)
    (s : Œì(M, U)) (x : V) : ((structureSheafInType R M).1.map i.op s).1 x = s.1 (i x) :=
  rfl

/-- The section of `structureSheaf R` on an open `U` sending each `x ‚àà U` to the element
`f/g` in the localization of `R` at `x`. -/
def const (f : M) (g : R) (U : Opens (PrimeSpectrum.Top R))
    (hu : U ‚â§ basicOpen g) :
    Œì(M, U) :=
  ‚ü®fun x => .mk f ‚ü®g, hu x.2‚ü©, fun x ‚Ü¶ ‚ü®U, x.2, ùüô _, f, g, fun y ‚Ü¶ ‚ü®hu y.2, rfl‚ü©‚ü©‚ü©

@[simp]
theorem const_apply (f : M) (g : R) (U : Opens (PrimeSpectrum.Top R))
    (hu : ‚àÄ x ‚àà U, g ‚àà (x : PrimeSpectrum.Top R).asIdeal.primeCompl) (x : U) :
    (const f g U hu).1 x = .mk f ‚ü®g, hu x x.2‚ü© :=
  rfl

theorem exists_const (U) (s : Œì(M, U)) (x : PrimeSpectrum.Top R)
    (hx : x ‚àà U) :
    ‚àÉ (g : R) (_ : x ‚àà basicOpen g) (i : basicOpen g ‚â§ U) (f : M),
      const f g _ le_rfl = (structureSheafInType R M).1.map i.hom.op s := by
  obtain ‚ü®V, hxV, iVU, f, g, hfg‚ü© := s.2 ‚ü®x, hx‚ü©
  obtain ‚ü®_, ‚ü®_, ‚ü®g', rfl‚ü©, rfl‚ü©, hxg', hg'U‚ü© :=
    PrimeSpectrum.isBasis_basic_opens.exists_subset_of_mem_open hxV V.2
  refine ‚ü®g' * g, ?_, ?_, g' ‚Ä¢ f, Subtype.ext <| funext fun ‚ü®y, hy‚ü© ‚Ü¶ ?_‚ü© <;>
    simp only [PrimeSpectrum.basicOpen_mul]
  ¬∑ exact ‚ü®hxg', (hfg ‚ü®x, hxV‚ü©).1‚ü©
  ¬∑ exact inf_le_left.trans (hg'U.trans iVU.le)
  ¬∑ rw [PrimeSpectrum.basicOpen_mul] at hy
    obtain ‚ü®hgy, H‚ü© := hfg ‚ü®y, hg'U hy.1‚ü©
    refine (LocalizedModule.mk_eq.mpr ‚ü®1, ?_‚ü©).trans H.symm
    simp [Submonoid.smul_def, ‚Üê smul_assoc]; ring_nf

@[simp]
theorem res_const (f : M) (g : R) (U hu V hv i) :
    (structureSheafInType R M).1.map i (const f g U hu) = const f g V hv :=
  rfl

@[simp]
theorem const_zero (f : R) (U hu) : const (0 : M) f U hu = 0 :=
  Subtype.ext <| funext fun x ‚Ü¶ by simp; rfl

@[simp]
theorem const_algebraMap (f : R) (U hu) : const (algebraMap R A f) f U hu = 1 :=
  Subtype.ext <| funext fun _ ‚Ü¶ (LocalizedModule.mk_eq.mpr
      ‚ü®1, by simp [Algebra.smul_def, Submonoid.smul_def]‚ü©).trans
    OreLocalization.one_def.symm

@[simp]
theorem const_self (f : R) (U hu) : const f f U hu = 1 :=
  const_algebraMap ..

set_option backward.isDefEq.respectTransparency false in
@[simp]
theorem const_one (U) : const (1 : A) (1 : R) U (by simp) = 1 := by
  simpa using const_algebraMap 1 (A := A) U

set_option backward.isDefEq.respectTransparency false in
theorem const_add (f‚ÇÅ f‚ÇÇ : M) (g‚ÇÅ g‚ÇÇ : R) (U hu‚ÇÅ hu‚ÇÇ) :
    const f‚ÇÅ g‚ÇÅ U hu‚ÇÅ + const f‚ÇÇ g‚ÇÇ U hu‚ÇÇ =
      const (g‚ÇÇ ‚Ä¢ f‚ÇÅ + g‚ÇÅ ‚Ä¢ f‚ÇÇ) (g‚ÇÅ * g‚ÇÇ) U (by simp [*, PrimeSpectrum.basicOpen_mul]) :=
  Subtype.ext <| funext fun _ ‚Ü¶ LocalizedModule.mk_add_mk

theorem smul_const (f : M) (r g : R) (U hu) :
    r ‚Ä¢ const f g U hu = const (r ‚Ä¢ f) g U hu :=
  Subtype.ext <| funext fun _ ‚Ü¶ LocalizedModule.smul'_mk _ _ _

set_option backward.isDefEq.respectTransparency false in
theorem const_mul (f‚ÇÅ f‚ÇÇ : A) (g‚ÇÅ g‚ÇÇ : R) (U hu‚ÇÅ hu‚ÇÇ) :
    const f‚ÇÅ g‚ÇÅ U hu‚ÇÅ * const f‚ÇÇ g‚ÇÇ U hu‚ÇÇ =
      const (f‚ÇÅ * f‚ÇÇ) (g‚ÇÅ * g‚ÇÇ) U (by simp [*, PrimeSpectrum.basicOpen_mul]) :=
  Subtype.ext <| funext fun _ ‚Ü¶ LocalizedModule.mk_mul_mk

theorem const_ext {f‚ÇÅ f‚ÇÇ : M} {g‚ÇÅ g‚ÇÇ : R} {U hu‚ÇÅ hu‚ÇÇ} (h : g‚ÇÇ ‚Ä¢ f‚ÇÅ = g‚ÇÅ ‚Ä¢ f‚ÇÇ) :
    const f‚ÇÅ g‚ÇÅ U hu‚ÇÅ = const f‚ÇÇ g‚ÇÇ U hu‚ÇÇ :=
  Subtype.ext <| funext fun x ‚Ü¶ LocalizedModule.mk_eq.mpr (by simp [h, Submonoid.smul_def])

theorem const_congr {f‚ÇÅ f‚ÇÇ : M} {g‚ÇÅ g‚ÇÇ : R} {U hu} (hf : f‚ÇÅ = f‚ÇÇ) (hg : g‚ÇÅ = g‚ÇÇ) :
    const f‚ÇÅ g‚ÇÅ U hu = const f‚ÇÇ g‚ÇÇ U (hg ‚ñ∏ hu) := by substs hf hg; rfl

theorem const_mul_rev (f g : R) (U hu‚ÇÅ hu‚ÇÇ) : const f g U hu‚ÇÅ * const g f U hu‚ÇÇ = 1 := by
  rw [const_mul, const_congr rfl (mul_comm g f), const_self]

theorem const_mul_cancel (f g‚ÇÅ g‚ÇÇ : R) (U hu‚ÇÅ hu‚ÇÇ) :
    const f g‚ÇÅ U hu‚ÇÅ * const g‚ÇÅ g‚ÇÇ U hu‚ÇÇ = const f g‚ÇÇ U hu‚ÇÇ := by
  rw [const_mul, const_ext]; simp; ring

theorem const_mul_cancel' (f g‚ÇÅ g‚ÇÇ : R) (U hu‚ÇÅ hu‚ÇÇ) :
    const g‚ÇÅ g‚ÇÇ U hu‚ÇÇ * const f g‚ÇÅ U hu‚ÇÅ = const f g‚ÇÇ U hu‚ÇÇ := by
  rw [mul_comm, const_mul_cancel]

theorem const_eq_const_of_smul_eq_smul (f‚ÇÅ f‚ÇÇ : M) (g‚ÇÅ g‚ÇÇ : R) (U hu‚ÇÅ hu‚ÇÇ) (H : g‚ÇÅ ‚Ä¢ f‚ÇÇ = g‚ÇÇ ‚Ä¢ f‚ÇÅ) :
    const f‚ÇÅ g‚ÇÅ U hu‚ÇÅ = const f‚ÇÇ g‚ÇÇ U hu‚ÇÇ :=
  Subtype.ext (funext fun x ‚Ü¶ by
    simp [LocalizedModule.mk_eq, Localizations, Submonoid.smul_def, H])

set_option backward.isDefEq.respectTransparency false in
variable (R M) in
/-- The canonical linear map interpreting an element of `M` as
a section of the structure sheaf. -/
def toOpen‚Çó (U : Opens (PrimeSpectrum.Top R)) :
    M ‚Üí‚Çó[R] Œì(M, U) where
  toFun m := const m 1 U (by simp)
  map_add' _ _ := by simp [const_add]
  map_smul' _ _ := by simp [smul_const]

set_option backward.isDefEq.respectTransparency false in
theorem toOpen‚Çó_eq_const (U : Opens (PrimeSpectrum.Top R)) (f : M) :
    toOpen‚Çó R M U f = const f 1 U (by simp) := rfl

end StructureSheaf

end Public

local notation "Œì("M", "U")" => (Functor.obj (Sheaf.val (structureSheafInType _ M))) (Opposite.op U)

namespace StructureSheaf

section basicOpen

set_option backward.isDefEq.respectTransparency false in
lemma isUnit_basicOpen (f : R) :
    IsUnit ((algebraMap R Œì(R, basicOpen f)) f) :=
  isUnit_iff_exists_inv.mpr ‚ü®const 1 f _ le_rfl, const_mul_rev _ _ _ (by simp) _‚ü©

set_option backward.isDefEq.respectTransparency false in
lemma isUnit_basicOpen_end (f : R) :
    IsUnit ((algebraMap R (Module.End R Œì(M, basicOpen f))) f) := by
  have := (isUnit_basicOpen f).map
    (algebraMap _ (Module.End Œì(R, basicOpen f) Œì(M, basicOpen f)))
  rw [Module.End.isUnit_iff] at this ‚ä¢
  convert this
  ext a
  simp

variable (R M) in
/-- The canonical linear map interpreting `s ‚àà M_f` as a section of the structure sheaf
on the basic open defined by `f ‚àà R`. -/
def toBasicOpen‚Çó (f : R) :
    LocalizedModule (.powers f) M ‚Üí‚Çó[R] Œì(M, PrimeSpectrum.basicOpen f) :=
  IsLocalizedModule.lift (.powers f) (LocalizedModule.mkLinearMap ..) (toOpen‚Çó R M _) <| by
    simp only [Subtype.forall]
    exact Submonoid.powers_le (P := (IsUnit.submonoid _).comap (algebraMap R _)).mpr
      (isUnit_basicOpen_end ..)

@[simp]
theorem toBasicOpen‚Çó_mk (s : R) (f : M) (g : Submonoid.powers s) :
    toBasicOpen‚Çó R M s (.mk f g) = const f g.1 (basicOpen s) (by
    have := PrimeSpectrum.le_basicOpen_pow s; aesop (add simp [Submonoid.mem_powers_iff])) := by
  obtain ‚ü®_, n, rfl‚ü© := g
  apply ((Module.End.isUnit_iff _).mp ((isUnit_basicOpen_end ..).pow n)).1 ?_
  rw [‚Üê map_pow]
  dsimp [toBasicOpen‚Çó]
  rw [‚Üê map_smul, LocalizedModule.smul'_mk, ‚Üê Submonoid.mk_smul (S := .powers s) _ ‚ü®n, rfl‚ü©,
    LocalizedModule.mk_cancel, ‚Üê LocalizedModule.mkLinearMap_apply, IsLocalizedModule.lift_apply,
    smul_const]
  dsimp [toOpen‚Çó]
  exact const_eq_const_of_smul_eq_smul (H := by simp) ..

theorem toBasicOpen‚Çó_injective (f : R) : Function.Injective (toBasicOpen‚Çó R M f) := by
  intro s t h_eq
  induction s using LocalizedModule.induction_on with | h a b =>
  induction t using LocalizedModule.induction_on with | h c d =>
  suffices f ‚àà ((‚ä• : Submodule R M).colon {d ‚Ä¢ a - b ‚Ä¢ c}).radical by
    rw [LocalizedModule.mk_eq]
    obtain ‚ü®n, hn‚ü© := this
    exact ‚ü®‚ü®f ^ n, n, rfl‚ü©, by simpa [sub_eq_zero, smul_sub] using Submodule.mem_colon.mp hn _ rfl‚ü©
  simp only [toBasicOpen‚Çó_mk] at h_eq
  rw [‚Üê PrimeSpectrum.vanishingIdeal_zeroLocus_eq_radical, PrimeSpectrum.mem_vanishingIdeal]
  intro p hfp
  contrapose hfp
  obtain ‚ü®u, hu‚ü© := LocalizedModule.mk_eq.mp congr(($h_eq).1 ‚ü®p, hfp‚ü©)
  rw [PrimeSpectrum.mem_zeroLocus, Set.not_subset]
  exact ‚ü®u.1, by simpa [sub_eq_zero, smul_sub], u.2‚ü©

set_option backward.isDefEq.respectTransparency false in
/-
Auxiliary lemma for surjectivity of `toBasicOpen`.
A local representation of a section `s` as fractions `a i / h i` on finitely many basic opens
`basicOpen (h i)` can be "normalized" in such a way that `a i * h j = h i * a j` for all `i, j`
-/
theorem exists_le_iSup_basicOpen_and_smul_eq_smul_and_eq_const
    (U : Opens (PrimeSpectrum.Top R)) (hU : IsCompact (U : Set (PrimeSpectrum.Top R)))
    (s : Œì(M, U)) :
    ‚àÉ (Œπ : Type u) (_ : Fintype Œπ) (a : Œπ ‚Üí M) (b : Œπ ‚Üí R) (ibU : ‚àÄ i, basicOpen (b i) ‚â§ U),
      (U ‚â§ ‚®Ü i, basicOpen (b i)) ‚àß (‚àÄ i j, b j ‚Ä¢ a i = b i ‚Ä¢ a j) ‚àß
          ‚àÄ i, (structureSheafInType R M).presheaf.map (ibU i).hom.op s =
              const (a i) (b i) _ le_rfl := by
  choose g hxg igU f H using fun x : U ‚Ü¶ exists_const U s x.1 x.2
  have (i j : _) : LocalizedModule.mk (g i ‚Ä¢ f j) ‚ü®g i * g j, Submonoid.mem_powers _‚ü© =
      LocalizedModule.mk (g j ‚Ä¢ f i) ‚ü®g i * g j, Submonoid.mem_powers _‚ü© := by
    refine toBasicOpen‚Çó_injective (g i * g j) ?_
    simp only [toBasicOpen‚Çó_mk]
    have := H i
    trans (structureSheafInType R M).val.map (homOfLE ?_).op s
    ¬∑ refine .trans (Subtype.ext <| funext fun a ‚Ü¶ ?_) congr((structureSheafInType R M).val.map
        (homOfLE ((PrimeSpectrum.basicOpen_mul (g i) (g j)).trans_le inf_le_right)).op $(H j))
      exact LocalizedModule.mk_eq.mpr ‚ü®1, by simp [Submonoid.smul_def, ‚Üê smul_assoc]; ring_nf‚ü©
    ¬∑ refine congr((structureSheafInType R M).val.map (homOfLE ((PrimeSpectrum.basicOpen_mul (g i)
        (g j)).trans_le inf_le_left)).op $(H i)).symm.trans (Subtype.ext <| funext fun a ‚Ü¶ ?_)
      exact LocalizedModule.mk_eq.mpr ‚ü®1, by simp [Submonoid.smul_def, ‚Üê smul_assoc]‚ü©
    ¬∑ exact ((PrimeSpectrum.basicOpen_mul (g i) (g j)).trans_le inf_le_right).trans (igU _)
  simp only [LocalizedModule.mk_eq, Submonoid.smul_def, Subtype.exists, Submonoid.mem_powers_iff,
    exists_prop, exists_exists_eq_and, ‚Üê mul_smul, ‚Üê pow_succ, ‚Üê mul_assoc _ (_ * _)] at this
  choose n hn using this
  obtain ‚ü®t, ht‚ü© := hU.elim_finite_subcover (fun i ‚Ü¶ (basicOpen (g i) : Set (PrimeSpectrum R)))
    (fun _ ‚Ü¶ (basicOpen _).2) (fun x hx ‚Ü¶ Set.mem_iUnion_of_mem ‚ü®x, hx‚ü© (hxg _))
  let N := (t √óÀ¢ t).sup fun x ‚Ü¶ n x.1 x.2 + 1
  refine ‚ü®t, inferInstance, fun i ‚Ü¶ g i ^ N ‚Ä¢ f i, fun i ‚Ü¶ (g i) ^ (N + 1),
    fun x ‚Ü¶ by simpa using igU x.1, fun x hx ‚Ü¶ by simpa using ht hx, fun i j ‚Ü¶ ?_, fun i ‚Ü¶ ?_‚ü©
  ¬∑ dsimp
    convert_to (g i * g ‚Üëj) ^ N ‚Ä¢ g j ‚Ä¢ f i = (g i * g ‚Üëj) ^ N ‚Ä¢ g i ‚Ä¢ f j
    ¬∑ module
    ¬∑ module
    have : n i j + 1 ‚â§ N := (t √óÀ¢ t).le_sup (f := fun x ‚Ü¶ n x.1 x.2 + 1) (b := ‚ü®_, _‚ü©) (by simp)
    rw [‚Üê Nat.sub_add_cancel this, pow_add, mul_smul, mul_smul]
    congr 1
    convert (hn i j).symm using 1 <;> module
  ¬∑ convert congr((structureSheafInType R M).presheaf.map (homOfLE ?_).op $((H i).symm)) using 1
    ¬∑ refine Subtype.ext <| funext fun x ‚Ü¶ LocalizedModule.mk_eq.mpr ‚ü®1, ?_‚ü©
      simp [Submonoid.smul_def, pow_succ', mul_smul]
    ¬∑ simp

set_option backward.isDefEq.respectTransparency false in
theorem toBasicOpen‚Çó_surjective (f : R) : Function.Surjective (toBasicOpen‚Çó R M f) := by
  intro s
  obtain ‚ü®Œπ, _, a, b, ibU, iU, hab, H‚ü© := exists_le_iSup_basicOpen_and_smul_eq_smul_and_eq_const _
    (PrimeSpectrum.isCompact_basicOpen _) s
  obtain ‚ü®n, hn‚ü© : f ‚àà (Ideal.span (Set.range b)).radical := by
    have : PrimeSpectrum.zeroLocus (Set.range b) ‚äÜ PrimeSpectrum.zeroLocus {f} := by
      simpa [‚Üê SetLike.coe_subset_coe, ‚Üê Set.compl_iInter,
        ‚Üê PrimeSpectrum.zeroLocus_iUnion, PrimeSpectrum.Top] using iU
    rw [‚Üê PrimeSpectrum.vanishingIdeal_zeroLocus_eq_radical, PrimeSpectrum.zeroLocus_span,
      PrimeSpectrum.mem_vanishingIdeal]
    exact fun x hx ‚Ü¶ by simpa using this hx
  replace hn := Ideal.mul_mem_right f _ hn
  rw [‚Üê pow_succ, Ideal.span, Finsupp.mem_span_range_iff_exists_finsupp] at hn
  obtain ‚ü®c, hc‚ü© := hn
  rw [Finsupp.sum_fintype _ _ (by simp)] at hc
  refine ‚ü®LocalizedModule.mk (‚àë i, c i ‚Ä¢ a i) ‚ü®f ^ (n + 1), _, rfl‚ü©, ?_‚ü©
  refine (structureSheafInType R M).eq_of_locally_eq' (fun i ‚Ü¶ basicOpen (b i)) _
    (fun i ‚Ü¶ (ibU _).hom) iU _ _ fun i ‚Ü¶ (Subtype.ext (funext fun x ‚Ü¶ ?_)).trans (H _).symm
  rw [toBasicOpen‚Çó_mk]
  refine LocalizedModule.mk_eq.mpr ‚ü®1, ?_‚ü©
  simp_rw [one_smul, Finset.smul_sum, Submonoid.smul_def, smul_comm (b i), hab _ i, ‚Üê smul_assoc,
    ‚Üê Finset.sum_smul, hc]

public instance (f : R) : IsLocalizedModule (.powers f) (toOpen‚Çó R M (basicOpen f)) := by
  convert IsLocalizedModule.of_linearEquiv (.powers f) (LocalizedModule.mkLinearMap (.powers f) M)
    (.ofBijective _ ‚ü®toBasicOpen‚Çó_injective _, toBasicOpen‚Çó_surjective _‚ü©)
  ext x
  simp [toOpen‚Çó]

instance isIso_toBasicOpen‚Çó (f : R) :
    IsIso (ModuleCat.ofHom (toBasicOpen‚Çó R M f)) :=
  (ConcreteCategory.isIso_iff_bijective _).mpr ‚ü®toBasicOpen‚Çó_injective _, toBasicOpen‚Çó_surjective _‚ü©

set_option backward.isDefEq.respectTransparency false in
public lemma toOpen‚Çó_top_bijective : Function.Bijective (toOpen‚Çó R M ‚ä§) := by
  have : IsLocalizedModule ‚ä• (toOpen‚Çó R M ‚ä§) := by
    convert inferInstanceAs (IsLocalizedModule (.powers 1) (toOpen‚Çó R M (basicOpen 1)))
    rw [PrimeSpectrum.basicOpen_one, Submonoid.powers_one]
  refine ‚ü®fun x y e ‚Ü¶ by simpa using (IsLocalizedModule.eq_iff_exists ‚ä• _).mp e, fun x ‚Ü¶ ?_‚ü©
  obtain ‚ü®‚ü®x, _, rfl‚ü©, rfl‚ü© := IsLocalizedModule.mk'_surjective ‚ä• (toOpen‚Çó R M ‚ä§) x
  exact ‚ü®x, (IsLocalizedModule.mk'_one ..).symm‚ü©

public lemma algebraMap_obj_top_bijective :
    Function.Bijective (algebraMap R Œì(R, (‚ä§ : Opens (PrimeSpectrum.Top R)))) :=
  toOpen‚Çó_top_bijective

public instance (f : R) : IsLocalization.Away f Œì(R, basicOpen f) :=
  (isLocalizedModule_iff_isLocalization' _ _).mp <|
    inferInstanceAs (IsLocalizedModule (.powers f) (toOpen‚Çó R R (basicOpen f)))

end basicOpen

section Stalk

variable (R) in
/-- The canonical ring homomorphism interpreting an element of `R` as an element of
the stalk of `structureSheaf R` at `x`. -/
@[expose] public def toStalk (x : PrimeSpectrum.Top R) :
    CommRingCat.of R ‚ü∂ (structurePresheafInCommRingCat R).stalk x :=
  CommRingCat.ofHom (algebraMap _ _) ‚â´ (structurePresheafInCommRingCat R).germ ‚ä§ x trivial

set_option backward.isDefEq.respectTransparency false in
@[elementwise, reassoc]
public lemma algebraMap_germ
    (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hxU : x ‚àà U) :
    CommRingCat.ofHom (algebraMap R Œì(R, U)) ‚â´ (structurePresheafInCommRingCat R).germ U x hxU =
      toStalk R x := by
  dsimp [toStalk]
  rw [‚Üê (structurePresheafInCommRingCat R).germ_res (homOfLE (le_top : U ‚â§ ‚ä§)) _ hxU]
  rfl

@[deprecated (since := "2026-02-10")] public alias toOpen_germ := algebraMap_germ

@[expose] public
instance (x : PrimeSpectrum.Top R) : Algebra R ((structurePresheafInCommRingCat R).stalk x) :=
  (toStalk R x).hom.toAlgebra

@[expose] public
instance (x : PrimeSpectrum.Top R) :
    Module R ‚Üë(TopCat.Presheaf.stalk (moduleStructurePresheaf R M).presheaf x) :=
  .compHom _ (toStalk R x).hom

variable (M) in
def germ‚Çó (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hxU : x ‚àà U) :
    Œì(M, U) ‚Üí‚Çó[R] ‚Üë(TopCat.Presheaf.stalk (moduleStructurePresheaf R M).presheaf x) where
  __ := (TopCat.Presheaf.germ (moduleStructurePresheaf R M).presheaf U x hxU).hom
  map_smul' r m := by
    change _ = toStalk R x _ ‚Ä¢ TopCat.Presheaf.germ (moduleStructurePresheaf R M).presheaf _ _ _ _
    rw [‚Üê algebraMap_germ_apply U x hxU]
    refine .trans ?_ (PresheafOfModules.germ_smul ..)
    congr 1
    exact (IsScalarTower.algebraMap_smul Œì(R, U) r m).symm

public
instance (x : PrimeSpectrum.Top R) :
    IsScalarTower R ((structurePresheafInCommRingCat R).stalk x)
      ‚Üë(TopCat.Presheaf.stalk (moduleStructurePresheaf R M).presheaf x) :=
  .of_algebraMap_smul fun _ _ ‚Ü¶ rfl

variable (R M) in
def modulePresheafStalkIso (x : PrimeSpectrum.Top R) :
    ‚Üë(TopCat.Presheaf.stalk (moduleStructurePresheaf R M).presheaf x) ‚âÉ‚Çó[R]
      (structurePresheafInModuleCat R M).stalk x where
  __ := (Limits.colimit.isoColimitCocone ‚ü®_, Limits.isColimitOfPreserves (forget‚ÇÇ (ModuleCat R) Ab)
    (Limits.colimit.isColimit ((OpenNhds.inclusion x).op ‚ãô
      structurePresheafInModuleCat R M))‚ü©:).addCommGroupIsoToAddEquiv
  map_smul' r m := by
    let Œ± : TopCat.Presheaf.stalk (moduleStructurePresheaf R M).presheaf x ‚âÖ
      (forget‚ÇÇ _ _).obj ((structurePresheafInModuleCat R M).stalk x) :=
      Limits.colimit.isoColimitCocone ‚ü®_, Limits.isColimitOfPreserves (forget‚ÇÇ (ModuleCat R) Ab)
      (Limits.colimit.isColimit ((OpenNhds.inclusion x).op ‚ãô
        structurePresheafInModuleCat R M))‚ü©
    obtain ‚ü®U, hxU, s, rfl‚ü© := TopCat.Presheaf.germ_exist _ _ m
    have : TopCat.Presheaf.germ (moduleStructurePresheaf R M).presheaf U x hxU ‚â´ Œ±.hom =
        (forget‚ÇÇ _ _).map ((structurePresheafInModuleCat R M).germ U x hxU) :=
      Limits.colimit.isoColimitCocone_Œπ_hom (C := Ab) ..
    have (m : _) : Œ±.hom (TopCat.Presheaf.germ (moduleStructurePresheaf R M).presheaf U x hxU m) =
        (structurePresheafInModuleCat R M).germ U x hxU m := congr($this m)
    change Œ±.hom (r ‚Ä¢ germ‚Çó M U x hxU _) =
      r ‚Ä¢ (show (structurePresheafInModuleCat R M).stalk x from _)
    rw [‚Üê map_smul]
    refine (this _).trans ?_
    dsimp [toStalk]
    erw [this]
    exact ((structurePresheafInModuleCat R M).germ U x hxU).hom.map_smul _ _

instance (x : PrimeSpectrum.Top R) :
    Module ((structurePresheafInCommRingCat R).stalk x)
      ((structurePresheafInModuleCat R M).stalk x) :=
  (modulePresheafStalkIso R M x).toAddEquiv.symm.module _

lemma toStalk_smul (x : PrimeSpectrum.Top R) (r : R)
    (m : (structurePresheafInModuleCat R M).stalk x) :
    toStalk R x r ‚Ä¢ m = r ‚Ä¢ m := by
  change modulePresheafStalkIso R M x (toStalk R x r ‚Ä¢ (modulePresheafStalkIso R M x).symm m) = _
  rw [‚Üê (modulePresheafStalkIso R M x).eq_symm_apply, map_smul]
  rfl

variable (R M) in
/-- The canonical ring homomorphism interpreting an element of `R` as an element of
the stalk of `structureSheaf R` at `x`. -/
def toStalk‚Çó' (x : PrimeSpectrum.Top R) :
    ModuleCat.of R M ‚ü∂ (structurePresheafInModuleCat R M).stalk x :=
  ModuleCat.ofHom (toOpen‚Çó R M ‚ä§) ‚â´ (structurePresheafInModuleCat R M).germ _ x trivial

set_option backward.isDefEq.respectTransparency false in
@[simp]
theorem toOpen‚Çó_germ (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hx : x ‚àà U) :
    ModuleCat.ofHom (toOpen‚Çó R M U) ‚â´
      (structurePresheafInModuleCat R M).germ U x hx = toStalk‚Çó' R M x := by
  rw [toStalk‚Çó', ‚Üê Presheaf.germ_res _ (homOfLE le_top) _ hx, ‚Üê Category.assoc]
  rfl

set_option backward.isDefEq.respectTransparency false in
theorem isUnit_toStalk (x : PrimeSpectrum.Top R) (f : R) (hf : x ‚àà basicOpen f) :
    IsUnit (toStalk R x f) := by
  convert (isUnit_basicOpen f).map ((structurePresheafInCommRingCat R).germ _ x hf).hom
  exact ((structurePresheafInCommRingCat R).germ_res_apply (homOfLE (le_top : basicOpen f ‚â§ ‚ä§))
    x hf (algebraMap R Œì(R, ‚ä§) f)).symm

theorem isUnit_toStalk‚Çó' (x : PrimeSpectrum.Top R) (f : R) (hf : x ‚àà basicOpen f) :
    IsUnit (algebraMap R (Module.End R ((structurePresheafInModuleCat R M).stalk x)) f) := by
  have := (isUnit_toStalk x f hf).map (algebraMap _
    (Module.End ((structurePresheafInCommRingCat R).stalk x)
      ((structurePresheafInModuleCat R M).stalk x)))
  rw [Module.End.isUnit_iff] at this ‚ä¢
  convert this
  ext a
  simp only [Module.algebraMap_end_apply]
  rw [toStalk_smul]

variable (R M) in
/-- The canonical ring homomorphism from the localization of `R` at `p` to the stalk
of the structure sheaf at the point `p`. -/
def localizationtoStalk‚Çó (x : PrimeSpectrum.Top R) :
    ModuleCat.of R (LocalizedModule x.asIdeal.primeCompl M) ‚ü∂
      (structurePresheafInModuleCat R M).stalk x :=
  ModuleCat.ofHom (IsLocalizedModule.lift x.asIdeal.primeCompl
    (LocalizedModule.mkLinearMap x.asIdeal.primeCompl M)
    (toStalk‚Çó' R M x).hom fun f ‚Ü¶ isUnit_toStalk‚Çó' x f.1 f.2:)

set_option backward.isDefEq.respectTransparency false in
@[simp]
theorem localizationtoStalk‚Çó_mk (x : PrimeSpectrum.Top R) (f : M) (s) :
    localizationtoStalk‚Çó R M x (.mk f s) = (structurePresheafInModuleCat R M).germ
      (PrimeSpectrum.basicOpen (s : R)) x s.2 (const f (s : R) _ fun _ ‚Ü¶ id) := by
  apply ((Module.End.isUnit_iff _).mp (isUnit_toStalk‚Çó' _ s.1 s.2)).1 ?_
  dsimp [localizationtoStalk‚Çó]
  rw [‚Üê map_smul, LocalizedModule.smul'_mk, ‚Üê Submonoid.smul_def, LocalizedModule.mk_cancel,
    ‚Üê LocalizedModule.mkLinearMap_apply, IsLocalizedModule.lift_apply, ‚Üê map_smul,
    ‚Üê toOpen‚Çó_germ (basicOpen ‚Üës) _ s.2, smul_const]
  dsimp [toOpen‚Çó]
  congr 1
  exact const_eq_const_of_smul_eq_smul (H := by simp) ..

variable (R M) in
/-- The ring homomorphism that takes a section of the structure sheaf of `R` on the open set `U`,
implemented as a subtype of dependent functions to localizations at prime ideals, and evaluates
the section on the point corresponding to a given prime ideal. -/
def openToLocalization‚Çó (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hx : x ‚àà U) :
    (structurePresheafInModuleCat R M).obj (op U) ‚ü∂
      .of R (LocalizedModule x.asIdeal.primeCompl M) :=
  ModuleCat.ofHom
  { toFun s := s.1 ‚ü®x, hx‚ü©
    map_smul' _ _ := rfl
    map_add' _ _ := rfl }

variable (R M) in
/-- The ring homomorphism from the stalk of the structure sheaf of `R` at a point corresponding to
a prime ideal `p` to the localization of `R` at `p`,
formed by gluing the `openToLocalization` maps. -/
def stalkToLocalization‚Çó (x : PrimeSpectrum.Top R) :
    (structurePresheafInModuleCat R M).stalk x ‚ü∂ .of R (LocalizedModule x.asIdeal.primeCompl M) :=
  Limits.colimit.desc ((OpenNhds.inclusion x).op ‚ãô structurePresheafInModuleCat R M)
    { pt := _
      Œπ.app U := openToLocalization‚Çó R M ((OpenNhds.inclusion _).obj (unop U)) x (unop U).2 }

@[reassoc (attr := simp)]
theorem germ_stalkToLocalization‚Çó
    (U : Opens (PrimeSpectrum.Top R)) (x : PrimeSpectrum.Top R) (hx : x ‚àà U) :
    (structurePresheafInModuleCat R M).germ U x hx ‚â´ stalkToLocalization‚Çó R M x =
      openToLocalization‚Çó R M U x hx :=
  Limits.colimit.Œπ_desc _ _

set_option backward.isDefEq.respectTransparency false in
@[simp]
theorem toStalk‚Çó'_stalkToFiberRingHom (x : PrimeSpectrum.Top R) :
    toStalk‚Çó' R M x ‚â´ stalkToLocalization‚Çó R M x =
      ModuleCat.ofHom (LocalizedModule.mkLinearMap _ _) := by
  rw [toStalk‚Çó', Category.assoc, germ_stalkToLocalization‚Çó]; rfl

open TopCat.Presheaf

set_option backward.isDefEq.respectTransparency false in
variable (R M) in
/-- The ring isomorphism between the stalk of the structure sheaf of `R` at a point `p`
corresponding to a prime ideal in `R` and the localization of `R` at `p`. -/
@[simps]
def stalkIso‚Çó (x : PrimeSpectrum.Top R) :
    (structurePresheafInModuleCat R M).stalk x ‚âÖ
      .of R (LocalizedModule x.asIdeal.primeCompl M) where
  hom := stalkToLocalization‚Çó R M x
  inv := localizationtoStalk‚Çó R M x
  hom_inv_id := by
    apply stalk_hom_ext
    intro U hxU
    ext s
    obtain ‚ü®g, hxg, igU, f, hs‚ü© :=
      exists_const _ s x hxU
    rw [germ_stalkToLocalization‚Çó_assoc, Category.comp_id, ‚Üê germ_res_apply _ igU.hom _ hxg]
    refine congr(localizationtoStalk‚Çó R M x (openToLocalization‚Çó R M _ x hxg $hs)).symm.trans ?_
    refine (localizationtoStalk‚Çó_mk ..).trans
      congr((structurePresheafInModuleCat R M).germ _ x hxg $hs)
  inv_hom_id := by
    ext1
    refine IsLocalizedModule.ext x.asIdeal.primeCompl (LocalizedModule.mkLinearMap ..)
      (IsLocalizedModule.map_units (LocalizedModule.mkLinearMap ..)) ?_
    ext
    dsimp [localizationtoStalk‚Çó]
    rw [‚Üê LocalizedModule.mkLinearMap_apply, IsLocalizedModule.lift_apply,
      elementwise_of% toStalk‚Çó'_stalkToFiberRingHom (M := M) x]
    simp

instance (x : PrimeSpectrum R) : IsIso (stalkToLocalization‚Çó R M x) :=
  (stalkIso‚Çó R M x).isIso_hom

instance (x : PrimeSpectrum R) : IsIso (localizationtoStalk‚Çó R M x) :=
  (stalkIso‚Çó R M x).isIso_inv

@[simp, reassoc]
theorem stalkToFiberRingHom_localizationToStalk (x : PrimeSpectrum.Top R) :
    stalkToLocalization‚Çó R M x ‚â´ localizationtoStalk‚Çó R M x = ùüô _ :=
  (stalkIso‚Çó R M x).hom_inv_id

@[simp, reassoc]
theorem localizationToStalk_stalkToFiberRingHom (x : PrimeSpectrum.Top R) :
    localizationtoStalk‚Çó R M x ‚â´ stalkToLocalization‚Çó R M x = ùüô _ :=
  (stalkIso‚Çó R M x).inv_hom_id

instance (x : PrimeSpectrum.Top R) :
    IsLocalizedModule x.asIdeal.primeCompl (toStalk‚Çó' R M x).hom := by
  convert IsLocalizedModule.of_linearEquiv x.asIdeal.primeCompl
    (LocalizedModule.mkLinearMap x.asIdeal.primeCompl M) (stalkIso‚Çó R M x).toLinearEquiv.symm
  ext m
  refine .trans ?_ (localizationtoStalk‚Çó_mk ..).symm
  dsimp +instances [toStalk‚Çó', toOpen‚Çó]
  rw! [PrimeSpectrum.basicOpen_one]
  rfl

set_option backward.isDefEq.respectTransparency false in
variable (R M) in
/-- The canonical ring homomorphism interpreting an element of `R` as an element of
the stalk of `structureSheaf R` at `x`. -/
@[expose] public
def toStalk‚Çó (x : PrimeSpectrum.Top R) :
    M ‚Üí‚Çó[R] ‚Üë(TopCat.Presheaf.stalk (moduleStructurePresheaf R M).presheaf x) where
  toFun m :=
    TopCat.Presheaf.germ (moduleStructurePresheaf R M).presheaf ‚ä§ x (by simp) (toOpen‚Çó R M ‚ä§ m)
  map_add' := by simp
  map_smul' r m := by
    change _ = toStalk R x r ‚Ä¢ TopCat.Presheaf.germ (moduleStructurePresheaf R M).presheaf _ _ _ _
    rw [map_smul]
    refine .trans ?_ ((moduleStructurePresheaf R M).germ_smul ..)
    congr 1
    exact (IsScalarTower.algebraMap_smul Œì(R, _) (M := Œì(M, _)) _ _).symm

public
instance (x : PrimeSpectrum.Top R) : IsLocalizedModule x.asIdeal.primeCompl (toStalk‚Çó R M x) := by
  convert IsLocalizedModule.of_linearEquiv x.asIdeal.primeCompl
    (toStalk‚Çó' R M x).hom (modulePresheafStalkIso R M x).symm
  ext m
  let Œ± : TopCat.Presheaf.stalk (moduleStructurePresheaf R M).presheaf x ‚âÖ
    (forget‚ÇÇ _ _).obj ((structurePresheafInModuleCat R M).stalk x) :=
    Limits.colimit.isoColimitCocone ‚ü®_, Limits.isColimitOfPreserves (forget‚ÇÇ (ModuleCat R) Ab)
    (Limits.colimit.isColimit ((OpenNhds.inclusion x).op ‚ãô
      structurePresheafInModuleCat R M))‚ü©
  refine Œ±.addCommGroupIsoToAddEquiv.eq_symm_apply.mpr ?_
  change Œ±.hom _ = _
  have : TopCat.Presheaf.germ (moduleStructurePresheaf R M).presheaf ‚ä§ x (by simp) ‚â´ Œ±.hom =
      (forget‚ÇÇ _ _).map ((structurePresheafInModuleCat R M).germ ‚ä§ x (by simp)) :=
    Limits.colimit.isoColimitCocone_Œπ_hom (C := Ab) ..
  exact congr($this _)

set_option backward.isDefEq.respectTransparency false in
variable (R) in
/-- The stalk of `Spec R` at `x` is isomorphic to the stalk of `R^~` at `x`. -/
@[expose] public
def commRingCatStalkEquivModuleStalk (x : PrimeSpectrum.Top R) :
    ‚Üë(TopCat.Presheaf.stalk (moduleStructurePresheaf R R).presheaf x) ‚âÉ‚Çó[R]
      (structurePresheafInCommRingCat R).stalk x where
  __ := (Limits.colimit.isoColimitCocone ‚ü®_, Limits.isColimitOfPreserves
      (forget‚ÇÇ CommRingCat RingCat ‚ãô forget‚ÇÇ RingCat AddCommGrpCat)
    (Limits.colimit.isColimit ((OpenNhds.inclusion x).op ‚ãô
      structurePresheafInCommRingCat R))‚ü©).addCommGroupIsoToAddEquiv
  map_smul' r m := by
    let Œ± : TopCat.Presheaf.stalk (moduleStructurePresheaf R R).presheaf x ‚âÖ
      (forget‚ÇÇ CommRingCat RingCat ‚ãô forget‚ÇÇ RingCat AddCommGrpCat).obj
        ((structurePresheafInCommRingCat R).stalk x) :=
      (Limits.colimit.isoColimitCocone ‚ü®_, Limits.isColimitOfPreserves
      (forget‚ÇÇ CommRingCat RingCat ‚ãô forget‚ÇÇ RingCat AddCommGrpCat)
      (Limits.colimit.isColimit ((OpenNhds.inclusion x).op ‚ãô
        structurePresheafInCommRingCat R))‚ü©)
    obtain ‚ü®U, hxU, s, rfl‚ü© := TopCat.Presheaf.germ_exist _ _ m
    have : (TopCat.Presheaf.germ (moduleStructurePresheaf R R).presheaf U x hxU) ‚â´ Œ±.hom =
        (forget‚ÇÇ CommRingCat RingCat ‚ãô forget‚ÇÇ RingCat AddCommGrpCat).map
          ((structurePresheafInCommRingCat R).germ U x hxU) :=
      Limits.colimit.isoColimitCocone_Œπ_hom ..
    change Œ±.hom (r ‚Ä¢ germ‚Çó R U x hxU _) = toStalk R _ _ * _
    rw [‚Üê map_smul, Algebra.smul_def]
    refine congr($this _).trans ?_
    refine (((structurePresheafInCommRingCat R).germ U x hxU).hom.map_mul _ _).trans ?_
    congr 1
    ¬∑ dsimp [toStalk]
      erw [‚Üê (structurePresheafInCommRingCat R).germ_res_apply (homOfLE (le_top : U ‚â§ ‚ä§)) _ hxU]
      rfl
    ¬∑ exact congr($this _).symm

set_option backward.isDefEq.respectTransparency false in
public instance (x : PrimeSpectrum.Top R) :
    IsLocalization.AtPrime ((structurePresheafInCommRingCat R).stalk x) x.asIdeal := by
  refine (isLocalizedModule_iff_isLocalization' _ _).mp ?_
  convert IsLocalizedModule.of_linearEquiv x.asIdeal.primeCompl (toStalk‚Çó R R x)
    (commRingCatStalkEquivModuleStalk R x)
  let Œ± : TopCat.Presheaf.stalk (moduleStructurePresheaf R R).presheaf x ‚âÖ
    (forget‚ÇÇ CommRingCat RingCat ‚ãô forget‚ÇÇ RingCat AddCommGrpCat).obj
      ((structurePresheafInCommRingCat R).stalk x) :=
    (Limits.colimit.isoColimitCocone ‚ü®_, Limits.isColimitOfPreserves
    (forget‚ÇÇ CommRingCat RingCat ‚ãô forget‚ÇÇ RingCat AddCommGrpCat)
    (Limits.colimit.isColimit ((OpenNhds.inclusion x).op ‚ãô
      structurePresheafInCommRingCat R))‚ü©)
  have : (TopCat.Presheaf.germ (moduleStructurePresheaf R R).presheaf ‚ä§ x (by simp)) ‚â´ Œ±.hom =
      (forget‚ÇÇ CommRingCat RingCat ‚ãô forget‚ÇÇ RingCat AddCommGrpCat).map
        ((structurePresheafInCommRingCat R).germ ‚ä§ x (by simp)) :=
    Limits.colimit.isoColimitCocone_Œπ_hom ..
  ext
  dsimp [toStalk‚Çó]
  simp only [map_one]
  refine .trans ?_ congr($this _).symm
  exact (((structurePresheafInCommRingCat R).germ ‚ä§ x (by simp)).hom.comp
    (algebraMap R Œì(R, _))).map_one.symm

variable (R) in
/-- The stalk of `Spec R` at `x` is isomorphic to `R‚Çö`,
where `p` is the prime corresponding to `x`. -/
public abbrev stalkIso (x : PrimeSpectrum R) :
    Localization.AtPrime x.asIdeal ‚âÉ‚Çê[R] (structurePresheafInCommRingCat R).stalk x :=
  IsLocalization.algEquiv x.asIdeal.primeCompl _ _

end Stalk

@[expose] public section StructureSheaf

variable (R)

/-- The structure sheaf on $Spec R$, valued in `CommRingCat`.

This is provided as a bundled `SheafedSpace` as `Spec.SheafedSpace R` later. -/
def _root_.AlgebraicGeometry.Spec.structureSheaf : Sheaf CommRingCat (PrimeSpectrum.Top R) :=
  ‚ü®structurePresheafInCommRingCat R,
    (TopCat.Presheaf.isSheaf_iff_isSheaf_comp _ _).mpr (TopCat.Presheaf.isSheaf_of_iso
      (structurePresheafCompForget R).symm (structureSheafInType R R).cond)‚ü©

open Spec (structureSheaf)

/-- The canonical ring homomorphism interpreting an element of `R` as
a section of the structure sheaf. -/
@[deprecated "algebraMap" (since := "2026-02-10")]
def toOpen (U : Opens (PrimeSpectrum.Top R)) :
    CommRingCat.of R ‚ü∂ (structureSheaf R).1.obj (op U) := CommRingCat.ofHom (algebraMap _ _)

@[simp]
theorem algebraMap_self_map (U V : (Opens (PrimeSpectrum.Top R))·µí·µñ) (i : V ‚ü∂ U) :
    CommRingCat.ofHom (algebraMap R _) ‚â´ (Spec.structureSheaf R).1.map i =
      CommRingCat.ofHom (algebraMap R _) :=
  rfl

@[deprecated (since := "2026-02-10")] alias toOpen_res := algebraMap_self_map

instance stalkAlgebra (p : PrimeSpectrum R) : Algebra R ((structureSheaf R).presheaf.stalk p) :=
  (toStalk R p).hom.toAlgebra

@[simp]
theorem stalkAlgebra_map (p : PrimeSpectrum R) (r : R) :
    algebraMap R ((structureSheaf R).presheaf.stalk p) r = toStalk R p r :=
  rfl

/-- Stalk of the structure sheaf at a prime p as localization of R -/
instance IsLocalization.to_stalk (p : PrimeSpectrum R) :
    IsLocalization.AtPrime ((structureSheaf R).presheaf.stalk p) p.asIdeal :=
  inferInstanceAs (IsLocalization.AtPrime ((structurePresheafInCommRingCat R).stalk p) p.asIdeal)

instance openAlgebra (U : (Opens (PrimeSpectrum R))·µí·µñ) : Algebra R ((structureSheaf R).val.obj U) :=
  inferInstanceAs (Algebra R ((structureSheafInType R R).presheaf.obj _))

/-- Sections of the structure sheaf of Spec R on a basic open as localization of R -/
instance IsLocalization.to_basicOpen (r : R) :
    IsLocalization.Away r ((structureSheaf R).val.obj (op <| basicOpen r)) :=
  inferInstanceAs (IsLocalization.Away r Œì(R, basicOpen r))

instance to_basicOpen_epi (r : R) :
    Epi (CommRingCat.ofHom <|
      algebraMap R ((structureSheaf R).val.obj (op <| basicOpen r))) :=
  ‚ü®fun _ _ h => CommRingCat.hom_ext (IsLocalization.ringHom_ext (Submonoid.powers r)
    (CommRingCat.hom_ext_iff.mp h))‚ü©

/-- The ring isomorphism between the ring `R` and the global sections `Œì(X, ùí™‚Çì)`. -/
@[simps! inv]
def globalSectionsIso : CommRingCat.of R ‚âÖ (structureSheaf R).1.obj (op ‚ä§) :=
  RingEquiv.toCommRingCatIso (.ofBijective _ algebraMap_obj_top_bijective)

theorem globalSectionsIso_hom (R : CommRingCat) :
    (globalSectionsIso R).hom = CommRingCat.ofHom (algebraMap _ _) :=
  rfl

set_option backward.isDefEq.respectTransparency false in
@[simp, reassoc, elementwise nosimp]
theorem toStalk_stalkSpecializes {R : Type*} [CommRing R] {x y : PrimeSpectrum R} (h : x ‚§≥ y) :
    toStalk R y ‚â´ (structureSheaf R).presheaf.stalkSpecializes h = toStalk R x := by
  dsimp [toStalk]
  simp [structureSheaf]

end StructureSheaf

@[expose] public section Comap

variable {S : Type u} [CommRing S] {N : Type u} [AddCommGroup N] [Module S N]
  {œÉ : R ‚Üí+* S} (f : M ‚Üí‚Çõ‚Çó[œÉ] N)

set_option backward.isDefEq.respectTransparency false in
/-- The map `M_{f y} ‚ü∂ N_{y}` used to build maps between structure sheaves. -/
def Localizations.comapFun (y : PrimeSpectrum.Top S) :
    Localizations M (y.comap œÉ) ‚Üí‚Çõ‚Çó[œÉ] Localizations N y :=
  letI := Module.compHom N œÉ
  letI := œÉ.toAlgebra
  haveI : IsScalarTower R S N := .of_algebraMap_smul fun _ _ ‚Ü¶ rfl
  letI f' : M ‚Üí‚Çó[R] N := { __ := f }
  letI g : LocalizedModule (y.comap œÉ).asIdeal.primeCompl M ‚Üí‚Çó[R]
      LocalizedModule y.asIdeal.primeCompl N :=
    IsLocalizedModule.lift (y.comap œÉ).asIdeal.primeCompl (LocalizedModule.mkLinearMap _ _)
      ((LocalizedModule.mkLinearMap _ _).restrictScalars R ‚àò‚Çó f') (by
      intro x
      have := IsLocalizedModule.map_units (S := y.asIdeal.primeCompl)
        (LocalizedModule.mkLinearMap y.asIdeal.primeCompl N) ‚ü®œÉ x, x.2‚ü©
      rw [Module.End.isUnit_iff] at this ‚ä¢
      convert this using 2 with a
      exact (IsScalarTower.algebraMap_smul ..).symm)
  { __ := g,
    map_smul' r x := by simpa [Localizations] using (IsScalarTower.algebraMap_smul ..).symm }

set_option backward.isDefEq.respectTransparency false in
@[simp]
lemma Localizations.comapFun_mk (y : PrimeSpectrum.Top S)
    (a : M) (b : (y.comap œÉ).asIdeal.primeCompl) :
    Localizations.comapFun f y (.mk a b) = .mk (f a) ‚ü®œÉ b.1, b.2‚ü© := by
  letI := Module.compHom N œÉ
  letI := œÉ.toAlgebra
  haveI : IsScalarTower R S N := .of_algebraMap_smul fun _ _ ‚Ü¶ rfl
  apply ((Module.End.isUnit_iff _).mp (IsLocalizedModule.map_units (S := y.asIdeal.primeCompl)
    (LocalizedModule.mkLinearMap y.asIdeal.primeCompl N) ‚ü®œÉ b, b.2‚ü©)).1
  dsimp
  rw [‚Üê (comapFun f y).map_smul‚Çõ‚Çó, LocalizedModule.smul'_mk, ‚Üê Submonoid.smul_def,
    LocalizedModule.mk_cancel, ‚Üê LocalizedModule.mkLinearMap_apply]
  dsimp [comapFun, Localizations]
  refine (IsLocalizedModule.lift_apply ..).trans ?_
  dsimp
  rw [‚Üê LocalizedModule.mk_cancel ‚ü®œÉ b.1, b.2‚ü©, LocalizedModule.smul'_mk]
  rfl

/--
Given a ring homomorphism `f : R ‚Üí+* S`, an open set `U` of the prime spectrum of `R` and an open
set `V` of the prime spectrum of `S`, such that `V ‚äÜ (comap f) ‚Åª¬π' U`, we can push a section `s`
on `U` to a section on `V`, by composing with `Localization.localRingHom _ _ f` from the left and
`comap f` from the right. Explicitly, if `s` evaluates on `comap f p` to `a / b`, its image on `V`
evaluates on `p` to `f(a) / f(b)`.

At the moment, we work with arbitrary dependent functions `s : Œ† x : U, Localizations R x`. Below,
we prove the predicate `isLocallyFraction` is preserved by this map, hence it can be extended to
a morphism between the structure sheaves of `R` and `S`.
-/
def comapFun (U : Opens (PrimeSpectrum.Top R)) (V : Opens (PrimeSpectrum.Top S))
    (hUV : V.1 ‚äÜ PrimeSpectrum.comap œÉ ‚Åª¬π' U.1) (s : ‚àÄ x : U, Localizations M x.1) (y : V) :
    Localizations N y.1 :=
  Localizations.comapFun f _ (s ‚ü®y.1.comap œÉ, hUV y.2‚ü©)

set_option backward.isDefEq.respectTransparency false in
theorem isLocallyFraction_comapFun (U : Opens (PrimeSpectrum.Top R))
    (V : Opens (PrimeSpectrum.Top S)) (hUV : V.1 ‚äÜ PrimeSpectrum.comap œÉ ‚Åª¬π' U.1)
    (s : ‚àÄ x : U, Localizations M x.1) (hs : (isLocallyFraction R M).toPrelocalPredicate.pred s) :
    (isLocallyFraction S N).toPrelocalPredicate.pred (comapFun f U V hUV s) := by
  letI := Module.compHom N œÉ
  letI := œÉ.toAlgebra
  haveI : IsScalarTower R S N := .of_algebraMap_smul fun _ _ ‚Ü¶ rfl
  rintro ‚ü®p, hpV‚ü©
  obtain ‚ü®W, m, iWU, a, b, h_frac‚ü© := hs ‚ü®PrimeSpectrum.comap œÉ p, hUV hpV‚ü©
  refine ‚ü®‚ü®_, (PrimeSpectrum.continuous_comap œÉ).isOpen_preimage _ W.2‚ü© ‚äì V,
    ‚ü®m, hpV‚ü©, Opens.infLERight _ _, f a, œÉ b, ?_‚ü©
  rintro ‚ü®q, ‚ü®hqW, hqV‚ü©‚ü©
  obtain ‚ü®hs, H‚ü© := h_frac ‚ü®PrimeSpectrum.comap œÉ q, hqW‚ü©
  refine ‚ü®hs, ?_‚ü©
  dsimp [comapFun] at H ‚ä¢
  rw [H]
  simp

/-- For a ring homomorphism `f : R ‚Üí+* S` and open sets `U` and `V` of the prime spectra of `R` and
`S` such that `V ‚äÜ (comap f) ‚Åª¬π U`, the induced ring homomorphism from the structure sheaf of `R`
at `U` to the structure sheaf of `S` at `V`.

Explicitly, this map is given as follows: For a point `p : V`, if the section `s` evaluates on `p`
to the fraction `a / b`, its image on `V` evaluates on `p` to the fraction `f(a) / f(b)`.
-/
def comap‚Çó (U : Opens (PrimeSpectrum.Top R)) (V : Opens (PrimeSpectrum.Top S))
    (hUV : V.1 ‚äÜ PrimeSpectrum.comap œÉ ‚Åª¬π' U.1) :
    Œì(M, U) ‚Üí‚Çõ‚Çó[œÉ] Œì(N, V) where
  toFun s := ‚ü®comapFun f U V hUV s.1, isLocallyFraction_comapFun f U V hUV s.1 s.2‚ü©
  map_add' s t := Subtype.ext <| funext fun _ ‚Ü¶ by dsimp [comapFun]; rw [map_add]
  map_smul' r m := Subtype.ext <| funext fun _ ‚Ü¶ by
    dsimp [comapFun]
    rw [map_smul‚Çõ‚Çó, ‚Üê IsScalarTower.algebraMap_smul S]

set_option backward.isDefEq.respectTransparency false in
theorem comap‚Çó_const (U : Opens (PrimeSpectrum.Top R)) (V : Opens (PrimeSpectrum.Top S))
    (hUV : V.1 ‚äÜ PrimeSpectrum.comap œÉ ‚Åª¬π' U.1) (a : M) (b : R) (hb : U ‚â§ basicOpen b) :
    comap‚Çó f U V hUV (const a b U hb) = const (f a) (œÉ b) V (hUV.trans (Set.preimage_mono hb)) :=
  Subtype.ext <| funext fun _ ‚Ü¶ by simp [comap‚Çó, comapFun]

section Ring

open Spec (structureSheaf)

variable {S : Type u} [CommRing S] {P : Type u} [CommRing P]

set_option backward.isDefEq.respectTransparency false in
@[simp]
theorem comap‚Çó_eq_localRingHom (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R))
    (V : Opens (PrimeSpectrum.Top S)) (hUV : V.1 ‚äÜ PrimeSpectrum.comap f ‚Åª¬π' U.1)
    (s : (structureSheaf R).1.obj (op U)) (p : V) :
    (comap‚Çó f.toSemilinearMap U V hUV s).1 p =
      Localization.localRingHom (PrimeSpectrum.comap f p.1).asIdeal _ f rfl
        (s.1 ‚ü®PrimeSpectrum.comap f p.1, hUV p.2‚ü©) := by
  dsimp [comap‚Çó, comapFun]
  suffices ‚áë(Localizations.comapFun f.toSemilinearMap p.1) =
      ‚áë(Localization.localRingHom (PrimeSpectrum.comap f p.1).asIdeal _ f rfl) from
    congr($this _)
  ext m
  induction m using LocalizedModule.induction_on with | h m s =>
  trans LocalizedModule.mk (f m) ‚ü®f ‚Üës, s.2‚ü©
  ¬∑ simp
  convert_to Localization.mk _ _ = Localization.localRingHom _ _ _ _ (Localization.mk _ _)
  simp [Localization.mk_eq_mk']

/-- For a ring homomorphism `f : R ‚Üí+* S` and open sets `U` and `V` of the prime spectra of `R` and
`S` such that `V ‚äÜ (comap f) ‚Åª¬π U`, the induced ring homomorphism from the structure sheaf of `R`
at `U` to the structure sheaf of `S` at `V`.

Explicitly, this map is given as follows: For a point `p : V`, if the section `s` evaluates on `p`
to the fraction `a / b`, its image on `V` evaluates on `p` to the fraction `f(a) / f(b)`.
-/
def comap (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R)) (V : Opens (PrimeSpectrum.Top S))
    (hUV : V.1 ‚äÜ PrimeSpectrum.comap f ‚Åª¬π' U.1) :
    (structureSheaf R).1.obj (op U) ‚Üí+* (structureSheaf S).1.obj (op V) where
  __ := comap‚Çó f.toSemilinearMap U V hUV
  map_one' := Subtype.ext <| funext fun _ ‚Ü¶ by
    dsimp
    simp only [comap‚Çó_eq_localRingHom, PrimeSpectrum.comap_asIdeal]
    exact (Localization.localRingHom ..).map_one
  map_mul' r s := Subtype.ext <| funext fun p ‚Ü¶ by
    dsimp
    change _ = (comap‚Çó f.toSemilinearMap U V hUV r).1 p * (comap‚Çó f.toSemilinearMap U V hUV s).1 p
    simp only [comap‚Çó_eq_localRingHom, PrimeSpectrum.comap_asIdeal]
    exact (Localization.localRingHom ..).map_mul _ _
  map_zero' := Subtype.ext <| funext fun _ ‚Ü¶ by
    dsimp
    simp only [comap‚Çó_eq_localRingHom, PrimeSpectrum.comap_asIdeal]
    exact (Localization.localRingHom ..).map_zero

@[simp]
theorem comap_apply (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R))
    (V : Opens (PrimeSpectrum.Top S)) (hUV : V.1 ‚äÜ PrimeSpectrum.comap f ‚Åª¬π' U.1)
    (s : (structureSheaf R).1.obj (op U)) (p : V) :
    (comap f U V hUV s).1 p =
      Localization.localRingHom (PrimeSpectrum.comap f p.1).asIdeal _ f rfl
        (s.1 ‚ü®PrimeSpectrum.comap f p.1, hUV p.2‚ü©) :=
  comap‚Çó_eq_localRingHom ..

set_option backward.isDefEq.respectTransparency false in
theorem comap_const (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R))
    (V : Opens (PrimeSpectrum.Top S)) (hUV : V.1 ‚äÜ PrimeSpectrum.comap f ‚Åª¬π' U.1) (a b : R)
    (hb : ‚àÄ x : PrimeSpectrum R, x ‚àà U ‚Üí b ‚àà x.asIdeal.primeCompl) :
    comap f U V hUV (const a b U hb) =
      const (f a) (f b) V fun p hpV => hb (PrimeSpectrum.comap f p) (hUV hpV) :=
  Subtype.ext <| funext fun p => by
    rw [comap_apply, const_apply, const_apply]
    convert_to Localization.localRingHom _ _ _ _ (Localization.mk _ _) = Localization.mk _ _
    simp [Localization.mk_eq_mk']

/-- For an inclusion `i : V ‚ü∂ U` between open sets of the prime spectrum of `R`, the comap of the
identity from OO_X(U) to OO_X(V) equals as the restriction map of the structure sheaf.

This is a generalization of the fact that, for fixed `U`, the comap of the identity from OO_X(U)
to OO_X(U) is the identity.
-/
theorem comap_id_eq_map (U V : Opens (PrimeSpectrum.Top R)) (iVU : V ‚ü∂ U) :
    (comap (RingHom.id R) U V fun _ hpV => leOfHom iVU <| hpV) =
      ((structureSheaf R).1.map iVU.op).hom :=
  RingHom.ext fun s => Subtype.ext <| funext fun p => by
    rw [comap_apply]
    exact congr($(Localization.localRingHom_id ..) _)

/--
The comap of the identity is the identity. In this variant of the lemma, two open subsets `U` and
`V` are given as arguments, together with a proof that `U = V`. This is useful when `U` and `V`
are not definitionally equal.
-/
theorem comap_id {U V : Opens (PrimeSpectrum.Top R)} (hUV : U = V) :
    (comap (RingHom.id R) U V fun p hpV => by rwa [hUV, PrimeSpectrum.comap_id]) =
      (eqToHom (show (structureSheaf R).1.obj (op U) = _ by rw [hUV])).hom := by
  rw [comap_id_eq_map U V (eqToHom hUV.symm), eqToHom_op, eqToHom_map]

@[simp]
theorem comap_id' (U : Opens (PrimeSpectrum.Top R)) :
    (comap (RingHom.id R) U U fun p hpU => by rwa [PrimeSpectrum.comap_id]) = RingHom.id _ := by
  rw [comap_id rfl]; rfl

set_option backward.isDefEq.respectTransparency false in
theorem comap_comp (f : R ‚Üí+* S) (g : S ‚Üí+* P) (U : Opens (PrimeSpectrum.Top R))
    (V : Opens (PrimeSpectrum.Top S)) (W : Opens (PrimeSpectrum.Top P))
    (hUV : ‚àÄ p ‚àà V, PrimeSpectrum.comap f p ‚àà U) (hVW : ‚àÄ p ‚àà W, PrimeSpectrum.comap g p ‚àà V) :
    (comap (g.comp f) U W fun p hpW => hUV (PrimeSpectrum.comap g p) (hVW p hpW)) =
      (comap g V W hVW).comp (comap f U V hUV) :=
  RingHom.ext fun s =>
    Subtype.ext <|
      funext fun p => by
        rw [comap_apply, Localization.localRingHom_comp _ (PrimeSpectrum.comap g p.1).asIdeal] <;>
        simp

@[elementwise, reassoc]
theorem toOpen_comp_comap (f : R ‚Üí+* S) (U : Opens (PrimeSpectrum.Top R)) :
    CommRingCat.ofHom (algebraMap _ _) ‚â´
      CommRingCat.ofHom (comap f U (Opens.comap ‚ü®_, PrimeSpectrum.continuous_comap f‚ü© U)
        fun _ ‚Ü¶ id) =
      CommRingCat.ofHom f ‚â´ CommRingCat.ofHom (algebraMap _ _) :=
  CommRingCat.hom_ext <| RingHom.ext fun _ ‚Ü¶ Subtype.ext <| funext fun x ‚Ü¶ by
    dsimp
    rw [comap_apply]
    exact Localization.localRingHom_to_map _ _ _ _ _

lemma comap_basicOpen (f : R ‚Üí+* S) (x : R) :
    comap f (PrimeSpectrum.basicOpen x) (PrimeSpectrum.basicOpen (f x))
        (PrimeSpectrum.comap_basicOpen f x).le =
      IsLocalization.map (M := .powers x) (T := .powers (f x)) _ f
        (Submonoid.powers_le.mpr (Submonoid.mem_powers _)) :=
  IsLocalization.ringHom_ext (.powers x) <| by
    simpa [CommRingCat.hom_ext_iff] using toOpen_comp_comap f _

end Ring

end Comap

end StructureSheaf

end AlgebraicGeometry
