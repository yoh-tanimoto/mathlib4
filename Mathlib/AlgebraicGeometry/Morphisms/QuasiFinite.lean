/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.AlgebraicGeometry.Artinian
public import Mathlib.AlgebraicGeometry.Fiber
public import Mathlib.AlgebraicGeometry.Morphisms.Finite
public import Mathlib.AlgebraicGeometry.Morphisms.UniversallyInjective
public import Mathlib.RingTheory.RingHom.QuasiFinite
public import Mathlib.RingTheory.ZariskisMainTheorem

/-!
# Quasi-finite morphisms

We say that a morphism `f : X ‚ü∂ Y` is locally quasi finite if `Œì(Y, U) ‚ü∂ Œì(X, V)` is
quasi-finite (in the mathlib sense) for every pair of affine opens that `f` maps one into the other.

This is equivalent to all the fibers `f‚Åª¬π(x)` having an open cover of `Œ∫(x)`-finite schemes.
Note that this does not require `f` to be quasi-compact nor locally of finite type.

We prove that this is stable under composition and base change, and is right cancellative.

## Main results
- `AlgebraicGeometry.LocallyQuasiFinite` : The class of locally quasi-finite morphisms.
- `AlgebraicGeometry.Scheme.Hom.isDiscrete_preimage_singleton`:
  Locally quasi-finite morphisms have discrete fibers.
- `AlgebraicGeometry.Scheme.Hom.finite_preimage_singleton`:
  Quasi-finite morphisms have finite fibers.
- `AlgebraicGeometry.locallyQuasiFinite_iff_isFinite_fiber`: If `f` is quasi-compact,
  then `f` is locally quasi-finite iff all the fibers `f‚Åª¬π(x)` are `Œ∫(x)`-finite.
- `AlgebraicGeometry.locallyQuasiFinite_iff_isDiscrete_preimage_singleton`:
  If `f` is locally of finite type, then `f` is locally quasi-finite iff `f` has discrete fibers.
- `AlgebraicGeometry.locallyQuasiFinite_iff_finite_preimage_singleton`:
  If `f` is of finite type, then `f` is locally quasi-finite iff `f` has finite fibers.
-/

@[expose] public section

open CategoryTheory hiding IsDiscrete
open Limits

namespace AlgebraicGeometry

universe u

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

open Scheme

/--
We say that a morphism `f : X ‚ü∂ Y` is locally quasi finite if `Œì(Y, U) ‚ü∂ Œì(X, V)` is
quasi-finite (in the mathlib sense) for every pair of affine opens that `f` maps one into the other.

Note that this does not require `f` to be quasi-compact nor locally of finite type.

Being locally quasi-finite implies that `f` has discrete and finite fibers
(via `f.isDiscrete_preimage_singleton` and `f.finite_preimage_singleton`).
The converse holds under various scenarios:

- `locallyQuasiFinite_iff_isDiscrete_preimage_singleton`:
  If `f` is quasi-compact, this is equivalent to `f ‚Åª¬π {x}` being `Œ∫(x)`-finite for all `x`.
- `locallyQuasiFinite_iff_isDiscrete_preimage_singleton`:
  If `f` is locally of finite type, this is equivalent to `f` having discrete fibers.
- `locallyQuasiFinite_iff_isDiscrete_preimage_singleton`:
  If `f` is locally of finite type, this is equivalent to `f` having finite fibers.
-/
@[mk_iff]
class LocallyQuasiFinite : Prop where
  quasiFinite_appLE :
    ‚àÄ {U : Y.Opens} (_ : IsAffineOpen U) {V : X.Opens} (_ : IsAffineOpen V) (e : V ‚â§ f ‚Åª¬π·µÅ U),
      (f.appLE U V e).hom.QuasiFinite

instance : HasRingHomProperty @LocallyQuasiFinite RingHom.QuasiFinite where
  isLocal_ringHomProperty := RingHom.QuasiFinite.propertyIsLocal
  eq_affineLocally' := by
    ext X Y f
    simp [locallyQuasiFinite_iff, affineLocally_iff_affineOpens_le, affineOpens]

instance : MorphismProperty.IsStableUnderComposition @LocallyQuasiFinite :=
  HasRingHomProperty.stableUnderComposition RingHom.QuasiFinite.stableUnderComposition

instance {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [LocallyQuasiFinite f] [LocallyQuasiFinite g] : LocallyQuasiFinite (f ‚â´ g) :=
  MorphismProperty.comp_mem _ f g ‚Äπ_‚Ä∫ ‚Äπ_‚Ä∫

instance (priority := low) [IsFinite f] : LocallyQuasiFinite f := by
  rw [HasAffineProperty.eq_targetAffineLocally @IsFinite] at ‚ÄπIsFinite f‚Ä∫
  rw [HasRingHomProperty.eq_affineLocally @LocallyQuasiFinite]
  refine ((targetAffineLocally_affineAnd_eq_affineLocally
    RingHom.QuasiFinite.propertyIsLocal).le f ?_).2
  exact targetAffineLocally_affineAnd_le (fun hf ‚Ü¶ .of_finite hf) f ‚Äπ_‚Ä∫

instance (priority := low) [IsImmersion f] : LocallyQuasiFinite f := by
  rw [‚Üê f.liftCoborder_Œπ]
  have := HasRingHomProperty.of_isOpenImmersion (P := @LocallyQuasiFinite)
    RingHom.QuasiFinite.holdsForLocalizationAway.containsIdentities (f := f.coborderRange.Œπ)
  infer_instance

theorem LocallyQuasiFinite.of_comp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [LocallyQuasiFinite (f ‚â´ g)] : LocallyQuasiFinite f :=
  HasRingHomProperty.of_comp (fun _ _ ‚Ü¶ RingHom.QuasiFinite.of_comp) ‚Äπ_‚Ä∫

theorem LocallyQuasiFinite.comp_iff {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [LocallyQuasiFinite g] :
    LocallyQuasiFinite (f ‚â´ g) ‚Üî LocallyQuasiFinite f :=
  ‚ü®fun _ ‚Ü¶ .of_comp f g, fun _ ‚Ü¶ inferInstance‚ü©

instance : MorphismProperty.IsMultiplicative @LocallyQuasiFinite where
  id_mem _ := inferInstance

instance : MorphismProperty.IsStableUnderBaseChange @LocallyQuasiFinite :=
  HasRingHomProperty.isStableUnderBaseChange RingHom.QuasiFinite.isStableUnderBaseChange

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [LocallyQuasiFinite g] :
    LocallyQuasiFinite (pullback.fst f g) :=
  MorphismProperty.pullback_fst f g inferInstance

instance {X Y S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S) [LocallyQuasiFinite f] :
    LocallyQuasiFinite (pullback.snd f g) :=
  MorphismProperty.pullback_snd f g inferInstance

instance (V : Y.Opens) [LocallyQuasiFinite f] : LocallyQuasiFinite (f ‚à£_ V) :=
  IsZariskiLocalAtTarget.restrict ‚Äπ_‚Ä∫ V

instance (U : X.Opens) (V : Y.Opens) (e) [LocallyQuasiFinite f] :
    LocallyQuasiFinite (f.resLE V U e) := by
  delta Scheme.Hom.resLE; infer_instance

instance : MorphismProperty.Respects @LocallyQuasiFinite @IsOpenImmersion :=
  HasRingHomProperty.respects_isOpenImmersion
  (RingHom.QuasiFinite.stableUnderComposition.stableUnderCompositionWithLocalizationAway
    RingHom.QuasiFinite.holdsForLocalizationAway).1

set_option backward.isDefEq.respectTransparency false in
nonrec lemma IsLocallyArtinian.of_locallyQuasiFinite [LocallyQuasiFinite f]
    [IsLocallyArtinian Y] : IsLocallyArtinian X := by
  change id _ -- avoid typeclass synthesis from getting stuck on the wlog hypothesis.
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ exact (isLocallyArtinian_iff_openCover (Y.affineCover.pullback‚ÇÅ f)).mpr fun i ‚Ü¶
      this (pullback.snd _ _) ‚ü®_, rfl‚ü©
  wlog hX : ‚àÉ S, X = Spec S
  ¬∑ exact (isLocallyArtinian_iff_openCover X.affineCover).mpr fun i ‚Ü¶
      this (X.affineCover.f i ‚â´ f) hY ‚ü®_, rfl‚ü©
  obtain ‚ü®R, rfl‚ü© := hY
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  simp only [isLocallyArtinianScheme_Spec, HasRingHomProperty.Spec_iff, id_eq] at *
  algebraize [œÜ.hom]
  have : Module.Finite R S := .of_quasiFinite
  exact .of_finite R S

set_option backward.isDefEq.respectTransparency false in
instance [LocallyQuasiFinite f] (y : Y) : IsLocallyArtinian (f.fiber y) :=
  .of_locallyQuasiFinite (pullback.snd _ _)

lemma Scheme.Hom.isDiscrete_preimage_singleton [LocallyQuasiFinite f] (y : Y) :
    IsDiscrete (f ‚Åª¬π' {y}) := by
  simpa [Scheme.Hom.range_fiberŒπ] using
    (isDiscrete_univ_iff.mpr inferInstance).image (f.fiberŒπ y).isEmbedding

lemma Scheme.Hom.isDiscrete_preimage [LocallyQuasiFinite f] {s : Set Y} (hs : IsDiscrete s) :
    IsDiscrete (f ‚Åª¬π' s) :=
  hs.preimage' f.continuous.continuousOn f.isDiscrete_preimage_singleton

instance [LocallyQuasiFinite f] [QuasiCompact f] (y : Y) : IsArtinianScheme (f.fiber y) where

lemma Scheme.Hom.finite_preimage_singleton [LocallyQuasiFinite f] [QuasiCompact f] (y : Y) :
    (f ‚Åª¬π' {y}).Finite := by
  simpa [Scheme.Hom.range_fiberŒπ] using Set.finite_univ.image (f.fiberŒπ y)

@[deprecated (since := "2026-02-05")]
alias IsFinite.finite_preimage_singleton := Scheme.Hom.finite_preimage_singleton

lemma Scheme.Hom.finite_preimage [LocallyQuasiFinite f] [QuasiCompact f]
    {s : Set Y} (hs : s.Finite) : (f ‚Åª¬π' s).Finite :=
  hs.preimage' fun _ _ ‚Ü¶ f.finite_preimage_singleton _

lemma Scheme.Hom.tendsto_cofinite_cofinite [LocallyQuasiFinite f] [QuasiCompact f] :
    Filter.Tendsto f .cofinite .cofinite :=
  .cofinite_of_finite_preimage_singleton f.finite_preimage_singleton

nonrec lemma IsFinite.of_locallyQuasiFinite (f : X ‚ü∂ Y) [LocallyQuasiFinite f]
    [QuasiCompact f] [IsLocallyArtinian Y] : IsFinite f := by
  change id _ -- avoid typeclass synthesis from getting stuck on the wlog hypothesis.
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ exact (IsZariskiLocalAtTarget.iff_of_openCover Y.affineCover).mpr fun i ‚Ü¶
      this (pullback.snd _ _) ‚ü®_, rfl‚ü©
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S
  ¬∑ have inst : IsArtinianScheme X :=
      { toIsLocallyArtinian := .of_locallyQuasiFinite f,
        toCompactSpace := QuasiCompact.compactSpace_of_compactSpace f }
    exact (MorphismProperty.cancel_left_of_respectsIso _ _ _).mp
      (this _ ((Scheme.isoSpec X).inv ‚â´ f) ‚ü®_, rfl‚ü©)
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  simp only [isLocallyArtinianScheme_Spec, HasRingHomProperty.Spec_iff, id_eq,
    IsFinite.SpecMap_iff] at *
  algebraize [œÜ.hom]
  exact .of_quasiFinite

set_option backward.isDefEq.respectTransparency false in
instance (f : X ‚ü∂ Y) [LocallyQuasiFinite f] [QuasiCompact f] (x : Y) :
    IsFinite (f.fiberToSpecResidueField x) :=
  .of_locallyQuasiFinite (pullback.snd _ _)

set_option backward.isDefEq.respectTransparency false in
nonrec lemma LocallyQuasiFinite.of_fiberToSpecResidueField
    (hf : ‚àÄ x, LocallyQuasiFinite (f.fiberToSpecResidueField x)) : LocallyQuasiFinite f := by
  change id _ -- avoid typeclass synthesis from getting stuck on the wlog hypothesis.
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ refine (IsZariskiLocalAtTarget.iff_of_openCover Y.affineCover).mpr fun i ‚Ü¶
      this (f := pullback.snd _ _) (fun x ‚Ü¶ ?_) ‚ü®_, rfl‚ü©
    have (x : Y) : IsLocallyArtinian (f.fiber x) :=
      .of_locallyQuasiFinite (f.fiberToSpecResidueField x)
    refine (MorphismProperty.cancel_right_of_respectsIso @LocallyQuasiFinite _
      (Spec.map ((Y.affineCover.f i).residueFieldMap _))).mp ?_
    let g : (pullback.snd f (Y.affineCover.f i)).fiber x ‚ü∂ f.fiber (Y.affineCover.f i x) :=
      pullback.map _ _ _ _ (pullback.fst _ _) (Spec.map ((Y.affineCover.f i).residueFieldMap _))
        (Y.affineCover.f i) (by simp [pullback.condition]) (by simp)
    have : IsClosedImmersion g := .of_isPreimmersion _ (isClosed_discrete _)
    convert (inferInstanceAs (LocallyQuasiFinite <| g ‚â´ f.fiberToSpecResidueField _)) using 1
    simp [g, Hom.fiberToSpecResidueField]
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S
  ¬∑ refine (IsZariskiLocalAtSource.iff_of_openCover X.affineCover).mpr fun i ‚Ü¶
      this _ _ (fun x ‚Ü¶ ?_) ‚ü®_, rfl‚ü©
    have (x : _) : IsLocallyArtinian (f.fiber x) :=
      .of_locallyQuasiFinite (f.fiberToSpecResidueField x)
    let g : (X.affineCover.f i ‚â´ f).fiber x ‚ü∂ f.fiber x :=
      pullback.map _ _ _ _ (X.affineCover.f i) (ùüô _) (ùüô _) (by simp) (by simp)
    have : IsClosedImmersion g := .of_isPreimmersion _ (isClosed_discrete _)
    convert (inferInstanceAs (LocallyQuasiFinite <| g ‚â´ f.fiberToSpecResidueField _)) using 1
    simp [g, Hom.fiberToSpecResidueField]
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  algebraize [œÜ.hom]
  simp only [HasRingHomProperty.Spec_iff, id_eq]
  refine ‚ü®fun P hP ‚Ü¶ ?_‚ü©
  suffices LocallyQuasiFinite (Spec.map <|
      CommRingCat.ofHom <| algebraMap P.ResidueField (P.Fiber S)) by
    simp only [HasRingHomProperty.Spec_iff (P := @LocallyQuasiFinite), CommRingCat.hom_ofHom,
      RingHom.quasiFinite_algebraMap] at this
    exact .of_quasiFinite
  obtain ‚ü®x, rfl‚ü© : ‚àÉ x : Spec R, P = x.asIdeal := ‚ü®‚ü®P, hP‚ü©, rfl‚ü©
  refine (MorphismProperty.arrow_mk_iso_iff _ (Arrow.isoMk ?_ ?_ ?_)).mp (hf x)
  ¬∑ refine asIso (pullback.map _ _ _ _ (ùüô _) (Spec.map (Spec.residueFieldIso _ x).inv) (ùüô _)
      ?_ ?_) ‚â™‚â´ pullbackSymmetry _ _ ‚â™‚â´ pullbackSpecIso ..
    ¬∑ simp; rfl
    ¬∑ simp [‚Üê Spec.map_comp, fromSpecResidueField, Spec.fromSpecStalk_eq]
  ¬∑ exact asIso (Spec.map (Spec.residueFieldIso _ x).inv)
  ¬∑ simp [Hom.fiberToSpecResidueField]

@[deprecated (since := "2026-02-15")]
alias LocallyQuasiFinite.of_isFinite_fiberToSpecResidueField :=
  LocallyQuasiFinite.of_fiberToSpecResidueField

lemma locallyQuasiFinite_iff_isFinite_fiber {f : X ‚ü∂ Y} [QuasiCompact f] :
    LocallyQuasiFinite f ‚Üî ‚àÄ x, IsFinite (f.fiberToSpecResidueField x) :=
  ‚ü®fun _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ .of_fiberToSpecResidueField f fun _ ‚Ü¶ inferInstance‚ü©

instance (priority := low) [IsPreimmersion f] : LocallyQuasiFinite f := by
  refine .of_fiberToSpecResidueField _ fun x ‚Ü¶ ?_
  have : IsClosedImmersion (f.fiberToSpecResidueField x) :=
    .of_isPreimmersion (pullback.snd _ _) (isClosed_discrete _)
  infer_instance

nonrec lemma locallyQuasiFinite_iff_isDiscrete_preimage_singleton
    {f : X ‚ü∂ Y} [LocallyOfFiniteType f] :
    LocallyQuasiFinite f ‚Üî ‚àÄ x, IsDiscrete (f ‚Åª¬π' {x}) := by
  refine ‚ü®fun _ ‚Ü¶ f.isDiscrete_preimage_singleton, fun H ‚Ü¶ ?_‚ü©
  change id _ -- avoid typeclass synthesis from getting stuck on the wlog hypothesis.
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ refine (IsZariskiLocalAtTarget.iff_of_openCover Y.affineCover).mpr fun i ‚Ü¶
      this (f := pullback.snd _ _) (fun x ‚Ü¶ ?_) ‚ü®_, rfl‚ü©
    convert (H (Y.affineCover.f i x)).preimage ((pullback.fst f _).continuous.continuousOn)
      (pullback.fst f (Y.affineCover.f i)).isOpenEmbedding.injective
    ext
    simp [‚Üê (Y.affineCover.f i).isOpenEmbedding.injective.eq_iff, ‚Üê Scheme.Hom.comp_apply,
      -Hom.comp_base, pullback.condition]
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S
  ¬∑ exact (IsZariskiLocalAtSource.iff_of_openCover X.affineCover).mpr fun i ‚Ü¶
      this _ (fun x ‚Ü¶ (H x).preimage (X.affineCover.f _).continuous.continuousOn
      (X.affineCover.f _).isOpenEmbedding.injective) ‚ü®_, rfl‚ü©
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  simp only [HasRingHomProperty.Spec_iff, id_eq] at *
  algebraize [œÜ.hom]
  exact (Algebra.QuasiFinite.iff_finite_comap_preimage_singleton).mpr fun x ‚Ü¶
    ((Spec.map œÜ).isCompact_preimage_singleton _).finite (H _)

nonrec lemma LocallyQuasiFinite.of_finite_preimage_singleton
    [LocallyOfFiniteType f] (hf : ‚àÄ x, (f ‚Åª¬π' {x}).Finite) : LocallyQuasiFinite f := by
  change id _ -- avoid typeclass synthesis from getting stuck on the wlog hypothesis.
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ refine (IsZariskiLocalAtTarget.iff_of_openCover Y.affineCover).mpr fun i ‚Ü¶
      this (f := pullback.snd _ _) (fun x ‚Ü¶ ?_) ‚ü®_, rfl‚ü©
    convert (hf (Y.affineCover.f i x)).preimage
      (pullback.fst f (Y.affineCover.f i)).isOpenEmbedding.injective.injOn
    ext
    simp [‚Üê (Y.affineCover.f i).isOpenEmbedding.injective.eq_iff, ‚Üê Scheme.Hom.comp_apply,
      -Hom.comp_base, pullback.condition]
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S
  ¬∑ exact (IsZariskiLocalAtSource.iff_of_openCover X.affineCover).mpr fun i ‚Ü¶ this _ _
      (fun x ‚Ü¶ ((hf x).preimage (X.affineCover.f _).isOpenEmbedding.injective.injOn:)) ‚ü®_, rfl‚ü©
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  simp only [HasRingHomProperty.Spec_iff, id_eq] at *
  algebraize [œÜ.hom]
  exact (Algebra.QuasiFinite.iff_finite_comap_preimage_singleton).mpr hf

lemma locallyQuasiFinite_iff_finite_preimage_singleton
    {f : X ‚ü∂ Y} [LocallyOfFiniteType f] [QuasiCompact f] :
    LocallyQuasiFinite f ‚Üî ‚àÄ x, (f ‚Åª¬π' {x}).Finite :=
  ‚ü®fun _ ‚Ü¶ f.finite_preimage_singleton, .of_finite_preimage_singleton f‚ü©

lemma LocallyQuasiFinite.of_injective {f : X ‚ü∂ Y} [LocallyOfFiniteType f]
    (hf : Function.Injective f) : LocallyQuasiFinite f :=
  .of_finite_preimage_singleton _ fun _ ‚Ü¶ (Set.subsingleton_singleton.preimage hf).finite

instance (priority := low) {f : X ‚ü∂ Y} [LocallyOfFiniteType f]
    [UniversallyInjective f] : LocallyQuasiFinite f := .of_injective f.injective

/-- A morphism `f : X ‚ü∂ Y` is quasi-finite at `x : X`
if the stalk map `ùí™_{X, x} ‚ü∂ ùí™_{Y, f x}` is quasi-finite. -/
def Scheme.Hom.QuasiFiniteAt (x : X) : Prop := (f.stalkMap x).hom.QuasiFinite

variable {f} in
lemma Scheme.Hom.QuasiFiniteAt.quasiFiniteAt
    {x : X} (hx : f.QuasiFiniteAt x) {V : X.Opens} (hV : IsAffineOpen V) {U : Y.Opens}
    (hU : IsAffineOpen U) (hVU : V ‚â§ f ‚Åª¬π·µÅ U) (hxV : x ‚àà V.1) :
    (f.appLE U V hVU).hom.QuasiFiniteAt (hV.primeIdealOf ‚ü®x, hxV‚ü©).asIdeal := by
  algebraize [(f.appLE U V hVU).hom]
  have H : (Y.presheaf.germ U _ (hVU hxV)).hom.QuasiFinite := by
    algebraize [(Y.presheaf.germ U _ (hVU hxV)).hom]
    have := hU.isLocalization_stalk ‚ü®f x, (hVU hxV)‚ü©
    rw [‚Üê (Y.presheaf.germ U _ (hVU hxV)).hom.algebraMap_toAlgebra,
      RingHom.quasiFinite_algebraMap]
    exact .of_isLocalization (hU.primeIdealOf ‚ü®_, hVU hxV‚ü©).asIdeal.primeCompl
  algebraize [(X.presheaf.germ V x hxV).hom]
  have := hV.isLocalization_stalk ‚ü®x, hxV‚ü©
  let e := IsLocalization.algEquiv (hV.primeIdealOf ‚ü®x, hxV‚ü©).asIdeal.primeCompl
    (X.presheaf.stalk (‚ü®x, hxV‚ü© : V.1)) (Localization.AtPrime (hV.primeIdealOf ‚ü®x, hxV‚ü©).asIdeal)
  rw [RingHom.QuasiFiniteAt, Algebra.QuasiFiniteAt, ‚Üê RingHom.quasiFinite_algebraMap]
  convert (RingHom.QuasiFinite.of_finite e.finite).comp (hx.comp H)
  rw [‚Üê CommRingCat.hom_comp, f.germ_stalkMap, ‚Üê X.presheaf.germ_res (homOfLE hVU) _ hxV,
    Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_map_assoc, CommRingCat.hom_comp, ‚Üê RingHom.comp_assoc,
    IsScalarTower.algebraMap_eq Œì(Y, U) Œì(X, V), e.toAlgHom.comp_algebraMap.symm]
  rfl

lemma Scheme.Hom.quasiFiniteAt [LocallyQuasiFinite f] (x : X) :
    f.QuasiFiniteAt x := by
  refine HasRingHomProperty.stalkMap ?_ ‚Äπ_‚Ä∫ x
  introv hf
  algebraize [f]
  refine .of_comp (g := algebraMap R _) ?_
  convert RingHom.quasiFinite_algebraMap.mpr (inferInstanceAs
    (Algebra.QuasiFinite R (Localization.AtPrime J)))
  ext; simp; rfl

set_option backward.isDefEq.respectTransparency false in
lemma Scheme.Hom.quasiFiniteAt_comp_iff_of_isOpenImmersion
    {Z : Scheme} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} {x : X} [IsOpenImmersion f] :
    (f ‚â´ g).QuasiFiniteAt x ‚Üî g.QuasiFiniteAt (f x) := by
  simp only [QuasiFiniteAt, stalkMap_comp, CommRingCat.hom_comp,
    RingHom.QuasiFinite.respectsIso.cancel_right_isIso]

lemma Scheme.Hom.quasiFiniteAt_comp_iff {Z : Scheme} {f : X ‚ü∂ Y} {g : Y ‚ü∂ Z} {x : X}
    [LocallyQuasiFinite g] :
    (f ‚â´ g).QuasiFiniteAt x ‚Üî f.QuasiFiniteAt x := by
  simp only [QuasiFiniteAt, stalkMap_comp]
  exact RingHom.QuasiFinite.comp_iff (g.quasiFiniteAt _)

lemma Scheme.Hom.quasiFiniteAt_iff {f : X ‚ü∂ Y} {x : X} :
    f.QuasiFiniteAt x ‚Üî LocallyQuasiFinite (X.fromSpecStalk x ‚â´ f) := by
  rw [‚Üê SpecMap_stalkMap_fromSpecStalk, LocallyQuasiFinite.comp_iff,
    HasRingHomProperty.Spec_iff (P := @LocallyQuasiFinite), QuasiFiniteAt]

set_option backward.isDefEq.respectTransparency false in
nonrec lemma Scheme.Hom.quasiFiniteAt_iff_isOpen_singleton_asFiber
    {f : X ‚ü∂ Y} [LocallyOfFiniteType f] {x : X} :
    f.QuasiFiniteAt x ‚Üî IsOpen {f.asFiber x} := by
  rw [‚Üê (f.fiberHomeo (f x)).isOpen_image]
  simp only [Set.image_singleton, asFiber, Homeomorph.apply_symm_apply]
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ obtain ‚ü®i, y, hy‚ü© := Y.affineCover.exists_eq (f x)
    obtain ‚ü®x, rfl, rfl‚ü© := Scheme.Pullback.exists_preimage_pullback _ _ hy.symm
    let Œπ := Y.affineCover.f i
    convert this (f := pullback.snd f Œπ) (x := x) ‚ü®_, rfl‚ü© using 1
    ¬∑ exact (RingHom.QuasiFinite.respectsIso.arrow_mk_iso_iff
        (Scheme.stalkMapIsoOfIsPullback (.of_hasPullback f Œπ) x))
    have H : pullback.snd f Œπ ‚Åª¬π' {pullback.snd f Œπ x} =
        pullback.fst f Œπ ‚Åª¬π' (f ‚Åª¬π' {f (pullback.fst f Œπ x)}) := by
      rw [‚Üê Set.preimage_comp, ‚Üê TopCat.coe_comp, ‚Üê Scheme.Hom.comp_base, ‚Üê Scheme.Hom.comp_apply,
        pullback.condition]
      simp [‚Üê Set.image_singleton, Set.preimage_comp, Set.preimage_image_eq _ Œπ.injective]
    let f' : pullback.snd f Œπ ‚Åª¬π' {pullback.snd f Œπ x} ‚Üí f ‚Åª¬π' {f (pullback.fst f Œπ x)} :=
      Set.MapsTo.restrict (pullback.fst f Œπ) _ _ fun a ha ‚Ü¶ H.le ha
    have : Topology.IsOpenEmbedding f' := by
      convert (f ‚Åª¬π' {f (pullback.fst f Œπ x)}).restrictPreimage_isOpenEmbedding
        (pullback.fst f Œπ).isOpenEmbedding using 0
      dsimp [f', Set.restrictPreimage]
      congr!
    rw [this.isOpen_iff_image_isOpen, Set.image_singleton]; rfl
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S
  ¬∑ obtain ‚ü®i, x, rfl‚ü© := X.affineCover.exists_eq x
    let Œπ := X.affineCover.f i
    convert this (x := x) _ (f := Œπ ‚â´ f) ‚ü®_, rfl‚ü© using 1
    ¬∑ exact quasiFiniteAt_comp_iff_of_isOpenImmersion.symm
    rw [((f ‚Åª¬π' {f (Œπ x)}).restrictPreimage_isOpenEmbedding
      Œπ.isOpenEmbedding).isOpen_iff_image_isOpen, Set.image_singleton]; rfl
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl‚ü© := Spec.map_surjective f
  rw [HasRingHomProperty.Spec_iff (P := @LocallyOfFiniteType)] at ‚ÄπLocallyOfFiniteType (Spec.map œÜ)‚Ä∫
  algebraize [œÜ.hom]
  rw [‚Üê Algebra.quasiFiniteAt_iff_isOpen_singleton_fiber]
  trans Algebra.QuasiFinite (Localization.AtPrime (x.asIdeal.under R))
    (Localization.AtPrime x.asIdeal)
  ¬∑ rw [‚Üê RingHom.quasiFinite_algebraMap]
    exact RingHom.QuasiFinite.respectsIso.arrow_mk_iso_iff (Scheme.arrowStalkMapSpecIso ..)
  exact ‚ü®fun _ ‚Ü¶ .trans _ (Localization.AtPrime (x.asIdeal.under R)) _,
    fun _ ‚Ü¶ .of_restrictScalars R _ _‚ü©

nonrec lemma Scheme.Hom.QuasiFiniteAt.isClopen_singleton_asFiber
    [LocallyOfFiniteType f] {x : X} (hx : f.QuasiFiniteAt x) : IsClopen {f.asFiber x} := by
  have := Scheme.Hom.quasiFiniteAt_iff_isOpen_singleton_asFiber.mp ‚Äπ_‚Ä∫
  exact ‚ü®isClosed_singleton_of_isLocallyClosed_singleton this.isLocallyClosed, this‚ü©

end AlgebraicGeometry
