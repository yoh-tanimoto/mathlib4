/-
Copyright (c) 2026 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.Algebra.Category.Ring.Adjunctions
public import Mathlib.AlgebraicGeometry.IdealSheaf.Functorial
public import Mathlib.AlgebraicGeometry.Morphisms.Flat

/-!
# Scheme-theoretically dominant morphisms

In this file, we define scheme-theoretically dominant morphisms as morphisms with trivial kernel.
## Main results
- `AlgebraicGeometry.IsSchemeTheoreticallyDominant`:
  The class of scheme-theoretically dominant morphisms.
- `AlgebraicGeometry.isSchemeTheoreticallyDominant_iff_isDominant`:
  If the target is reduced and the map is quasi-compact, then scheme-theoretically dominant
  is equivalent to dominant.
- `AlgebraicGeometry.IsSchemeTheoreticallyDominant.of_isPullback`:
  quasicompact + scheme-theoretically dominant is stable under flat base change.

-/

@[expose] public section

open CategoryTheory MorphismProperty Limits

namespace AlgebraicGeometry

variable {X Y Z S : Scheme} (f : X ‚ü∂ S) (g : Y ‚ü∂ S)

/-- A morphism is scheme-theoretically dominant if its kernel is trivial. -/
@[mk_iff]
class IsSchemeTheoreticallyDominant (f : X ‚ü∂ Y) : Prop where
  ker_eq_bot (f) : f.ker = ‚ä•

alias Scheme.Hom.ker_eq_bot := IsSchemeTheoreticallyDominant.ker_eq_bot

instance (priority := low) [IsIso f] : IsSchemeTheoreticallyDominant f :=
  ‚ü®by simp‚ü©

set_option backward.isDefEq.respectTransparency false in
instance (priority := low) [IsSchemeTheoreticallyDominant f] [QuasiCompact f] :
    IsDominant f := by
  rw [isDominant_iff, DenseRange, dense_iff_closure_eq, ‚Üê Scheme.Hom.support_ker,
    f.ker_eq_bot, Scheme.IdealSheafData.support_bot, TopologicalSpace.Closeds.coe_top]

instance (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z) [IsSchemeTheoreticallyDominant f]
    [IsSchemeTheoreticallyDominant g] :
    IsSchemeTheoreticallyDominant (f ‚â´ g) := by
  rw [isSchemeTheoreticallyDominant_iff, Scheme.Hom.ker_comp, f.ker_eq_bot,
    Scheme.IdealSheafData.map_bot, g.ker_eq_bot]

instance : IsMultiplicative @IsSchemeTheoreticallyDominant where
  id_mem _ := inferInstance
  comp_mem _ _ _ _ := inferInstance

set_option backward.isDefEq.respectTransparency false in
lemma IsSchemeTheoreticallyDominant.of_isDominant (f : X ‚ü∂ Y) [IsDominant f] [IsReduced Y] :
    IsSchemeTheoreticallyDominant f := by
  rw [isSchemeTheoreticallyDominant_iff, ‚Üê Scheme.IdealSheafData.support_eq_top_iff,
    ‚Üê SetLike.coe_injective.eq_iff, TopologicalSpace.Closeds.coe_top, ‚Üê Set.univ_subset_iff,
    ‚Üê f.denseRange.closure_eq, f.ker.support.isClosed.closure_subset_iff]
  exact f.range_subset_ker_support

/-- If the target is reduced and the map is quasi-compact, then scheme-theoretically dominant
is equivalent to dominant. -/
lemma isSchemeTheoreticallyDominant_iff_isDominant (f : X ‚ü∂ Y) [QuasiCompact f] [IsReduced Y] :
    IsSchemeTheoreticallyDominant f ‚Üî IsDominant f :=
  ‚ü®fun _ ‚Ü¶ inferInstance, fun _ ‚Ü¶ .of_isDominant _‚ü©

lemma Scheme.Hom.app_injective (f : X ‚ü∂ Y) [IsSchemeTheoreticallyDominant f] [QuasiCompact f]
    (U : Y.Opens) :
    Function.Injective (f.app U) := by
  wlog hU : IsAffineOpen U generalizing U; swap
  ¬∑ rw [RingHom.injective_iff_ker_eq_bot, ‚Üê f.ker_apply ‚ü®U, hU‚ü©, f.ker_eq_bot]
    simp
  rw [injective_iff_map_eq_zero]
  intro s hs
  refine Y.IsSheaf.section_ext fun x hx ‚Ü¶ ?_
  obtain ‚ü®_, ‚ü®V, hV, rfl‚ü©, hxV, hVU : V ‚â§ U‚ü© :=
    Y.isBasis_affineOpens.exists_subset_of_mem_open hx U.isOpen
  refine ‚ü®V, hVU, hxV, this V hV ?_‚ü©
  rw [‚Üê ConcreteCategory.comp_apply, f.naturality]
  simp [hs]

lemma IsSchemeTheoreticallyDominant.isReduced (f : X ‚ü∂ Y) [IsSchemeTheoreticallyDominant f]
    [QuasiCompact f] [IsReduced X] : IsReduced Y :=
  ‚ü®fun _ ‚Ü¶ isReduced_of_injective _ (f.app_injective _)‚ü©

set_option backward.isDefEq.respectTransparency false in
instance IsSchemeTheoreticallyDominant.pullbackSnd (f : X ‚ü∂ S) (g : Y ‚ü∂ S)
    [IsSchemeTheoreticallyDominant f] [QuasiCompact f] [Flat g] :
    IsSchemeTheoreticallyDominant (pullback.snd f g) := by
  rw [isSchemeTheoreticallyDominant_iff]
  let hùí∞ := Y.isBasis_affineOpens.isOpenCover_mem_and_le
    (S.isBasis_affineOpens.isOpenCover.comap g.base.hom)
  refine Scheme.IdealSheafData.ext_of_iSup_eq_top (fun U ‚Ü¶ ‚ü®_, by exact U.2.1‚ü©) hùí∞ ?_
  rintro ‚ü®‚ü®V, ‚ü®U, hU‚ü©‚ü©, hV, hVU : V ‚â§ g ‚Åª¬π·µÅ U‚ü©
  simp only [Scheme.Hom.ker_apply, Scheme.IdealSheafData.ideal_bot, Pi.bot_apply, ‚Üê le_bot_iff]
  intro x hx
  have := mono_pushoutSection_of_isCompact_of_flat_right
    (.of_hasPullback f g) (UY := pullback.snd f g ‚Åª¬π·µÅ V) hVU le_rfl (by
      grw [‚Üê Scheme.Hom.comp_preimage, pullback.condition, Scheme.Hom.comp_preimage, right_eq_inf,
        hVU]) hU hV (f.isCompact_preimage hU.isCompact)
  rw [@ConcreteCategory.mono_iff_injective_of_preservesPullback] at this
  refine CommRingCat.inr_injective_of_flat (f.appLE U (f ‚Åª¬π·µÅ U) le_rfl) (g.appLE U V hVU)
    (by simpa [Scheme.Hom.appLE] using f.app_injective U) (g.flat_appLE hU hV hVU) ?_
  apply this
  simpa [‚Üê CommRingCat.comp_apply, ‚Üê Scheme.Hom.app_eq_appLE] using hx

lemma IsSchemeTheoreticallyDominant.of_isPullback {f : X ‚ü∂ S} {g : Y ‚ü∂ S}
    {pX : Z ‚ü∂ X} {pY : Z ‚ü∂ Y} (H : IsPullback pX pY f g)
    [IsSchemeTheoreticallyDominant f] [QuasiCompact f] [Flat g] :
    IsSchemeTheoreticallyDominant pY := by
  rw [‚Üê H.isoPullback_hom_snd]
  infer_instance

instance IsSchemeTheoreticallyDominant.pullbackFst (f : X ‚ü∂ S) (g : Y ‚ü∂ S)
    [IsSchemeTheoreticallyDominant g] [QuasiCompact g] [Flat f] :
    IsSchemeTheoreticallyDominant (pullback.fst f g) :=
  .of_isPullback (.flip <| .of_hasPullback _ _)

end AlgebraicGeometry
