/-
Copyright (c) 2025 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.AlgebraicGeometry.Morphisms.Proper
public import Mathlib.RingTheory.Ideal.IdempotentFG
public import Mathlib.RingTheory.RingHom.Unramified
public import Mathlib.RingTheory.Unramified.LocalRing

/-!
# Formally unramified morphisms

A morphism of schemes `f : X ‚ü∂ Y` is formally unramified if for each affine `U ‚äÜ Y` and
`V ‚äÜ f ‚Åª¬π' U`, the induced map `Œì(Y, U) ‚ü∂ Œì(X, V)` is formally unramified.

We show that these properties are local, and are stable under compositions and base change.

-/

@[expose] public section


noncomputable section

open CategoryTheory CategoryTheory.Limits Opposite TopologicalSpace

universe v u

open AlgebraicGeometry

/-- If `S` is a formally unramified `R`-algebra, essentially of finite type, the diagonal is an
open immersion. -/
instance Algebra.FormallyUnramified.isOpenImmersion_SpecMap_lmul {R S : Type u} [CommRing R]
    [CommRing S] [Algebra R S] [Algebra.FormallyUnramified R S] [Algebra.EssFiniteType R S] :
    IsOpenImmersion (Spec.map (CommRingCat.ofHom (TensorProduct.lmul' R (S := S)).toRingHom)) := by
  rw [isOpenImmersion_SpecMap_iff_of_surjective _ (fun x ‚Ü¶ ‚ü®1 ‚äó‚Çú x, by simp‚ü©)]
  apply (Ideal.isIdempotentElem_iff_of_fg _ (KaehlerDifferential.ideal_fg R S)).mp
  apply (Ideal.cotangent_subsingleton_iff _).mp
  exact inferInstanceAs <| Subsingleton Œ©[S‚ÅÑR]

namespace AlgebraicGeometry

variable {X Y : Scheme.{u}} (f : X ‚ü∂ Y)

/-- A morphism of schemes `f : X ‚ü∂ Y` is formally unramified if for each affine `U ‚äÜ Y` and
`V ‚äÜ f ‚Åª¬π' U`, The induced map `Œì(Y, U) ‚ü∂ Œì(X, V)` is formally unramified.

See `FormallyUnramified.hom_ext` and `FormallyUnramified.of_hom_ext`
for the infinitesimal lifting criterion. -/
@[mk_iff]
class FormallyUnramified (f : X ‚ü∂ Y) : Prop where
  formallyUnramified_appLE (f) :
    ‚àÄ {U : Y.Opens} (_ : IsAffineOpen U) {V : X.Opens} (_ : IsAffineOpen V) (e : V ‚â§ f ‚Åª¬π·µÅ U),
      (f.appLE U V e).hom.FormallyUnramified

alias Scheme.Hom.formallyUnramified_appLE := FormallyUnramified.formallyUnramified_appLE

@[deprecated (since := "2026-01-20")]
alias FormallyUnramified.formallyUnramified_of_affine_subset := Scheme.Hom.formallyUnramified_appLE

namespace FormallyUnramified

instance : HasRingHomProperty @FormallyUnramified RingHom.FormallyUnramified where
  isLocal_ringHomProperty := RingHom.FormallyUnramified.propertyIsLocal
  eq_affineLocally' := by
    ext X Y f
    rw [formallyUnramified_iff, affineLocally_iff_forall_isAffineOpen]

instance : MorphismProperty.IsStableUnderComposition @FormallyUnramified :=
  HasRingHomProperty.stableUnderComposition RingHom.FormallyUnramified.stableUnderComposition

/-- `f : X ‚ü∂ S` is formally unramified if `X ‚ü∂ X √ó‚Çõ X` is an open immersion.
In particular, monomorphisms (e.g. immersions) are formally unramified.
The converse is true if `f` is locally of finite type. -/
instance (priority := 900) [IsOpenImmersion (pullback.diagonal f)] : FormallyUnramified f := by
  wlog hY : ‚àÉ R, Y = Spec R
  ¬∑ rw [IsZariskiLocalAtTarget.iff_of_openCover (P := @FormallyUnramified) Y.affineCover]
    intro i
    have inst : IsOpenImmersion (pullback.diagonal (pullback.snd f (Y.affineCover.f i))) :=
      MorphismProperty.pullback_snd (P := .diagonal @IsOpenImmersion) _ _ ‚Äπ_‚Ä∫
    exact this (pullback.snd _ _) ‚ü®_, rfl‚ü©
  obtain ‚ü®R, rfl‚ü© := hY
  wlog hX : ‚àÉ S, X = Spec S generalizing X
  ¬∑ rw [IsZariskiLocalAtSource.iff_of_openCover (P := @FormallyUnramified) X.affineCover]
    intro i
    have inst : IsOpenImmersion (pullback.diagonal (X.affineCover.f i ‚â´ f)) :=
      MorphismProperty.comp_mem (.diagonal @IsOpenImmersion) _ _
        (inferInstanceAs (IsOpenImmersion _)) ‚Äπ_‚Ä∫
    exact this (_ ‚â´ _) ‚ü®_, rfl‚ü©
  obtain ‚ü®S, rfl‚ü© := hX
  obtain ‚ü®œÜ, rfl : Spec.map œÜ = f‚ü© := Spec.homEquiv.symm.surjective f
  rw [HasRingHomProperty.Spec_iff (P := @FormallyUnramified)]
  algebraize [œÜ.hom]
  let F := (Algebra.TensorProduct.lmul' R (S := S)).toRingHom
  have hF : Function.Surjective F := fun x ‚Ü¶ ‚ü®.mk _ _ _ x 1, by simp [F]‚ü©
  have : IsOpenImmersion (Spec.map (CommRingCat.ofHom F)) := by
    rwa [‚Üê MorphismProperty.cancel_right_of_respectsIso (P := @IsOpenImmersion) _
      (pullbackSpecIso R S S).inv, ‚Üê AlgebraicGeometry.diagonal_SpecMap R S]
  obtain ‚ü®e, he, he'‚ü© := (isOpenImmersion_SpecMap_iff_of_surjective _ hF).mp this
  refine ‚ü®subsingleton_of_forall_eq 0 fun x ‚Ü¶ ?_‚ü©
  obtain ‚ü®‚ü®x, hx‚ü©, rfl‚ü© := Ideal.toCotangent_surjective _ x
  obtain ‚ü®x, rfl‚ü© := Ideal.mem_span_singleton.mp (he'.le hx)
  refine (Ideal.toCotangent_eq_zero _ _).mpr ?_
  rw [pow_two, Subtype.coe_mk, ‚Üê he, mul_assoc]
  exact Ideal.mul_mem_mul (he'.ge (Ideal.mem_span_singleton_self e)) hx

theorem of_comp {X Y Z : Scheme} (f : X ‚ü∂ Y) (g : Y ‚ü∂ Z)
    [FormallyUnramified (f ‚â´ g)] : FormallyUnramified f :=
  HasRingHomProperty.of_comp (fun {R S T _ _ _} f g H ‚Ü¶ by
    algebraize [f, g, g.comp f]
    exact Algebra.FormallyUnramified.of_restrictScalars R S T) ‚Äπ_‚Ä∫

instance : MorphismProperty.IsMultiplicative @FormallyUnramified where
  id_mem _ := inferInstance

instance : MorphismProperty.IsStableUnderBaseChange @FormallyUnramified :=
  HasRingHomProperty.isStableUnderBaseChange RingHom.FormallyUnramified.isStableUnderBaseChange

open MorphismProperty in
/-- The diagonal of a formally unramified morphism of finite type is an open immersion. -/
instance isOpenImmersion_diagonal [FormallyUnramified f] [LocallyOfFiniteType f] :
    IsOpenImmersion (pullback.diagonal f) := by
  wlog hX : (‚àÉ S, X = Spec S) ‚àß ‚àÉ R, Y = Spec R
  ¬∑ let ùí∞Y := Y.affineCover
    let ùí∞X (j : (Y.affineCover.pullback‚ÇÅ f).I‚ÇÄ) :
        ((Y.affineCover.pullback‚ÇÅ f).X j).OpenCover := Scheme.affineCover _
    apply IsZariskiLocalAtTarget.of_range_subset_iSup _
      (Scheme.Pullback.range_diagonal_subset_diagonalCoverDiagonalRange f ùí∞Y ùí∞X)
    intro ‚ü®i, j‚ü©
    rw [arrow_mk_iso_iff (P := @IsOpenImmersion)
      (Scheme.Pullback.diagonalRestrictIsoDiagonal f ùí∞Y ùí∞X i j)]
    have hu : FormallyUnramified ((ùí∞X i).f j ‚â´ pullback.snd f (ùí∞Y.f i)) :=
      comp_mem _ _ _ inferInstance (pullback_snd _ _ inferInstance)
    have hfin : LocallyOfFiniteType ((ùí∞X i).f j ‚â´ pullback.snd f (ùí∞Y.f i)) :=
      comp_mem _ _ _ inferInstance (pullback_snd _ _ inferInstance)
    exact this _ ‚ü®‚ü®_, rfl‚ü©, ‚ü®_, rfl‚ü©‚ü©
  obtain ‚ü®‚ü®S, rfl‚ü©, R, rfl‚ü© := hX
  obtain ‚ü®f, rfl‚ü© := Spec.map_surjective f
  rw [HasRingHomProperty.Spec_iff (P := @FormallyUnramified),
    HasRingHomProperty.Spec_iff (P := @LocallyOfFiniteType)] at *
  algebraize [f.hom]
  rw [show f = CommRingCat.ofHom (algebraMap R S) from rfl, diagonal_SpecMap R S,
    cancel_right_of_respectsIso (P := @IsOpenImmersion)]
  infer_instance

lemma stalkMap [FormallyUnramified f] (x : X) : (f.stalkMap x).hom.FormallyUnramified :=
  HasRingHomProperty.stalkMap
    (fun f hf p q ‚Ü¶
      RingHom.FormallyUnramified.holdsForLocalization.localRingHom
        RingHom.FormallyUnramified.stableUnderComposition
        RingHom.FormallyUnramified.isStableUnderBaseChange.localizationPreserves _ hf) ‚Äπ_‚Ä∫ x

set_option backward.isDefEq.respectTransparency false in
instance [FormallyUnramified f] [LocallyOfFiniteType f] (x : X) :
    letI : Algebra (Y.residueField (f.base x)) (X.residueField x) :=
      (f.residueFieldMap x).hom.toAlgebra
    Algebra.IsSeparable (Y.residueField (f.base x)) (X.residueField x) := by
  algebraize [(f.stalkMap x).hom]
  have : IsLocalHom (algebraMap (Y.presheaf.stalk (f x)) (X.presheaf.stalk x)) :=
    inferInstanceAs <| IsLocalHom (f.stalkMap x).hom
  suffices h : Algebra.IsSeparable
      (IsLocalRing.ResidueField <| Y.presheaf.stalk (f x))
      (IsLocalRing.ResidueField <| X.presheaf.stalk x) by
    convert h
    refine Algebra.algebra_ext _ _ fun x ‚Ü¶ ?_
    obtain ‚ü®x, rfl‚ü© := IsLocalRing.residue_surjective x
    rfl
  have : Algebra.EssFiniteType (Y.presheaf.stalk (f x)) (X.presheaf.stalk x) := by
    rw [‚Üê RingHom.essFiniteType_algebraMap, RingHom.algebraMap_toAlgebra]
    exact LocallyOfFiniteType.stalkMap f x
  have : Algebra.FormallyUnramified (Y.presheaf.stalk (f x)) (X.presheaf.stalk x) := by
    rw [‚Üê RingHom.formallyUnramified_algebraMap, RingHom.algebraMap_toAlgebra]
    exact stalkMap f x
  infer_instance

set_option backward.isDefEq.respectTransparency false in
/--
Given any commuting diagram
```
Z' --‚Üí X
|      |
‚Üì      ‚Üì
Z  --‚Üí Y
```
With `X ‚ü∂ Y` formally unramified and `Z' ‚ü∂ Z` an infinitesimal thickening, there exists at most
one arrow `Z ‚ü∂ X` making the diagram commute.
-/
@[stacks 04F1]
protected lemma hom_ext {Z' Z : Scheme} (i : Z' ‚ü∂ Z) (hi : IsNilpotent i.ker) [IsClosedImmersion i]
    (f : X ‚ü∂ Y) [FormallyUnramified f]
    {g‚ÇÅ g‚ÇÇ : Z ‚ü∂ X} (hig : i ‚â´ g‚ÇÅ = i ‚â´ g‚ÇÇ) (hgf : g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f) : g‚ÇÅ = g‚ÇÇ := by
  have : IsDominant i := by
    obtain ‚ü®n, hn‚ü© := hi
    rw [isDominant_iff, denseRange_iff_closure_range, ‚Üê i.support_ker,
      ‚Üê i.ker.support_pow (n + 1) (by simp), pow_succ, hn]
    simp
  refine Scheme.hom_ext_of_forall _ _ fun x ‚Ü¶ ?_
  obtain ‚ü®_, ‚ü®U, hU, rfl‚ü©, hxU, -‚ü© :=
    Y.isBasis_affineOpens.exists_subset_of_mem_open (Set.mem_univ (f (g‚ÇÅ x))) isOpen_univ
  obtain ‚ü®_, ‚ü®V, hV, rfl‚ü©, hxV, hVU : V ‚â§ f ‚Åª¬π·µÅ U‚ü© :=
    X.isBasis_affineOpens.exists_subset_of_mem_open hxU (f ‚Åª¬π·µÅ U).isOpen
  have : g‚ÇÅ.base = g‚ÇÇ.base := by ext x; obtain ‚ü®x, rfl‚ü© := i.surjective x; exact congr($hig x)
  obtain ‚ü®_, ‚ü®W, hW, rfl‚ü©, hxW, hWV : W ‚â§ _‚ü© := Z.isBasis_affineOpens.exists_subset_of_mem_open
    (And.intro hxV (by simpa [‚Üê this])) (g‚ÇÅ ‚Åª¬π·µÅ V ‚äì g‚ÇÇ ‚Åª¬π·µÅ V).isOpen
  refine ‚ü®W, hxW, ?_‚ü©
  have := f.formallyUnramified_appLE hU hV hVU
  algebraize [(f.appLE U V hVU).hom,
    ((g‚ÇÅ ‚â´ f).appLE U W (by grw [hWV, inf_le_left, hVU]; rfl)).hom]
  let œà‚ÇÅ : Œì(X, V) ‚Üí‚Çê[Œì(Y, U)] Œì(Z, W) := ‚ü®(g‚ÇÅ.appLE _ _ (hWV.trans inf_le_left)).hom, fun r ‚Ü¶ by
    simp [RingHom.algebraMap_toAlgebra, ‚Üê CategoryTheory.comp_apply, -CommRingCat.hom_comp,
      Scheme.Hom.appLE_comp_appLE]‚ü©
  let œà‚ÇÇ : Œì(X, V) ‚Üí‚Çê[Œì(Y, U)] Œì(Z, W) := ‚ü®(g‚ÇÇ.appLE _ _ (hWV.trans inf_le_right)).hom, fun r ‚Ü¶ by
    simp [RingHom.algebraMap_toAlgebra, ‚Üê CategoryTheory.comp_apply, -CommRingCat.hom_comp,
      Scheme.Hom.appLE_comp_appLE, hgf, - Scheme.Hom.comp_appLE]‚ü©
  suffices œà‚ÇÅ = œà‚ÇÇ by
    simpa [œà‚ÇÅ, œà‚ÇÇ, -Iso.cancel_iso_hom_left, IsAffineOpen.isoSpec_hom] using
      congr(hW.isoSpec.hom ‚â´ Spec.map (CommRingCat.ofHom ($this).toRingHom) ‚â´ hV.fromSpec)
  refine Algebra.FormallyUnramified.ext' (i.app W).hom ?_ œà‚ÇÅ œà‚ÇÇ ?_
  ¬∑ obtain ‚ü®n, hn‚ü© := hi
    exact ‚ü®n, by simpa using congr(($hn).ideal ‚ü®W, hW‚ü©)‚ü©
  ¬∑ simp [œà‚ÇÅ, œà‚ÇÇ, ‚Üê CategoryTheory.comp_apply, -CommRingCat.hom_comp, hig,
      Scheme.Hom.app_eq_appLE, Scheme.Hom.appLE_comp_appLE, - Scheme.Hom.comp_appLE]

/--
To show that `f : X ‚ü∂ Y` is formally unramified,
it suffices to check for that every following commuting diagram
```
Spec R --‚Üí X
  |        |
  ‚Üì        ‚Üì
Spec S --‚Üí Y
```
with `S = R/I` for some `I¬≤ = 0`, there exists at most one arrow `Spec S ‚ü∂ X` making
the diagram commute.
-/
protected lemma of_hom_ext (f : X ‚ü∂ Y)
    (H : ‚àÄ (R S : CommRingCat) (œÜ : R ‚ü∂ S) (_ : Function.Surjective œÜ)
      (_ : RingHom.ker œÜ.hom ^ 2 = ‚ä•) (g‚ÇÅ g‚ÇÇ : Spec R ‚ü∂ X)
      (_ : Spec.map œÜ ‚â´ g‚ÇÅ = Spec.map œÜ ‚â´ g‚ÇÇ) (_ : g‚ÇÅ ‚â´ f = g‚ÇÇ ‚â´ f), g‚ÇÅ = g‚ÇÇ) :
    FormallyUnramified f := by
  refine ‚ü®fun {U hU V hV hVU} ‚Ü¶ ?_‚ü©
  letI := (f.appLE U V hVU).hom.toAlgebra
  refine Algebra.FormallyUnramified.iff_comp_injective.mpr fun R _ _ I hI g‚ÇÅ g‚ÇÇ hg‚ÇÅg‚ÇÇ ‚Ü¶ ?_
  have hg‚ÇÅ : f.appLE U V hVU ‚â´ CommRingCat.ofHom g‚ÇÅ = CommRingCat.ofHom (algebraMap _ R) :=
    CommRingCat.hom_ext g‚ÇÅ.comp_algebraMap
  have hg‚ÇÇ : f.appLE U V hVU ‚â´ CommRingCat.ofHom g‚ÇÇ = CommRingCat.ofHom (algebraMap _ R) :=
    CommRingCat.hom_ext g‚ÇÇ.comp_algebraMap
  have := H (.of R) (.of (R ‚ß∏ I)) (CommRingCat.ofHom (Ideal.Quotient.mk‚Çê Œì(Y, U) I))
    Ideal.Quotient.mk_surjective (by simpa)
    (Spec.map (CommRingCat.ofHom g‚ÇÅ) ‚â´ hV.fromSpec) (Spec.map (CommRingCat.ofHom g‚ÇÇ) ‚â´ hV.fromSpec)
    (by simp only [‚Üê Spec.map_comp_assoc, ‚Üê CommRingCat.ofHom_comp, ‚Üê AlgHom.comp_toRingHom, *])
    (by simp only [Category.assoc, ‚Üê hU.SpecMap_appLE_fromSpec f hV hVU, ‚Üê Spec.map_comp_assoc, *])
  rw [cancel_mono, Spec.map_inj] at this
  exact AlgHom.ext fun x ‚Ü¶ congr($this x)

end FormallyUnramified

end AlgebraicGeometry
