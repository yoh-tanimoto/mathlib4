/-
Copyright (c) 2026 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.AlgebraicGeometry.Morphisms.Finite
public import Mathlib.FieldTheory.IsAlgClosed.Basic

/-!
# Schemes over algebraically closed fields

We show that if `X` is locally of finite type over an algebraically closed field `k`,
then the closed points of `X` are in bijection with the `k`-points of `X`.
See `AlgebraicGeometry.pointEquivClosedPoint`.

-/

@[expose] public noncomputable section

open CategoryTheory

namespace AlgebraicGeometry

universe u

variable {X Y : Scheme.{u}} {K : Type u} [Field K] [IsAlgClosed K]
    (f : X ‚ü∂ Spec (.of K)) [LocallyOfFiniteType f] (x : X) (hx : IsClosed {x})

set_option backward.isDefEq.respectTransparency false in
/-- If `X` is a locally of finite type `k`-scheme and `k` is algebraically closed, then
the residue field of any closed point of `x` is isomorphic to `k`. -/
def residueFieldIsoBase : X.residueField x ‚âÖ .of K :=
  letI : IsIso (Spec.preimage (X.fromSpecResidueField x ‚â´ f)) := by
    have : IsFinite (X.fromSpecResidueField x ‚â´ f) := by
      rw [isClosed_singleton_iff_isClosedImmersion] at hx
      rw [isFinite_iff_locallyOfFiniteType_of_jacobsonSpace]
      infer_instance
    rw [ConcreteCategory.isIso_iff_bijective]
    refine IsAlgClosed.ringHom_bijective_of_isIntegral _ ?_
    rw [‚Üê IsIntegralHom.SpecMap_iff, Spec.map_preimage]
    infer_instance
  (asIso (Spec.preimage (X.fromSpecResidueField x ‚â´ f))).symm

@[simp, reassoc]
lemma SpecMap_residueFieldIsoBase_inv :
    Spec.map (residueFieldIsoBase f x hx).inv = X.fromSpecResidueField x ‚â´ f :=
  Spec.map_preimage _

/-- If `k` is algebraically closed, this is the `k`-point of `X` associated to a closed point. -/
noncomputable
def pointOfClosedPoint : Spec (.of K) ‚ü∂ X :=
  Spec.map (residueFieldIsoBase f x hx).hom ‚â´ X.fromSpecResidueField x

@[reassoc (attr := simp)]
lemma pointOfClosedPoint_comp : pointOfClosedPoint f x hx ‚â´ f = ùüô _ := by
  simp [pointOfClosedPoint, ‚Üê SpecMap_residueFieldIsoBase_inv, ‚Üê Spec.map_comp]

@[simp]
lemma pointOfClosedPoint_apply (a : _) : pointOfClosedPoint f x hx a = x := by
  simp [pointOfClosedPoint]

/-- If `k` is algebraically closed,
then the closed points of `X` are in bijection with the `k`-points of `X`. -/
@[simps]
def pointEquivClosedPoint :
    {p : Spec (.of K) ‚ü∂ X // p ‚â´ f = ùüô _} ‚âÉ closedPoints X where
  toFun p := ‚ü®p.1 (IsLocalRing.closedPoint K), by
    have := isClosedImmersion_of_comp_eq_id _ _ p.2
    have := p.1.isClosedEmbedding.isClosed_range
    rwa [Set.range_eq_singleton] at this
    exact fun x ‚Ü¶ congr(p.1 $(Subsingleton.elim _ _))‚ü©
  invFun x := ‚ü®pointOfClosedPoint f x.1 x.2, pointOfClosedPoint_comp f x.1 x.2‚ü©
  left_inv p := by
    ext
    refine ((Scheme.SpecToEquivOfField _ _).symm_apply_eq (x := ‚ü®_, _‚ü©)).mpr ?_
    rw [Scheme.SpecToEquivOfField_eq_iff]
    dsimp [Scheme.SpecToEquivOfField]
    simp only [Category.id_comp, exists_const]
    generalize_proofs _ h
    refine (Category.comp_id _).symm.trans (((residueFieldIsoBase f _ h).eq_inv_comp).mp ?_)
    rw [‚Üê Spec.map_injective.eq_iff]
    simp only [Spec.map_id, Spec.map_comp, SpecMap_residueFieldIsoBase_inv]
    rw [reassoc_of% Scheme.descResidueField_stalkClosedPointTo_fromSpecResidueField, p.2]
  right_inv x := by simp

lemma ext_of_apply_closedPoint_eq
    {f g : Spec (.of K) ‚ü∂ X} (h : X ‚ü∂ Spec (.of K))
    [LocallyOfFiniteType h]
    (hf : f ‚â´ h = ùüô _) (hg : g ‚â´ h = ùüô _)
    (H : f (IsLocalRing.closedPoint K) = g (IsLocalRing.closedPoint K)) : f = g :=
  congr($((pointEquivClosedPoint h).injective (a‚ÇÅ := ‚ü®f, hf‚ü©) (a‚ÇÇ := ‚ü®g, hg‚ü©) (Subtype.ext H)).1)

/-- Let `X` and `Y` be locally of finite type `K`-schemes with `K` algebraically closed and `Y`
separated over `K`. Suppose `X` is reduced, then two `K`-morphisms `f g : X ‚ü∂ Y` are equal if
they are equal on the closed points of a dense locally closed subset of `X`. -/
lemma ext_of_apply_eq {f g : X ‚ü∂ Y} (i : Y ‚ü∂ Spec (.of K)) [IsSeparated i] [LocallyOfFiniteType i]
    [IsReduced X] [LocallyOfFiniteType (f ‚â´ i)]
    (S : Set X) (hS : IsLocallyClosed S) (hS' : Dense S)
    (H : ‚àÄ x ‚àà S, IsClosed {x} ‚Üí f x = g x)
    (H' : f ‚â´ i = g ‚â´ i) : f = g := by
  have : JacobsonSpace ‚Ü•X := LocallyOfFiniteType.jacobsonSpace (f ‚â´ i)
  refine ext_of_fromSpecResidueField_eq f g i (S ‚à© closedPoints X) ?_ ?_ H'
  ¬∑ rwa [dense_iff_closure_eq, JacobsonSpace.closure_inter_closedPoints_eq_closure hS,
      ‚Üê dense_iff_closure_eq]
  ¬∑ intro x ‚ü®hxS, hx‚ü©
    rw [‚Üê cancel_epi (Spec.map (residueFieldIsoBase (f ‚â´ i) x hx).hom)]
    refine ext_of_apply_closedPoint_eq i ?_ ?_ (by simpa using H x hxS hx) <;>
      simp only [Category.assoc, ‚Üê SpecMap_residueFieldIsoBase_inv (f ‚â´ i) x hx, ‚Üê Spec.map_comp,
        Iso.inv_hom_id, Spec.map_id, ‚Üê H']

end AlgebraicGeometry
