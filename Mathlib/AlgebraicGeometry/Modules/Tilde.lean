/-
Copyright (c) 2024 Weihong Xu. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Kevin Buzzard, Johan Commelin, Amelia Livingston, Sophie Morel, Jujian Zhang, Weihong Xu,
  Andrew Yang
-/
module

public import Mathlib.Algebra.Module.LocalizedModule.Basic
public import Mathlib.AlgebraicGeometry.AffineScheme
public import Mathlib.AlgebraicGeometry.Modules.Sheaf
public import Mathlib.Algebra.Category.ModuleCat.Sheaf.Quasicoherent
public import Mathlib.Algebra.Category.ModuleCat.FilteredColimits
public import Mathlib.CategoryTheory.Limits.ConcreteCategory.WithAlgebraicStructures

/-!

# Construction of M^~

Given any commutative ring `R` and `R`-module `M`, we construct the sheaf `M^~` of `ùí™_SpecR`-modules
such that `M^~(U)` is the set of dependent functions that are locally fractions.

## Main definitions
* `AlgebraicGeometry.tilde` : `M^~` as a sheaf of `ùí™_{Spec R}`-modules.
* `AlgebraicGeometry.tilde.adjunction` : `~` is left adjoint to taking global sections.

-/

@[expose] public noncomputable section

universe u

open TopCat AlgebraicGeometry TopologicalSpace CategoryTheory Opposite

variable {R : CommRingCat.{u}} (M : ModuleCat.{u} R)

namespace AlgebraicGeometry

open _root_.PrimeSpectrum

set_option backward.isDefEq.respectTransparency false in
/-- The forgetful functor from `ùí™_{Spec R}` modules to sheaves of `R`-modules. -/
def modulesSpecToSheaf :
    (Spec R).Modules ‚•§ TopCat.Sheaf (ModuleCat R) (Spec R) :=
  SheafOfModules.forgetToSheafModuleCat (Spec R).ringCatSheaf (.op ‚ä§)
    (Limits.initialOpOfTerminal Limits.isTerminalTop) ‚ãô
  sheafCompose _ (ModuleCat.restrictScalars (StructureSheaf.globalSectionsIso R).hom.hom)

/-- The global section functor for `ùí™_{Spec R}` modules -/
noncomputable
def moduleSpecŒìFunctor : (Spec (.of R)).Modules ‚•§ ModuleCat R :=
  modulesSpecToSheaf ‚ãô TopCat.Sheaf.forget _ _ ‚ãô (evaluation _ _).obj (.op ‚ä§)

set_option backward.isDefEq.respectTransparency false in
open PrimeSpectrum in
/-- The forgetful functor from `ùí™_{Spec R}` modules to sheaves of `R`-modules is fully faithful. -/
def SpecModulesToSheafFullyFaithful : (modulesSpecToSheaf (R := R)).FullyFaithful where
  preimage {M N} f := ‚ü®fun U ‚Ü¶ ModuleCat.ofHom ‚ü®(f.1.app U).hom.toAddHom, by
    intro t m
    apply TopCat.Presheaf.IsSheaf.section_ext (modulesSpecToSheaf.obj N).2
    intro x hxU
    obtain ‚ü®a, ‚ü®_, ‚ü®r, rfl‚ü©, rfl‚ü©, hxr, hrU : basicOpen _ ‚â§ _‚ü© :=
      PrimeSpectrum.isBasis_basic_opens.exists_subset_of_mem_open hxU U.unop.2
    refine ‚ü®_, hrU, hxr, ?_‚ü©
    refine Eq.trans ?_ (N.val.map_smul (homOfLE hrU).op t _).symm
    change N.1.map (homOfLE hrU).op (f.1.app _ _) = _ ‚Ä¢ N.1.map (homOfLE hrU).op (f.1.app _ _)
    have (x : _) :
        f.1.app _ (M.1.map (homOfLE hrU).op _) = N.1.map (homOfLE hrU).op (f.1.app _ x) :=
      congr($(f.1.naturality (homOfLE hrU).op).hom x)
    rw [‚Üê this, ‚Üê this, M.val.map_smul]
    generalize (Spec R).ringCatSheaf.val.map (homOfLE hrU).op t = t
    letI := Module.compHom (R := Œì(Spec R, basicOpen r)) Œì(M, basicOpen r)
      (algebraMap R Œì(Spec R, basicOpen r))
    haveI : IsScalarTower R Œì(Spec R, basicOpen r) Œì(M, basicOpen r) :=
      .of_algebraMap_smul fun _ _ ‚Ü¶ rfl
    letI := Module.compHom Œì(N, basicOpen r) (algebraMap R Œì(Spec R, basicOpen r))
    haveI : IsScalarTower R Œì(Spec R, basicOpen r) Œì(N, basicOpen r) :=
      .of_algebraMap_smul fun _ _ ‚Ü¶ rfl
    exact (IsLocalization.linearMap_compatibleSMul (.powers (M := R) r)
      Œì(Spec R, basicOpen r) Œì(M, basicOpen r) Œì(N, basicOpen r)).map_smul
      (f.val.app _).hom _ _‚ü©, fun i ‚Ü¶ by ext x; exact congr($(f.1.naturality i).hom x)‚ü©
  map_preimage f := rfl
  preimage_map f := rfl

/--
`M^~` as a sheaf of `ùí™_{Spec R}`-modules
-/
def tilde : (Spec R).Modules where
  val := moduleStructurePresheaf R M
  isSheaf := (TopCat.Presheaf.isSheaf_iff_isSheaf_comp (forget AddCommGrpCat) _).2
    (structureSheafInType R M).2

namespace tilde

set_option backward.isDefEq.respectTransparency false in
/-- (Implementation). The image of `tilde` under `modulesSpecToSheaf` is isomorphic to
`structurePresheafInModuleCat`. They are defeq as types but the `Smul` instance are not defeq. -/
noncomputable
def modulesSpecToSheafIso :
    (modulesSpecToSheaf.obj (tilde M)).1 ‚âÖ structurePresheafInModuleCat R M :=
  NatIso.ofComponents (fun U ‚Ü¶ LinearEquiv.toModuleIso
    (X‚ÇÅ := (modulesSpecToSheaf.obj (tilde M)).presheaf.obj _)
    { __ := AddEquiv.refl _,
      map_smul' r m := IsScalarTower.algebraMap_smul (M := ((structureSheafInType R M).val.obj U))
        ((structureSheafInType R R).val.obj U) r m }) fun _ ‚Ü¶ rfl

/-- The map from `M` to `Œì(M, U)`. This is a localiation map when `U = D(f)`. -/
def toOpen (U : (Spec R).Opens) : M ‚ü∂ (modulesSpecToSheaf.obj (tilde M)).presheaf.obj (.op U) :=
  ModuleCat.ofHom (StructureSheaf.toOpen‚Çó R M U) ‚â´ ((modulesSpecToSheafIso M).app _).inv

@[reassoc (attr := simp)]
theorem toOpen_res (U V : Opens (PrimeSpectrum.Top R)) (i : V ‚ü∂ U) :
    toOpen M U ‚â´ (modulesSpecToSheaf.obj (tilde M)).presheaf.map i.op = toOpen M V :=
  rfl

instance (f : R) : IsLocalizedModule (.powers f) (toOpen M (basicOpen f)).hom :=
  .of_linearEquiv (.powers f) (StructureSheaf.toOpen‚Çó R M (basicOpen f))
    ((modulesSpecToSheafIso M).app _).toLinearEquiv.symm

noncomputable
instance (x : PrimeSpectrum.Top R) : Module R ((tilde M).presheaf.stalk x) :=
  inferInstanceAs (Module R ‚Üë(TopCat.Presheaf.stalk (moduleStructurePresheaf R M).presheaf x))

/--
If `x` is a point of `Spec R`, this is the morphism of `R`-modules from `M` to the stalk of
`M^~` at `x`.
-/
noncomputable def toStalk (x : PrimeSpectrum.Top R) :
    ModuleCat.of R M ‚ü∂ ModuleCat.of R ((tilde M).presheaf.stalk x) :=
  ModuleCat.ofHom (StructureSheaf.toStalk‚Çó ..)

instance (x : PrimeSpectrum.Top R) :
    IsLocalizedModule x.asIdeal.primeCompl (toStalk M x).hom :=
  inferInstanceAs (IsLocalizedModule x.asIdeal.primeCompl (StructureSheaf.toStalk‚Çó ..))

/-- The tilde construction is functorial. -/
protected noncomputable def map {M N : ModuleCat R} (f : M ‚ü∂ N) : tilde M ‚ü∂ tilde N :=
  SpecModulesToSheafFullyFaithful.preimage ‚ü®(modulesSpecToSheafIso M).hom ‚â´
    { app U := ModuleCat.ofHom (StructureSheaf.comap‚Çó f.hom _ _ .rfl) } ‚â´
    (modulesSpecToSheafIso N).inv‚ü©

@[simp, reassoc]
protected lemma map_id {M : ModuleCat R} : tilde.map (ùüô M) = ùüô _ := by
  ext p x
  exact Subtype.ext (funext fun y ‚Ü¶ DFunLike.congr_fun (LocalizedModule.map_id _) _)

@[simp, reassoc]
protected lemma map_comp {M N P : ModuleCat R} (f : M ‚ü∂ N) (g : N ‚ü∂ P) :
    tilde.map (f ‚â´ g) = tilde.map f ‚â´ tilde.map g := by
  ext p x
  exact Subtype.ext (funext
    fun y ‚Ü¶ DFunLike.congr_fun (IsLocalizedModule.map_comp' y.1.asIdeal.primeCompl
      (LocalizedModule.mkLinearMap y.1.asIdeal.primeCompl M)
      (LocalizedModule.mkLinearMap y.1.asIdeal.primeCompl N)
      (LocalizedModule.mkLinearMap y.1.asIdeal.primeCompl P) _ _) _)

@[reassoc (attr := simp)]
lemma toOpen_map_app {M N : ModuleCat R} (f : M ‚ü∂ N)
    (U : TopologicalSpace.Opens (PrimeSpectrum R)) :
    toOpen M U ‚â´ (modulesSpecToSheaf.map (tilde.map f)).1.app _ =
    f ‚â´ toOpen N U := by
  ext x; exact Subtype.ext (funext fun y ‚Ü¶ IsLocalizedModule.map_apply y.1.asIdeal.primeCompl
    (LocalizedModule.mkLinearMap y.1.asIdeal.primeCompl M)
     (LocalizedModule.mkLinearMap y.1.asIdeal.primeCompl N) _ x)

variable (R) in
/-- Tilde as a functor -/
@[simps] protected noncomputable def functor : ModuleCat R ‚•§ (Spec (.of R)).Modules where
  obj := tilde
  map := tilde.map

instance isIso_toOpen_top {M : ModuleCat R} : IsIso (toOpen M ‚ä§) := by
  rw [toOpen, isIso_comp_right_iff, ConcreteCategory.isIso_iff_bijective]
  exact StructureSheaf.toOpen‚Çó_top_bijective

/-- The isomorphism between the global sections of `M^~` and `M`. -/
@[simps! hom]
noncomputable def isoTop (M : ModuleCat R) :
    M ‚âÖ (modulesSpecToSheaf.obj (tilde M)).presheaf.obj (.op ‚ä§) :=
  asIso (toOpen M ‚ä§)

open PrimeSpectrum in
lemma isUnit_algebraMap_end_basicOpen (M : (Spec (.of R)).Modules) (f : R) :
    IsUnit (algebraMap R (Module.End R
      ((modulesSpecToSheaf.obj M).presheaf.obj (.op (basicOpen f)))) f) := by
  rw [Module.End.isUnit_iff]
  change Function.Bijective (algebraMap Œì(Spec R, basicOpen f)
      (Module.End Œì(Spec R, basicOpen f) Œì(M, basicOpen f)) (algebraMap R _ f))
  rw [‚Üê Module.End.isUnit_iff]
  exact (IsLocalization.Away.algebraMap_isUnit _).map _

end tilde

set_option backward.isDefEq.respectTransparency false in
/-- This is the counit of the tilde-Gamma adjunction. -/
noncomputable def Scheme.Modules.fromTildeŒì (M : (Spec (.of R)).Modules) :
    tilde ((modulesSpecToSheaf.obj M).presheaf.obj (.op ‚ä§)) ‚ü∂ M :=
  SpecModulesToSheafFullyFaithful.preimage
    ‚ü®TopCat.Sheaf.restrictHomEquivHom _ _ isBasis_basic_opens
    { app (f : R·µí·µñ) := by
        refine (ModuleCat.ofHom (IsLocalizedModule.lift (.powers (M := R) f.unop)
          (tilde.toOpen _ (PrimeSpectrum.basicOpen f.unop)).hom
          ((modulesSpecToSheaf.obj M).val.map (homOfLE le_top).op).hom ?_):)
        rw [Subtype.forall]
        change Submonoid.powers _ ‚â§ (IsUnit.submonoid _).comap _
        simp only [inducedFunctor_obj, Submonoid.powers_le, Submonoid.mem_comap]
        exact tilde.isUnit_algebraMap_end_basicOpen M f.unop
      naturality {f g : R·µí·µñ} i := by
        letI N := (modulesSpecToSheaf.obj M).presheaf.obj (.op ‚ä§)
        ext1
        apply IsLocalizedModule.ext (.powers (M := R) f.unop)
          (tilde.toOpen _ (PrimeSpectrum.basicOpen (R := R) f.unop)).hom
        ¬∑ rw [Subtype.forall]
          change Submonoid.powers _ ‚â§ (IsUnit.submonoid _).comap _
          simp only [Submonoid.powers_le, Submonoid.mem_comap, IsUnit.mem_submonoid_iff]
          obtain ‚ü®n, a, e‚ü© : ‚àÉ n, f.unop ‚à£ g.unop ^ n := by
            simpa only [Ideal.mem_radical_iff, Ideal.mem_span_singleton] using
              (basicOpen_le_basicOpen_iff _ _).mp (i.1.hom.le)
          refine ((Commute.isUnit_mul_iff (b := algebraMap R _ a) (.map (.all _ _) _)).mp ?_).1
          rw [‚Üê map_mul, ‚Üê e, map_pow]
          exact (tilde.isUnit_algebraMap_end_basicOpen M g.unop).pow n
        ¬∑ dsimp [‚Üê ModuleCat.hom_comp]
          rw [tilde.toOpen_res_assoc]
          ext x
          dsimp
          simp only [IsLocalizedModule.lift_apply, ‚Üê ModuleCat.comp_apply, ‚Üê Functor.map_comp]
          rfl }‚ü©

set_option backward.isDefEq.respectTransparency false in
@[reassoc]
lemma Scheme.Modules.toOpen_fromTildeŒì_app (M : (Spec (.of R)).Modules) (U) :
    tilde.toOpen ((modulesSpecToSheaf.obj M).presheaf.obj (.op ‚ä§)) U ‚â´
      (modulesSpecToSheaf.map M.fromTildeŒì).1.app (.op U) =
    (modulesSpecToSheaf.obj M).1.map (homOfLE le_top).op := by
  wlog hU : U = PrimeSpectrum.basicOpen 1 generalizing U
  ¬∑ rw [‚Üê tilde.toOpen_res _ (PrimeSpectrum.basicOpen 1) _ (homOfLE (by simp)), Category.assoc,
      NatTrans.naturality, ‚Üê Category.assoc, this, ‚Üê Functor.map_comp, ‚Üê op_comp, homOfLE_comp]
    simp
  subst hU
  simp only [fromTildeŒì, inducedFunctor_obj,
    homOfLE_leOfHom, Functor.FullyFaithful.map_preimage, TopCat.Sheaf.extend_hom_app]
  ext x
  refine (IsLocalizedModule.lift_apply (.powers (M := R) 1)
    (tilde.toOpen _ (PrimeSpectrum.basicOpen (R := R) 1)).hom
    ((modulesSpecToSheaf.obj M).val.map (homOfLE le_top).op).hom (by simp) x)

set_option backward.isDefEq.respectTransparency false in
/-- This is the counit of the tilde-Gamma adjunction. -/
noncomputable def Scheme.Modules.fromTildeŒìNatTrans :
    moduleSpecŒìFunctor (R := R) ‚ãô tilde.functor (R := R) ‚ü∂ ùü≠ _ where
  app := fromTildeŒì
  naturality {M N} f := by
    apply SpecModulesToSheafFullyFaithful.map_injective
    apply CategoryTheory.Sheaf.hom_ext
    apply (TopCat.Sheaf.restrictHomEquivHom _ _ PrimeSpectrum.isBasis_basic_opens).symm.injective
    ext r : 3
    apply IsLocalizedModule.ext (.powers (M := R) r.unop)
      (tilde.toOpen ((modulesSpecToSheaf.obj M).presheaf.obj (.op ‚ä§))
        (PrimeSpectrum.basicOpen (R := R) r.unop)).hom
    ¬∑ rw [Subtype.forall]
      change Submonoid.powers _ ‚â§ (IsUnit.submonoid _).comap _
      simp only [Submonoid.powers_le, Submonoid.mem_comap, IsUnit.mem_submonoid_iff]
      exact tilde.isUnit_algebraMap_end_basicOpen ..
    dsimp [TopCat.Sheaf.restrictHomEquivHom, Functor.IsCoverDense.restrictHomEquivHom,
      moduleSpecŒìFunctor, Sheaf.forget]
    simp only [‚Üê ModuleCat.hom_comp, Functor.map_comp]
    rw [CategoryTheory.Sheaf.comp_val, CategoryTheory.Sheaf.comp_val]
    congr 1
    simp only [NatTrans.comp_app]
    rw [tilde.toOpen_map_app_assoc, toOpen_fromTildeŒì_app N (PrimeSpectrum.basicOpen r.unop),
      toOpen_fromTildeŒì_app_assoc M (PrimeSpectrum.basicOpen r.unop),
      ‚Üê (modulesSpecToSheaf.map f).val.naturality]

/-- `tilde.isoTop` bundled as a natural isomorphism.
This is the unit of the tilde-Gamma adjunction. -/
def tilde.toTildeŒìNatIso : ùü≠ _ ‚âÖ tilde.functor R ‚ãô moduleSpecŒìFunctor :=
  NatIso.ofComponents tilde.isoTop fun f ‚Ü¶ (tilde.toOpen_map_app f _).symm

set_option backward.isDefEq.respectTransparency false in
open Scheme.Modules in
/-- The tilde-Gamma adjuntion. -/
def tilde.adjunction : tilde.functor R ‚ä£ moduleSpecŒìFunctor where
  unit := toTildeŒìNatIso.hom
  counit := fromTildeŒìNatTrans
  left_triangle_components M := by
    apply SpecModulesToSheafFullyFaithful.map_injective
    apply CategoryTheory.Sheaf.hom_ext
    apply (TopCat.Sheaf.restrictHomEquivHom _ _ PrimeSpectrum.isBasis_basic_opens).symm.injective
    ext r : 3
    apply IsLocalizedModule.ext (.powers (M := R) r.unop)
      (toOpen _ (PrimeSpectrum.basicOpen (R := R) r.unop)).hom
    ¬∑ rw [Subtype.forall]
      change Submonoid.powers _ ‚â§ (IsUnit.submonoid _).comap _
      simp only [Submonoid.powers_le, Submonoid.mem_comap, IsUnit.mem_submonoid_iff]
      exact isUnit_algebraMap_end_basicOpen ..
    dsimp [toTildeŒìNatIso, isoTop,
      TopCat.Sheaf.restrictHomEquivHom, Functor.IsCoverDense.restrictHomEquivHom,
      fromTildeŒìNatTrans, moduleSpecŒìFunctor, Sheaf.forget, sheafToPresheaf]
    simp only [‚Üê ModuleCat.hom_comp, Functor.map_comp]
    congr 1
    rw [CategoryTheory.Sheaf.comp_val]
    dsimp
    rw [toOpen_map_app_assoc, toOpen_fromTildeŒì_app]
    rfl
  right_triangle_components M := by
    dsimp [toTildeŒìNatIso, fromTildeŒìNatTrans, tilde.isoTop, moduleSpecŒìFunctor, Sheaf.forget]
    rw [toOpen_fromTildeŒì_app]
    exact (modulesSpecToSheaf.obj M).val.map_id _

instance : IsIso (tilde.adjunction (R := R)).unit := by
  dsimp [tilde.adjunction]; infer_instance

/-- The tilde functor is fully faithful. We will later show that the essential image is
exactly quasi-coherent modules. -/
def tilde.fullyFaithfulFunctor : (tilde.functor R).FullyFaithful :=
  tilde.adjunction.fullyFaithfulLOfIsIsoUnit

instance : (tilde.functor R).Full := tilde.fullyFaithfulFunctor.full
instance : (tilde.functor R).Faithful := tilde.fullyFaithfulFunctor.faithful
instance : (tilde.functor R).IsLeftAdjoint := tilde.adjunction.isLeftAdjoint
instance : (tilde.functor R).Additive :=
  have := Limits.preservesBinaryBiproducts_of_preservesBinaryCoproducts (tilde.functor R)
  Functor.additive_of_preservesBinaryBiproducts _

section

variable {M N : ModuleCat R} (f g : M ‚ü∂ N)

@[simp] lemma tilde.map_zero : tilde.map (0 : M ‚ü∂ N) = 0 :=
  (tilde.functor R).map_zero _ _

@[simp] lemma tilde.map_add : tilde.map (f + g) = tilde.map f + tilde.map g :=
  (tilde.functor R).map_add

@[simp] lemma tilde.map_sub : tilde.map (f - g) = tilde.map f - tilde.map g :=
  (tilde.functor R).map_sub

@[simp] lemma tilde.map_neg : tilde.map (-f) = - tilde.map f :=
  (tilde.functor R).map_neg

end

lemma isIso_fromTildeŒì_iff {M : (Spec R).Modules} :
    IsIso M.fromTildeŒì ‚Üî (tilde.functor R).essImage M :=
  tilde.adjunction.isIso_counit_app_iff_mem_essImage

section IsQuasicoherent

open Limits

/-- Tilde of `R` as an `R`-module is isomorphic to the structure sheaf `ùí™_{Spec R}`. -/
noncomputable
def tildeSelf : tilde (ModuleCat.of R R) ‚âÖ SheafOfModules.unit.{u} _ := .refl _

instance : IsIso (Scheme.Modules.fromTildeŒì (SheafOfModules.unit.{u} (Spec R).ringCatSheaf)) :=
  isIso_fromTildeŒì_iff.mpr ‚ü®_, ‚ü®tildeSelf‚ü©‚ü©

/-- Tilde of direct sums of `R` as an `R`-module is isomorphic to the free sheaf. -/
noncomputable
def tildeFinsupp (Œπ : Type u) : tilde (ModuleCat.of R (Œπ ‚Üí‚ÇÄ R)) ‚âÖ SheafOfModules.free.{u} Œπ :=
  letI H : IsColimit <| (tilde.functor R).mapCocone (ModuleCat.finsuppCocone R R Œπ) :=
    isColimitOfPreserves (tilde.functor R) (ModuleCat.finsuppCoconeIsColimit R R Œπ)
  letI iso : (Discrete.functor fun (_ : Œπ) ‚Ü¶ ModuleCat.of R R) ‚ãô tilde.functor R ‚âÖ
         Discrete.functor fun _ ‚Ü¶ SheafOfModules.unit.{u} _ :=
      Discrete.natIso (fun _ ‚Ü¶ tildeSelf)
  IsColimit.coconePointUniqueUpToIso
    ((IsColimit.precomposeHomEquiv iso.symm _).symm H) (coproductIsCoproduct _)

instance (Œπ : Type u) :
    IsIso (Scheme.Modules.fromTildeŒì (R := R) (SheafOfModules.free.{u} Œπ)) :=
  isIso_fromTildeŒì_iff.mpr ‚ü®_, ‚ü®tildeFinsupp _‚ü©‚ü©

set_option backward.isDefEq.respectTransparency false in
/-- Given a presentation of a module `M`, we may construct an associated presentation of `M^~`. -/
noncomputable
def presentationTilde (s : Set M) (hs : Submodule.span R s = ‚ä§)
    (t : Set (s ‚Üí‚ÇÄ R))
    (ht : Submodule.span R t = LinearMap.ker (Finsupp.linearCombination R ((‚Üë) : s ‚Üí M))) :
    (tilde M).Presentation := by
  haveI H‚ÇÅ : Function.Exact
      (ModuleCat.ofHom (Finsupp.linearCombination (Œ± := t) R (‚Üë)))
      (ModuleCat.ofHom (Finsupp.linearCombination (Œ± := s) (M := M) R (‚Üë))) :=
    (LinearMap.exact_iff.mpr (by simp [Finsupp.range_linearCombination, ht]))
  refine SheafOfModules.presentationOfIsCokernelFree.{u}
      ((tildeFinsupp t).inv ‚â´ tilde.map (ModuleCat.ofHom (Finsupp.linearCombination R (‚Üë))) ‚â´
        (tildeFinsupp s).hom) ((tildeFinsupp s).inv ‚â´
          tilde.map (ModuleCat.ofHom (Finsupp.linearCombination R (‚Üë)))) (by
    simp only [Category.assoc, Iso.hom_inv_id_assoc, Preadditive.IsIso.comp_left_eq_zero]
    rw [‚Üê tilde.map_comp, ‚Üê ModuleCat.ofHom_comp]
    convert tilde.map_zero
    exact congr(ModuleCat.ofHom $(H‚ÇÅ.linearMap_comp_eq_zero))) ?_
  letI h‚ÇÅ := ModuleCat.isColimitCokernelCofork _ _ H‚ÇÅ
    (by simp [‚Üê LinearMap.range_eq_top, Finsupp.range_linearCombination, hs])
  refine IsCokernel.ofIso _ (CokernelCofork.mapIsColimit _ h‚ÇÅ (tilde.functor R)) _ (tildeFinsupp t)
    (tildeFinsupp s) (.refl _) (by simp) (by simp)

set_option backward.isDefEq.respectTransparency false in
instance : (tilde M).IsQuasicoherent :=
  (presentationTilde.{u} _ .univ (by simp) _ (Submodule.span_eq _)).isQuasicoherent

end IsQuasicoherent

end AlgebraicGeometry

namespace ModuleCat

@[deprecated (since := "2026-02-11")] noncomputable alias tilde := AlgebraicGeometry.tilde
@[deprecated (since := "2026-02-11")] noncomputable alias Tilde.toOpen := tilde.toOpen
@[deprecated (since := "2026-02-11")] alias Tilde.toOpen_res := tilde.toOpen_res
@[deprecated (since := "2026-02-11")] noncomputable alias Tilde.toStalk := tilde.toStalk

end ModuleCat
