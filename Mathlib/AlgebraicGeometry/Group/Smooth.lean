/-
Copyright (c) 2026 Andrew Yang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Andrew Yang
-/
module

public import Mathlib.AlgebraicGeometry.AlgClosed.Basic
public import Mathlib.AlgebraicGeometry.Morphisms.Smooth
public import Mathlib.CategoryTheory.Monoidal.Grp_

/-!
# Smoothness of group schemes

## Main results
- `AlgebraicGeometry.smooth_of_grpObj_of_isAlgClosed`:
  If `G` is a group scheme over an algebraically closed field `k` that is reduced and locally
  of finite type, then `G` is smooth over `k`.
-/

@[expose] public section

open CategoryTheory

namespace AlgebraicGeometry

universe u

variable {K : Type u} [Field K] [IsAlgClosed K] {G : Scheme} (f : G ‚ü∂ Spec (.of K))
    [LocallyOfFinitePresentation f] [IsReduced G] [GrpObj (Over.mk f)]

set_option backward.isDefEq.respectTransparency false in
open MonObj MonoidalCategory CartesianMonoidalCategory in
/--
If `G` is a group scheme over an algebraically closed field `k` that is reduced and locally
of finite type, then `G` is smooth over `k`.
-/
lemma smooth_of_grpObj_of_isAlgClosed : Smooth f := by
  have := LocallyOfFiniteType.jacobsonSpace f
  have : Nonempty G := ‚ü®Œ∑[Over.mk f].1 (IsLocalRing.closedPoint _)‚ü©
  rw [‚Üê Scheme.Hom.smoothLocus_eq_top_iff, ‚Üê TopologicalSpace.Opens.coe_eq_univ,
    ‚Üê not_ne_iff, ‚Üê Set.nonempty_compl]
  intro H
  obtain ‚ü®x, hx, hxc‚ü© :=
    nonempty_inter_closedPoints H f.smoothLocus.2.isClosed_compl.isLocallyClosed
  obtain ‚ü®y, hy : y ‚àà f.smoothLocus, hyc‚ü© := nonempty_inter_closedPoints
    f.dense_smoothLocus_of_perfectField.nonempty f.smoothLocus.2.isLocallyClosed
  let x' : ùüô_ _ ‚ü∂ Over.mk f := Over.homMk _ ((pointEquivClosedPoint f).symm ‚ü®x, hxc‚ü©).2
  let y' : ùüô_ _ ‚ü∂ Over.mk f := Over.homMk _ ((pointEquivClosedPoint f).symm ‚ü®y, hyc‚ü©).2
  let Œ± := (GrpObj.mulRight (A := Over.mk f) x').symm ‚â™‚â´
    (GrpObj.mulRight (A := Over.mk f) y')
  have hŒ± : x' ‚â´ Œ±.hom = y' := by
    dsimp only [Iso.trans_hom, Iso.symm_hom, Œ±]
    rw [‚Üê Category.assoc, ‚Üê Iso.eq_comp_inv]
    simp [comp_lift_assoc]
  have hŒ±' : Œ±.hom.left x = y := by
    simpa [x', y', pointEquivClosedPoint] using congr(($hŒ±).left (IsLocalRing.closedPoint K))
  have hŒ±f : Œ±.hom.left ‚â´ f = f := Œ±.hom.w
  rw! [‚Üê hŒ±', ‚Üê Œ±.hom.left.mem_preimage, Scheme.Hom.preimage_smoothLocus_eq,
    show Œ±.hom.left ‚â´ f = f from Œ±.hom.w] at hy
  exact hx hy

end AlgebraicGeometry
