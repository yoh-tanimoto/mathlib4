/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/

import Mathlib.Analysis.SpecialFunctions.Gamma.Beta
import Mathlib.Data.Real.Sign
import Mathlib.Analysis.PSeries
import Mathlib.NumberTheory.ZetaFunctions.SillySumLemma
/-!
# Dirichlet series as Mellin transforms

Here we prove general results of the form "the Mellin transform of a power series in exp (-t) is
a Dirichlet series".
-/

lemma Real.sign_eq_cast_sign (x : â„) : sign x = SignType.sign x := by
  rcases lt_trichotomy x 0 with h | h | h <;>
  simp [h, sign_of_pos, sign_of_neg]

lemma Int.sign_eq_cast_sign (x : â„¤) : sign x = SignType.sign x := by
  rcases lt_trichotomy x 0 with h | h | h <;>
  simp [h, sign_eq_one_iff_pos, sign_eq_neg_one_iff_neg]

lemma Real.sign_mul_abs (x : â„) : sign x * |x| = x := by
  rw [sign_eq_cast_sign, _root_.sign_mul_abs]

open Filter Topology Asymptotics Real Set MeasureTheory
open Complex hiding abs_of_nonneg

lemma summable_int_iff_summable_nat {Î± : Type*}
    [AddCommGroup Î±] [UniformSpace Î±] [UniformAddGroup Î±] [CompleteSpace Î±] {f : â„¤ â†’ Î±} :
    Summable f â†” (Summable fun (n : â„•) => f â†‘n) âˆ§ (Summable fun (n : â„•) => f (-â†‘n)) := by
  refine âŸ¨fun p â†¦ âŸ¨?_, ?_âŸ©, fun p â†¦ summable_int_of_summable_nat p.1 p.2âŸ© <;>
    apply p.comp_injective
  exacts [Nat.cast_injective, neg_injective.comp Nat.cast_injective]

lemma summable_one_div_nat_add_rpow (a : â„) (s : â„) :
    Summable (fun n : â„• â†¦ 1 / |n + a| ^ s) â†” 1 < s := by
  suffices : âˆ€ (b c : â„),
      Summable (fun n : â„• â†¦ 1 / |n + b| ^ s) â†’ Summable (fun n : â„• â†¦ 1 / |n + c| ^ s)
  Â· simp_rw [â† summable_one_div_nat_rpow, Iff.intro (this a 0) (this 0 a), add_zero, Nat.abs_cast]
  refine fun b c h â†¦ summable_of_isBigO_nat h (isBigO_of_div_tendsto_nhds ?_ 1 ?_)
  Â· filter_upwards [eventually_gt_atTop (Nat.ceil |b|)] with n hn hx
    have hna : 0 < n + b := by linarith [lt_of_abs_lt ((abs_neg b).symm â–¸ Nat.lt_of_ceil_lt hn)]
    exfalso
    revert hx
    positivity
  Â· simp_rw [Pi.div_def, div_div, mul_one_div, one_div_div]
    refine (?_ : Tendsto (fun x : â„ â†¦ |x + b| ^ s / |x + c| ^ s) atTop (ğ“ 1)).comp
      tendsto_nat_cast_atTop_atTop
    have : Tendsto (fun x : â„ â†¦ 1 + (b - c) / x) atTop (ğ“ 1)
    Â· simpa using tendsto_const_nhds.add ((tendsto_const_nhds (X := â„)).div_atTop tendsto_id)
    have : Tendsto (fun x â†¦ (x + b) / (x + c)) atTop (ğ“ 1)
    Â· refine (this.comp (tendsto_id.atTop_add (tendsto_const_nhds (x := c)))).congr' ?_
      filter_upwards [eventually_gt_atTop (-c)] with x hx
      field_simp [(by linarith : 0 < x + c).ne']
    apply (one_rpow s â–¸ (continuousAt_rpow_const _ s (by simp)).tendsto.comp this).congr'
    filter_upwards [eventually_gt_atTop (-b), eventually_gt_atTop (-c)] with x hb hc
    rw [neg_lt_iff_pos_add] at hb hc
    rw [Function.comp_apply, div_rpow hb.le hc.le, abs_of_pos hb, abs_of_pos hc]

lemma summable_one_div_int_add_rpow (a : â„) (s : â„) :
    Summable (fun n : â„¤ â†¦ 1 / |n + a| ^ s) â†” 1 < s := by
  simp_rw [summable_int_iff_summable_nat, â† abs_neg (â†‘(-_ : â„¤) + a), neg_add, Int.cast_neg,
    neg_neg, Int.cast_ofNat, summable_one_div_nat_add_rpow, and_self]

variable {Î¹ : Type*} [Countable Î¹]

/-- Most basic version of the "Mellin transform = Dirichlet series" argument. -/
lemma hasSum_mellin {a : Î¹ â†’ â„‚} {p : Î¹ â†’ â„} {F : â„ â†’ â„‚} {s : â„‚}
    (hp : âˆ€ i, a i = 0 âˆ¨ 0 < p i) (hs : 0 < s.re)
    (hF : âˆ€ t âˆˆ Ioi 0, HasSum (fun i â†¦ a i * rexp (-p i * t)) (F t))
    (h_sum : Summable fun i â†¦ â€–a iâ€– / (p i) ^ s.re) :
    HasSum (fun i â†¦ Gamma s * a i / p i ^ s) (mellin F s) := by
  simp_rw [mellin, smul_eq_mul, â† set_integral_congr measurableSet_Ioi
    (fun t ht â†¦ congr_arg _ (hF t ht).tsum_eq), â† tsum_mul_left]
  convert hasSum_integral_of_summable_norm (Î¼ := volume.restrict (Ioi 0))
    (F := fun i t â†¦ t ^ (s - 1) * (a i * rexp (-p i * t))) (fun i â†¦ ?_) ?_ using 2 with i
  Â· simp_rw [â† mul_assoc, mul_comm _ (a _), mul_assoc (a _), mul_div_assoc, integral_mul_left]
    rcases hp i with hai | hpi
    Â· rw [hai, zero_mul, zero_mul]
    have := integral_cpow_mul_exp_neg_mul_Ioi hs hpi
    simp_rw [â† ofReal_mul, â† ofReal_neg, â† ofReal_exp, â† neg_mul (p i)] at this
    rw [this, one_div, inv_cpow _ _ (arg_ofReal_of_nonneg hpi.le â–¸ pi_pos.ne), div_eq_inv_mul]
  Â· -- integrability of terms
    rcases hp i with hai | hpi
    Â· simpa only [hai, zero_mul, mul_zero] using integrable_zero _ _ _
    simp_rw [â† mul_assoc, mul_comm _ (a i), mul_assoc]
    have := Complex.GammaIntegral_convergent hs
    rw [â† mul_zero (p i), â† integrableOn_Ioi_comp_mul_left_iff _ _ hpi] at this
    refine (IntegrableOn.congr_fun (this.const_mul (1 / p i ^ (s - 1)))
      (fun t (ht : 0 < t) â†¦ ?_) measurableSet_Ioi).const_mul _
    simp_rw [mul_comm (â†‘(rexp _) : â„‚), â† mul_assoc, neg_mul, ofReal_mul]
    rw [mul_cpow_ofReal_nonneg hpi.le ht.le, â† mul_assoc, one_div, inv_mul_cancel, one_mul]
    Â· rw [Ne.def, cpow_eq_zero_iff, not_and_or]
      exact Or.inl (ofReal_ne_zero.mpr hpi.ne')
  Â· -- summability of integrals of norms
    apply Summable.of_norm
    convert h_sum.mul_left (Real.Gamma s.re) using 2 with i
    simp_rw [â† mul_assoc, mul_comm _ (a i), mul_assoc, norm_mul (a i), integral_mul_left]
    rw [â† mul_div_assoc, mul_comm (Real.Gamma _), mul_div_assoc, norm_mul â€–a iâ€–, norm_norm]
    rcases hp i with hai | hpi
    Â· simp only [hai, norm_zero, zero_mul]
    congr 1
    have := Real.integral_rpow_mul_exp_neg_mul_Ioi hs hpi
    simp_rw [â† neg_mul (p i), one_div, inv_rpow hpi.le, â† div_eq_inv_mul] at this
    rw [norm_of_nonneg (integral_nonneg (fun _ â†¦ norm_nonneg _)), â† this]
    refine set_integral_congr measurableSet_Ioi (fun t ht â†¦ ?_)
    rw [norm_mul, norm_real, Real.norm_eq_abs, Real.abs_exp, Complex.norm_eq_abs,
      abs_cpow_eq_rpow_re_of_pos ht, sub_re, one_re]

/-- Shortcut version for the commonly arising special case when `p i = Ï€ * q i` for some other
sequence `q`. -/
lemma hasSum_mellin_pi_mul {a : Î¹ â†’ â„‚} {q : Î¹ â†’ â„} {F : â„ â†’ â„‚} {s : â„‚}
    (hq : âˆ€ i, a i = 0 âˆ¨ 0 < q i) (hs : 0 < s.re)
    (hF : âˆ€ t âˆˆ Ioi 0, HasSum (fun i â†¦ a i * rexp (-Ï€ * q i * t)) (F t))
    (h_sum : Summable fun i â†¦ â€–a iâ€– / (q i) ^ s.re) :
    HasSum (fun i â†¦ Gamma s * Ï€ ^ (-s) * a i / q i ^ s) (mellin F s) := by
  have hp i : a i = 0 âˆ¨ 0 < Ï€ * q i := by rcases hq i with h | h <;> simp [h, pi_pos]
  convert hasSum_mellin hp hs (by simpa using hF) ?_ using 2 with i
  Â· have : a i / â†‘(Ï€ * q i) ^ s = Ï€ ^ (-s) * a i / q i ^ s := by
      rcases hq i with h | h
      Â· simp [h]
      Â· rw [ofReal_mul, mul_cpow_ofReal_nonneg pi_pos.le h.le, â† div_div, cpow_neg,
          â† div_eq_inv_mul]
    simp_rw [mul_div_assoc, this]
    ring_nf
  Â· have (i : Î¹) : â€–a iâ€– / â†‘(Ï€ * q i) ^ s.re = Ï€ ^ (-s.re) * â€–a iâ€– / q i ^ s.re := by
      rcases hq i with h | h
      Â· simp [h]
      rw [mul_rpow pi_pos.le h.le, â† div_div, rpow_neg pi_pos.le, â† div_eq_inv_mul]
    simpa only [this, mul_div_assoc] using h_sum.mul_left _

/-- Version allowing some constant terms (which are omitted from the sums). -/
lemma hasSum_mellin_pi_mulâ‚€ {a : Î¹ â†’ â„‚} {p : Î¹ â†’ â„} {F : â„ â†’ â„‚} {s : â„‚}
    (hp : âˆ€ i, 0 â‰¤ p i) (hs : 0 < s.re)
    (hF : âˆ€ t âˆˆ Ioi 0, HasSum (fun i â†¦ if p i = 0 then 0 else a i * rexp (-Ï€ * p i * t)) (F t))
    (h_sum : Summable fun i â†¦ â€–a iâ€– / (p i) ^ s.re) :
    HasSum (fun i â†¦ if p i = 0 then 0 else Gamma s * Ï€ ^ (-s) * a i / p i ^ s) (mellin F s) := by
  let a' i := if p i = 0 then 0 else a i
  have hp' i : a' i = 0 âˆ¨ 0 < p i := by
    simp only [a']
    split_ifs with h <;> tauto
    exact Or.inr (lt_of_le_of_ne (hp i) (Ne.symm h))
  have (i t) : (if p i = 0 then 0 else a i * rexp (-Ï€ * p i * t)) =
      (a' i * rexp (-Ï€ * p i * t)) := by
    simp only [a', ite_mul, zero_mul]
  simp_rw [this] at hF
  convert hasSum_mellin_pi_mul hp' hs hF ?_ using 2
  Â· simp only [div_eq_mul_inv, mul_ite, mul_zero, ite_mul, zero_mul]
  Â· refine h_sum.of_norm_bounded _ (fun i â†¦ ?_)
    simp only
    split_ifs
    Â· simp only [norm_zero, zero_div]
      positivity
    Â· rw [norm_of_nonneg]
      have := hp i
      positivity


/-- Deligne's archimedean Gamma factor for a real infinite place, see
"Valeurs de fonctions L et periodes d'integrales" Â§ 5.3. -/
noncomputable def Gammaâ„ (s : â„‚) := Ï€ ^ (-s / 2) * Complex.Gamma (s / 2)

lemma Gammaâ„_ne_zero_of_re_pos {s : â„‚} (hs : 0 < re s) : Gammaâ„ s â‰  0 := by
  apply mul_ne_zero
  Â· simp [pi_pos.ne']
  Â· apply Complex.Gamma_ne_zero_of_re_pos
    rw [div_ofNat_re]
    exact div_pos hs two_pos

lemma differentiable_Gammaâ„_inv : Differentiable â„‚ (fun s â†¦ (Gammaâ„ s)â»Â¹) := by
  conv => enter [2, s]; rw [Gammaâ„, mul_inv]
  refine Differentiable.mul (fun s â†¦ .inv ?_ (by simp [pi_pos.ne'])) ?_
  Â· refine ((differentiableAt_id.neg.div_const (2 : â„‚)).const_cpow ?_)
    exact Or.inl (ofReal_ne_zero.mpr pi_pos.ne')
  Â· exact differentiable_one_div_Gamma.comp (differentiable_id.div_const _)

lemma Gammaâ„_residue_zero : Tendsto (fun s â†¦ s * Gammaâ„ s) (ğ“[â‰ ] 0) (ğ“ 2) := by
  have h : Tendsto (fun z : â„‚ â†¦ z / 2 * Gamma (z / 2)) (ğ“[â‰ ] 0) (ğ“ 1) := by
    refine tendsto_self_mul_Gamma_nhds_zero.comp ?_
    rw [tendsto_nhdsWithin_iff, (by simp : ğ“ (0 : â„‚) = ğ“ (0 / 2))]
    exact âŸ¨(tendsto_id.div_const _).mono_left nhdsWithin_le_nhds,
      eventually_of_mem self_mem_nhdsWithin fun x hx â†¦ div_ne_zero hx two_ne_zeroâŸ©
  have h' : Tendsto (fun s : â„‚ â†¦ 2 * (Ï€ : â„‚) ^ (-s / 2)) (ğ“[â‰ ] 0) (ğ“ 2) := by
    rw [(by simp : ğ“ 2 = ğ“ (2 * (Ï€ : â„‚) ^ (-(0 : â„‚) / 2)))]
    refine Tendsto.mono_left (ContinuousAt.tendsto ?_) nhdsWithin_le_nhds
    exact continuousAt_const.mul ((continuousAt_const_cpow (ofReal_ne_zero.mpr pi_pos.ne')).comp
      (continuousAt_id.neg.div_const _))
  convert mul_one (2 : â„‚) â–¸ (h'.mul h) using 2 with z
  rw [Gammaâ„]
  ring_nf

/-- Tailored version for even Jacobi theta functions. -/
lemma hasSum_mellin_pi_mul_sq {a : Î¹ â†’ â„‚} {r : Î¹ â†’ â„} {F : â„ â†’ â„‚} {s : â„‚} (hs : 0 < s.re)
    (hF : âˆ€ t âˆˆ Ioi 0, HasSum (fun i â†¦ if r i = 0 then 0 else a i * rexp (-Ï€ * r i ^ 2 * t)) (F t))
    (h_sum : Summable fun i â†¦ â€–a iâ€– / |r i| ^ s.re) :
    HasSum (fun i â†¦ if r i = 0 then 0 else Gammaâ„ s * a i / |r i| ^ s)
    (mellin F (s / 2)) := by
  have hs' : 0 < (s / 2).re := by rw [div_ofNat_re]; positivity
  have h (i) : r i ^ 2 = 0 â†” r i = 0 := by simp
  simp_rw [â† h] at hF
  have hp i : 0 â‰¤ (r i) ^ 2 := sq_nonneg _
  convert hasSum_mellin_pi_mulâ‚€ hp hs' hF ?_ using 3 with i
  Â· rw [h]
  Â· rw [Gammaâ„, â† _root_.sq_abs, ofReal_pow, â† cpow_nat_mul']
    ring_nf
    all_goals rw [arg_ofReal_of_nonneg (abs_nonneg _)]; linarith [pi_pos]
  Â· convert h_sum using 3 with i
    rw [â† _root_.sq_abs, â† rpow_natCast_mul (abs_nonneg _), div_ofNat_re, Nat.cast_ofNat,
      mul_div_cancel' _ two_pos.ne']

/-- Tailored version for odd Jacobi theta functions. -/
lemma hasSum_mellin_pi_mul_sq' {a : Î¹ â†’ â„‚} {r : Î¹ â†’ â„} {F : â„ â†’ â„‚} {s : â„‚}
    (hs : 0 < (s + 1).re)
    (hF : âˆ€ t âˆˆ Ioi 0, HasSum (fun i â†¦ a i * r i * rexp (-Ï€ * r i ^ 2 * t)) (F t))
    (h_sum : Summable fun i â†¦ â€–a iâ€– / |r i| ^ s.re) :
    HasSum (fun i â†¦ Gammaâ„ (s + 1) * a i * Real.sign (r i) / |r i| ^ s)
    (mellin F ((s + 1) / 2)) := by
  have (i t) : (a i * r i * rexp (-Ï€ * r i ^ 2 * t)) = if r i = 0 then 0 else
    (a i * r i * rexp (-Ï€ * r i ^ 2 * t)) := by split_ifs with h <;> simp [h]
  conv at hF => enter [t, ht, 1, i]; rw [this]
  convert hasSum_mellin_pi_mul_sq hs hF ?_ using 2 with i
  Â· rcases eq_or_ne (r i) 0 with h | h <;>
      simp only [h, â†“reduceIte, Real.sign_zero, ofReal_zero, mul_zero, zero_mul, zero_div]
    rw [cpow_add _ _ (ofReal_ne_zero.mpr <| abs_ne_zero.mpr h), cpow_one]
    conv_rhs => enter [1]; rw [â† (r i).sign_mul_abs, ofReal_mul]
    field_simp [h]
    ring_nf
  Â· -- this case is delicate because the conclusion specifically omits terms with `r i = 0`
    -- but the hypothesis `hF` doesn't, so need to deal with `re s = 0` separately
    simp_rw [norm_mul, norm_real, Real.norm_eq_abs, mul_div_assoc]
    rcases eq_or_ne s.re 0 with hs' | hs'
    Â· simp only [hs', rpow_zero, div_one, add_re, one_re, zero_add, rpow_one] at h_sum âŠ¢
      refine h_sum.of_norm_bounded _ (fun i â†¦ ?_)
      rw [norm_mul, norm_norm, Real.norm_eq_abs, abs_div, _root_.abs_abs]
      exact mul_le_of_le_one_right (norm_nonneg _) (div_self_le_one _)
    convert h_sum using 2 with i
    rcases eq_or_ne (r i) 0 with h | h
    Â· simp_rw [h, abs_zero, zero_rpow hs.ne', zero_rpow hs', div_zero, mul_zero]
    Â· rw [add_re, one_re, rpow_add (abs_pos.mpr h), rpow_one]
      field_simp [h]
      ring_nf
