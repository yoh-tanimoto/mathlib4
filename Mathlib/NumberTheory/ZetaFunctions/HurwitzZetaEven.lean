/-
Copyright (c) 2024 David Loeffler. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: David Loeffler
-/
import Mathlib.NumberTheory.ZetaFunctions.AbstractFuncEq
import Mathlib.NumberTheory.ZetaFunctions.KernelBounds
import Mathlib.NumberTheory.ModularForms.JacobiTheta.TwoVariable

/-!
# Even Hurwitz zeta functions

In this file we study the functions on `‚ÑÇ` which are the meromorphic continuation of the following
series (convergent for `1 < re s`), where `a ‚àà ‚Ñù` is a parameter:

`completedHurwitzZetaEven a s = 1 / 2 * ‚ñ° * ‚àë' n : ‚Ñ§, 1 / |n + a| ^ s`

and

`completedCosZeta a s = ‚ñ° * ‚àë' n : ‚Ñï, cos (2 * œÄ * a * n) / |n| ^ s`

where `‚ñ°` denotes a Gamma factor. Note that the term for `n = -a` in the first sum is omitted
if `a` is an integer, and the term for `n = 0` is omitted in the second sum (always).

Of course, we cannot *define* these functions by the above formulae (since existence of the
meromorphic continuation is not at all obvious); we in fact construct them as Mellin transforms of
various versions of the Jacobi theta function. We also define modified versions with a subscript `0`
which are entire functions differing from the above by multiples of `1 / s` and `1 / (1 - s)`.

##¬†Main definitions and theorems

* `completedHurwitzZetaEven`: the completed Hurwitz zeta function
* `completedCosZeta`: the completed cosine zeta function
* `differentiableAt_completedHurwitzZetaEven` and `differentiableAt_completedCosZeta`:
  differentiability away from `s = 0` and `s = 1`
* `completedHurwitzZetaEven_one_sub`: the functional equation
  `completedHurwitzZetaEven a (1 - s) = completedCosZeta a s`
* `completedHurwitzZetaEven_eq_tsum_int` and `completedCosZeta_eq_tsum_nat`: relation between the
  zeta functions and corresponding Dirichlet series for `1 < re s`
-/
noncomputable section

open Complex Filter Topology Asymptotics Real Set Classical MeasureTheory

section kernel_defs
/-!
## Definitions and elementary properties of kernels
-/

/-- Even Hurwitz zeta kernel (function whose Mellin transform will be the even part of the
completed Hurwit zeta function). See `evenKernel_def` for the defining formula, and
`hasSum_int_evenKernel` for an expression as a sum over `‚Ñ§`. -/
@[irreducible] def evenKernel (a : UnitAddCircle) (x : ‚Ñù) : ‚Ñù :=
  (show Function.Periodic
    (fun Œæ : ‚Ñù ‚Ü¶ rexp (-œÄ * Œæ ^ 2 * x) * re (jacobiTheta‚ÇÇ (Œæ * I * x) (I * x))) 1 by
      intro Œæ
      simp_rw [ofReal_add, ofReal_one, add_mul, one_mul, jacobiTheta‚ÇÇ_add_left']
      have : cexp (-‚ÜëœÄ * I * ((I * ‚Üëx) + 2 * (‚ÜëŒæ * I * ‚Üëx))) =
        rexp (œÄ * (x + 2 * Œæ * x))
      ¬∑ simp_rw [ofReal_exp, push_cast]
        ring_nf
        rw [I_sq]
        ring_nf
      rw [this, re_ofReal_mul, ‚Üê mul_assoc, ‚Üê Real.exp_add]
      ring_nf).lift a

lemma evenKernel_def (a x : ‚Ñù) :
    ‚Üë(evenKernel ‚Üëa x) = cexp (-œÄ * a ^ 2 * x) * jacobiTheta‚ÇÇ (a * I * x) (I * x) := by
  rw [evenKernel, Function.Periodic.lift_coe]
  simp_rw [push_cast, Complex.re_eq_add_conj, jacobiTheta‚ÇÇ_conj, map_mul, conj_I, conj_ofReal,
    mul_neg, neg_mul, jacobiTheta‚ÇÇ_neg_left, neg_neg, ‚Üê mul_two, mul_div_cancel _ (two_ne_zero' ‚ÑÇ)]

/-- For `x ‚â§ 0` the defining sum diverges, so the kernel is 0. -/
lemma evenKernel_undef (a : UnitAddCircle) {x : ‚Ñù} (hx : x ‚â§ 0) : evenKernel a x = 0 := by
  induction' a using QuotientAddGroup.induction_on' with a'
  rw [‚Üê ofReal_inj, evenKernel_def, jacobiTheta‚ÇÇ_undef, mul_zero, ofReal_zero]
  rwa [I_mul_im, ofReal_re]

/-- Cosine Hurwitz zeta kernel. See `cosKernel_def` for the defining formula, and
`hasSum_int_cosKernel` for expression as a sum. -/
@[irreducible] def cosKernel (a : UnitAddCircle) (x : ‚Ñù) : ‚Ñù :=
  (show Function.Periodic (fun Œæ : ‚Ñù ‚Ü¶ re (jacobiTheta‚ÇÇ Œæ (I * x))) 1 by
    intro Œæ; simp_rw [ofReal_add, ofReal_one, jacobiTheta‚ÇÇ_add_left]).lift a

lemma cosKernel_def (a x : ‚Ñù) : ‚Üë(cosKernel ‚Üëa x) = jacobiTheta‚ÇÇ a (I * x) := by
  rw [cosKernel, Function.Periodic.lift_coe]
  simp_rw [Complex.re_eq_add_conj, jacobiTheta‚ÇÇ_conj, map_mul, conj_ofReal, conj_I, neg_mul,
    neg_neg, ‚Üê mul_two, mul_div_cancel _ (two_ne_zero' ‚ÑÇ)]

lemma cosKernel_undef (a : UnitAddCircle) {x : ‚Ñù} (hx : x ‚â§ 0) : cosKernel a x = 0 := by
  induction' a using QuotientAddGroup.induction_on' with a'
  rw [‚Üê ofReal_inj, cosKernel_def, jacobiTheta‚ÇÇ_undef, ofReal_zero]
  rwa [I_mul_im, ofReal_re]

/-- For `a = 0`, both kernels agree. -/
lemma evenKernel_eq_cosKernel_of_zero : evenKernel 0 = cosKernel 0 := by
  ext1 x
  rw [‚Üê ofReal_inj, ‚Üê QuotientAddGroup.mk_zero, cosKernel_def, evenKernel_def]
  simp

lemma evenKernel_neg (a : UnitAddCircle) (x : ‚Ñù) : evenKernel (-a) x = evenKernel a x := by
  induction' a using QuotientAddGroup.induction_on' with a'
  rw [‚Üê ofReal_inj]
  simp_rw [‚Üê QuotientAddGroup.mk_neg, evenKernel_def, ofReal_neg, neg_mul,
    jacobiTheta‚ÇÇ_neg_left, neg_sq]

lemma cosKernel_neg (a : UnitAddCircle) (x : ‚Ñù) : cosKernel (-a) x = cosKernel a x := by
  induction' a using QuotientAddGroup.induction_on' with a'
  rw [‚Üê ofReal_inj]
  simp_rw [‚Üê QuotientAddGroup.mk_neg, cosKernel_def, ofReal_neg, jacobiTheta‚ÇÇ_neg_left]

lemma continuousOn_evenKernel (a : UnitAddCircle) : ContinuousOn (evenKernel a) (Ioi 0) := by
  induction' a using QuotientAddGroup.induction_on' with a'
  change ContinuousOn (fun x ‚Ü¶ re (evenKernel a' x : ‚ÑÇ)) (Ioi 0)
  apply Complex.continuous_re.comp_continuousOn
  simp_rw [evenKernel_def a']
  refine fun x hx ‚Ü¶ ((Continuous.continuousAt ?_).mul ?_).continuousWithinAt
  ¬∑ exact Complex.continuous_exp.comp (continuous_const.mul continuous_ofReal)
  ¬∑ have h := continuousAt_jacobiTheta‚ÇÇ (a' * I * x) (?_ : 0 < im (I * x))
    ¬∑ refine h.comp (f := fun u : ‚Ñù ‚Ü¶ (a' * I * u, I * u)) (Continuous.continuousAt ?_)
      continuity
    ¬∑ rwa [mul_im, I_re, I_im, zero_mul, one_mul, zero_add, ofReal_re]

lemma continuousOn_cosKernel (a : UnitAddCircle) : ContinuousOn (cosKernel a) (Ioi 0) := by
  induction' a using QuotientAddGroup.induction_on' with a'
  change ContinuousOn (fun x ‚Ü¶ re (cosKernel a' x : ‚ÑÇ)) (Ioi 0)
  apply Complex.continuous_re.comp_continuousOn
  simp_rw [cosKernel_def]
  apply ContinuousAt.continuousOn (fun x hx ‚Ü¶ ?_)
  have h := continuousAt_jacobiTheta‚ÇÇ a' (?_ : 0 < im (I * x))
  ¬∑ refine h.comp (f := fun u : ‚Ñù ‚Ü¶ ((a' : ‚ÑÇ), I * u)) (Continuous.continuousAt ?_)
    continuity
  ¬∑ rwa [mul_im, I_re, I_im, zero_mul, one_mul, zero_add, ofReal_re]

lemma evenKernel_functional_equation (a : UnitAddCircle) (x : ‚Ñù) :
    evenKernel a x = 1 / x ^ (1 / 2 : ‚Ñù) * cosKernel a (1 / x) := by
  rcases le_or_lt x 0 with hx | hx
  ¬∑ rw [evenKernel_undef _ hx, cosKernel_undef, mul_zero]
    exact div_nonpos_of_nonneg_of_nonpos zero_le_one hx
  induction' a using QuotientAddGroup.induction_on' with a'
  rw [‚Üê ofReal_inj, ofReal_mul, evenKernel_def, cosKernel_def,
    jacobiTheta‚ÇÇ_functional_equation]
  have : I * ‚Üë(1 / x) = -1 / (I * ‚Üëx)
  ¬∑ push_cast; field_simp [hx.ne']; ring_nf; rw [I_sq]; ring_nf
  rw [this]
  have : (a' * I * x / (I * x)) = a' := by { field_simp [hx.ne']; ring_nf }
  rw [this]
  have : 1 / (-I * (I * x)) ^ (1 / 2 : ‚ÑÇ) = 1 / ‚Üë(x  ^ (1 / 2 : ‚Ñù))
  ¬∑ rw [neg_mul, ‚Üê mul_assoc, I_mul_I, neg_one_mul, neg_neg,
      ofReal_cpow hx.le, ofReal_div, ofReal_one, ofReal_ofNat]
  rw [this]
  have : -‚ÜëœÄ * I * (‚Üëa' * I * ‚Üëx) ^ 2 / (I * ‚Üëx) = - (-œÄ * a' ^ 2 * x)
  ¬∑ rw [mul_pow, mul_pow, I_sq]; field_simp [hx.ne']; ring_nf
  rw [this, ‚Üê mul_assoc, mul_comm (cexp _), mul_assoc _ (cexp _) (cexp _), ‚Üê Complex.exp_add,
    neg_add_self, Complex.exp_zero, mul_one, ofReal_div, ofReal_one]

end kernel_defs

section asymp

/-!
## Formulae for the kernels as sums
-/

lemma hasSum_int_evenKernel (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ rexp (-œÄ * (n + a) ^ 2 * t)) (evenKernel a t) := by
  rw [‚Üê hasSum_ofReal, evenKernel_def]
  convert (hasSum_jacobiTheta‚ÇÇ_term _ (by rwa [I_mul_im, ofReal_re])).mul_left _
    using 2 with n
  rw [jacobiTheta‚ÇÇ_term, ‚Üê Complex.exp_add]
  push_cast
  ring_nf
  rw [I_sq]
  ring_nf

lemma hasSum_int_cosKernel (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ cexp (2 * œÄ * I * a * n) * rexp (-œÄ * n ^ 2 * t)) ‚Üë(cosKernel a t) := by
  rw [cosKernel_def a t]
  convert hasSum_jacobiTheta‚ÇÇ_term a (by rwa [I_mul_im, ofReal_re] : 0 < im (I * t)) using 2 with n
  rw [jacobiTheta‚ÇÇ_term, ofReal_exp, ‚Üê Complex.exp_add]
  push_cast
  ring_nf
  rw [I_sq]
  ring_nf

/-- Modified version of `hasSum_int_evenKernel` omitting the constant term at `‚àû`. -/
lemma hasSum_int_evenKernel‚ÇÄ (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ if n = -a then 0 else rexp (-œÄ * (n + a) ^ 2 * t))
    (evenKernel a t - if (a : UnitAddCircle) = 0 then 1 else 0) := by
  simp_rw [AddCircle.coe_eq_zero_iff]
  split_ifs with h
  ¬∑ obtain ‚ü®k, rfl‚ü© := h
    simp_rw [zsmul_eq_mul, mul_one, ‚Üê Int.cast_neg, Int.cast_inj]
    simpa using hasSum_ite_sub_hasSum (hasSum_int_evenKernel (k : ‚Ñù) ht) (-k)
  ¬∑ suffices : ‚àÄ (n : ‚Ñ§), ‚Üën ‚â† -a; simpa [this] using hasSum_int_evenKernel a ht
    contrapose! h
    let ‚ü®n, hn‚ü© := h
    exact ‚ü®-n, by rwa [zsmul_one, Int.cast_neg, neg_eq_iff_eq_neg]‚ü©

lemma hasSum_int_cosKernel‚ÇÄ (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ if n = 0 then 0 else cexp (2 * œÄ * I * a * n) * rexp (-œÄ * n ^ 2 * t))
    (‚Üë(cosKernel a t) - 1) := by simpa using hasSum_ite_sub_hasSum (hasSum_int_cosKernel a ht) 0

lemma hasSum_nat_cosKernel‚ÇÄ (a : ‚Ñù) {t : ‚Ñù} (ht : 0 < t) :
    HasSum (fun n : ‚Ñï ‚Ü¶ 2 * Real.cos (2 * œÄ * a * (n + 1)) * rexp (-œÄ * (n + 1) ^ 2 * t))
    (cosKernel a t - 1) := by
  rw [‚Üê hasSum_ofReal, ofReal_sub, ofReal_one]
  have := (hasSum_int_cosKernel a ht).sum_nat_of_sum_int
  rw [‚Üê hasSum_nat_add_iff' 1] at this
  simp_rw [Finset.sum_range_one, Nat.cast_zero, neg_zero, Int.cast_zero, zero_pow two_ne_zero,
    mul_zero, zero_mul, Complex.exp_zero, Real.exp_zero, ofReal_one, mul_one, Int.cast_neg,
    Int.cast_ofNat, neg_sq, ‚Üê add_mul, add_sub_assoc, ‚Üê sub_sub, sub_self, zero_sub,
    ‚Üê sub_eq_add_neg, mul_neg] at this
  convert this with n
  push_cast
  rw [Complex.cos, mul_div_cancel' _ two_ne_zero]
  ring_nf

-- do we need a `nat` version for evenKernel?

/-!
## Asymptotics of the kernels as `t ‚Üí ‚àû`
-/

/-- The function `cosKernel a - 1` has exponential decay at `+‚àû`, for any `a`. -/
lemma isBigO_atTop_cosKernel_sub (a : UnitAddCircle) :
    ‚àÉ p, 0 < p ‚àß IsBigO atTop (cosKernel a ¬∑ - 1) (fun x ‚Ü¶ Real.exp (-p * x)) := by
  induction' a using QuotientAddGroup.induction_on with a
  obtain ‚ü®p, hp, hp'‚ü© := HurwitzKernelBounds.isBigO_atTop_F_nat_zero_sub zero_le_one
  refine ‚ü®p, hp, .trans ?_ (hp'.const_mul_left 2)‚ü©
  simp_rw [eq_false_intro one_ne_zero, if_false, sub_zero]
  apply Eventually.isBigO
  filter_upwards [eventually_gt_atTop 0] with t ht
  rw [‚Üê (hasSum_nat_cosKernel‚ÇÄ a ht).tsum_eq, HurwitzKernelBounds.F_nat]
  apply tsum_of_norm_bounded (g := fun n ‚Ü¶ 2 * HurwitzKernelBounds.f_nat 0 1 t n)
  ¬∑ exact (HurwitzKernelBounds.summable_f_nat 0 1 ht).hasSum.mul_left _
  ¬∑ intro n
    rw [norm_mul, norm_mul, norm_two, mul_assoc, mul_le_mul_iff_of_pos_left two_pos,
      norm_of_nonneg (exp_pos _).le, HurwitzKernelBounds.f_nat, pow_zero, one_mul, Real.norm_eq_abs]
    exact mul_le_of_le_one_left (exp_pos _).le (abs_cos_le_one _)

/-- The function `evenKernel a - L` has exponential decay at `+‚àû`, where `L = 1` if
`a = 0` and `L = 0` otherwise. -/
lemma isBigO_atTop_evenKernel_sub (a : UnitAddCircle) : ‚àÉ r : ‚Ñù, 0 < r ‚àß
    (evenKernel a ¬∑ - (if a = 0 then 1 else 0)) =O[atTop] (rexp <| -r * ¬∑) := by
  obtain ‚ü®b, hb, rfl‚ü© : ‚àÉ b : ‚Ñù, b ‚àà Ico 0 1 ‚àß ‚Üëb = a
  ¬∑ let b := (QuotientAddGroup.equivIcoMod (zero_lt_one' ‚Ñù) 0) a
    have hb : ‚Üëb = a := (QuotientAddGroup.equivIcoMod (zero_lt_one' ‚Ñù) 0).symm_apply_apply a
    refine ‚ü®b.1, ‚ü®b.2.1, by simpa only [zero_add] using b.2.2‚ü©, hb‚ü©
  obtain ‚ü®p, hp, hp'‚ü© := HurwitzKernelBounds.isBigO_atTop_F_int_zero_sub hb
  refine ‚ü®p, hp, (EventuallyEq.isBigO ?_).trans hp'‚ü©
  filter_upwards [eventually_gt_atTop 0] with t ht
  simp_rw [‚Üê (hasSum_int_evenKernel b ht).tsum_eq, HurwitzKernelBounds.F_int,
    HurwitzKernelBounds.f_int, pow_zero, one_mul]
  congr 2
  rw [‚Üê QuotientAddGroup.mk_zero, AddCircle.coe_eq_coe_iff_of_mem_Ico (hp := ‚ü®one_pos‚ü©) (a := 0)]
  ¬∑ simpa only [zero_add, mem_Ico] using hb
  ¬∑ simp only [zero_add, mem_Ico, le_refl, zero_lt_one, and_self]

end asymp

section FEPair
/-!
## Construction of a FE-pair
-/

/-- A `WeakFEPair` structure with `f = evenKernel a` and `g = cosKernel a`. -/
def hurwitzEvenFEPair (a : UnitAddCircle) : WeakFEPair ‚ÑÇ where
  f := ofReal' ‚àò evenKernel a
  g := ofReal' ‚àò cosKernel a
  hf_int := (continuous_ofReal.comp_continuousOn (continuousOn_evenKernel a)).locallyIntegrableOn
    measurableSet_Ioi
  hg_int := (continuous_ofReal.comp_continuousOn (continuousOn_cosKernel a)).locallyIntegrableOn
    measurableSet_Ioi
  k := 1 / 2
  hk := by norm_num
  hŒµ := one_ne_zero
  f‚ÇÄ := if a = 0 then 1 else 0
  hf_top r := by
    let ‚ü®v, hv, hv'‚ü© := isBigO_atTop_evenKernel_sub a
    rw [‚Üê isBigO_norm_left] at hv' ‚ä¢
    conv at hv' =>
      enter [2, x]; rw [‚Üê norm_real, ofReal_sub, apply_ite ((‚Üë) : ‚Ñù ‚Üí ‚ÑÇ), ofReal_one, ofReal_zero]
    exact hv'.trans (isLittleO_exp_neg_mul_rpow_atTop hv _).isBigO
  g‚ÇÄ := 1
  hg_top r := by
    obtain ‚ü®p, hp, hp'‚ü© := isBigO_atTop_cosKernel_sub a
    rw [‚Üê isBigO_norm_left] at hp' ‚ä¢
    convert hp'.trans (isLittleO_exp_neg_mul_rpow_atTop hp _).isBigO
    rw [‚Üê norm_real, ofReal_sub, ofReal_one, Function.comp_apply]
  h_feq x hx := by
    simp_rw [Function.comp_apply, one_mul, smul_eq_mul, ‚Üê ofReal_mul,
      evenKernel_functional_equation, one_div x, one_div x‚Åª¬π, inv_rpow (le_of_lt hx),
      one_div, inv_inv]

lemma hurwitzEvenFEPair_zero_symm :
    (hurwitzEvenFEPair 0).symm = hurwitzEvenFEPair 0 := by
  unfold hurwitzEvenFEPair WeakFEPair.symm
  congr 1 <;> simp only [evenKernel_eq_cosKernel_of_zero, inv_one, if_true]

lemma hurwitzEvenFEPair_neg (a : UnitAddCircle) : hurwitzEvenFEPair (-a) = hurwitzEvenFEPair a := by
  unfold hurwitzEvenFEPair
  congr 1 <;> simp only [Function.comp_def, evenKernel_neg, cosKernel_neg, neg_eq_zero]

/-!
## Definition of the even Hurwitz zeta function
-/

/--
The meromorphic function of `s` which agrees with
`1 / 2 * Gamma (s / 2) * œÄ ^ (-s / 2) * ‚àë' (n : ‚Ñ§), 1 / |n + a| ^ s` for `1 < re s`.
-/
def completedHurwitzZetaEven (a : UnitAddCircle) (s : ‚ÑÇ) : ‚ÑÇ :=
  ((hurwitzEvenFEPair a).Œõ (s / 2)) / 2

/-- The entire function differing from `completedHurwitzZetaEven a s` by a linear combination of
`1 / s` and `1 / (1 - s)`. -/
def completedHurwitzZetaEven‚ÇÄ (a : UnitAddCircle) (s : ‚ÑÇ) : ‚ÑÇ :=
  ((hurwitzEvenFEPair a).Œõ‚ÇÄ (s / 2)) / 2

lemma completedHurwitzZetaEven_eq (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven a s =
    completedHurwitzZetaEven‚ÇÄ a s - (if a = 0 then 1 else 0) / s - 1 / (1 - s) := by
  rw [completedHurwitzZetaEven, WeakFEPair.Œõ, sub_div, sub_div]
  congr 1
  ¬∑ change completedHurwitzZetaEven‚ÇÄ a s - (1 / (s / 2)) ‚Ä¢ (if a = 0 then 1 else 0) / 2 =
      completedHurwitzZetaEven‚ÇÄ a s - (if a = 0 then 1 else 0) / s
    rw [smul_eq_mul, mul_comm, mul_div_assoc, div_div, div_mul_cancel _ two_ne_zero, mul_one_div]
  ¬∑ change (1 / (‚Üë(1 / 2 : ‚Ñù) - s / 2)) ‚Ä¢ 1 / 2 = 1 / (1 - s)
    push_cast
    rw [smul_eq_mul, mul_one, ‚Üê sub_div, div_div, div_mul_cancel _ two_ne_zero]

/--
The meromorphic function of `s` which agrees with
`Gamma (s / 2) * œÄ ^ (-s / 2) * ‚àë' n : ‚Ñï, cos (2 * œÄ * a * n) / n ^ s` for `1 < re s`.
-/
def completedCosZeta (a : UnitAddCircle) (s : ‚ÑÇ) : ‚ÑÇ :=
  ((hurwitzEvenFEPair a).symm.Œõ (s / 2)) / 2

/-- The entire function differing from `completedCosZeta a s` by a linear combination of
`1 / s` and `1 / (1 - s)`. -/
def completedCosZeta‚ÇÄ (a : UnitAddCircle) (s : ‚ÑÇ) : ‚ÑÇ :=
  ((hurwitzEvenFEPair a).symm.Œõ‚ÇÄ (s / 2)) / 2

lemma completedCosZeta_eq (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta a s =
    completedCosZeta‚ÇÄ a s - 1 / s - (if a = 0 then 1 else 0) / (1 - s) := by
  rw [completedCosZeta, WeakFEPair.Œõ, sub_div, sub_div]
  congr 1
  ¬∑ rw [ completedCosZeta‚ÇÄ, WeakFEPair.symm, hurwitzEvenFEPair, smul_eq_mul, mul_one, div_div,
      div_mul_cancel _ (two_ne_zero' ‚ÑÇ)]
  ¬∑ simp_rw [WeakFEPair.symm, hurwitzEvenFEPair, push_cast, inv_one, smul_eq_mul,
      mul_comm _ (if _ then _ else _), mul_div_assoc, div_div, ‚Üê sub_div,
      div_mul_cancel _ (two_ne_zero' ‚ÑÇ), mul_one_div]

/-!
## Parity and functional equations
-/

lemma completedHurwitzZetaEven_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven (-a) s = completedHurwitzZetaEven a s := by
  simp only [completedHurwitzZetaEven, hurwitzEvenFEPair_neg]

lemma completedHurwitzZetaEven‚ÇÄ_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven‚ÇÄ (-a) s = completedHurwitzZetaEven‚ÇÄ a s := by
  simp only [completedHurwitzZetaEven‚ÇÄ, hurwitzEvenFEPair_neg]

lemma completedCosZeta_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta (-a) s = completedCosZeta a s := by
  simp only [completedCosZeta, hurwitzEvenFEPair_neg]

lemma completedCosZeta‚ÇÄ_neg (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta‚ÇÄ (-a) s = completedCosZeta‚ÇÄ a s := by
  simp only [completedCosZeta‚ÇÄ, hurwitzEvenFEPair_neg]

/-- Functional equation for the even Hurwitz zeta function. -/
lemma completedHurwitzZetaEven_one_sub (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven a (1 - s) = completedCosZeta a s := by
  rw [completedHurwitzZetaEven, completedCosZeta, sub_div,
    (by norm_num : (1 / 2 : ‚ÑÇ) = ‚Üë(1 / 2 : ‚Ñù)),
    (by rfl : (1 / 2 : ‚Ñù) = (hurwitzEvenFEPair a).k),
    (hurwitzEvenFEPair a).functional_equation (s / 2),
    (by rfl : (hurwitzEvenFEPair a).Œµ = 1),
    one_smul]

/-- Functional equation for the even Hurwitz zeta function with poles removed. -/
lemma completedHurwitzZetaEven‚ÇÄ_one_sub (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedHurwitzZetaEven‚ÇÄ a (1 - s) = completedCosZeta‚ÇÄ a s := by
  rw [completedHurwitzZetaEven‚ÇÄ, completedCosZeta‚ÇÄ, sub_div,
    (by norm_num : (1 / 2 : ‚ÑÇ) = ‚Üë(1 / 2 : ‚Ñù)),
    (by rfl : (1 / 2 : ‚Ñù) = (hurwitzEvenFEPair a).k),
    (hurwitzEvenFEPair a).functional_equation‚ÇÄ (s / 2),
    (by rfl : (hurwitzEvenFEPair a).Œµ = 1),
    one_smul]

/-- Functional equation for the even Hurwitz zeta function (alternative form). -/
lemma completedCosZeta_one_sub (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta a (1 - s) = completedHurwitzZetaEven a s := by
  rw [‚Üê completedHurwitzZetaEven_one_sub, sub_sub_cancel]

/-- Functional equation for the even Hurwitz zeta function with poles removed (alternative form). -/
lemma completedCosZeta‚ÇÄ_one_sub (a : UnitAddCircle) (s : ‚ÑÇ) :
    completedCosZeta‚ÇÄ a (1 - s) = completedHurwitzZetaEven‚ÇÄ a s := by
  rw [‚Üê completedHurwitzZetaEven‚ÇÄ_one_sub, sub_sub_cancel]

/-!
## Differentiability and residues
-/

/--
The even Hurwitz completed zeta is differentiable away from `s = 0` and `s = 1` (and also at
`s = 0` if `a ‚â† 0`)
-/
lemma differentiableAt_completedHurwitzZetaEven
    (a : UnitAddCircle) {s : ‚ÑÇ} (hs : s ‚â† 0 ‚à® a ‚â† 0) (hs' : s ‚â† 1) :
    DifferentiableAt ‚ÑÇ (completedHurwitzZetaEven a) s := by
  refine (((hurwitzEvenFEPair a).differentiableAt_Œõ ?_ (Or.inl ?_)).comp s
    (differentiableAt_id.div_const _)).div_const _
  ¬∑ rw [div_ne_zero_iff, eq_true_intro two_ne_zero, and_true]
    refine Or.imp (by tauto) (fun ha ‚Ü¶ ?_) hs
    simp only [hurwitzEvenFEPair, eq_false_intro ha, if_false]
  ¬∑ change s / 2 ‚â† ‚Üë(1 / 2 : ‚Ñù)
    rw [ofReal_div, ofReal_one, ofReal_ofNat]
    exact hs' ‚àò (div_left_inj' two_ne_zero).mp

lemma differentiable_completedHurwitzZetaEven‚ÇÄ (a : UnitAddCircle) :
    Differentiable ‚ÑÇ (completedHurwitzZetaEven‚ÇÄ a) :=
  ((hurwitzEvenFEPair a).differentiable_Œõ‚ÇÄ.comp (differentiable_id.div_const _)).div_const _

lemma differentiableAt_completedCosZeta
    (a : UnitAddCircle) {s : ‚ÑÇ} (hs : s ‚â† 0) (hs' : s ‚â† 1 ‚à® a ‚â† 0) :
    DifferentiableAt ‚ÑÇ (completedCosZeta a) s := by
  refine (((hurwitzEvenFEPair a).symm.differentiableAt_Œõ (Or.inl ?_) ?_).comp s
      (differentiableAt_id.div_const _)).div_const _
  ¬∑ rwa [div_ne_zero_iff, eq_true_intro two_ne_zero, and_true]
  ¬∑ change s / 2 ‚â† ‚Üë(1 / 2 : ‚Ñù) ‚à® (if a = 0 then 1 else 0) = 0
    refine Or.imp (fun h ‚Ü¶ ?_) (fun ha ‚Ü¶ ?_) hs'
    ¬∑ simpa only [push_cast] using h ‚àò (div_left_inj' two_ne_zero).mp
    ¬∑ simp_rw [eq_false_intro ha, if_false]

lemma differentiable_completedCosZeta‚ÇÄ (a : UnitAddCircle) :
    Differentiable ‚ÑÇ (completedCosZeta‚ÇÄ a) :=
  ((hurwitzEvenFEPair a).symm.differentiable_Œõ‚ÇÄ.comp (differentiable_id.div_const _)).div_const _

/-- The residue of `completedHurwitzZetaEven a s` at `s = 1` is equal to `1`. -/
lemma completedHurwitzZetaEven_residue_one (a : UnitAddCircle) :
    Tendsto (fun s ‚Ü¶ (s - 1) * completedHurwitzZetaEven a s) (ùìù[‚â†] 1) (ùìù 1) := by
  have h1 : Tendsto (fun s : ‚ÑÇ ‚Ü¶ (s - ‚Üë(1  / 2 : ‚Ñù)) * _) (ùìù[‚â†] ‚Üë(1  / 2 : ‚Ñù))
    (ùìù ((1 : ‚ÑÇ) * (1 : ‚ÑÇ))) := (hurwitzEvenFEPair a).Œõ_residue_k
  simp only [push_cast, one_mul] at h1
  have h2 : Tendsto (fun s : ‚ÑÇ ‚Ü¶ s / 2) (ùìù[‚â†] 1) (ùìù[‚â†] (1 / 2)) :=
    le_of_eq ((Homeomorph.mulRight‚ÇÄ _ (inv_ne_zero (two_ne_zero' ‚ÑÇ))).map_punctured_nhds_eq 1)
  refine (h1.comp h2).congr (fun s ‚Ü¶ ?_)
  rw [completedHurwitzZetaEven, Function.comp_apply]
  ring_nf

/-- The residue of `completedHurwitzZetaEven a s` at `s = 0` is equal to `-1` if `a = 0`, and `0`
otherwise. -/
lemma completedHurwitzZetaEven_residue_zero (a : UnitAddCircle) :
    Tendsto (fun s ‚Ü¶ s * completedHurwitzZetaEven a s) (ùìù[‚â†] 0) (ùìù (if a = 0 then -1 else 0)) := by
  have h1 : Tendsto (fun s : ‚ÑÇ ‚Ü¶ s * _) (ùìù[‚â†] 0)
    (ùìù (-(if a = 0 then 1 else 0))) := (hurwitzEvenFEPair a).Œõ_residue_zero
  have : -(if a = 0 then (1 : ‚ÑÇ) else 0) = (if a = 0 then -1 else 0) := by { split_ifs <;> simp }
  simp only [this, push_cast, one_mul] at h1
  have h2 : Tendsto (fun s : ‚ÑÇ ‚Ü¶ s / 2) (ùìù[‚â†] 0) (ùìù[‚â†] (0 / 2)) :=
    le_of_eq ((Homeomorph.mulRight‚ÇÄ _ (inv_ne_zero (two_ne_zero' ‚ÑÇ))).map_punctured_nhds_eq 0)
  rw [zero_div] at h2
  refine (h1.comp h2).congr (fun s ‚Ü¶ ?_)
  rw [completedHurwitzZetaEven, Function.comp_apply]
  ring_nf

end FEPair

/-!
## Relation to the Dirichlet series for `1 < re s`
-/

/-- Auxiliary lemma for `completedHurwitzZetaEven_eq_tsum_int`, computing the Mellin transform of an
individual term in the series. -/
theorem mellin_exp_neg_pi_mul_sq {s : ‚ÑÇ} (hs : 0 < s.re) {n : ‚Ñù} (hn : n ‚â† 0) :
    mellin (fun t ‚Ü¶ rexp (-œÄ * n ^ 2 * t) : ‚Ñù ‚Üí ‚ÑÇ) (s / 2) =
      Gamma (s / 2) * œÄ ^ (-s / 2) / |n| ^ s := by
  have : 0 < (s / 2).re
  ¬∑ simpa only [‚Üê ofReal_ofNat, div_ofReal_re] using div_pos hs two_pos
  rw [Complex.Gamma_eq_integral this, GammaIntegral_eq_mellin]
  have : 0 < œÄ * n ^ 2 := by positivity
  have := mellin_comp_mul_left (fun t ‚Ü¶ ‚Üë(rexp (-t)) : ‚Ñù ‚Üí ‚ÑÇ) (s / 2) this
  simp_rw [‚Üê neg_mul, smul_eq_mul] at this
  rw [this, mul_div_assoc, mul_comm (mellin _ _), ofReal_mul,
    mul_cpow_ofReal_nonneg pi_pos.le (sq_nonneg _), neg_div]
  congr 2
  rw [cpow_neg, ‚Üê _root_.sq_abs, pow_two, ofReal_mul, mul_cpow_ofReal_nonneg (abs_nonneg _)
    (abs_nonneg _), ‚Üê cpow_add, add_halves']
  rwa [ofReal_ne_zero, abs_ne_zero]
#align integral_cpow_mul_exp_neg_pi_mul_sq mellin_exp_neg_pi_mul_sq

/-- Formula for `completedCosZeta` as a Dirichlet series in the convergence range
(first version, with sum over `‚Ñ§ - {0}`). -/
lemma hasSum_int_completedCosZeta (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ if n = 0 then 0 else
    Gamma (s / 2) * œÄ ^ (-s / 2) * cexp (2 * œÄ * I * a * n) / (‚Üë|n| : ‚ÑÇ) ^ s / 2)
    (completedCosZeta a s) := by
  let Œº : Measure ‚Ñù := volume.restrict (Ioi 0)
  let F (n : ‚Ñ§) (t : ‚Ñù) : ‚ÑÇ :=
    t ^ (s / 2 - 1) * (if n = 0 then 0 else cexp (2 * œÄ * I * a * n) * rexp (-œÄ * n ^ 2 * t)) / 2
  let f (t : ‚Ñù) : ‚ÑÇ := (t : ‚ÑÇ) ^ (s / 2 - 1) * (cosKernel a t - 1) / 2
  let bound (n : ‚Ñ§) (t : ‚Ñù) : ‚Ñù :=
    if n = 0 then 0 else t ^ (s.re / 2 - 1) * rexp (-œÄ * n ^ 2 * t) / 2
  have hF_meas (n : ‚Ñ§) : AEStronglyMeasurable (F n) Œº
  ¬∑ by_cases hn : n = 0
    ¬∑ simp_rw [hn, if_true, mul_zero, zero_div]
      exact aestronglyMeasurable_const
    ¬∑ simp_rw [hn, if_false]
      refine ((ContinuousOn.mul ?_ ?_).div_const _).aestronglyMeasurable measurableSet_Ioi
      ¬∑ exact ContinuousAt.continuousOn
          fun t ht ‚Ü¶ continuousAt_ofReal_cpow_const _ _ (Or.inr (ne_of_gt ht))
      ¬∑ apply Continuous.continuousOn
        continuity
  have h_lim : ‚àÄ·µê (t : ‚Ñù) ‚àÇŒº, HasSum (fun n ‚Ü¶ F n t) (f t)
  ¬∑ rw [ae_restrict_iff' measurableSet_Ioi]
    filter_upwards with t ht
    exact ((hasSum_int_cosKernel‚ÇÄ a ht).mul_left _).div_const _
  have h_bound (n : ‚Ñ§) : ‚àÄ·µê (t : ‚Ñù) ‚àÇŒº, ‚ÄñF n t‚Äñ ‚â§ bound n t
  ¬∑ rw [ae_restrict_iff' measurableSet_Ioi]
    filter_upwards with t ht
    by_cases hn : n = 0
    ¬∑ simp_rw [hn, if_true, mul_zero, zero_div, norm_zero, le_refl]
    ¬∑ simp_rw [hn, if_false]
      rw [norm_div, ‚Üê ofReal_ofNat, norm_real, ofReal_ofNat, norm_two,
        div_le_div_right two_pos, norm_mul, norm_mul, norm_real, norm_of_nonneg (exp_pos _).le,
        ‚Üê mul_assoc, mul_le_mul_iff_of_pos_right (exp_pos _), Complex.norm_eq_abs,
        abs_cpow_eq_rpow_re_of_pos ht, sub_re, one_re, ‚Üê ofReal_ofNat, div_ofReal_re,
        (by { push_cast; ring } : (2 : ‚Ñù) * œÄ * I * a * n = ‚Üë(2 * œÄ * a * n : ‚Ñù) * I),
        Complex.norm_eq_abs, Complex.abs_exp, mul_I_re, ofReal_im, neg_zero, Real.exp_zero,
        mul_one]
  have bound_hasSum (t : ‚Ñù) (ht : 0 < t) :
      HasSum (bound ¬∑ t) (t ^ (s.re / 2 - 1) * (cosKernel 0 t - 1) / 2)
  ¬∑ have := hasSum_int_evenKernel‚ÇÄ 0 ht
    simp_rw [add_zero, QuotientAddGroup.mk_zero, neg_zero, if_true,
      evenKernel_eq_cosKernel_of_zero, Int.cast_eq_zero] at this
    convert (this.mul_left (t ^ (s.re / 2 - 1))).div_const (2 : ‚Ñù) using 2 with n
    split_ifs with hn
    ¬∑ simp_rw [hn, if_true, mul_zero, zero_div]
    ¬∑ simp_rw [hn, if_false]
  have bound_summable : ‚àÄ·µê (t : ‚Ñù) ‚àÇŒº, Summable fun (n : ‚Ñ§) ‚Ü¶ bound n t
  ¬∑ rw [ae_restrict_iff' measurableSet_Ioi]
    filter_upwards with t ht
    exact (bound_hasSum t ht).summable
  have bound_integrable : Integrable (fun (t : ‚Ñù) ‚Ü¶ ‚àë' (n : ‚Ñ§), bound n t) Œº
  ¬∑ erw [‚Üê IntegrableOn,
      integrableOn_congr_fun (fun t ht ‚Ü¶ (bound_hasSum t ht).tsum_eq) measurableSet_Ioi]
    apply Integrable.div_const
    have : 1 / 2 < ((s.re : ‚ÑÇ) / 2).re
    ¬∑ rwa [‚Üê ofReal_ofNat, div_ofReal_re, ofReal_re, div_lt_div_right two_pos]
    have := ((hurwitzEvenFEPair 0).symm.hasMellin this).1.re
    refine IntegrableOn.congr_fun this (fun t (ht : 0 < t) ‚Ü¶ ?_) measurableSet_Ioi
    rw [smul_eq_mul, (by push_cast; rfl : (s.re : ‚ÑÇ) / 2 - 1 = ‚Üë(s.re / 2 - 1)),
      ‚Üê ofReal_cpow ht.le, IsROrC.re_eq_complex_re, re_ofReal_mul]
    rfl
  convert (hasSum_integral_of_dominated_convergence
      bound hF_meas h_bound bound_summable bound_integrable h_lim) using 2 with n
  ¬∑ -- show the individual terms match up
    by_cases hn : n = 0
    ¬∑ simp_rw [hn, if_true, mul_zero, zero_div]
      rw [integral_zero]
    simp_rw [hn, if_false, integral_div]
    congr 1
    simp_rw [‚Üê mul_assoc _ (cexp _), mul_comm _ (cexp _), mul_assoc (cexp _),
        integral_mul_left, mul_div_assoc _ (_ * _)]
    congr 1
    simpa only [‚Üê Int.cast_abs, ofReal_int_cast]
      using (mellin_exp_neg_pi_mul_sq (zero_lt_one.trans hs) (Int.cast_ne_zero.mpr hn)).symm
  ¬∑ -- show ‚à´ f = Hurwitz zeta
    rw [completedCosZeta]
    have : 1 / 2 < re (s / 2) := by rwa [‚Üê ofReal_ofNat, div_ofReal_re, div_lt_div_right two_pos]
    convert congr_arg (¬∑ / 2) ((hurwitzEvenFEPair a).symm.hasMellin this).2.symm
    simp only [f, mellin, integral_div]
    rfl

/-- Formula for `completedCosZeta` as a Dirichlet series in the convergence range
(second version, with sum over `‚Ñï`). -/
lemma hasSum_nat_completedCosZeta (a : ‚Ñù) {s : ‚ÑÇ} (hs : 1 < re s) :
    HasSum (fun n : ‚Ñï ‚Ü¶ Gamma (s / 2) * œÄ ^ (-s / 2) * Real.cos (2 * œÄ * a * n) / (n : ‚ÑÇ) ^ s)
    (completedCosZeta a s) := by
  have := (hasSum_int_completedCosZeta a hs).sum_nat_of_sum_int
  simp_rw [if_true, add_zero, neg_eq_zero, Nat.cast_eq_zero, abs_neg, Nat.abs_cast] at this
  convert this using 2 with n
  split_ifs with h
  ¬∑ have : s ‚â† 0 := fun p ‚Ü¶ (not_lt.mpr zero_le_one) (zero_re ‚ñ∏ p ‚ñ∏ hs)
    simp_rw [h, Nat.cast_zero, zero_cpow this, div_zero, add_zero]
  simp only [ofReal_cos, Complex.cos, push_cast]
  ring_nf

/-- Formula for `completedHurwitzZetaEven` as a Dirichlet series in the convergence range. -/
lemma hasSum_int_completedHurwitzZetaEven (a : ‚Ñù) (s : ‚ÑÇ) (hs : 1 < re s) :
    HasSum (fun n : ‚Ñ§ ‚Ü¶ if n = -a then 0 else
    Gamma (s / 2) * œÄ ^ (-s / 2) / (‚Üë|n + a| : ‚ÑÇ) ^ s / 2)
    (completedHurwitzZetaEven a s) := by
  let Œº : Measure ‚Ñù := volume.restrict (Ioi 0)
  let F (n : ‚Ñ§) (t : ‚Ñù) : ‚ÑÇ :=
    if n = -a then 0 else t ^ (s / 2 - 1) * rexp (-œÄ * (n + a) ^ 2 * t) / 2
  let f (t : ‚Ñù) : ‚ÑÇ := (t : ‚ÑÇ) ^ (s / 2 - 1) *
    (evenKernel a t - (if (a : UnitAddCircle) = 0 then 1 else 0)) / 2
  have hF_meas (n : ‚Ñ§) : AEStronglyMeasurable (F n) Œº
  ¬∑ simp only [F]
    split_ifs with h
    ¬∑ measurability
    ¬∑ refine ((ContinuousOn.mul ?_ ?_).div_const _).aestronglyMeasurable measurableSet_Ioi
      ¬∑ apply ContinuousAt.continuousOn
        exact fun t ht ‚Ü¶ continuousAt_ofReal_cpow_const _ _ (Or.inr (ne_of_gt ht))
      ¬∑ exact Continuous.continuousOn (by continuity)
  have h_lim : ‚àÄ·µê (t : ‚Ñù) ‚àÇŒº, HasSum (fun n ‚Ü¶ F n t) (f t)
  ¬∑ rw [ae_restrict_iff' measurableSet_Ioi]
    filter_upwards with t ht
    have := hasSum_ofReal.mpr (hasSum_int_evenKernel‚ÇÄ a ht)
    convert (this.mul_left ((t : ‚ÑÇ) ^ (s / 2 - 1))).div_const 2 with n
    ¬∑ simp only [F]
      split_ifs <;> simp only [ofReal_zero, mul_zero, zero_div]
    ¬∑ split_ifs <;> simp only [ofReal_sub, ofReal_one, sub_zero]
  let bound (n : ‚Ñ§) (t : ‚Ñù) : ‚Ñù :=
    if n = -a then 0 else t ^ (s.re / 2 - 1) * rexp (-œÄ * (n + a) ^ 2 * t) / 2
  have h_bound (n : ‚Ñ§) : ‚àÄ·µê (t : ‚Ñù) ‚àÇŒº, ‚ÄñF n t‚Äñ ‚â§ bound n t
  ¬∑ rw [ae_restrict_iff' measurableSet_Ioi]
    filter_upwards with t ht
    simp only [F, bound]
    split_ifs
    ¬∑ rw [norm_zero]
    ¬∑ rw [norm_div, ‚Üê ofReal_ofNat, norm_real 2, ofReal_ofNat, norm_two,
        div_le_div_right two_pos, norm_mul, norm_real, norm_of_nonneg (exp_pos _).le,
        mul_le_mul_iff_of_pos_right (exp_pos _), Complex.norm_eq_abs,
        abs_cpow_eq_rpow_re_of_pos ht, sub_re, one_re, ‚Üê ofReal_ofNat, div_ofReal_re]
  have bound_hasSum (t : ‚Ñù) (ht : 0 < t) : HasSum (bound ¬∑ t)
      (t ^ (s.re / 2 - 1) * (evenKernel a t - (if (a : UnitAddCircle) = 0 then 1 else 0)) / 2)
  ¬∑ convert ((hasSum_int_evenKernel‚ÇÄ a ht).mul_left _).div_const 2 using 2 with n
    simp only [mul_ite, bound]
    split_ifs with h <;> simp only [mul_zero, zero_div]
  have bound_summable : ‚àÄ·µê (t : ‚Ñù) ‚àÇŒº, Summable fun (n : ‚Ñ§) ‚Ü¶ bound n t
  ¬∑ rw [ae_restrict_iff' measurableSet_Ioi]
    filter_upwards with t ht
    exact (bound_hasSum t ht).summable
  have bound_integrable : Integrable (fun (t : ‚Ñù) ‚Ü¶ ‚àë' (n : ‚Ñ§), bound n t) Œº
  ¬∑ erw [‚Üê IntegrableOn,
      integrableOn_congr_fun (fun t ht ‚Ü¶ (bound_hasSum t ht).tsum_eq) measurableSet_Ioi]
    apply Integrable.div_const
    have : 1 / 2 < ((s.re : ‚ÑÇ) / 2).re
    ¬∑ rwa [‚Üê ofReal_ofNat, div_ofReal_re, ofReal_re, div_lt_div_right two_pos]
    have := ((hurwitzEvenFEPair a).hasMellin this).1.re
    refine IntegrableOn.congr_fun this (fun t (ht : 0 < t) ‚Ü¶ ?_) measurableSet_Ioi
    simp_rw [smul_eq_mul, (by push_cast; rfl : (s.re : ‚ÑÇ) / 2 - 1 = ‚Üë(s.re / 2 - 1)),
      ‚Üê ofReal_cpow ht.le, IsROrC.re_eq_complex_re, re_ofReal_mul, sub_re]
    simp only [hurwitzEvenFEPair, apply_ite re, one_re, zero_re, ofReal_re, Function.comp_apply]
  convert MeasureTheory.hasSum_integral_of_dominated_convergence
      bound hF_meas h_bound bound_summable bound_integrable h_lim using 2 with n
  ¬∑ -- show the individual terms match up
    simp only [F]
    split_ifs with h
    ¬∑ simp only [integral_zero]
    ¬∑ rw [integral_div]
      exact congr_arg (¬∑ / 2) (mellin_exp_neg_pi_mul_sq (zero_lt_one.trans hs)
        (h ‚àò add_eq_zero_iff_eq_neg.mp)).symm
  ¬∑ -- show ‚à´ f = Hurwitz zeta
    rw [completedHurwitzZetaEven]
    have : 1 / 2 < re (s / 2) := by rwa [‚Üê ofReal_ofNat, div_ofReal_re, div_lt_div_right two_pos]
    convert congr_arg (¬∑ / 2) ((hurwitzEvenFEPair a).hasMellin this).2.symm
    simp only [integral_div]
    rfl
