/-
Copyright (c) 2026 Michael Rothgang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Michael Rothgang
-/
module

public import Mathlib.Algebra.Order.BigOperators.Expect
public import Mathlib.Algebra.Order.Field.Power
public import Mathlib.Data.EReal.Inv
public import Mathlib.Data.Real.Sqrt
public import Mathlib.Logic.Equiv.PartialEquiv
public import Mathlib.Tactic.ContinuousFunctionalCalculus
public import Mathlib.Tactic.Positivity
public import Mathlib.Topology.Algebra.Module.FiniteDimension

/-! # Continuous linear maps with a continuous left/right inverse

This file defines continuous linear maps which admit a continuous left/right inverse.

We prove that both of these classes of maps are closed under products, composition and contain
linear equivalences, and a sufficient criterion in finite dimension: a surjective linear map on a
finite-dimensional space always admits a continuous right inverse; an injective linear map on a
finite-dimensional space always admits a continuous left inverse.

This concept is used to give an equivalent definition of immersions and submersions of manifolds.

## Main definitions and results

* `ContinuousLinearMap.HasRightInverse`: a continuous linear map admits a left inverse
  which is a continuous linear map itself
* `ContinuousLinearMap.HasRightInverse`: a continuous linear map admits a right inverse
  which is a continuous linear map itself
* `ContinuousLinearEquiv.hasRightInverse` and `ContinuousLinearEquiv.hasRightInverse`:
  a continuous linear equivalence admits a continuous left (resp. right) inverse
* `ContinuousLinearMap.HasLeftInverse.comp`, `ContinuousLinearMap.HasRightInverse.comp`:
  if `f : E ‚Üí F` and `g : F ‚Üí G` both admit a continuous left (resp. right) inverse,
  so does `g.comp f`.
* `ContinuousLinearMap.HasLefttInverse.of_comp`, `ContinuousLinearMap.HasRightInverse.of_comp`:
  suppose `f : E ‚Üí F` and `g : F ‚Üí G` are continuous linear maps.
  If `g.comp f : E ‚Üí G` admits a continuous left inverse, then so does `f`.
  If `g.comp f : E ‚Üí G` admits a continuous right inverse, then so does `g`.
* `ContinuousLinearMap.HasLeftInverse.prodMap`, `ContinuousLinearMap.HasRightInverse.prodMap`:
  having a continuous right inverse is closed under taking products
* `ContinuousLinearMap.HasLeftInverse.inl`, `ContinuousLinearMap.HasLeftInverse.inr`:
  `ContinuousLinearMap.inl` and `.inr` have a continuous left inverse
* `ContinuousLinearMap.HasRightInverse.fst`, `ContinuousLinearMap.HasRightInverse.snd`:
  `ContinuousLinearMap.fst` and `.snd` hav a continuous right inverse
* `ContinuousLinearMap.HasLeftInverse.of_injective_of_finiteDimensional`:
  if `f : E ‚Üí F` is injective and `F` is finite-dimensional, `f` has a continuous left inverse.
* `ContinuousLinearMap.HasRightInverse.of_surjective_of_finiteDimensional`:
  if `f : E ‚Üí F` is surjective and `F` is finite-dimensional, `f` has a continuous right inverse.

## TODO

* Suppose `E` and `F` are Banach and `f : E ‚Üí F` is Fredholm.
  If `f` is surjective, it has a continuous right inverse.
  If `f` is injective, it has a continuout left inverse.
* `f` has a continuous left inverse if and only if it is injective, has closed range,
  and its range admits a closed complement

-/

public section

open Function Set

variable {R : Type*} [Semiring R] {E E' F F' G : Type*}
  [TopologicalSpace E] [AddCommMonoid E] [Module R E]
  [TopologicalSpace E'] [AddCommMonoid E'] [Module R E']
  [TopologicalSpace F] [AddCommMonoid F] [Module R F]
  [TopologicalSpace F'] [AddCommMonoid F'] [Module R F']

noncomputable section

/-- A continuous linear map admits a left inverse which is a continuous linear map itself. -/
@[expose] protected def ContinuousLinearMap.HasLeftInverse (f : E ‚ÜíL[R] F) : Prop :=
  ‚àÉ g : F ‚ÜíL[R] E, LeftInverse g f

/-- A continuous linear map admits a right inverse which is a continuous linear map itself. -/
@[expose] protected def ContinuousLinearMap.HasRightInverse (f : E ‚ÜíL[R] F) : Prop :=
  ‚àÉ g : F ‚ÜíL[R] E, RightInverse g f

namespace ContinuousLinearMap

namespace HasLeftInverse

variable {f : E ‚ÜíL[R] F}

/-- Choice of continuous left inverse for `f : F ‚ÜíL[R] E`, given that such an inverse exists. -/
def leftInverse (h : f.HasLeftInverse) : F ‚ÜíL[R] E := Classical.choose h

lemma leftInverse_leftInverse (h : f.HasLeftInverse) : LeftInverse h.leftInverse f :=
  Classical.choose_spec h

lemma injective (h : f.HasLeftInverse) : Injective f :=
  h.leftInverse_leftInverse.injective

example (h : f.HasLeftInverse) (x : E) : h.leftInverse (f x) = x :=
  h.leftInverse_leftInverse x

lemma congr {g : E ‚ÜíL[R] F} (hf : f.HasLeftInverse) (hfg : g = f) :
    g.HasLeftInverse :=
  hfg ‚ñ∏ hf

/-- A continuous linear equivalence has a continuous left inverse. -/
lemma _root_.ContinuousLinearEquiv.hasLeftInverse (f : E ‚âÉL[R] F) :
    f.toContinuousLinearMap.HasLeftInverse :=
  ‚ü®f.symm, rightInverse_of_comp (by simp)‚ü©

@[simp] lemma _root_.ContinuousLinearEquiv.leftInverse_hasLeftInverse (f : E ‚âÉL[R] F) :
    f.hasLeftInverse.leftInverse = f.symm := by
  ext y
  calc f.hasLeftInverse.leftInverse y
    _ = f.hasLeftInverse.leftInverse (f (f.symm y)) := by simp
    _ = f.symm y := f.hasLeftInverse.leftInverse_leftInverse (f.symm y)

/-- An invertible continuous linear map has a continuous left inverse. -/
lemma of_isInvertible (hf : IsInvertible f) : f.HasLeftInverse := by
  obtain ‚ü®e, rfl‚ü© := hf
  exact e.hasLeftInverse

/-- If `f` and `g` admit continuous left inverses, so does `f √ó g`. -/
lemma prodMap {g : E' ‚ÜíL[R] F'} (hf : f.HasLeftInverse) (hg : g.HasLeftInverse) :
    (f.prodMap g).HasLeftInverse := by
  obtain ‚ü®finv, hfinv‚ü© := hf
  obtain ‚ü®ginv, hginv‚ü© := hg
  use finv.prodMap ginv
  simp [hfinv, hginv]

variable [TopologicalSpace G] [AddCommMonoid G] [Module R G]

lemma comp {g : F ‚ÜíL[R] G} (hg : g.HasLeftInverse) (hf : f.HasLeftInverse) :
    (g.comp f).HasLeftInverse := by
  obtain ‚ü®finv, hfinv‚ü© := hf
  obtain ‚ü®ginv, hginv‚ü© := hg
  refine ‚ü®finv.comp ginv, fun x ‚Ü¶ ?_‚ü©
  simp only [coe_comp', Function.comp_apply]
  rw [hginv, hfinv]

lemma of_comp {g : F ‚ÜíL[R] G} (hfg : (g.comp f).HasLeftInverse) :
    f.HasLeftInverse := by
  obtain ‚ü®fginv, hfginv‚ü© := hfg
  refine ‚ü®fginv.comp g, fun y ‚Ü¶ ?_‚ü©
  simp only [coe_comp', Function.comp_apply]
  exact hfginv y

lemma comp_continuousLinearEquivalence {f‚ÇÄ : F' ‚âÉL[R] E} (hf : f.HasLeftInverse) :
    (f.comp f‚ÇÄ.toContinuousLinearMap).HasLeftInverse :=
  hf.comp f‚ÇÄ.hasLeftInverse

lemma continuousLinearEquivalence_comp {g : F ‚âÉL[R] F'} (hf : f.HasLeftInverse) :
    (g.toContinuousLinearMap.comp f).HasLeftInverse :=
  g.hasLeftInverse.comp hf

/-- `ContinuousLinearMap.inl` has a continuous left inverse. -/
protected lemma inl : (ContinuousLinearMap.inl R F G).HasLeftInverse := by
  use ContinuousLinearMap.fst _ _ _
  intro x
  simp

/-- `ContinuousLinearMap.inr` has a continuous left inverse. -/
protected lemma inr : (ContinuousLinearMap.inr R F G).HasLeftInverse := by
  use ContinuousLinearMap.snd _ _ _
  intro x
  simp

section NontriviallyNormedField

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {E F : Type*}
  [TopologicalSpace E] [AddCommGroup E] [Module ùïú E] [IsTopologicalAddGroup E] [ContinuousSMul ùïú E]
  [TopologicalSpace F] [AddCommGroup F] [Module ùïú F] [IsTopologicalAddGroup F] [ContinuousSMul ùïú F]
  [T2Space F] {f : E ‚ÜíL[ùïú] F}

/-- If `f : E ‚Üí F` is injective and `E` is finite-dimensional,
`f` has a continuous left inverse. -/
lemma of_injective_of_finiteDimensional [CompleteSpace ùïú] [FiniteDimensional ùïú F]
    (hf : Injective f) :
    f.HasLeftInverse := by
  -- An injective linear map has a linear inverse; this inverse is automatically continuous
  -- because its domain is finite-dimensional.
  obtain ‚ü®g, hg‚ü© :=
    f.toLinearMap.exists_leftInverse_of_injective (f.ker_eq_bot_of_injective hf)
  exact ‚ü®‚ü®g, LinearMap.continuous_of_finiteDimensional _‚ü©, fun x ‚Ü¶ congr($hg x)‚ü©

end NontriviallyNormedField

end HasLeftInverse

namespace HasRightInverse

variable {f : E ‚ÜíL[R] F}

/-- Choice of continuous right inverse for `f : F ‚ÜíL[R] E`, given that such an inverse exists. -/
def rightInverse (h : f.HasRightInverse) : F ‚ÜíL[R] E := Classical.choose h

lemma rightInverse_rightInverse (h : f.HasRightInverse) : RightInverse h.rightInverse f :=
  Classical.choose_spec h

lemma surjective (h : f.HasRightInverse) : Surjective f :=
  h.rightInverse_rightInverse.surjective

lemma congr {g : E ‚ÜíL[R] F} (hf : f.HasRightInverse) (hfg : g = f) :
    g.HasRightInverse :=
  hfg ‚ñ∏ hf

/-- A continuous linear equivalence has a continuous right inverse. -/
lemma _root_.ContinuousLinearEquiv.hasRightInverse (f : E ‚âÉL[R] F) :
    f.toContinuousLinearMap.HasRightInverse :=
  ‚ü®f.symm, rightInverse_of_comp (by simp)‚ü©

@[simp] lemma _root_.ContinuousLinearEquiv.rightInverse_hasRightInverse (f : E ‚âÉL[R] F) :
    f.hasRightInverse.rightInverse = f.symm := by
  ext y
  exact f.injective <| by simpa using f.hasRightInverse.rightInverse_rightInverse y

/-- An invertible continuous linear map splits. -/
lemma of_isInvertible (hf : IsInvertible f) : f.HasRightInverse := by
  obtain ‚ü®e, rfl‚ü© := hf
  exact e.hasRightInverse

/-- If `f` and `g` split, then so does `f √ó g`. -/
lemma prodMap {g : E' ‚ÜíL[R] F'} (hf : f.HasRightInverse) (hg : g.HasRightInverse) :
    (f.prodMap g).HasRightInverse := by
  obtain ‚ü®finv, hfinv‚ü© := hf
  obtain ‚ü®ginv, hginv‚ü© := hg
  use finv.prodMap ginv
  simp [hfinv, hginv]

variable [TopologicalSpace G] [AddCommMonoid G] [Module R G]

lemma comp {g : F ‚ÜíL[R] G} (hg : g.HasRightInverse) (hf : f.HasRightInverse) :
    (g.comp f).HasRightInverse := by
  obtain ‚ü®finv, hfinv‚ü© := hf
  obtain ‚ü®ginv, hginv‚ü© := hg
  refine ‚ü®finv.comp ginv, fun x ‚Ü¶ ?_‚ü©
  simp only [coe_comp', Function.comp_apply]
  rw [hfinv, hginv]

lemma of_comp {g : F ‚ÜíL[R] G} (hfg : (g.comp f).HasRightInverse) :
    g.HasRightInverse := by
  obtain ‚ü®fginv, hfginv‚ü© := hfg
  exact ‚ü®f.comp fginv, fun y ‚Ü¶ by simpa using hfginv y‚ü©

lemma comp_continuousLinearEquivalence {f‚ÇÄ : F' ‚âÉL[R] E} (hf : f.HasRightInverse) :
    (f.comp f‚ÇÄ.toContinuousLinearMap).HasRightInverse :=
  hf.comp f‚ÇÄ.hasRightInverse

lemma continuousLinearEquivalence_comp {g : F ‚âÉL[R] F'} (hf : f.HasRightInverse) :
    (g.toContinuousLinearMap.comp f).HasRightInverse :=
  g.hasRightInverse.comp hf

/-- `ContinuousLinearMap.fst` has a continuous right inverse. -/
protected lemma fst : (ContinuousLinearMap.fst R F G).HasRightInverse := by
  use (ContinuousLinearMap.id _ _).prod 0
  intro x
  simp

/-- `ContinuousLinearMap.snd` has a continuous right inverse. -/
protected lemma snd : (ContinuousLinearMap.snd R F G).HasRightInverse := by
  use ContinuousLinearMap.prod 0 (.id R G)
  intro x
  simp

section NontriviallyNormedField

variable {ùïú : Type*} [NontriviallyNormedField ùïú] {E F : Type*}
  [TopologicalSpace E] [AddCommGroup E] [Module ùïú E] [IsTopologicalAddGroup E] [ContinuousSMul ùïú E]
  [TopologicalSpace F] [AddCommGroup F] [Module ùïú F] [IsTopologicalAddGroup F] [ContinuousSMul ùïú F]
  [T2Space F] {f : E ‚ÜíL[ùïú] F}

/-- If `f : E ‚Üí F` is surjective and `F` is finite-dimensional,
`f` has a continuous right inverse. -/
lemma of_surjective_of_finiteDimensional [CompleteSpace ùïú] [FiniteDimensional ùïú F]
    (hf : Surjective f) :
    f.HasRightInverse := by
  -- A surjective linear map has a linear inverse, which is automatically continuous
  -- because its domain is finite-dimensional.
  obtain ‚ü®g, hg‚ü© :=
    f.toLinearMap.exists_rightInverse_of_surjective (f.range_eq_top_of_surjective hf)
  exact ‚ü®‚ü®g, g.continuous_of_finiteDimensional‚ü©, fun x ‚Ü¶ congr($hg x)‚ü©

end NontriviallyNormedField

end HasRightInverse

end ContinuousLinearMap

end
