import Mathlib.Analysis.NormedSpace.Star.ContinuousFunctionalCalculus
import Mathlib.Analysis.NormedSpace.Star.CFC.CFCv2

noncomputable section

section prereqs

variable {A : Type*} [NormedRing A] [StarRing A] [CstarRing A] [CompleteSpace A]
variable [NormedAlgebra â„‚ A] [StarModule â„‚ A]
variable {B : Type*} [NormedRing B] [StarRing B] [CstarRing B] [CompleteSpace B]
variable [NormedAlgebra â„‚ B] [StarModule â„‚ B]

lemma StarAlgEquiv.nnnorm_map (Ï† : A â‰ƒâ‹†â‚[â„‚] B) (a : A) : â€–Ï† aâ€–â‚Š = â€–aâ€–â‚Š := by
  have : spectralRadius â„‚ (Ï† (star a * a)) = spectralRadius â„‚ (star a * a) := by
    rw [spectralRadius, spectralRadius]
    congr!
    exact AlgEquiv.spectrum_eq Ï† (star a * a)
  iterate 2 rw [IsSelfAdjoint.spectralRadius_eq_nnnorm] at this
  Â· norm_cast at this
    simpa [CstarRing.nnnorm_star_mul_self, map_star, â†sq]
  Â· exact IsSelfAdjoint.star_mul_self a
  Â· simpa only [map_mul, map_star] using IsSelfAdjoint.star_mul_self (Ï† a)

lemma StarAlgEquiv.norm_map (Ï† : A â‰ƒâ‹†â‚[â„‚] B) (a : A) : â€–Ï† aâ€– = â€–aâ€– :=
  congr_arg NNReal.toReal (Ï†.nnnorm_map a)

lemma StarAlgEquiv.isometry (Ï† : A â‰ƒâ‹†â‚[â„‚] B) : Isometry Ï† :=
  AddMonoidHomClass.isometry_of_norm Ï† Ï†.norm_map

end prereqs

section Normal

variable {A : Type*} [NormedRing A] [StarRing A] [CstarRing A] [CompleteSpace A]
variable [NormedAlgebra â„‚ A] [StarModule â„‚ A]

-- yes, we have all the necessary assumptions
example (a : A) [IsStarNormal a] : C(spectrum â„‚ a, â„‚) â†’â‹†â‚[â„‚] elementalStarAlgebra â„‚ a :=
  continuousFunctionalCalculus a

-- we want this instance
instance {ğ•œ A : Type*} [NormedField ğ•œ] [NormedRing A] [CompleteSpace A]
    [NormedAlgebra ğ•œ A] [ProperSpace ğ•œ] (a : A) : CompactSpace (spectrum ğ•œ a) :=
  isCompact_iff_compactSpace.mp <| spectrum.isCompact a

instance : CFC â„‚ (IsStarNormal : A â†’ Prop) where
  toStarAlgHom {a} ha := (elementalStarAlgebra â„‚ a).subtype.comp <| continuousFunctionalCalculus a
  hom_closedEmbedding {a} ha :=
    isometry_subtype_coe.comp (continuousFunctionalCalculus a).isometry |>.closedEmbedding
  hom_id {a} ha := congr_arg Subtype.val <| continuousFunctionalCalculus_map_id a
  hom_map_spectrum {a} ha f := by
    simp only [StarAlgHom.comp_apply, StarAlgHom.coe_coe, StarSubalgebra.coe_subtype]
    rw [â† StarSubalgebra.spectrum_eq (elementalStarAlgebra.isClosed â„‚ a),
      AlgEquiv.spectrum_eq (continuousFunctionalCalculus a), ContinuousMap.spectrum_eq_range]
  predicate_hom {a} ha f := âŸ¨by rw [â† map_star]; exact Commute.all (star f) f |>.map _âŸ©

lemma IsSelfAdjoint.spectrumRestricts {a : A} (ha : IsSelfAdjoint a) :
    SpectrumRestricts a Complex.reCLM where
  rightInvOn _x hx := ha.mem_spectrum_eq_re hx |>.symm
  left_inv := Complex.ofReal_re

/-- An element in a Câ‹†-algebra is selfadjoint if and only if it is normal and its spectrum is
contained in `â„`. -/
lemma isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts {a : A} :
    IsSelfAdjoint a â†” IsStarNormal a âˆ§ SpectrumRestricts a Complex.reCLM := by
  refine âŸ¨fun ha â†¦ âŸ¨ha.isStarNormal, ha.spectrumRestrictsâŸ©, ?_âŸ©
  rintro âŸ¨haâ‚, haâ‚‚âŸ©
  classical
  rw [isSelfAdjoint_iff]
  nth_rw 2 [â† cfcBare_id haâ‚ (R := â„‚)]
  rw [â† cfcBare_star haâ‚ (R := â„‚)]
  refine cfcBare_congr fun x hx â†¦ ?_
  obtain âŸ¨x, -, rflâŸ© := haâ‚‚.algebraMap_image.symm â–¸ hx
  exact Complex.conj_ofReal _

instance : CFC â„ (IsSelfAdjoint : A â†’ Prop) :=
  cfc_of_spectrumRestricts (q := IsStarNormal) (p := IsSelfAdjoint) Complex.reCLM
    Complex.isometry_ofReal (fun _ â†¦ isSelfAdjoint_iff_isStarNormal_and_spectrumRestricts)
    (fun _ _ â†¦ inferInstance)

end Normal

section PrePositive


open NNReal ENNReal

def ContinuousMap.toNNReal : C(â„, â„â‰¥0) := .mk Real.toNNReal continuous_real_toNNReal

@[simp]
lemma ContinuousMap.coe_toNNReal : â‡‘ContinuousMap.toNNReal = Real.toNNReal := rfl

-- MOVE ME
lemma spectrumRestricts_nnreal_iff {A : Type*} [Ring A] [Algebra â„ A] {a : A} :
    SpectrumRestricts a ContinuousMap.toNNReal â†” âˆ€ x âˆˆ spectrum â„ a, 0 â‰¤ x := by
  refine âŸ¨fun h x hx â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· obtain âŸ¨x, -, rflâŸ© := h.algebraMap_image.symm â–¸ hx
    exact coe_nonneg x
  Â· exact spectrumRestricts_of_subset_range_algebraMap _ _ (fun _ â†¦ Real.toNNReal_coe)
      fun x hx â†¦ âŸ¨âŸ¨x, h x hxâŸ©, rflâŸ©

-- MOVE ME
lemma spectrumRestricts_real_iff {A : Type*} [Ring A] [Algebra â„‚ A] {a : A} :
    SpectrumRestricts a Complex.reCLM â†” âˆ€ x âˆˆ spectrum â„‚ a, x = x.re := by
  refine âŸ¨fun h x hx â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· obtain âŸ¨x, -, rflâŸ© := h.algebraMap_image.symm â–¸ hx
    simp
  Â· exact spectrumRestricts_of_subset_range_algebraMap _ _ Complex.ofReal_re
      fun x hx â†¦ âŸ¨x.re, (h x hx).symmâŸ©

-- MOVE ME
lemma spectrumRestricts_nnreal_iff_spectralRadius_le {A : Type*} [Ring A] [Algebra â„ A]
    {a : A} {t : â„â‰¥0} (ht : spectralRadius â„ a â‰¤ t) :
    SpectrumRestricts a ContinuousMap.toNNReal â†” spectralRadius â„ (algebraMap â„ A t - a) â‰¤ t := by
  have : spectrum â„ a âŠ† Set.Icc (-t) t := by
    intro x hx
    rw [Set.mem_Icc, â† abs_le, â† Real.norm_eq_abs, â† coe_nnnorm, NNReal.coe_le_coe,
      â† ENNReal.coe_le_coe]
    exact le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) x hx |>.trans ht
  rw [spectrumRestricts_nnreal_iff]
  refine âŸ¨fun h â†¦ iSupâ‚‚_le fun x hx â†¦ ?_, fun h â†¦ ?_âŸ©
  Â· rw [â† spectrum.singleton_sub_eq] at hx
    obtain âŸ¨y, hy, rflâŸ© : âˆƒ y âˆˆ spectrum â„ a, â†‘t - y = x := by simpa using hx
    obtain âŸ¨hty, hytâŸ© := Set.mem_Icc.mp <| this hy
    lift y to â„â‰¥0 using h y hy
    rw [â† NNReal.coe_sub (by exact_mod_cast hyt)]
    simp
  Â· replace h : âˆ€ x âˆˆ spectrum â„ a, â€–t - xâ€–â‚Š â‰¤ t := by
      simpa [spectralRadius, iSupâ‚‚_le_iff, â† spectrum.singleton_sub_eq] using h
    peel h with x hx h_le
    rw [â† NNReal.coe_le_coe, coe_nnnorm, Real.norm_eq_abs, abs_le] at h_le
    linarith [h_le.2]

-- MOVE ME
@[to_additive]
theorem Isometry.nnnorm_map_of_map_one {E F : Type*} [SeminormedGroup E] [SeminormedGroup F]
    {f : E â†’ F} (hi : Isometry f) (hâ‚ : f 1 = 1) (x : E) :
    â€–f xâ€–â‚Š = â€–xâ€–â‚Š :=
  Subtype.ext <| hi.norm_map_of_map_one hâ‚ x

-- MOVE ME
instance {E : Type*} [NormedAddCommGroup E] [NormedSpace â„‚ E] : NormedSpace â„ E :=
  inferInstance

-- MOVE ME
instance {A : Type*} [SeminormedRing A] [NormedAlgebra â„‚ A] : NormedAlgebra â„ A where
  norm_smul_le r a := by simpa using norm_smul_le (r : â„‚) a

-- MOVE ME
lemma SpectrumRestricts.spectralRadius_eq {ğ•œâ‚ ğ•œâ‚‚ A : Type*} [NormedField ğ•œâ‚] [NormedField ğ•œâ‚‚]
    [NormedRing A] [NormedAlgebra ğ•œâ‚ A] [NormedAlgebra ğ•œâ‚‚ A] [Algebra ğ•œâ‚ ğ•œâ‚‚] [IsScalarTower ğ•œâ‚ ğ•œâ‚‚ A]
    {f : ğ•œâ‚‚ â†’ ğ•œâ‚} (h_isom : Isometry (algebraMap ğ•œâ‚ ğ•œâ‚‚)) {a : A} (h : SpectrumRestricts a f) :
    spectralRadius ğ•œâ‚ a = spectralRadius ğ•œâ‚‚ a := by
  rw [spectralRadius, spectralRadius]
  apply le_antisymm
  all_goals apply iSupâ‚‚_le fun x hx â†¦ ?_
  Â· have := h_isom.nnnorm_map_of_map_zero (map_zero _) x
    refine (congr_arg ((â†‘) : â„â‰¥0 â†’ â„â‰¥0âˆ) this).symm.trans_le <| le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) _ ?_
    exact (spectrum.algebraMap_mem_iff _ _).mpr hx
  Â· have âŸ¨y, hy, hy'âŸ© := h.algebraMap_image.symm â–¸ hx
    subst hy'
    rw [h_isom.nnnorm_map_of_map_zero (map_zero _)]
    exact le_iSupâ‚‚ (Î± := â„â‰¥0âˆ) y hy

-- MOVE ME
protected lemma IsSelfAdjoint.algebraMap {R : Type*} (A : Type*) [CommSemiring R] [Semiring A]
    [Algebra R A] [StarRing R] [StarMul A] [StarModule R A] {r : R} (hr : IsSelfAdjoint r) :
    IsSelfAdjoint (algebraMap R A r) := by
  rw [isSelfAdjoint_iff, â† algebraMap_star_comm]
  exact congr(algebraMap R A $(hr.star_eq))

variable {A : Type*} [NormedRing A] [StarRing A] [CstarRing A] [CompleteSpace A]
variable [NormedAlgebra â„‚ A] [StarModule â„‚ A]

lemma spectrumRestricts_nnreal_iff_nnnorm {a : A} {t : â„â‰¥0} (ha : IsSelfAdjoint a)
    (ht : â€–aâ€–â‚Š â‰¤ t) : SpectrumRestricts a ContinuousMap.toNNReal â†” â€–algebraMap â„ A t - aâ€–â‚Š â‰¤ t := by
  have : IsSelfAdjoint (algebraMap â„ A t - a) := IsSelfAdjoint.algebraMap A (.all (t : â„)) |>.sub ha
  rw [â† ENNReal.coe_le_coe, â† IsSelfAdjoint.spectralRadius_eq_nnnorm,
    â† SpectrumRestricts.spectralRadius_eq (f := Complex.reCLM) (algebraMap_isometry â„ â„‚)] at ht âŠ¢
  exact spectrumRestricts_nnreal_iff_spectralRadius_le ht
  all_goals
    try apply IsSelfAdjoint.spectrumRestricts
    assumption

lemma SpectrumRestricts.nnreal_add {a b : A} (haâ‚ : IsSelfAdjoint a)
    (hbâ‚ : IsSelfAdjoint b) (haâ‚‚ : SpectrumRestricts a ContinuousMap.toNNReal)
    (hbâ‚‚ : SpectrumRestricts b ContinuousMap.toNNReal) :
    SpectrumRestricts (a + b) ContinuousMap.toNNReal := by
  rw [spectrumRestricts_nnreal_iff_nnnorm (haâ‚.add hbâ‚) (nnnorm_add_le a b), NNReal.coe_add,
    map_add, add_sub_add_comm]
  refine nnnorm_add_le _ _ |>.trans ?_
  gcongr
  all_goals rw [â† spectrumRestricts_nnreal_iff_nnnorm]
  all_goals first | rfl | assumption

lemma IsSelfAdjoint.sq_spectrumRestricts {a : A} (ha : IsSelfAdjoint a) :
    SpectrumRestricts (a ^ 2) ContinuousMap.toNNReal := by
  classical
  rw [spectrumRestricts_nnreal_iff, â† cfc_id ha (R := â„), â† map_pow, cfc_map_spectrum ha]
  rintro - âŸ¨x, -, rflâŸ©
  exact sq_nonneg x

open ComplexStarModule

-- MOVE ME
lemma star_mul_self_add_self_mul_star {A : Type*} [Ring A] [StarRing A]
    [Algebra â„‚ A] [StarModule â„‚ A] (a : A) :
    star a * a + a * star a = 2 â€¢ ((â„œ a) ^ 2 + (â„‘ a) ^ 2) :=
  have a_eq := (realPart_add_I_smul_imaginaryPart a).symm
  calc
    star a * a + a * star a = _ :=
      congr((star $(a_eq)) * $(a_eq) + $(a_eq) * (star $(a_eq)))
    _ = 2 â€¢ ((â„œ a) ^ 2 + (â„‘ a) ^ 2) := by
      simp [mul_add, add_mul, smul_smul, two_smul, sq]
      abel

lemma SpectrumRestricts.eq_zero_of_neg {a : A} (ha : IsSelfAdjoint a)
    (haâ‚ : SpectrumRestricts a ContinuousMap.toNNReal) (haâ‚‚ : SpectrumRestricts (-a) ContinuousMap.toNNReal) :
    a = 0 := by
  nontriviality A
  rw [spectrumRestricts_nnreal_iff] at haâ‚ haâ‚‚
  classical
  apply eq_zero_of_spectrum_eq_zero (R := â„) ha
  refine Set.eq_singleton_iff_nonempty_unique_mem.mpr âŸ¨?_, ?_âŸ©
  Â· exact ha.spectrumRestricts.image.symm â–¸ (spectrum.nonempty a).image _
  Â· simp only [â† spectrum.neg_eq, Set.mem_neg] at haâ‚‚
    peel haâ‚ with x hx _
    linarith [haâ‚‚ (-x) ((neg_neg x).symm â–¸ hx)]

-- Move Me
lemma SpectrumRestricts.of_spectrum_eq  {R S A : Type*} [CommSemiring R] [CommSemiring S]
    [Ring A] [Algebra S A] [Algebra R A] [Algebra R S] [IsScalarTower R S A] {a b : A} {f : S â†’ R}
    (ha : SpectrumRestricts a f) (h : spectrum S a = spectrum S b) :
    SpectrumRestricts b f where
  rightInvOn := h â–¸ ha.rightInvOn
  left_inv := ha.left_inv

lemma SpectrumRestricts.smul_of_nonneg {A : Type*} [Ring A] [Algebra â„ A] {a : A}
    (ha : SpectrumRestricts a ContinuousMap.toNNReal) {r : â„} (hr : 0 â‰¤ r) :
    SpectrumRestricts (r â€¢ a) ContinuousMap.toNNReal := by
  rw [spectrumRestricts_nnreal_iff] at ha âŠ¢
  nontriviality A
  intro x hx
  by_cases hr' : r = 0
  Â· simp [hr'] at hx âŠ¢
    exact hx.symm.le
  Â· lift r to â„Ë£ using IsUnit.mk0 r hr'
    rw [â† Units.smul_def, spectrum.unit_smul_eq_smul, Set.mem_smul_set_iff_inv_smul_mem] at hx
    refine le_of_smul_le_smul_left ?_ (inv_pos.mpr <| lt_of_le_of_ne hr <| ne_comm.mpr hr')
    simpa [Units.smul_def] using ha _ hx

lemma spectrum_star_mul_self_nonneg {b : A} : âˆ€ x âˆˆ spectrum â„ (star b * b), 0 â‰¤ x := by
  set a := star b * b
  classical
  let a_neg : A := cfc a (- ContinuousMap.id â„ âŠ” 0)
  set c := b * a_neg
  have h_eq_a_neg : - (star c * c) = a_neg ^ 3 := by
    simp (config := { zeta := false }) only [c, a_neg, star_mul]
    rw [â† mul_assoc, mul_assoc _ _ b, â† map_star, â† cfc_id (IsSelfAdjoint.star_mul_self b) (R := â„),
      â† map_mul, â† map_mul, â† map_pow, â† map_neg]
    congr
    ext x
    by_cases hx : x â‰¤ 0
    Â· rw [â† neg_nonneg] at hx
      simp [sup_eq_left.mpr hx, pow_succ']
    Â· rw [not_le, â† neg_neg_iff_pos] at hx
      simp [sup_eq_right.mpr hx.le]
  have h_c_specâ‚€ : SpectrumRestricts (- (star c * c)) ContinuousMap.toNNReal := by
    simp only [spectrumRestricts_nnreal_iff, h_eq_a_neg, â† map_pow,
      cfc_map_spectrum (IsSelfAdjoint.star_mul_self b)]
    rintro - âŸ¨x, -, rflâŸ©
    simp
  have c_eq := star_mul_self_add_self_mul_star c
  rw [â† eq_sub_iff_add_eq', sub_eq_add_neg] at c_eq
  have h_c_specâ‚ : SpectrumRestricts (c * star c) ContinuousMap.toNNReal := by
    rw [c_eq]
    refine SpectrumRestricts.nnreal_add ?_ ?_ ?_ h_c_specâ‚€
    Â· exact IsSelfAdjoint.smul (by rfl) <| ((â„œ c).prop.pow 2).add ((â„‘ c).prop.pow 2)
    Â· exact (IsSelfAdjoint.star_mul_self c).neg
    Â· rw [nsmul_eq_smul_cast â„]
      refine (â„œ c).2.sq_spectrumRestricts.nnreal_add ((â„œ c).2.pow 2) ((â„‘ c).2.pow 2)
        (â„‘ c).2.sq_spectrumRestricts |>.smul_of_nonneg <| by norm_num
  have h_c_specâ‚‚ : SpectrumRestricts (star c * c) ContinuousMap.toNNReal := by
    rw [spectrumRestricts_nnreal_iff] at h_c_specâ‚ âŠ¢
    intro x hx
    replace hx := Set.subset_diff_union _ {(0 : â„)} hx
    rw [spectrum.nonzero_mul_eq_swap_mul, Set.diff_union_self, Set.union_singleton,
      Set.mem_insert_iff] at hx
    obtain (rfl | hx) := hx
    exacts [le_rfl, h_c_specâ‚ x hx]
  have bar := h_c_specâ‚‚.eq_zero_of_neg (.star_mul_self c) h_c_specâ‚€
  rw [bar, neg_zero] at h_eq_a_neg
  simp (config := {zeta := false}) only [a_neg, â† map_pow, â† map_zero (cfc a (R := â„))] at h_eq_a_neg
  have baz := cfc_eqOn_of_eq (IsSelfAdjoint.star_mul_self b) h_eq_a_neg
  intro x hx
  specialize baz hx
  by_contra! hx'
  rw [â† neg_pos] at hx'
  simp [sup_eq_left.mpr hx'.le] at baz
  exact (pow_pos hx' 3).ne baz


end PrePositive


variable {A : Type*} [NormedRing A] [CompleteSpace A]
variable [PartialOrder A] [StarOrderedRing A] [CstarRing A]
variable [NormedAlgebra â„‚ A] [StarModule â„‚ A]

lemma nonneg_iff_isSelfAdjoint_and_spectrumRestricts {a : A} :
    0 â‰¤ a â†” IsSelfAdjoint a âˆ§ SpectrumRestricts a ContinuousMap.toNNReal := by
  refine âŸ¨fun ha â†¦ ?_, ?_âŸ©
  Â· rw [StarOrderedRing.nonneg_iff] at ha
    induction ha using AddSubmonoid.closure_induction' with
    | Hs x hx =>
      obtain âŸ¨b, rflâŸ© := hx
      simp only
      refine âŸ¨IsSelfAdjoint.star_mul_self b, ?_âŸ©
      rw [spectrumRestricts_nnreal_iff]
      exact spectrum_star_mul_self_nonneg
    | H1 =>
      rw [spectrumRestricts_nnreal_iff]
      nontriviality A
      simp
    | Hmul x _ y _ hx hy =>
      exact âŸ¨hx.1.add hy.1, hx.2.nnreal_add hx.1 hy.1 hy.2âŸ©
  Â· rintro âŸ¨haâ‚, haâ‚‚âŸ©
    classical
    let s := cfc a (.mk Real.sqrt Real.continuous_sqrt)
    have : a = star s * s := by
      rw [â† cfc_id haâ‚ (R := â„)]
      simp only [â† map_star, â† map_mul]
      apply cfc_congr haâ‚
      rw [spectrumRestricts_nnreal_iff] at haâ‚‚
      peel haâ‚‚ with x hx _
      simp [Real.mul_self_sqrt this]
    exact this â–¸ star_mul_self_nonneg s

open NNReal
instance : CFC â„â‰¥0 (fun x : A â†¦ 0 â‰¤ x) :=
  cfc_of_spectrumRestricts (q := IsSelfAdjoint) ContinuousMap.toNNReal
    isometry_subtype_coe (fun _ â†¦ nonneg_iff_isSelfAdjoint_and_spectrumRestricts)
    (fun _ _ â†¦ inferInstance)

end
