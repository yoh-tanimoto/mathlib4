/-
Copyright (c) 2019 Jean Lo. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jean Lo, Bhavik Mehta, Ya√´l Dillies
-/
import Mathlib.Analysis.Convex.Basic
import Mathlib.Analysis.Convex.Hull
import Mathlib.Analysis.NormedSpace.Basic

#align_import analysis.locally_convex.basic from "leanprover-community/mathlib"@"f2ce6086713c78a7f880485f7917ea547a215982"

/-!
# Local convexity

This file defines absorbent and balanced sets.

An absorbent set is one that "surrounds" the origin. The idea is made precise by requiring that any
point belongs to all large enough scalings of the set. This is the vector world analog of a
topological neighborhood of the origin.

A balanced set is one that is everywhere around the origin. This means that `a ‚Ä¢ s ‚äÜ s` for all `a`
of norm less than `1`.

## Main declarations

For a module over a normed ring:
* `Absorbs`: A set `s` absorbs a set `t` if all large scalings of `s` contain `t`.
* `Absorbent`: A set `s` is absorbent if every point eventually belongs to all large scalings of
  `s`.
* `Balanced`: A set `s` is balanced if `a ‚Ä¢ s ‚äÜ s` for all `a` of norm less than `1`.

## References

* [H. H. Schaefer, *Topological Vector Spaces*][schaefer1966]

## Tags

absorbent, balanced, locally convex, LCTVS
-/


open Set Filter

open scoped Pointwise Topology

variable {ùïú ùïù E : Type*} {Œπ : Sort*} {Œ∫ : Œπ ‚Üí Sort*}

section SeminormedRing

variable [SeminormedRing ùïú]

section SMul

variable (ùïú) [SMul ùïú E]

/-- A set `A` absorbs another set `B` if `B` is contained in all scalings of `A` by elements of
sufficiently large norm. -/
def Absorbs (A B : Set E) :=
  ‚àÉ r, ‚àÄ a : ùïú, r ‚â§ ‚Äña‚Äñ ‚Üí B ‚äÜ a ‚Ä¢ A
#align absorbs Absorbs

variable {ùïú} {s t u v A B : Set E}

lemma Absorbs.exists_pos (h : Absorbs ùïú A B) : ‚àÉ r > 0, ‚àÄ a : ùïú, r ‚â§ ‚Äña‚Äñ ‚Üí B ‚äÜ a ‚Ä¢ A :=
  let ‚ü®r, hr‚ü© := h
  ‚ü®max r 1, by simp, fun a ha ‚Ü¶ hr a ((le_max_left _ _).trans ha)‚ü©

lemma absorbs_iff_cobounded : Absorbs ùïú A B ‚Üî ‚àÄ·∂† a in Bornology.cobounded ùïú, B ‚äÜ a ‚Ä¢ A := by
  simp only [Absorbs, ‚Üê comap_norm_atTop, (atTop_basis.comap _).eventually_iff, true_and]
  rfl

alias ‚ü®Absorbs.eventually_cobounded, Absorbs.of_eventually_cobounded‚ü© := absorbs_iff_cobounded

@[simp]
theorem Absorbs.empty {s : Set E} : Absorbs ùïú s (‚àÖ : Set E) :=
  ‚ü®1, fun _a _ha => Set.empty_subset _‚ü©
#align absorbs_empty Absorbs.empty

theorem Absorbs.mono (hs : Absorbs ùïú s u) (hst : s ‚äÜ t) (hvu : v ‚äÜ u) : Absorbs ùïú t v :=
  let ‚ü®r, h‚ü© := hs
  ‚ü®r, fun _a ha => hvu.trans <| (h _ ha).trans <| smul_set_mono hst‚ü©
#align absorbs.mono Absorbs.mono

theorem Absorbs.mono_left (hs : Absorbs ùïú s u) (h : s ‚äÜ t) : Absorbs ùïú t u :=
  hs.mono h Subset.rfl
#align absorbs.mono_left Absorbs.mono_left

theorem Absorbs.mono_right (hs : Absorbs ùïú s u) (h : v ‚äÜ u) : Absorbs ùïú s v :=
  hs.mono Subset.rfl h
#align absorbs.mono_right Absorbs.mono_right

@[simp]
theorem absorbs_union : Absorbs ùïú s (u ‚à™ v) ‚Üî Absorbs ùïú s u ‚àß Absorbs ùïú s v := by
  simp only [absorbs_iff_cobounded, union_subset_iff, Filter.eventually_and]
#align absorbs_union absorbs_union

theorem Absorbs.union (hu : Absorbs ùïú s u) (hv : Absorbs ùïú s v) : Absorbs ùïú s (u ‚à™ v) :=
  absorbs_union.2 ‚ü®hu, hv‚ü©
#align absorbs.union Absorbs.union

theorem Set.Finite.absorbs_iUnion {Œπ : Type*} {s : Set E} {t : Set Œπ} {f : Œπ ‚Üí Set E}
    (hi : t.Finite) : Absorbs ùïú s (‚ãÉ i ‚àà t, f i) ‚Üî ‚àÄ i ‚àà t, Absorbs ùïú s (f i) := by
  simp only [absorbs_iff_cobounded, iUnion_subset_iff, eventually_all_finite hi]
#align set.finite.absorbs_Union Set.Finite.absorbs_iUnion

theorem absorbs_iUnion_finset {Œπ : Type*} {t : Finset Œπ} {f : Œπ ‚Üí Set E} :
    Absorbs ùïú s (‚ãÉ i ‚àà t, f i) ‚Üî ‚àÄ i ‚àà t, Absorbs ùïú s (f i) :=
  t.finite_toSet.absorbs_iUnion
#align absorbs_Union_finset absorbs_iUnion_finset

variable (ùïú)

/-- A set is absorbent if it absorbs every singleton. -/
def Absorbent (A : Set E) :=
  ‚àÄ x, ‚àÉ r, ‚àÄ a : ùïú, r ‚â§ ‚Äña‚Äñ ‚Üí x ‚àà a ‚Ä¢ A
#align absorbent Absorbent

variable {ùïú}

theorem absorbent_iff_forall_absorbs_singleton : Absorbent ùïú A ‚Üî ‚àÄ x, Absorbs ùïú A {x} := by
  simp_rw [Absorbs, Absorbent, singleton_subset_iff]
#align absorbent_iff_forall_absorbs_singleton absorbent_iff_forall_absorbs_singleton

theorem Absorbent.subset (hA : Absorbent ùïú A) (hAB : A ‚äÜ B) : Absorbent ùïú B := by
  rw [absorbent_iff_forall_absorbs_singleton] at *
  exact fun x ‚Ü¶ (hA x).mono_left hAB
#align absorbent.subset Absorbent.subset

theorem Absorbent.absorbs (hs : Absorbent ùïú s) {x : E} : Absorbs ùïú s {x} :=
  absorbent_iff_forall_absorbs_singleton.1 hs _
#align absorbent.absorbs Absorbent.absorbs

#noalign absorbent_iff_nonneg_lt

theorem Absorbent.absorbs_finite {s : Set E} (hs : Absorbent ùïú s) {v : Set E} (hv : v.Finite) :
    Absorbs ùïú s v := by
  rw [‚Üê Set.biUnion_of_singleton v]
  exact hv.absorbs_iUnion.mpr fun _ _ => hs.absorbs
#align absorbent.absorbs_finite Absorbent.absorbs_finite

variable (ùïú)

/-- A set `A` is balanced if `a ‚Ä¢ A` is contained in `A` whenever `a` has norm at most `1`. -/
def Balanced (A : Set E) :=
  ‚àÄ a : ùïú, ‚Äña‚Äñ ‚â§ 1 ‚Üí a ‚Ä¢ A ‚äÜ A
#align balanced Balanced

variable {ùïú}

theorem balanced_iff_smul_mem : Balanced ùïú s ‚Üî ‚àÄ ‚¶Éa : ùïú‚¶Ñ, ‚Äña‚Äñ ‚â§ 1 ‚Üí ‚àÄ ‚¶Éx : E‚¶Ñ, x ‚àà s ‚Üí a ‚Ä¢ x ‚àà s :=
  forall‚ÇÇ_congr fun _a _ha => smul_set_subset_iff
#align balanced_iff_smul_mem balanced_iff_smul_mem

alias ‚ü®Balanced.smul_mem, _‚ü© := balanced_iff_smul_mem
#align balanced.smul_mem Balanced.smul_mem

@[simp]
theorem balanced_empty : Balanced ùïú (‚àÖ : Set E) := fun _ _ => by rw [smul_set_empty]
#align balanced_empty balanced_empty

@[simp]
theorem balanced_univ : Balanced ùïú (univ : Set E) := fun _a _ha => subset_univ _
#align balanced_univ balanced_univ

theorem Balanced.union (hA : Balanced ùïú A) (hB : Balanced ùïú B) : Balanced ùïú (A ‚à™ B) := fun _a ha =>
  smul_set_union.subset.trans <| union_subset_union (hA _ ha) <| hB _ ha
#align balanced.union Balanced.union

theorem Balanced.inter (hA : Balanced ùïú A) (hB : Balanced ùïú B) : Balanced ùïú (A ‚à© B) := fun _a ha =>
  smul_set_inter_subset.trans <| inter_subset_inter (hA _ ha) <| hB _ ha
#align balanced.inter Balanced.inter

theorem balanced_iUnion {f : Œπ ‚Üí Set E} (h : ‚àÄ i, Balanced ùïú (f i)) : Balanced ùïú (‚ãÉ i, f i) :=
  fun _a ha => (smul_set_iUnion _ _).subset.trans <| iUnion_mono fun _ => h _ _ ha
#align balanced_Union balanced_iUnion

theorem balanced_iUnion‚ÇÇ {f : ‚àÄ i, Œ∫ i ‚Üí Set E} (h : ‚àÄ i j, Balanced ùïú (f i j)) :
    Balanced ùïú (‚ãÉ (i) (j), f i j) :=
  balanced_iUnion fun _ => balanced_iUnion <| h _
#align balanced_Union‚ÇÇ balanced_iUnion‚ÇÇ

theorem balanced_iInter {f : Œπ ‚Üí Set E} (h : ‚àÄ i, Balanced ùïú (f i)) : Balanced ùïú (‚ãÇ i, f i) :=
  fun _a ha => (smul_set_iInter_subset _ _).trans <| iInter_mono fun _ => h _ _ ha
#align balanced_Inter balanced_iInter

theorem balanced_iInter‚ÇÇ {f : ‚àÄ i, Œ∫ i ‚Üí Set E} (h : ‚àÄ i j, Balanced ùïú (f i j)) :
    Balanced ùïú (‚ãÇ (i) (j), f i j) :=
  balanced_iInter fun _ => balanced_iInter <| h _
#align balanced_Inter‚ÇÇ balanced_iInter‚ÇÇ

variable [SMul ùïù E] [SMulCommClass ùïú ùïù E]

theorem Balanced.smul (a : ùïù) (hs : Balanced ùïú s) : Balanced ùïú (a ‚Ä¢ s) := fun _b hb =>
  (smul_comm _ _ _).subset.trans <| smul_set_mono <| hs _ hb
#align balanced.smul Balanced.smul

end SMul

section Module

variable [AddCommGroup E] [Module ùïú E] {s s‚ÇÅ s‚ÇÇ t t‚ÇÅ t‚ÇÇ : Set E}

@[simp]
lemma absorbs_neg_left : Absorbs ùïú (-s) t ‚Üî Absorbs ùïú s t :=
  exists_congr fun r ‚Ü¶ neg_surjective.forall.trans <| by simp

alias ‚ü®Absorbs.of_neg_left, Absorbs.neg_left‚ü© := absorbs_neg_left

@[simp]
lemma absorbs_neg_right : Absorbs ùïú s (-t) ‚Üî Absorbs ùïú s t :=
  exists_congr fun r ‚Ü¶ neg_surjective.forall.trans <| by simp

alias ‚ü®Absorbs.of_neg_right, Absorbs.neg_right‚ü© := absorbs_neg_right

lemma absorbs_neg_neg : Absorbs ùïú (-s) (-t) ‚Üî Absorbs ùïú s t :=
  absorbs_neg_left.trans absorbs_neg_right

alias ‚ü®Absorbs.of_neg_neg, Absorbs.neg_neg‚ü© := absorbs_neg_neg

#align absorbs.neg Absorbs.neg_neg

lemma balanced_neg : Balanced ùïú (-s) ‚Üî Balanced ùïú s := forall‚ÇÇ_congr fun _ _ ‚Ü¶ by simp

alias ‚ü®Balanced.of_neg, Balanced.neg‚ü© := balanced_neg
#align balanced.neg Balanced.neg

theorem Absorbs.add : Absorbs ùïú s‚ÇÅ t‚ÇÅ ‚Üí Absorbs ùïú s‚ÇÇ t‚ÇÇ ‚Üí Absorbs ùïú (s‚ÇÅ + s‚ÇÇ) (t‚ÇÅ + t‚ÇÇ) :=
  fun ‚ü®r‚ÇÅ, h‚ÇÅ‚ü© ‚ü®r‚ÇÇ, h‚ÇÇ‚ü© =>
  ‚ü®max r‚ÇÅ r‚ÇÇ, fun _a ha =>
    (add_subset_add (h‚ÇÅ _ <| le_of_max_le_left ha) <| h‚ÇÇ _ <| le_of_max_le_right ha).trans
      (smul_add _ _ _).superset‚ü©
#align absorbs.add Absorbs.add

theorem Balanced.add (hs : Balanced ùïú s) (ht : Balanced ùïú t) : Balanced ùïú (s + t) := fun _a ha =>
  (smul_add _ _ _).subset.trans <| add_subset_add (hs _ ha) <| ht _ ha
#align balanced.add Balanced.add

theorem Absorbs.sub (h‚ÇÅ : Absorbs ùïú s‚ÇÅ t‚ÇÅ) (h‚ÇÇ : Absorbs ùïú s‚ÇÇ t‚ÇÇ) :
    Absorbs ùïú (s‚ÇÅ - s‚ÇÇ) (t‚ÇÅ - t‚ÇÇ) := by
  simp_rw [sub_eq_add_neg]
  exact h‚ÇÅ.add h‚ÇÇ.neg_neg
#align absorbs.sub Absorbs.sub

theorem Balanced.sub (hs : Balanced ùïú s) (ht : Balanced ùïú t) : Balanced ùïú (s - t) := by
  simp_rw [sub_eq_add_neg]
  exact hs.add ht.neg
#align balanced.sub Balanced.sub

theorem balanced_zero : Balanced ùïú (0 : Set E) := fun _a _ha => (smul_zero _).subset
#align balanced_zero balanced_zero

lemma Balanced.neg_eq [NormOneClass ùïú] (hs : Balanced ùïú s) : -s = s := by
  apply Subset.antisymm
  ¬∑ simpa using hs (-1) (by simp)
  ¬∑ simpa using hs.neg (-1) (by simp)

theorem Balanced.neg_mem_iff [NormOneClass ùïú] (hs : Balanced ùïú s) {x : E} : -x ‚àà s ‚Üî x ‚àà s := by
  simpa using Set.ext_iff.1 hs.neg_eq x
#align balanced.neg_mem_iff Balanced.neg_mem_iff

end Module

end SeminormedRing

section NormedDivisionRing

variable [NormedDivisionRing ùïú] [AddCommGroup E] [Module ùïú E] {s t A B : Set E} {a b : ùïú} {x : E}

/-- A balanced set absorbs itself. -/
theorem Balanced.absorbs_self (hA : Balanced ùïú A) : Absorbs ùïú A A := by
  refine' ‚ü®1, fun a ha x hx => _‚ü©
  rw [mem_smul_set_iff_inv_smul_mem‚ÇÄ (norm_pos_iff.1 <| zero_lt_one.trans_le ha)]
  refine' hA a‚Åª¬π _ (smul_mem_smul_set hx)
  rw [norm_inv]
  exact inv_le_one ha
#align balanced.absorbs_self Balanced.absorbs_self

theorem Balanced.subset_smul (hA : Balanced ùïú A) (ha : 1 ‚â§ ‚Äña‚Äñ) : A ‚äÜ a ‚Ä¢ A := by
  refine' (subset_set_smul_iff‚ÇÄ _).2 (hA a‚Åª¬π _)
  ¬∑ rintro rfl
    rw [norm_zero] at ha
    exact zero_lt_one.not_le ha
  ¬∑ rw [norm_inv]
    exact inv_le_one ha
#align balanced.subset_smul Balanced.subset_smul

theorem Balanced.smul_eq (hA : Balanced ùïú A) (ha : ‚Äña‚Äñ = 1) : a ‚Ä¢ A = A :=
  (hA _ ha.le).antisymm <| hA.subset_smul ha.ge
#align balanced.smul_eq Balanced.smul_eq

theorem Balanced.mem_smul_iff (hs : Balanced ùïú s) (h : ‚Äña‚Äñ = ‚Äñb‚Äñ) : a ‚Ä¢ x ‚àà s ‚Üî b ‚Ä¢ x ‚àà s := by
  obtain ‚ü®c, hc, rfl‚ü© : ‚àÉ c : ùïú, ‚Äñc‚Äñ = 1 ‚àß a = c * b := by
    obtain rfl | hb := eq_or_ne b 0
    ¬∑ use 1; simp_all
    ¬∑ refine ‚ü®a / b, ?_, (div_mul_cancel _ hb).symm‚ü©
      rw [norm_div, h, div_self (norm_ne_zero_iff.2 hb)]
  rw [mul_smul, ‚Üê mem_inv_smul_set_iff‚ÇÄ, hs.smul_eq]
  ¬∑ simp [hc]
  ¬∑ rintro rfl; simp at hc
#align balanced.mem_smul_iff Balanced.mem_smul_iff

lemma absorbs_iff_nhdsWithin_zero :
    Absorbs ùïú s t ‚Üî ‚àÄ·∂† c : ùïú in ùìù[‚â†] 0, MapsTo (c ‚Ä¢ ¬∑) t s := by
  rw [absorbs_iff_cobounded, ‚Üê inv_nhdsWithin_ne_zero, ‚Üê Filter.map_inv, eventually_map]
  refine eventually_congr <| eventually_mem_nhdsWithin.mono fun c hc ‚Ü¶ ?_
  rw [‚Üê preimage_smul‚ÇÄ hc]; rfl

lemma absorbs_singleton_iff_nhdsWithin_zero : Absorbs ùïú s {x} ‚Üî ‚àÄ·∂† c : ùïú in ùìù[‚â†] 0, c ‚Ä¢ x ‚àà s := by
  simp [absorbs_iff_nhdsWithin_zero]

lemma absorbent_iff_nhdsWithin_zero :
    Absorbent ùïú s ‚Üî ‚àÄ x : E, ‚àÄ·∂† c : ùïú in ùìù[‚â†] 0, c ‚Ä¢ x ‚àà s := by
  simp only [absorbent_iff_forall_absorbs_singleton, absorbs_singleton_iff_nhdsWithin_zero]

lemma absorbent_iff_nhds_zero :
    Absorbent ùïú s ‚Üî ‚àÄ x : E, ‚àÄ·∂† c : ùïú in ùìù 0, c = 0 ‚à® c ‚Ä¢ x ‚àà s := by
  simp [absorbent_iff_nhdsWithin_zero, nhdsWithin, eventually_inf_principal, or_iff_not_imp_left]

lemma Set.Finite.absorbs_sInter {S : Set (Set E)} (hS : S.Finite) {t : Set E} :
    Absorbs ùïú (‚ãÇ‚ÇÄ S) t ‚Üî ‚àÄ s ‚àà S, Absorbs ùïú s t := by
  simp only [absorbs_iff_nhdsWithin_zero, eventually_all_finite hS, mapsTo', subset_sInter_iff]

@[simp]
lemma absorbs_iInter {Œπ : Sort*} [Finite Œπ] {t : Set E} {f : Œπ ‚Üí Set E} :
    Absorbs ùïú (‚ãÇ i, f i) t ‚Üî ‚àÄ i, Absorbs ùïú (f i) t :=
  (finite_range f).absorbs_sInter.trans forall_range_iff

lemma Set.Finite.absorbs_iInter {Œπ : Type*} {t : Set E} {I : Set Œπ} {f : Œπ ‚Üí Set E}
    (hI : I.Finite) : Absorbs ùïú (‚ãÇ i ‚àà I, f i) t ‚Üî ‚àÄ i ‚àà I, Absorbs ùïú (f i) t := by
  rw [‚Üê sInter_image, (hI.image f).absorbs_sInter, ball_image_iff]

@[simp]
lemma absorbs_inter {s‚ÇÅ s‚ÇÇ : Set E} : Absorbs ùïú (s‚ÇÅ ‚à© s‚ÇÇ) t ‚Üî Absorbs ùïú s‚ÇÅ t ‚àß Absorbs ùïú s‚ÇÇ t := by
  rw [‚Üê sInter_pair, (Set.toFinite _).absorbs_sInter]; simp
#align absorbs_inter absorbs_inter

lemma Absorbs.inter {s‚ÇÅ s‚ÇÇ : Set E} (h‚ÇÅ : Absorbs ùïú s‚ÇÅ t) (h‚ÇÇ : Absorbs ùïú s‚ÇÇ t) :
    Absorbs ùïú (s‚ÇÅ ‚à© s‚ÇÇ) t :=
  absorbs_inter.2 ‚ü®h‚ÇÅ, h‚ÇÇ‚ü©
#align absorbs.inter Absorbs.inter

theorem absorbent_univ : Absorbent ùïú (univ : Set E) := by
  refine' fun x => ‚ü®1, fun a ha => _‚ü©
  rw [smul_set_univ‚ÇÄ (norm_pos_iff.1 <| zero_lt_one.trans_le ha)]
  exact trivial
#align absorbent_univ absorbent_univ

section NontriviallyNormed

variable [NeBot (ùìù[‚â†] (0 : ùïú))]

@[simp]
theorem absorbs_zero_iff : Absorbs ùïú s 0 ‚Üî (0 : E) ‚àà s := by
  refine ‚ü®fun h ‚Ü¶ ?_, fun h => ‚ü®1, fun a _ => zero_subset.2 <| zero_mem_smul_set h‚ü©‚ü©
  obtain ‚ü®c, -, hc‚ü© : ‚àÉ c : ùïú, c ‚â† 0 ‚àß c ‚Ä¢ 0 ‚àà s :=
    (eventually_mem_nhdsWithin.and (absorbs_singleton_iff_nhdsWithin_zero.1 h)).exists
  rwa [smul_zero] at hc
#align absorbs_zero_iff absorbs_zero_iff

theorem Absorbent.zero_mem (hs : Absorbent ùïú s) : (0 : E) ‚àà s :=
  absorbs_zero_iff.1 <| absorbent_iff_forall_absorbs_singleton.1 hs _
#align absorbent.zero_mem Absorbent.zero_mem

end NontriviallyNormed

section ConstSMul

variable [TopologicalSpace E] [ContinuousConstSMul ùïú E]

protected theorem Balanced.closure (hA : Balanced ùïú A) : Balanced ùïú (closure A) := fun a ha =>
  (image_closure_subset_closure_image <| continuous_const_smul a).trans <|
    closure_mono <| hA _ ha
#align balanced.closure Balanced.closure

/-- The union of `{0}` with the interior of a balanced set is balanced. -/
theorem Balanced.zero_union_interior (hA : Balanced ùïú A) :
    Balanced ùïú ((0 : Set E) ‚à™ interior A) := by
  intro a ha
  obtain rfl | h := eq_or_ne a 0
  ¬∑ rw [zero_smul_set]
    exacts [subset_union_left _ _, ‚ü®0, Or.inl rfl‚ü©]
  ¬∑ rw [smul_set_union]
    apply union_subset_union
    ¬∑ rw [smul_zero]
    ¬∑ calc
        a ‚Ä¢ interior A ‚äÜ interior (a ‚Ä¢ A) := (isOpenMap_smul‚ÇÄ h).image_interior_subset A
        _ ‚äÜ interior A := interior_mono (hA _ ha)
#align balanced_zero_union_interior Balanced.zero_union_interior

/-- The interior of a balanced set is balanced if it contains the origin. -/
protected theorem Balanced.interior (hA : Balanced ùïú A) (h : (0 : E) ‚àà interior A) :
    Balanced ùïú (interior A) := by
  rw [‚Üê insert_eq_of_mem h]
  exact hA.zero_union_interior
#align balanced.interior Balanced.interior

end ConstSMul

variable [TopologicalSpace E] [ContinuousSMul ùïú E]

/-- Every neighbourhood of the origin is absorbent. -/
theorem absorbent_nhds_zero (hA : A ‚àà ùìù (0 : E)) : Absorbent ùïú A := by
  rw [absorbent_iff_nhds_zero]
  intro x
  have : Tendsto (¬∑ ‚Ä¢ x : ùïú ‚Üí E) (ùìù 0) (ùìù 0) :=
    (continuous_id.smul continuous_const).tendsto' _ _ (zero_smul _ _)
  exact mem_of_superset (this hA) fun _ ‚Ü¶ .inr
#align absorbent_nhds_zero absorbent_nhds_zero

end NormedDivisionRing

section NormedField

variable [NormedDivisionRing ùïú] [NormedRing ùïù] [MulActionWithZero ùïú ùïù] [BoundedSMul ùïú ùïù]
  [AddCommGroup E] [Module ùïú E] [SMulWithZero ùïù E] [IsScalarTower ùïú ùïù E]
  {s t u v A B : Set E} {x : E} {a b : ùïú}

/-- Scalar multiplication (by possibly different types) of a balanced set is monotone. -/
theorem Balanced.smul_mono (hs : Balanced ùïù s) {a : ùïù} {b : ùïú} (h : ‚Äña‚Äñ ‚â§ ‚Äñb‚Äñ) : a ‚Ä¢ s ‚äÜ b ‚Ä¢ s := by
  obtain rfl | hb := eq_or_ne b 0
  ¬∑ obtain rfl : a = 0 := by simpa using h
    obtain rfl | h := s.eq_empty_or_nonempty <;> simp [zero_smul_set, *]
  rintro _ ‚ü®x, hx, rfl‚ü©
  refine' ‚ü®b‚Åª¬π ‚Ä¢ a ‚Ä¢ x, _, smul_inv_smul‚ÇÄ hb _‚ü©
  rw [‚Üê smul_assoc]
  refine' hs _ _ (smul_mem_smul_set hx)
  rw [norm_smul, norm_inv, ‚Üê div_eq_inv_mul]
  exact div_le_one_of_le h (norm_nonneg _)
#align balanced.smul_mono Balanced.smul_mono

end NormedField

section NontriviallyNormedField

variable [NontriviallyNormedField ùïú] [AddCommGroup E] [Module ùïú E] {s : Set E}
variable [Module ‚Ñù E] [SMulCommClass ‚Ñù ùïú E]

protected theorem Balanced.convexHull (hs : Balanced ùïú s) : Balanced ùïú (convexHull ‚Ñù s) := by
  suffices Convex ‚Ñù { x | ‚àÄ a : ùïú, ‚Äña‚Äñ ‚â§ 1 ‚Üí a ‚Ä¢ x ‚àà convexHull ‚Ñù s } by
    rw [balanced_iff_smul_mem] at hs ‚ä¢
    refine' fun a ha x hx => convexHull_min _ this hx a ha
    exact fun y hy a ha => subset_convexHull ‚Ñù s (hs ha hy)
  intro x hx y hy u v hu hv huv a ha
  simp only [smul_add, ‚Üê smul_comm]
  exact convex_convexHull ‚Ñù s (hx a ha) (hy a ha) hu hv huv
#align balanced_convex_hull_of_balanced Balanced.convexHull

end NontriviallyNormedField

section Real

variable [AddCommGroup E] [Module ‚Ñù E] {s : Set E}

theorem balanced_iff_neg_mem (hs : Convex ‚Ñù s) : Balanced ‚Ñù s ‚Üî ‚àÄ ‚¶Éx‚¶Ñ, x ‚àà s ‚Üí -x ‚àà s := by
  refine' ‚ü®fun h x => h.neg_mem_iff.2, fun h a ha => smul_set_subset_iff.2 fun x hx => _‚ü©
  rw [Real.norm_eq_abs, abs_le] at ha
  rw [show a = -((1 - a) / 2) + (a - -1) / 2 by ring, add_smul, neg_smul, ‚Üê smul_neg]
  exact
    hs (h hx) hx (div_nonneg (sub_nonneg_of_le ha.2) zero_le_two)
      (div_nonneg (sub_nonneg_of_le ha.1) zero_le_two) (by ring)
#align balanced_iff_neg_mem balanced_iff_neg_mem

end Real
