/-
Copyright (c) 2019 Jeremy Avigad. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Jeremy Avigad, SÃ©bastien GouÃ«zel, Yury Kudryashov
-/
module

public import Mathlib.Analysis.Calculus.FDeriv.Basic

/-!
# The derivative of a composition (chain rule)

For detailed documentation of the FrÃ©chet derivative,
see the module docstring of `Mathlib/Analysis/Calculus/FDeriv/Basic.lean`.

This file contains the usual formulas (and existence assertions) for the derivative of
composition of functions (the chain rule).
-/

public section


open Filter Asymptotics ContinuousLinearMap Set Metric Topology NNReal ENNReal

noncomputable section

section

variable {ğ•œ : Type*} [NontriviallyNormedField ğ•œ]
variable {E : Type*} [NormedAddCommGroup E] [NormedSpace ğ•œ E]
variable {F : Type*} [NormedAddCommGroup F] [NormedSpace ğ•œ F]
variable {G : Type*} [NormedAddCommGroup G] [NormedSpace ğ•œ G]
variable {G' : Type*} [NormedAddCommGroup G'] [NormedSpace ğ•œ G']
variable {f g : E â†’ F} {f' g' : E â†’L[ğ•œ] F} {x : E} {s : Set E} {L : Filter (E Ã— E)}

section Composition

/-!
### Derivative of the composition of two functions

For composition lemmas, we put `x` explicit to help the elaborator, as otherwise Lean tends to
get confused since there are too many possibilities for composition. -/


variable (x)

theorem HasFDerivAtFilter.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G} {L' : Filter (F Ã— F)}
    (hg : HasFDerivAtFilter g g' L') (hf : HasFDerivAtFilter f f' L)
    (hL : Tendsto (Prod.map f f) L L') :
    HasFDerivAtFilter (g âˆ˜ f) (g' âˆ˜L f') L := by
  -- This proof can be golfed a lot. However, it should be left this way for readability.
  refine .of_isLittleOTVS <| calc
    (fun p â†¦ (g âˆ˜ f) p.1 - (g âˆ˜ f) p.2 - (g' âˆ˜L f') (p.1 - p.2))
      = fun p â†¦ (g (f p.1) - g (f p.2) - g' (f p.1 - f p.2)) +
          g' (f p.1 - f p.2 - f' (p.1 - p.2)) := by
      ext; simp
    _ =o[ğ•œ; L] (fun p â†¦ p.1 - p.2) := .add ?Hg ?Hf
  case Hg => calc (fun p â†¦ g (f p.1) - g (f p.2) - g' (f p.1 - f p.2))
    _ =o[ğ•œ; L] (fun p â†¦ f p.1 - f p.2) :=
      hg.isLittleOTVS.comp_tendsto hL
    _ =O[ğ•œ; L] (fun p â†¦ p.1 - p.2) := hf.isBigOTVS_sub
  case Hf => calc (fun p â†¦ g' (f p.1 - f p.2 - f' (p.1 - p.2)))
    _ =O[ğ•œ; L] (fun p â†¦ f p.1 - f p.2 - f' (p.1 - p.2)) := g'.isBigOTVS_comp
    _ =o[ğ•œ; L] (fun p â†¦ p.1 - p.2) := hf.isLittleOTVS

/-- The chain rule for derivatives in the sense of strict differentiability. -/
@[fun_prop]
protected theorem HasStrictFDerivAt.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
    (hg : HasStrictFDerivAt g g' (f x)) (hf : HasStrictFDerivAt f f' x) :
    HasStrictFDerivAt (fun x => g (f x)) (g'.comp f') x :=
  HasFDerivAtFilter.comp hg hf <| hf.continuousAt.tendsto.prodMap_nhds hf.continuousAt.tendsto

@[fun_prop]
theorem HasFDerivWithinAt.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G} {t : Set F}
    (hg : HasFDerivWithinAt g g' t (f x)) (hf : HasFDerivWithinAt f f' s x) (hst : MapsTo f s t) :
    HasFDerivWithinAt (g âˆ˜ f) (g'.comp f') s x :=
  HasFDerivAtFilter.comp hg hf <| .prodMap (hf.continuousWithinAt.tendsto_nhdsWithin hst) <|
    tendsto_pure_pure ..

@[fun_prop]
theorem HasFDerivAt.comp_hasFDerivWithinAt {g : F â†’ G} {g' : F â†’L[ğ•œ] G}
    (hg : HasFDerivAt g g' (f x)) (hf : HasFDerivWithinAt f f' s x) :
    HasFDerivWithinAt (g âˆ˜ f) (g'.comp f') s x :=
  hg.hasFDerivWithinAt.comp x hf (mapsTo_univ _ _)

@[fun_prop]
theorem HasFDerivWithinAt.comp_of_tendsto {g : F â†’ G} {g' : F â†’L[ğ•œ] G} {t : Set F}
    (hg : HasFDerivWithinAt g g' t (f x)) (hf : HasFDerivWithinAt f f' s x)
    (hst : Tendsto f (ğ“[s] x) (ğ“[t] f x)) : HasFDerivWithinAt (g âˆ˜ f) (g'.comp f') s x :=
  HasFDerivAtFilter.comp hg hf <| hst.prodMap <| tendsto_pure_pure ..

theorem HasFDerivWithinAt.comp_hasFDerivAt {g : F â†’ G} {g' : F â†’L[ğ•œ] G} {t : Set F}
    (hg : HasFDerivWithinAt g g' t (f x)) (hf : HasFDerivAt f f' x)
    (ht : âˆ€á¶  x' in ğ“ x, f x' âˆˆ t) : HasFDerivAt (g âˆ˜ f) (g' âˆ˜L f') x :=
  HasFDerivAtFilter.comp hg hf <| .prodMap (tendsto_nhdsWithin_iff.mpr âŸ¨hf.continuousAt, htâŸ©) <|
    tendsto_pure_pure ..

theorem HasFDerivWithinAt.comp_hasFDerivAt_of_eq {g : F â†’ G} {g' : F â†’L[ğ•œ] G} {t : Set F} {y : F}
    (hg : HasFDerivWithinAt g g' t y) (hf : HasFDerivAt f f' x)
    (ht : âˆ€á¶  x' in ğ“ x, f x' âˆˆ t) (hy : y = f x) : HasFDerivAt (g âˆ˜ f) (g' âˆ˜L f') x := by
  subst y; exact hg.comp_hasFDerivAt x hf ht

/-- The chain rule. -/
@[fun_prop]
theorem HasFDerivAt.comp {g : F â†’ G} {g' : F â†’L[ğ•œ] G} (hg : HasFDerivAt g g' (f x))
    (hf : HasFDerivAt f f' x) : HasFDerivAt (g âˆ˜ f) (g'.comp f') x :=
  HasFDerivAtFilter.comp hg hf <| hf.continuousAt.tendsto.prodMap <| tendsto_pure_pure ..

@[fun_prop]
theorem DifferentiableWithinAt.comp {g : F â†’ G} {t : Set F}
    (hg : DifferentiableWithinAt ğ•œ g t (f x)) (hf : DifferentiableWithinAt ğ•œ f s x)
    (h : MapsTo f s t) : DifferentiableWithinAt ğ•œ (g âˆ˜ f) s x :=
  (hg.hasFDerivWithinAt.comp x hf.hasFDerivWithinAt h).differentiableWithinAt

@[fun_prop]
theorem DifferentiableWithinAt.comp' {g : F â†’ G} {t : Set F}
    (hg : DifferentiableWithinAt ğ•œ g t (f x)) (hf : DifferentiableWithinAt ğ•œ f s x) :
    DifferentiableWithinAt ğ•œ (g âˆ˜ f) (s âˆ© f â»Â¹' t) x :=
  hg.comp x (hf.mono inter_subset_left) inter_subset_right

@[fun_prop]
theorem DifferentiableAt.fun_comp' {f : E â†’ F} {g : F â†’ G} (hg : DifferentiableAt ğ•œ g (f x))
    (hf : DifferentiableAt ğ•œ f x) : DifferentiableAt ğ•œ (fun x â†¦ g (f x)) x :=
  (hg.hasFDerivAt.comp x hf.hasFDerivAt).differentiableAt

@[fun_prop]
theorem DifferentiableAt.comp {g : F â†’ G} (hg : DifferentiableAt ğ•œ g (f x))
    (hf : DifferentiableAt ğ•œ f x) : DifferentiableAt ğ•œ (g âˆ˜ f) x :=
  (hg.hasFDerivAt.comp x hf.hasFDerivAt).differentiableAt

@[fun_prop]
theorem DifferentiableAt.comp_differentiableWithinAt {g : F â†’ G} (hg : DifferentiableAt ğ•œ g (f x))
    (hf : DifferentiableWithinAt ğ•œ f s x) : DifferentiableWithinAt ğ•œ (g âˆ˜ f) s x :=
  hg.differentiableWithinAt.comp x hf (mapsTo_univ _ _)

theorem fderivWithin_comp {g : F â†’ G} {t : Set F} (hg : DifferentiableWithinAt ğ•œ g t (f x))
    (hf : DifferentiableWithinAt ğ•œ f s x) (h : MapsTo f s t) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (g âˆ˜ f) s x = (fderivWithin ğ•œ g t (f x)).comp (fderivWithin ğ•œ f s x) :=
  (hg.hasFDerivWithinAt.comp x hf.hasFDerivWithinAt h).fderivWithin hxs

theorem fderivWithin_comp_of_eq {g : F â†’ G} {t : Set F} {y : F}
    (hg : DifferentiableWithinAt ğ•œ g t y) (hf : DifferentiableWithinAt ğ•œ f s x) (h : MapsTo f s t)
    (hxs : UniqueDiffWithinAt ğ•œ s x) (hy : f x = y) :
    fderivWithin ğ•œ (g âˆ˜ f) s x = (fderivWithin ğ•œ g t (f x)).comp (fderivWithin ğ•œ f s x) := by
  subst hy; exact fderivWithin_comp _ hg hf h hxs

/-- A variant for the derivative of a composition, written without `âˆ˜`. -/
theorem fderivWithin_comp' {g : F â†’ G} {t : Set F} (hg : DifferentiableWithinAt ğ•œ g t (f x))
    (hf : DifferentiableWithinAt ğ•œ f s x) (h : MapsTo f s t) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (fun y â†¦ g (f y)) s x
      = (fderivWithin ğ•œ g t (f x)).comp (fderivWithin ğ•œ f s x) :=
  fderivWithin_comp _ hg hf h hxs

/-- A variant for the derivative of a composition, written without `âˆ˜`. -/
theorem fderivWithin_comp_of_eq' {g : F â†’ G} {t : Set F} {y : F}
    (hg : DifferentiableWithinAt ğ•œ g t y) (hf : DifferentiableWithinAt ğ•œ f s x) (h : MapsTo f s t)
    (hxs : UniqueDiffWithinAt ğ•œ s x) (hy : f x = y) :
    fderivWithin ğ•œ (fun y â†¦ g (f y)) s x
      = (fderivWithin ğ•œ g t (f x)).comp (fderivWithin ğ•œ f s x) := by
  subst hy; exact fderivWithin_comp _ hg hf h hxs

/-- A version of `fderivWithin_comp` that is useful to rewrite the composition of two derivatives
  into a single derivative. This version always applies, but creates a new side-goal `f x = y`. -/
theorem fderivWithin_fderivWithin {g : F â†’ G} {f : E â†’ F} {x : E} {y : F} {s : Set E} {t : Set F}
    (hg : DifferentiableWithinAt ğ•œ g t y) (hf : DifferentiableWithinAt ğ•œ f s x) (h : MapsTo f s t)
    (hxs : UniqueDiffWithinAt ğ•œ s x) (hy : f x = y) (v : E) :
    fderivWithin ğ•œ g t y (fderivWithin ğ•œ f s x v) = fderivWithin ğ•œ (g âˆ˜ f) s x v := by
  subst y
  rw [fderivWithin_comp x hg hf h hxs, coe_comp', Function.comp_apply]

/-- Ternary version of `fderivWithin_comp`, with equality assumptions of basepoints added, in
  order to apply more easily as a rewrite from right-to-left. -/
theorem fderivWithin_compâ‚ƒ {g' : G â†’ G'} {g : F â†’ G} {t : Set F} {u : Set G} {y : F} {y' : G}
    (hg' : DifferentiableWithinAt ğ•œ g' u y') (hg : DifferentiableWithinAt ğ•œ g t y)
    (hf : DifferentiableWithinAt ğ•œ f s x) (h2g : MapsTo g t u) (h2f : MapsTo f s t) (h3g : g y = y')
    (h3f : f x = y) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (g' âˆ˜ g âˆ˜ f) s x =
      (fderivWithin ğ•œ g' u y').comp ((fderivWithin ğ•œ g t y).comp (fderivWithin ğ•œ f s x)) := by
  substs h3g h3f
  exact (hg'.hasFDerivWithinAt.comp x (hg.hasFDerivWithinAt.comp x hf.hasFDerivWithinAt h2f) <|
    h2g.comp h2f).fderivWithin hxs

theorem fderiv_comp {g : F â†’ G} (hg : DifferentiableAt ğ•œ g (f x)) (hf : DifferentiableAt ğ•œ f x) :
    fderiv ğ•œ (g âˆ˜ f) x = (fderiv ğ•œ g (f x)).comp (fderiv ğ•œ f x) :=
  (hg.hasFDerivAt.comp x hf.hasFDerivAt).fderiv

/-- A variant for the derivative of a composition, written without `âˆ˜`. -/
theorem fderiv_comp' {g : F â†’ G} (hg : DifferentiableAt ğ•œ g (f x)) (hf : DifferentiableAt ğ•œ f x) :
    fderiv ğ•œ (fun y â†¦ g (f y)) x = (fderiv ğ•œ g (f x)).comp (fderiv ğ•œ f x) :=
  fderiv_comp x hg hf

theorem fderiv_comp_fderivWithin {g : F â†’ G} (hg : DifferentiableAt ğ•œ g (f x))
    (hf : DifferentiableWithinAt ğ•œ f s x) (hxs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ (g âˆ˜ f) s x = (fderiv ğ•œ g (f x)).comp (fderivWithin ğ•œ f s x) :=
  (hg.hasFDerivAt.comp_hasFDerivWithinAt x hf.hasFDerivWithinAt).fderivWithin hxs

@[fun_prop]
theorem DifferentiableOn.fun_comp {g : F â†’ G} {t : Set F} (hg : DifferentiableOn ğ•œ g t)
    (hf : DifferentiableOn ğ•œ f s) (st : MapsTo f s t) :
    DifferentiableOn ğ•œ (fun x â†¦ g (f x)) s :=
  fun x hx => DifferentiableWithinAt.comp x (hg (f x) (st hx)) (hf x hx) st

@[fun_prop]
theorem DifferentiableOn.comp {g : F â†’ G} {t : Set F} (hg : DifferentiableOn ğ•œ g t)
    (hf : DifferentiableOn ğ•œ f s) (st : MapsTo f s t) : DifferentiableOn ğ•œ (g âˆ˜ f) s :=
  fun x hx => DifferentiableWithinAt.comp x (hg (f x) (st hx)) (hf x hx) st

@[fun_prop]
theorem Differentiable.fun_comp {g : F â†’ G} (hg : Differentiable ğ•œ g) (hf : Differentiable ğ•œ f) :
    Differentiable ğ•œ (fun x â†¦ g (f x)) :=
  fun x => DifferentiableAt.comp x (hg (f x)) (hf x)

@[fun_prop]
theorem Differentiable.comp {g : F â†’ G} (hg : Differentiable ğ•œ g) (hf : Differentiable ğ•œ f) :
    Differentiable ğ•œ (g âˆ˜ f) :=
  fun x => DifferentiableAt.comp x (hg (f x)) (hf x)

@[fun_prop]
theorem Differentiable.comp_differentiableOn {g : F â†’ G} (hg : Differentiable ğ•œ g)
    (hf : DifferentiableOn ğ•œ f s) : DifferentiableOn ğ•œ (g âˆ˜ f) s :=
  hg.differentiableOn.comp hf (mapsTo_univ _ _)


@[fun_prop]
protected theorem Differentiable.iterate {f : E â†’ E} (hf : Differentiable ğ•œ f) (n : â„•) :
    Differentiable ğ•œ f^[n] :=
  Nat.recOn n differentiable_id fun _ ihn => ihn.comp hf

@[fun_prop]
protected theorem DifferentiableOn.iterate {f : E â†’ E} (hf : DifferentiableOn ğ•œ f s)
    (hs : MapsTo f s s) (n : â„•) : DifferentiableOn ğ•œ f^[n] s :=
  Nat.recOn n differentiableOn_id fun _ ihn => ihn.comp hf hs

variable {x}

protected theorem HasFDerivAtFilter.iterate {f : E â†’ E} {f' : E â†’L[ğ•œ] E}
    (hf : HasFDerivAtFilter f f' L) (hL : Tendsto (Prod.map f f) L L) (n : â„•) :
    HasFDerivAtFilter f^[n] (f' ^ n) L := by
  induction n with
  | zero => exact hasFDerivAtFilter_id L
  | succ n ihn =>
    rw [Function.iterate_succ, pow_succ]
    exact ihn.comp hf hL

@[fun_prop]
protected theorem HasFDerivAt.iterate {f : E â†’ E} {f' : E â†’L[ğ•œ] E} (hf : HasFDerivAt f f' x)
    (hx : f x = x) (n : â„•) : HasFDerivAt f^[n] (f' ^ n) x := by
  refine HasFDerivAtFilter.iterate hf ?_ n
  simpa [hx] using hf.continuousAt.tendsto.prodMap (tendsto_pure_pure f x)

@[fun_prop]
protected theorem HasFDerivWithinAt.iterate {f : E â†’ E} {f' : E â†’L[ğ•œ] E}
    (hf : HasFDerivWithinAt f f' s x) (hx : f x = x) (hs : MapsTo f s s) (n : â„•) :
    HasFDerivWithinAt f^[n] (f' ^ n) s x := by
  refine HasFDerivAtFilter.iterate hf ?_ n
  simpa [hx] using hf.continuousWithinAt.tendsto_nhdsWithin hs |>.prodMap (tendsto_pure_pure f x)

@[fun_prop]
protected theorem HasStrictFDerivAt.iterate {f : E â†’ E} {f' : E â†’L[ğ•œ] E}
    (hf : HasStrictFDerivAt f f' x) (hx : f x = x) (n : â„•) :
    HasStrictFDerivAt f^[n] (f' ^ n) x := by
  refine HasFDerivAtFilter.iterate hf ?_ n
  simpa [hx, ContinuousAt] using hf.continuousAt.prodMap' hf.continuousAt

@[fun_prop]
protected theorem DifferentiableAt.iterate {f : E â†’ E} (hf : DifferentiableAt ğ•œ f x) (hx : f x = x)
    (n : â„•) : DifferentiableAt ğ•œ f^[n] x :=
  (hf.hasFDerivAt.iterate hx n).differentiableAt

@[fun_prop]
protected theorem DifferentiableWithinAt.iterate {f : E â†’ E} (hf : DifferentiableWithinAt ğ•œ f s x)
    (hx : f x = x) (hs : MapsTo f s s) (n : â„•) : DifferentiableWithinAt ğ•œ f^[n] s x :=
  (hf.hasFDerivWithinAt.iterate hx hs n).differentiableWithinAt

end Composition

end
