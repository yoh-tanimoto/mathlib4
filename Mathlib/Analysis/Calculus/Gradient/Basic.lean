/-
Copyright (c) 2023 Ziyu Wang. All rights reserved.
Released under Apache 2.0 license as described in the file LICENSE.
Authors: Ziyu Wang, Chenyi Li, SÃ©bastien GouÃ«zel, Penghao Yu, Zhipeng Cao
-/
module

public import Mathlib.Analysis.InnerProductSpace.Dual
public import Mathlib.Analysis.Calculus.FDeriv.Basic
public import Mathlib.Analysis.Calculus.Deriv.Basic

/-!
# Gradient

## Main Definitions

Let `f` be a function from a Hilbert Space `F` to `ğ•œ` (`ğ•œ` is `â„` or `â„‚`), `x` be a point in `F`
and `f'` be a vector in F. Then

  `HasGradientWithinAt f f' s x`

says that `f` has a gradient `f'` at `x`, where the domain of interest
is restricted to `s`. We also have

  `HasGradientAt f f' x := HasGradientWithinAt f f' x univ`

## Main results

This file develops the following aspects of the theory of gradients:
* definitions of gradients, both within a set and on the whole space.
* translating between `HasGradientAtFilter` and `HasFDerivAtFilter`,
  `HasGradientWithinAt` and `HasFDerivWithinAt`, `HasGradientAt` and `HasFDerivAt`,
  `gradient` and `fderiv`.
* uniqueness of gradients.
* translating between `HasGradientAtFilter` and `HasDerivAtFilter`,
  `HasGradientAt` and `HasDerivAt`, `gradient` and `deriv` when `F = ğ•œ`.
* the theorems about the inner product of the gradient.
* the congruence of the gradient.
* the gradient of constant functions.
* the continuity of a function admitting a gradient.
-/

@[expose] public section

@[expose] public section

open ComplexConjugate Topology InnerProductSpace Function Set

noncomputable section

variable {ğ•œ F : Type*} [RCLike ğ•œ]
variable [NormedAddCommGroup F] [InnerProductSpace ğ•œ F] [CompleteSpace F]
variable {f : F â†’ ğ•œ} {f' x y : F}

/-- A function `f` has the gradient `f'` as derivative along the filter `L` if
  `f x' = f x + âŸ¨f', x' - xâŸ© + o (x' - x)` when `x'` converges along the filter `L`. -/
def HasGradientAtFilter (f : F â†’ ğ•œ) (f' x : F) (L : Filter F) :=
  HasFDerivAtFilter f (toDual ğ•œ F f') (L Ã—Ë¢ pure x)

/-- `f` has the gradient `f'` at the point `x` within the subset `s` if
  `f x' = f x + âŸ¨f', x' - xâŸ© + o (x' - x)` where `x'` converges to `x` inside `s`. -/
def HasGradientWithinAt (f : F â†’ ğ•œ) (f' : F) (s : Set F) (x : F) :=
  HasGradientAtFilter f f' x (ğ“[s] x)

/-- `f` has the gradient `f'` at the point `x` if
  `f x' = f x + âŸ¨f', x' - xâŸ© + o (x' - x)` where `x'` converges to `x`. -/
def HasGradientAt (f : F â†’ ğ•œ) (f' x : F) :=
  HasGradientAtFilter f f' x (ğ“ x)

/-- Gradient of `f` at the point `x` within the set `s`, if it exists.  Zero otherwise.

If the derivative exists (i.e., `âˆƒ f', HasGradientWithinAt f f' s x`), then
`f x' = f x + âŸ¨f', x' - xâŸ© + o (x' - x)` where `x'` converges to `x` inside `s`. -/
def gradientWithin (f : F â†’ ğ•œ) (s : Set F) (x : F) : F :=
  (toDual ğ•œ F).symm (fderivWithin ğ•œ f s x)

/-- Gradient of `f` at the point `x`, if it exists.  Zero otherwise.
Denoted as `âˆ‡` within the Gradient namespace.

If the derivative exists (i.e., `âˆƒ f', HasGradientAt f f' x`), then
`f x' = f x + âŸ¨f', x' - xâŸ© + o (x' - x)` where `x'` converges to `x`. -/
def gradient (f : F â†’ ğ•œ) (x : F) : F :=
  (toDual ğ•œ F).symm (fderiv ğ•œ f x)

@[inherit_doc]
scoped[Gradient] notation "âˆ‡" => gradient

local notation "âŸª" x ", " y "âŸ«" => inner ğ•œ x y

open scoped Gradient

variable {s : Set F} {L : Filter F}

theorem hasGradientWithinAt_iff_hasFDerivWithinAt {s : Set F} :
    HasGradientWithinAt f f' s x â†” HasFDerivWithinAt f (toDual ğ•œ F f') s x :=
  Iff.rfl

theorem hasFDerivWithinAt_iff_hasGradientWithinAt {frechet : StrongDual ğ•œ F} {s : Set F} :
    HasFDerivWithinAt f frechet s x â†” HasGradientWithinAt f ((toDual ğ•œ F).symm frechet) s x := by
  rw [hasGradientWithinAt_iff_hasFDerivWithinAt, (toDual ğ•œ F).apply_symm_apply frechet]

theorem hasGradientAt_iff_hasFDerivAt :
    HasGradientAt f f' x â†” HasFDerivAt f (toDual ğ•œ F f') x :=
  Iff.rfl

theorem hasFDerivAt_iff_hasGradientAt {frechet : StrongDual ğ•œ F} :
    HasFDerivAt f frechet x â†” HasGradientAt f ((toDual ğ•œ F).symm frechet) x := by
  rw [hasGradientAt_iff_hasFDerivAt, (toDual ğ•œ F).apply_symm_apply frechet]

alias âŸ¨HasGradientWithinAt.hasFDerivWithinAt, _âŸ© := hasGradientWithinAt_iff_hasFDerivWithinAt

alias âŸ¨HasFDerivWithinAt.hasGradientWithinAt, _âŸ© := hasFDerivWithinAt_iff_hasGradientWithinAt

alias âŸ¨HasGradientAt.hasFDerivAt, _âŸ© := hasGradientAt_iff_hasFDerivAt

alias âŸ¨HasFDerivAt.hasGradientAt, _âŸ© := hasFDerivAt_iff_hasGradientAt

theorem gradient_eq_zero_of_not_differentiableAt (h : Â¬DifferentiableAt ğ•œ f x) : âˆ‡ f x = 0 := by
  rw [gradient, fderiv_zero_of_not_differentiableAt h, map_zero]

theorem HasGradientAt.unique {gradf gradg : F}
    (hf : HasGradientAt f gradf x) (hg : HasGradientAt f gradg x) :
    gradf = gradg :=
  (toDual ğ•œ F).injective (hf.hasFDerivAt.unique hg.hasFDerivAt)

theorem DifferentiableAt.hasGradientAt (h : DifferentiableAt ğ•œ f x) :
    HasGradientAt f (âˆ‡ f x) x := by
  rw [hasGradientAt_iff_hasFDerivAt, gradient, (toDual ğ•œ F).apply_symm_apply (fderiv ğ•œ f x)]
  exact h.hasFDerivAt

theorem HasGradientAt.differentiableAt (h : HasGradientAt f f' x) :
    DifferentiableAt ğ•œ f x :=
  h.hasFDerivAt.differentiableAt

theorem DifferentiableWithinAt.hasGradientWithinAt (h : DifferentiableWithinAt ğ•œ f s x) :
    HasGradientWithinAt f (gradientWithin f s x) s x := by
  rw [hasGradientWithinAt_iff_hasFDerivWithinAt, gradientWithin,
    (toDual ğ•œ F).apply_symm_apply (fderivWithin ğ•œ f s x)]
  exact h.hasFDerivWithinAt

theorem HasGradientWithinAt.differentiableWithinAt (h : HasGradientWithinAt f f' s x) :
    DifferentiableWithinAt ğ•œ f s x :=
  h.hasFDerivWithinAt.differentiableWithinAt

@[simp]
theorem hasGradientWithinAt_univ : HasGradientWithinAt f f' univ x â†” HasGradientAt f f' x := by
  rw [hasGradientWithinAt_iff_hasFDerivWithinAt, hasGradientAt_iff_hasFDerivAt]
  exact hasFDerivWithinAt_univ

theorem DifferentiableOn.hasGradientAt (h : DifferentiableOn ğ•œ f s) (hs : s âˆˆ ğ“ x) :
    HasGradientAt f (âˆ‡ f x) x :=
  (h.hasFDerivAt hs).hasGradientAt

theorem HasGradientAt.gradient (h : HasGradientAt f f' x) : âˆ‡ f x = f' :=
  h.differentiableAt.hasGradientAt.unique h

theorem gradient_eq {f' : F â†’ F} (h : âˆ€ x, HasGradientAt f (f' x) x) : âˆ‡ f = f' :=
  funext fun x => (h x).gradient

section OneDimension

variable {g : ğ•œ â†’ ğ•œ} {g' u : ğ•œ} {L' : Filter ğ•œ}

theorem HasGradientAtFilter.hasDerivAtFilter (h : HasGradientAtFilter g g' u L') :
    HasDerivAtFilter g (conj g') (L' Ã—Ë¢ pure u) :=
  h

theorem HasDerivAtFilter.hasGradientAtFilter (h : HasDerivAtFilter g g' (L' Ã—Ë¢ pure u)) :
    HasGradientAtFilter g (conj g') u L' := by
  have : ContinuousLinearMap.smulRight (1 : ğ•œ â†’L[ğ•œ] ğ•œ) g' = (toDual ğ•œ ğ•œ) (conj g') := by
    ext; simp
  rwa [HasGradientAtFilter, â† this]

theorem HasGradientAt.hasDerivAt (h : HasGradientAt g g' u) : HasDerivAt g (conj g') u := by
  rw [hasGradientAt_iff_hasFDerivAt, hasFDerivAt_iff_hasDerivAt] at h
  simpa using h

theorem HasDerivAt.hasGradientAt (h : HasDerivAt g g' u) : HasGradientAt g (conj g') u := by
  rw [hasGradientAt_iff_hasFDerivAt, hasFDerivAt_iff_hasDerivAt]
  simpa

theorem gradient_eq_deriv : âˆ‡ g u = conj (deriv g u) := by
  by_cases h : DifferentiableAt ğ•œ g u
  Â· rw [h.hasGradientAt.hasDerivAt.deriv, RCLike.conj_conj]
  Â· rw [gradient_eq_zero_of_not_differentiableAt h, deriv_zero_of_not_differentiableAt h, map_zero]

end OneDimension

section OneDimensionReal

variable {g : â„ â†’ â„} {g' u : â„} {L' : Filter â„}

theorem HasGradientAtFilter.hasDerivAtFilter' (h : HasGradientAtFilter g g' u L') :
    HasDerivAtFilter g g' (L' Ã—Ë¢ pure u) := h.hasDerivAtFilter

theorem HasDerivAtFilter.hasGradientAtFilter' (h : HasDerivAtFilter g g' (L' Ã—Ë¢ pure u)) :
    HasGradientAtFilter g g' u L' := h.hasGradientAtFilter

theorem HasGradientAt.hasDerivAt' (h : HasGradientAt g g' u) :
    HasDerivAt g g' u := h.hasDerivAt

theorem HasDerivAt.hasGradientAt' (h : HasDerivAt g g' u) :
    HasGradientAt g g' u := h.hasGradientAt

theorem gradient_eq_deriv' : âˆ‡ g u = deriv g u := gradient_eq_deriv

end OneDimensionReal

open Filter

section GradientProperties

theorem hasGradientAtFilter_iff_isLittleO :
    HasGradientAtFilter f f' x L â†”
    (fun x' : F => f x' - f x - âŸªf', x' - xâŸ«) =o[L] fun x' => x' - x :=
  hasFDerivAtFilter_iff_isLittleO.trans <| by simp [Function.comp_def]

theorem hasGradientWithinAt_iff_isLittleO :
    HasGradientWithinAt f f' s x â†”
    (fun x' : F => f x' - f x - âŸªf', x' - xâŸ«) =o[ğ“[s] x] fun x' => x' - x :=
  hasGradientAtFilter_iff_isLittleO

theorem hasGradientWithinAt_iff_tendsto :
    HasGradientWithinAt f f' s x â†”
    Tendsto (fun x' => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - âŸªf', x' - xâŸ«â€–) (ğ“[s] x) (ğ“ 0) :=
  hasFDerivWithinAt_iff_tendsto

theorem hasGradientAt_iff_isLittleO : HasGradientAt f f' x â†”
    (fun x' : F => f x' - f x - âŸªf', x' - xâŸ«) =o[ğ“ x] fun x' => x' - x :=
  hasGradientAtFilter_iff_isLittleO

theorem hasGradientAt_iff_tendsto :
    HasGradientAt f f' x â†”
    Tendsto (fun x' => â€–x' - xâ€–â»Â¹ * â€–f x' - f x - âŸªf', x' - xâŸ«â€–) (ğ“ x) (ğ“ 0) :=
  hasFDerivAt_iff_tendsto

theorem HasGradientAtFilter.isBigO_sub (h : HasGradientAtFilter f f' x L) :
    (fun x' => f x' - f x) =O[L] fun x' => x' - x :=
  HasFDerivAtFilter.isBigO_sub h |>.comp_tendsto prod_pure.ge

theorem hasGradientWithinAt_congr_set' {s t : Set F} (y : F) (h : s =á¶ [ğ“[{y}á¶œ] x] t) :
    HasGradientWithinAt f f' s x â†” HasGradientWithinAt f f' t x :=
  hasFDerivWithinAt_congr_set' y h

theorem hasGradientWithinAt_congr_set {s t : Set F} (h : s =á¶ [ğ“ x] t) :
    HasGradientWithinAt f f' s x â†” HasGradientWithinAt f f' t x :=
  hasFDerivWithinAt_congr_set h

theorem hasGradientAt_iff_isLittleO_nhds_zero : HasGradientAt f f' x â†”
    (fun h => f (x + h) - f x - âŸªf', hâŸ«) =o[ğ“ 0] fun h => h :=
  hasFDerivAt_iff_isLittleO_nhds_zero

end GradientProperties

section Inner

lemma HasGradientWithinAt.fderivWithin_apply
    (h : HasGradientWithinAt f f' s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    fderivWithin ğ•œ f s x y = âŸªf', yâŸ« := by
  rw [h.hasFDerivWithinAt.fderivWithin hs, toDual_apply_apply]

lemma HasGradientAt.fderiv_apply (h : HasGradientAt f f' x) : fderiv ğ•œ f x y = âŸªf', yâŸ« := by
  rw [h.hasFDerivAt.fderiv, toDual_apply_apply]

lemma inner_gradientWithin_left
    (h : DifferentiableWithinAt ğ•œ f s x) (hs : UniqueDiffWithinAt ğ•œ s x) :
    âŸªgradientWithin f s x, yâŸ« = fderivWithin ğ•œ f s x y := by
  rw [h.hasGradientWithinAt.fderivWithin_apply hs]

lemma inner_gradient_left (h : DifferentiableAt ğ•œ f x) : âŸªâˆ‡ f x, yâŸ« = fderiv ğ•œ f x y := by
  rw [h.hasGradientAt.fderiv_apply]

lemma inner_gradientWithin_right
    (h : DifferentiableWithinAt ğ•œ f s y) (hs : UniqueDiffWithinAt ğ•œ s y) :
    âŸªx, gradientWithin f s yâŸ« = conj (fderivWithin ğ•œ f s y x) := by
  rw [â† inner_conj_symm, inner_gradientWithin_left h hs]

lemma inner_gradient_right (h : DifferentiableAt ğ•œ f y) : âŸªx, âˆ‡ f yâŸ« = conj (fderiv ğ•œ f y x) := by
  rw [â† inner_conj_symm, h.hasGradientAt.fderiv_apply]

end Inner

section congr

/-! ### Congruence properties of the Gradient -/

variable {fâ‚€ fâ‚ : F â†’ ğ•œ} {fâ‚€' fâ‚' : F} {t : Set F}

theorem Filter.EventuallyEq.hasGradientAtFilter_iff (hâ‚€ : fâ‚€ =á¶ [L] fâ‚) (hx : fâ‚€ x = fâ‚ x)
    (hâ‚ : fâ‚€' = fâ‚') : HasGradientAtFilter fâ‚€ fâ‚€' x L â†” HasGradientAtFilter fâ‚ fâ‚' x L :=
  (hâ‚€.prodMap <| by assumption).hasFDerivAtFilter_iff <| by simp [hâ‚]

theorem HasGradientAtFilter.congr_of_eventuallyEq (h : HasGradientAtFilter f f' x L)
    (hL : fâ‚ =á¶ [L] f) (hx : fâ‚ x = f x) : HasGradientAtFilter fâ‚ f' x L := by
  rwa [hL.hasGradientAtFilter_iff hx rfl]

theorem HasGradientWithinAt.congr_mono (h : HasGradientWithinAt f f' s x) (ht : âˆ€ x âˆˆ t, fâ‚ x = f x)
    (hx : fâ‚ x = f x) (hâ‚ : t âŠ† s) : HasGradientWithinAt fâ‚ f' t x :=
  HasFDerivWithinAt.congr_mono h ht hx hâ‚

theorem HasGradientWithinAt.congr (h : HasGradientWithinAt f f' s x) (hs : âˆ€ x âˆˆ s, fâ‚ x = f x)
    (hx : fâ‚ x = f x) : HasGradientWithinAt fâ‚ f' s x :=
  h.congr_mono hs hx (by tauto)

theorem HasGradientWithinAt.congr_of_mem (h : HasGradientWithinAt f f' s x)
    (hs : âˆ€ x âˆˆ s, fâ‚ x = f x) (hx : x âˆˆ s) : HasGradientWithinAt fâ‚ f' s x :=
  h.congr hs (hs _ hx)

theorem HasGradientWithinAt.congr_of_eventuallyEq (h : HasGradientWithinAt f f' s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : fâ‚ x = f x) : HasGradientWithinAt fâ‚ f' s x :=
  HasGradientAtFilter.congr_of_eventuallyEq h hâ‚ hx

theorem HasGradientWithinAt.congr_of_eventuallyEq_of_mem (h : HasGradientWithinAt f f' s x)
    (hâ‚ : fâ‚ =á¶ [ğ“[s] x] f) (hx : x âˆˆ s) : HasGradientWithinAt fâ‚ f' s x :=
  h.congr_of_eventuallyEq hâ‚ (hâ‚.eq_of_nhdsWithin hx)

theorem HasGradientAt.congr_of_eventuallyEq (h : HasGradientAt f f' x) (hâ‚ : fâ‚ =á¶ [ğ“ x] f) :
    HasGradientAt fâ‚ f' x :=
  HasGradientAtFilter.congr_of_eventuallyEq h hâ‚ (mem_of_mem_nhds hâ‚ :)

theorem Filter.EventuallyEq.gradient_eq (hL : fâ‚ =á¶ [ğ“ x] f) : âˆ‡ fâ‚ x = âˆ‡ f x := by
  unfold gradient
  rwa [Filter.EventuallyEq.fderiv_eq]

protected theorem Filter.EventuallyEq.gradient (h : fâ‚ =á¶ [ğ“ x] f) : âˆ‡ fâ‚ =á¶ [ğ“ x] âˆ‡ f :=
  h.eventuallyEq_nhds.mono fun _ h => h.gradient_eq

end congr

/-! ### The Gradient of constant functions -/

section Const

variable (c : ğ•œ) (s x L)

theorem hasGradientAtFilter_const : HasGradientAtFilter (fun _ => c) 0 x L := by
  rw [HasGradientAtFilter, map_zero]; exact hasFDerivAtFilter_const c _

theorem hasGradientWithinAt_const : HasGradientWithinAt (fun _ => c) 0 s x :=
  hasGradientAtFilter_const _ _ _

theorem hasGradientAt_const : HasGradientAt (fun _ => c) 0 x :=
  hasGradientAtFilter_const _ _ _

theorem gradient_fun_const : âˆ‡ (fun _ => c) x = 0 := by simp [gradient]

theorem gradient_const : âˆ‡ (const F c) x = 0 := gradient_fun_const x c

@[simp]
theorem gradient_fun_const' : (âˆ‡ fun _ : F => c) = fun _ => 0 :=
  funext fun x => gradient_const x c

@[simp]
theorem gradient_const' : âˆ‡ (const F c) = 0 := gradient_fun_const' c

end Const

section Continuous

/-! ### Continuity of a function admitting a gradient -/

nonrec theorem HasGradientAtFilter.tendsto_nhds (hL : L â‰¤ ğ“ x) (h : HasGradientAtFilter f f' x L) :
    Tendsto f L (ğ“ (f x)) :=
  h.tendsto_nhds hL

theorem HasGradientWithinAt.continuousWithinAt (h : HasGradientWithinAt f f' s x) :
    ContinuousWithinAt f s x :=
  HasGradientAtFilter.tendsto_nhds inf_le_left h

theorem HasGradientAt.continuousAt (h : HasGradientAt f f' x) : ContinuousAt f x :=
  HasGradientAtFilter.tendsto_nhds le_rfl h

protected theorem HasGradientAt.continuousOn {f' : F â†’ F} (h : âˆ€ x âˆˆ s, HasGradientAt f (f' x) x) :
    ContinuousOn f s :=
  fun x hx => (h x hx).continuousAt.continuousWithinAt

end Continuous
